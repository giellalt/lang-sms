! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or feedback@divvun.no

!! # Skolt Saami Numerals

!LEXICON num      
!!! Lexicon ''@LEXNAME@'' gives the tag __+Num__
!+Num: # ;

LEXICON Numeral   !!= * **@CODE@**  initial lexica
!            MILJON           ; ! a noun of its own
!            UNDERDUHAT       ; ! for generator under 1000
!            JUSTDUHAT        ; ! going via 1000
!            OVERDUHAT        ; ! for generator over 1000
!            OLD              ; ! for "thirteen hundred, etc.
!            num-imprecise    ;
!            num-collective   ;
 +Use/Circ: ARABIC           ; ! for the arabic numerals        !
 +Use/Circ: ROMAN            ; ! for the roman numerals         !
 +Use/Circ: NUM-PREFIXES     ; ! for §34 etc.                   !
 +Use/Circ: ISOLATED-NUMEXP  ; ! for ½ etc.                     !
!            11TO99F          ;
!            21TO99           ;
!            DUHAT            ;
!            1TO9DUHAT        ;
!            O-NUPPELOHKAI    ;
!            NUPPELOHKAICUODI ;
!            NUPPELOHKAI      ;


! Arabic numerals
! ---------------


LEXICON NUM-PREFIXES   !!= * **@CODE@**
    §+Use/Circ:§      ARABIC ; ! §24
   §§+Use/Circ:§§     ARABIC ; ! §§24
  §% +Use/Circ:§%     ARABIC ; ! § 24
 §§% +Use/Circ:§§%    ARABIC ; ! §§ 24
   %-+Use/Circ:%-     ARABIC ; ! -24
   %‒+Use/Circ:%‒     ARABIC ; ! -24 U+2012
   %–+Use/Circ:%–     ARABIC ; ! -24 U+2013
   %—+Use/Circ:%—     ARABIC ; ! -24 U+2014
   %―+Use/Circ:%―     ARABIC ; ! -24 U+2015
   %++Use/Circ:%+     ARABIC ; ! <plus>24
 %-% +Use/Circ:%-%    ARABIC ; ! - 24
 %‒% +Use/Circ:%‒%    ARABIC ; ! - 24 U+2012
 %–% +Use/Circ:%–%    ARABIC ; ! - 24 U+2013
 %—% +Use/Circ:%—%    ARABIC ; ! - 24 U+2014
 %―% +Use/Circ:%―%    ARABIC ; ! - 24 U+2015
 %+% +Use/Circ:%+%    ARABIC ; ! <plus> 24
   %*+Use/Circ:%*     ARABIC ; ! *24
   %$+Use/Circ:%$     ARABIC ; ! $24
   %€+Use/Circ:%€     ARABIC ; ! €24
   %<+Use/Circ:%[%<%] ARABIC ; ! <24
   %>+Use/Circ:%[%>%] ARABIC ; ! >24
  s%.+Use/Circ:s%.    ARABIC ; ! s.24

LEXICON ARABIC   !!= * **@CODE@** arabic numerals
< [1|2|3|4|5|6|7|8|9|%0] > ARABICLOOP      ;
< [1|2|3|4|5|6|7|8|9|%0] > ARABICLOOPORD   ;
< [1|2|3|4|5|6|7|8|9|%0] > ARABICLOOPCOLL  ;
< [1|2|3|4|5|6|7|8|9|%0] > ARABICLOOPphone ;

LEXICON ARABICLOOPphone   !!= * **@CODE@** +358(0)16671254
< [1|2|3|4|5|6|7|8|9|%0] > ARABICLOOPphone ;
!  +Use/Circ:              ARABICLOOPphone ; ! Infinite loop on the surface side
  (+Use/Circ:(             ARABICLOOPphone ;
  )+Use/Circ:)             ARABICLOOPphone ;
 %—+Use/Circ:%—            ARABICLOOPphone ; 
                           ARABICCASEphone ;

LEXICON ARABICCASEphone   !!= * **@CODE@**  
 +Num+Sg+Nom: # ;
 +Num+Sg+Acc: # ;

LEXICON ARABICLOOP   !!= * **@CODE@**
         +Use/Circ:         ARABIC ;
       %-+Use/Circ:%-       ARABIC ; ! 1-2 multipart numbers
       %–+Use/Circ:%–       ARABIC ; ! 1–2 multipart numbers
       %—+Use/Circ:%—       ARABIC ; ! 1—2 multipart numbers
     %.%-+Use/Circ:%.%-     ARABIC ; ! 1.-2 multipart numbers ! !
     %.%–+Use/Circ:%.%–     ARABIC ; ! 1.–2 multipart numbers ! !
     %.%—+Use/Circ:%.%—     ARABIC ; ! 1.—2 multipart numbers ! !
     %-% +Use/Circ:%-%      ARABIC ; ! 1- 2 multipart numbers
     %–% +Use/Circ:%–%      ARABIC ; ! 1– 2 multipart numbers
     %—% +Use/Circ:%—%      ARABIC ; ! 1— 2 multipart numbers
     % %-+Use/Circ:% %-     ARABIC ; ! 1 -2 multipart numbers
     % %–+Use/Circ:% %–     ARABIC ; ! 1 –2 multipart numbers
     % %—+Use/Circ:% %—     ARABIC ; ! 1 —2 multipart numbers
   % %-% +Use/Circ:% %-%    ARABIC ; ! 1 - 2 multipart numbers
   % %–% +Use/Circ:% %–%    ARABIC ; ! 1 – 2 multipart numbers
   % %—% +Use/Circ:% %—%    ARABIC ; ! 1 — 2 multipart numbers
 % %-%-% +Use/Circ:% %-%-%  ARABIC ; ! 1 -- 2 multipart numbers
 % %–%–% +Use/Circ:% %–%–%  ARABIC ; ! 1 –– 2 multipart numbers
 % %—%—% +Use/Circ:% %—%—%  ARABIC ; ! 1 —— 2 multipart numbers
       %.+Use/Circ:%.       ARABIC ;
       %:+Use/Circ:%:       ARABIC ;
       %,+Use/Circ:%,       ARABIC ;
       % +Use/Circ:%        ARABIC ; ! Allowing for "23 500".
         +Use/Circ:         ARABIC ; ! Allowing for "23 500" w/nbsp (init char = alt-space)
       %/+Use/Circ:%/       ARABIC ; ! 24/12
     %/% +Use/Circ:%/%      ARABIC ; ! 24/ 12
     % %/+Use/Circ:% %/     ARABIC ; ! 24 /12
        '+Use/Circ:'        ARABIC ;

     +Num+Use/Circ:         RNum   ; ! Num Cmp Noun, with hyphen

         ,%-+Num+Use/Circ:,%-    ARABICCASES     ; ! 10,-
,%-+Num+Err/Orth+Use/Circ:.%-    ARABICCASES     ; ! 10.- It is wrong, but written.
   % %-+Err/Orth+Use/Circ:% %-   ARABICDELIMITER ; ! 1 - multipart numbers - "1 -:s"?
 % %-%-+Err/Orth+Use/Circ:% %-%- ARABICDELIMITER ; ! 1 -- multipart numbers
              %%+Use/Circ:%%     ARABICDELIMITER ; ! 50%. It is wrong, but 75 % of us write "75%".
            % %%+Use/Circ:% %%   ARABICDELIMITER ; ! the correct ones as well... 50 % etc.
                                 ARABICDELIMITER ; ! list of number-case delimiters

LEXICON ARABICLOOPORD   !!= * **@CODE@** ordinals
 +A+Ord+Attr:%. # ;
 +A+Ord+Use/Circ: ARABICDELIMITERORD ;
 < [a|b|c|d|e|f|f %  f] > ACASETAG ;

LEXICON ACASETAG   !!= * **@CODE@**  is for 1e+Num+Sg+Num:1e
+Num: ARABICCASE0 ;
+Num:%: ARABICCASE ;

LEXICON ARABICLOOPCOLL   !!= * **@CODE@** collectives
 +N+Coll+Use/Circ: ARABICDELIMITERCOLL ;


LEXICON ARABICDELIMITER   !!= * **@CODE@**  blank + 3 delim, what does the lg counc prefer?
 +Num:            ARABICCASE0      ; ! This is the 1984 case, ie NGA
 +Num:%:          ARABICCASE       ; ! This is the 1984:s case.
 +Num+Err/Orth:   ARABICCASE       ; ! This is the 1984s case.
 +Num+Err/Orth:'  ARABICCASE       ; ! This is the 1984's case.
 +Num+Err/Orth:’  ARABICCASE       ; ! optio-B07 = ’
 +Num+Err/Orth:%- ARABICCASE       ; ! This is the 1984-s case.
 +Num+Err/Orth:´  ARABICCASE       ; ! This is the 1984´s case.
 +Num:            ARABICCOMPOUNDS ; ! 6-jáhkasaš, 


LEXICON ARABICDELIMITERORD   !!= * **@CODE@** ordinals
!             ARABICCASE0   ; ! This is the 1984 case, ie NGA
          :%: ARABICCASEORD ; ! This is the 1984:s case.
 +Err/Orth:   ARABICCASEORD ; ! This is the 1984s case.
 +Err/Orth:'  ARABICCASEORD ; ! This is the 1984's case.
 +Err/Orth:%- ARABICCASEORD ; ! This is the 1984-s case.

LEXICON ARABICDELIMITERCOLL   !!= * **@CODE@** collectives
!             ARABICCASE0    ; ! This is the 1984 case, ie NGA
          :%: ARABICCASECOLL ; ! This is the 1984:s case.
 +Err/Orth:   ARABICCASECOLL ; ! This is the 1984s case.
 +Err/Orth:'  ARABICCASECOLL ; ! This is the 1984's case.
 +Err/Orth:%- ARABICCASECOLL ; ! This is the 1984-s case.

LEXICON ARABICCASES   !!= * **@CODE@**  to distinguish between 0 and oblique
ARABICCASE0 ;
ARABICCASE ;

LEXICON ARABICCASE0   !!= * **@CODE@**
 +Sg+Nom:      #      ;
 +Sg+Gen:      #      ;
 +Sg+Acc:      #      ;
 +Sg+Ill+Attr: #      ;
 +Sg+Loc+Attr: #      ;
 +Cmp/SplitR:%-    ENDLEX ;
 +Cmp/SplitR:%–    ENDLEX ;

LEXICON ARABICCASE   !!= * **@CODE@**
!   +Sg+Nom+Err/Orth+Use/Circ:   # ;
!     f+Nom+Err/Orth+Use/Circ:f  # ; ! s. 123f.
!    ff+Nom+Err/Orth+Use/Circ:ff # ; ! s. 123ff.
!  b+Sg+Nom+Err/Orth+Use/Circ:b  # ; ! § 123b
!  c+Sg+Nom+Err/Orth+Use/Circ:c  # ; ! § 123c
!  d+Sg+Nom+Err/Orth+Use/Circ:d  # ; ! § 123d
!  e+Sg+Nom+Err/Orth+Use/Circ:e  # ; ! § 123e
!  š+Sg+Nom+Err/Orth+Use/Circ:š  # ;

!+Sg+Gen+Err/Orth:      # ;
!+Sg+Acc+Err/Orth:      # ;
 +Sg+Gen+Err/Orth:a     # ; ! But here 123a 123
 +Sg+Acc+Err/Orth:a     # ;
 +Sg+Gen+Err/Orth:ža    # ;
 +Sg+Acc+Err/Orth:ža    # ;
 +Sg+Ill+Use/NG:%>i     # ;
 +Sg+Ill+Err/Orth:ij    # ;
 +Sg+Ill+Err/Orth:žij   # ;
 +Sg+Ill+Err/Orth:áj    # ; ! Testing over the weekend. !
 +Sg+Loc:%>s            K ;
 +Sg+Loc+Err/Orth:is    K ;
 +Sg+Loc+Err/Orth:žis   K ;
 +Sg+Loc+Err/Orth:as    # ;
 +Sg+Com:%>in           # ;
 +Sg+Com+Err/Orth:žijn  # ;
 +Sg+Com+Err/Orth:ain   # ;
 +Sg+Ess:%>n            # ;
 +Sg+Ess+Err/Orth:žin   # ;
 +Sg+Ess+Err/Orth:an    # ;
 +Pl+Nom:%>t            K ;
 +Pl+Gen+Err/Orth:ijd9  K ;
 +Pl+Gen+Use/NG:%>id9   K ;
 +Pl+Acc+Err/Orth:ijd9  K ;
 +Pl+Acc+Use/NG:%>id9   K ;
 +Pl+Ill+Err/Orth:ijde  K ; ! 100:iide čuđiide
 +Pl+Ill+Err/Orth:ijdda K ; ! 1000:idda duháhiidda
 +Pl+Ill+Use/NG:%>ide   K ; ! 100:iide čuđiide
 +Pl+Ill+Err/Orth:idda  K ; ! 1000:idda duháhiidda
 +Pl+Loc:%>in           K ;
 +Pl+Com:%>igujn        K ;

 LEXICON ARABICCASEORD   !!= * **@CODE@** ordinals
 +Sg+Gen:%>da     K ;
 +Sg+Acc:%>da     K ;
 +Sg+Ill:%>dij    K ;
 +Sg+Loc:%>dis    K ;
 +Sg+Com:%>dijn   K ;
 +Sg+Ess:%>din    K ;
 +Pl+Nom:%>dat    K ;
 +Pl+Gen:%>dijd9  K ;
 +Pl+Acc:%>dijd9  K ;
 +Pl+Ill:%>dijdda K ;
 +Pl+Loc:%>dijn   K ;
 +Pl+Com:%>digujn K ;

 LEXICON ARABICCASECOLL   !!= * **@CODE@** collectives
 +Sg+Gen:%>sa   K ;
 +Sg+Acc:%>sa   K ;
 +Sg+Ill:%>sij  K ;
 +Sg+Loc:%>sis  K ;
 +Sg+Com:%>sijn K ;


! The Roman numerals !
! ------------------ !

LEXICON ROMAN   !!= * **@CODE@**  roman numerals
 +Use/Circ: ROM-THOUSAND ; !
 +Use/Circ: ROM-HUNDRED ; !
 +Use/Circ: ROM-TEN ; !
 +Use/Circ: ROM-ONE ; !

LEXICON ROM-THOUSAND   !!= * **@CODE@** 
 M+Use/Circ:M ROM-THOUSAND-TAG; !
 MM+Use/Circ:MM ROM-THOUSAND-TAG; !
 MMM+Use/Circ:MMM ROM-THOUSAND-TAG; !
 MMMM+Use/Circ:MMMM ROM-THOUSAND-TAG; !
 MMMMM+Use/Circ:MMMMM ROM-THOUSAND-TAG; !

LEXICON ROM-THOUSAND-TAG   !!= * **@CODE@** 
 +Use/Circ: ROMNUMTAG ; !
 +Use/Circ: ROM-HUNDRED ; !
 +Use/Circ: ROM-TEN ;				!
 +Use/Circ: ROM-ONE ;				!
 +Use/Circ: ROM-SPLIT ; !

LEXICON ROM-HUNDRED   !!= * **@CODE@** 
 C+Use/Circ:C ROM-HUNDRED-TAG; !
 CC+Use/Circ:CC ROM-HUNDRED-TAG; !
 CCC+Use/Circ:CCC ROM-HUNDRED-TAG; !
 CD+Use/Circ:CD ROM-HUNDRED-TAG; !
 D+Use/Circ:D ROM-HUNDRED-TAG; !
 DC+Use/Circ:DC ROM-HUNDRED-TAG; !
 DCC+Use/Circ:DCC ROM-HUNDRED-TAG; !
 DCCC+Use/Circ:DCCC ROM-HUNDRED-TAG; !
 CM+Use/Circ:CM ROM-HUNDRED-TAG; !

LEXICON ROM-HUNDRED-TAG   !!= * **@CODE@** 
 +Use/Circ: ROMNUMTAG ; !
 +Use/Circ: ROM-TEN ; !
 +Use/Circ: ROM-ONE ;				!
 +Use/Circ: ROM-SPLIT ; !

LEXICON ROM-TEN   !!= * **@CODE@** 
 X+Use/Circ:X ROM-TEN-TAG; !
 XX+Use/Circ:XX ROM-TEN-TAG; !
 XXX+Use/Circ:XXX ROM-TEN-TAG; !
 XL+Use/Circ:XL ROM-TEN-TAG; !
 L+Use/Circ:L ROM-TEN-TAG; !
 LX+Use/Circ:LX ROM-TEN-TAG; !
 LXX+Use/Circ:LXX ROM-TEN-TAG; !
 LXXX+Use/Circ:LXXX ROM-TEN-TAG; !
 XC+Use/Circ:XC ROM-TEN-TAG; !

LEXICON ROM-TEN-TAG   !!= * **@CODE@** 
 +Use/Circ: ROMNUMTAG ; !
 +Use/Circ: ROM-ONE ; !
 +Use/Circ: ROM-SPLIT ; !

LEXICON ROM-ONE   !!= * **@CODE@** 
 I+Use/Circ:I ROM-ONE-TAG; !
 II+Use/Circ:II ROM-ONE-TAG; !
 III+Use/Circ:III ROM-ONE-TAG; !
 IV+Use/Circ:IV ROM-ONE-TAG; !
 V+Use/Circ:V ROM-ONE-TAG; !
 VI+Use/Circ:VI ROM-ONE-TAG; !
 VII+Use/Circ:VII ROM-ONE-TAG; !
 VIII+Use/Circ:VIII ROM-ONE-TAG; !
 IX+Use/Circ:IX ROM-ONE-TAG; !

LEXICON ROM-ONE-TAG   !!= * **@CODE@** 
 +Use/Circ: ROMNUMTAG ; !
! +N: ROMNUMTAG ; ! !The Olav viđeš fix (Roman V now gets A)
 +Use/Circ: ROM-SPLIT ; !

! Here, we split the Roman numerals, in order to account for cases like "Kapihtal II-IV".
! We may send this first part directly to ROM-TAG below (and get the +Num tag), or we may
! send them through a second loop, identical to the first one, but marked wit "2" (the lexica
! are called 2ROMAN, etc. We do this instead of making a loop, since we do not want cases
! like "II-IV-VI-VII-IX". If that should turn out to be a good idea, a loop would do the
! trick.

LEXICON ROM-SPLIT   !!= * **@CODE@** 
 +Use/Circ:%- 2ROMAN ; ! II-VI, etc. !


! Here goes loop 2.

LEXICON 2ROMAN   !!= * **@CODE@** 
 +Use/Circ: 2ROM-THOUSAND ; !
 +Use/Circ: 2ROM-HUNDRED ; !
 +Use/Circ: 2ROM-TEN ; !
 +Use/Circ: 2ROM-ONE ; !

LEXICON 2ROM-THOUSAND   !!= * **@CODE@** 
 M+Use/Circ:M 2ROM-THOUSAND-TAG; !
 MM+Use/Circ:MM 2ROM-THOUSAND-TAG; !
 MMM+Use/Circ:MMM 2ROM-THOUSAND-TAG; !
 MMMM+Use/Circ:MMMM 2ROM-THOUSAND-TAG; !
 MMMMM+Use/Circ:MMMMM 2ROM-THOUSAND-TAG; !

LEXICON 2ROM-THOUSAND-TAG   !!= * **@CODE@** 
 +Use/Circ: ROMNUMTAG ; !
 +Use/Circ: 2ROM-HUNDRED ; !
 +Use/Circ: 2ROM-TEN ;				 !
 +Use/Circ: 2ROM-ONE ;				!

LEXICON 2ROM-HUNDRED   !!= * **@CODE@** 
 C+Use/Circ:C 2ROM-HUNDRED-TAG; !
 CC+Use/Circ:CC 2ROM-HUNDRED-TAG; !
 CCC+Use/Circ:CCC 2ROM-HUNDRED-TAG; !
 CD+Use/Circ:CD 2ROM-HUNDRED-TAG; !
 D+Use/Circ:D 2ROM-HUNDRED-TAG; !
 DC+Use/Circ:DC 2ROM-HUNDRED-TAG; !
 DCC+Use/Circ:DCC 2ROM-HUNDRED-TAG; !
 DCCC+Use/Circ:DCCC 2ROM-HUNDRED-TAG; !
 CM+Use/Circ:CM 2ROM-HUNDRED-TAG; !

LEXICON 2ROM-HUNDRED-TAG   !!= * **@CODE@** 
 +Use/Circ: ROMNUMTAG ; !
 +Use/Circ: 2ROM-TEN ; !
 +Use/Circ: 2ROM-ONE ;				!
LEXICON 2ROM-TEN   !!= * **@CODE@** 
 X+Use/Circ:X 2ROM-TEN-TAG; !
 XX+Use/Circ:XX 2ROM-TEN-TAG; !
 XXX+Use/Circ:XXX 2ROM-TEN-TAG; !
 XL+Use/Circ:XL 2ROM-TEN-TAG; !
 L+Use/Circ:L 2ROM-TEN-TAG; !
 LX+Use/Circ:LX 2ROM-TEN-TAG; !
 LXX+Use/Circ:LXX 2ROM-TEN-TAG; !
 LXXX+Use/Circ:LXXX 2ROM-TEN-TAG; !
 XC+Use/Circ:XC 2ROM-TEN-TAG; !

LEXICON 2ROM-TEN-TAG   !!= * **@CODE@** 
 +Use/Circ: ROMNUMTAG ; !
 +Use/Circ: 2ROM-ONE ; !

LEXICON 2ROM-ONE   !!= * **@CODE@** 
 I+Use/Circ:I 2ROM-ONE-TAG; !
 II+Use/Circ:II 2ROM-ONE-TAG; !
 III+Use/Circ:III 2ROM-ONE-TAG; !
 IV+Use/Circ:IV 2ROM-ONE-TAG; !
 V+Use/Circ:V 2ROM-ONE-TAG; !
 VI+Use/Circ:VI 2ROM-ONE-TAG; !
 VII+Use/Circ:VII 2ROM-ONE-TAG; !
 VIII+Use/Circ:VIII 2ROM-ONE-TAG; !
 IX+Use/Circ:IX 2ROM-ONE-TAG; !

LEXICON 2ROM-ONE-TAG   !!= * **@CODE@** 
 +Use/Circ: ROMNUMTAG ; !

LEXICON ROMNUMTAG   !!= * **@CODE@**
 +Num+Nom+Use/Circ:%> # ; !
 +Num+Gen+Use/Circ:%> # ; !
 +Num+Acc+Use/Circ:%> # ; !
 +A+Ord+Use/Circ:%.%> # ; ! !III. as ordinal.
 +A+Ord+Use/Circ:%> # ; ! !Looking for trouble...
 +Num+Use/Circ:%: ROMNUMTAGOBL ; !
 +Num+Use/Circ+Err/Orth:'  ROMNUMTAGOBL ; !
 +Num+Use/Circ+Err/Orth:%- ROMNUMTAGOBL ; !
 +Num+Use/Circ+Err/Orth:%´ ROMNUMTAGOBL ; !
! +Num: ROMNUMTAGOBL ; !

LEXICON ROMNUMTAGOBL   !!= * **@CODE@** 
 +Gen+Err/Orth+Use/Circ:%>a # ; !  But here 123a 123
 +Acc+Err/Orth+Use/Circ:%>a # ; ! 
 +Ill+Use/Circ:%>i # ; !
 +Ill+Err/Orth+Use/Circ:%>ij # ; !
 +Ill+Err/Orth+Use/Circ:%>áj # ; !
 +Loc+Use/Circ:%>s # ; !
 +Com+Use/Circ:%>in # ; !
 +Ess+Use/Circ:%>n # ; !


! A final section with some isolated numeral expresssions !
! ------------------------------------------------------- !

LEXICON ISOLATED-NUMEXP   !!= * **@CODE@** some isolated numeral expressions
 ½+Num:½ # ;
 ¹+Num:¹ # ;
 ²+Num:² # ;
 ³+Num:³ # ;
 ¼+Num:¼ # ;
 ¾+Num:¾ # ;
 okta+Num+Sg+Nom:okt # ; ! Okt mánná










