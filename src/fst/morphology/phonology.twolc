! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@uit.no or feedback@divvun.no

!! # Skolt Sámi TWOLC file

!! This file documents the [phonology.twolc file](http://github.com/giellalt/lang-sms/blob/main/src/fst/phonology.twolc) 

!! # Introduction
!! The twolc rule file for Skolt Saami is divided into 5 main sections:
!!
!! 1. Alphabets, Sets and Definitions
!! 1. Consonant shift rules (tbw)
!! 1. Vowel alternation rules
!! 1. Consonant gradation rules
!! 1. Rules for cleaning up and composing end result


!! # Alphabets, sets and definitions

Alphabet !!≈ ## @CODE@

!! Regular letters:

!! ```
 a b c d e f g h i j k l m n o p q r s t u v w x y z !!≈ * @CODE@
 A B C D E F G H I J K L M N O P Q R S T U V W X Y Z !!≈ * @CODE@
 ä å â õ                                             !!≈ * @CODE@
 Ä Å Â Õ                                             !!≈ * @CODE@
 č ǩ ǯ ǧ ž đ ǥ ʒ š ŋ                                 !!≈ * @CODE@
 Č Ǩ Ǯ Ǧ Ž Đ Ǥ Ʒ Š Ŋ                                 !!≈ * @CODE@
 ẹ                                                   !!≈ * @CODE@ Pedagogical purposes
 Ẹ                                                   !!≈ * @CODE@ Pedagogical purposes
                                                     !!≈ * @CODE@
 æ ø ö á é í                                         !!≈ * @CODE@
 Æ Ø Ö Á                                             !!≈ * @CODE@
 É Ó Ú Í À È Ò Ù Ì Ë Ü Ï Ê Ô Û Î Ã Ý þ Ñ Ð           !!≈ * @CODE@
 é ó ú í à è ò ù ì ë ü ï ê ô û î ã ý þ ñ ð ß ª ß ç   !!≈ * @CODE@
!! ```

!! Orthographic, suprasegmental markers:

 ʹ !!≈ * `@CODE@` - MODIFIER LETTER PRIME: U+02B9 suprasegmental palatization
 ʼ !!≈ * `@CODE@` - MODIFIER LETTER APOSTROPHE: U+02BC Syllable marker that appears in writing
 ˈ !!≈ * `@CODE@` - MODIFIER LETTER VERTICAL LINE: U+02C8 as overlong consonant marker that does not appear in writing except for pedagogical purposes

!! Symbol pairs for consonant lengthening:
%{XC%}:m  !!≈ * __@CODE@__
%{XC%}:n  !!≈ * __@CODE@__
%{XC%}:v  !!≈ * __@CODE@__
%{XC%}:d  !!≈ * __@CODE@__
%{XC%}:r  !!≈ * __@CODE@__
%{XC%}:l  !!≈ * __@CODE@__
%{XC%}:k  !!≈ * __@CODE@__
%{XC%}:ǥ  !!≈ * __@CODE@__
%{XC%}:s  !!≈ * __@CODE@__
%{XC%}:š  !!≈ * __@CODE@__
%{XC%}:z  !!≈ * __@CODE@__
%{XC%}:ž  !!≈ * __@CODE@__

!! Symbol pairs for vowel length:
%{õuØ%}:0   !!≈ * __@CODE@__
%{ouØ%}:0   !!≈ * __@CODE@__
%{âõØ%}:0   !!≈ * __@CODE@__
%{ẹiØ%}:0   !!≈ * __@CODE@__
%{äaØ%}:0   !!≈ * __@CODE@__
%{äåØ%}:0   !!≈ * __@CODE@__
%{eiØ%}:0   !!≈ * __@CODE@__
%{åoØ%}:0   !!≈ * __@CODE@__
%{ẹeiØ%}:0  !!≈ * __@CODE@__

!! Symbol pairs for vowel height, by default vowels are low:
%{õu%}:õ     !!≈ * __@CODE@__
%{ou%}:o     !!≈ * __@CODE@__
%{âõ%}:â     !!≈ * __@CODE@__
%{ẹi%}:ẹ     !!≈ * __@CODE@__
%{äa%}:ä     !!≈ * __@CODE@__
%{äå%}:ä     !!≈ * __@CODE@__
%{ei%}:e     !!≈ * __@CODE@__
%{åo%}:å     !!≈ * __@CODE@__
%{ẹei%}:ẹ    !!≈ * __@CODE@__
%{âõe%}:â    !!≈ * __@CODE@__
%{âõäe%}:â   !!≈ * __@CODE@__

!! Trigger symbols:
 %^Pen:0      !!≈ * __@CODE@__ - for combinations 2015-09-28 Miikka
 %^RmVow:0    !!≈ * __@CODE@__ - for removing vowels e, â, a before final consonant in stem
 %^UltRmVow:0 !!≈ * __@CODE@__ - for removing vowels e, â, a after final consonant in stem
!%^PenRmVow:0 ! ≈ * __@CODE@__ - for removing penultimate vowels e, â, a, i.e. vowel befor final stem consonant/consonants
 %^RmCns:0    !!≈ * __@CODE@__ - for removing stem final consonant, e.g. final z in ǩeeʹstes:ǩeâsttez ǩeâstta
 %^1VOW:0     !!≈ * __@CODE@__ - Vow trigger
 %^2VOW:0     !!≈ * __@CODE@__ - Vow trigger
 %^3VOW:0     !!≈ * __@CODE@__ - Vow trigger
 %^4VOW:0     !!≈ * __@CODE@__ - Vow trigger
 %^5VOW:0     !!≈ * __@CODE@__ - Vow trigger
 %^VOWLower:0 !!≈ * __@CODE@__ - i>e, õ>â, â>ä, u>o, o>å
 %^VOWRaise:0 !!≈ * __@CODE@__ - e>i, ä>e
 %^U2Õ:0      !!≈ * __@CODE@__ - lowers u>õ
 %^PAL:0      !!≈ * __@CODE@__ - Palatalisation
 %^PALNo:0    !!≈ * __@CODE@__ - Palatalisation
 %^PALI:0     !!≈ * __@CODE@__ - Palatalisation
 %^PALE:0     !!≈ * __@CODE@__ - Palatalisation
 %^PALÄ:0     !!≈ * __@CODE@__ - Palatalisation
 %^PALẸ:0     !!≈ * __@CODE@__ - Palatalisation
 %^PALÂ:0     !!≈ * __@CODE@__ - Palatalisation
 %^PALÕ:0     !!≈ * __@CODE@__ - Palatalisation

!! Penultimate Palatalisation
%^PALK:0      !!≈ * __@CODE@__ - causes k:ǩ and g:ǧ and ǥ:j

!! Miscellaneous triggers:
 %^VC:0       !!≈ * @CODE@ Voicing s:z š:ž
 %^Â:0        !!≈ * @CODE@ Indicates placement of â in stem

!! CHARACTERISTIC BREAKDOWN 2015-02-17

%^Allegro:0  !!≈ * __@CODE@__ - Allegro in general shortens consonant and preceding vowel
%^VV2V:0         !!≈ * __@CODE@__ - Shortens vowel
%^V2VV:0         !!≈ * __@CODE@__ - Lengthens vowel
%^CC2C:0         !!≈ * __@CODE@__ - Shortens consonant
%^CC2CAllegro:0  !!≈ * __@CODE@__ - Shortens consonant and preceding vowel
%^C2CC:0         !!≈ * __@CODE@__ - Lengthens consonant
%^CC2CCC:0       !!≈ * __@CODE@__ - Strong to Extra Strong consonant
%^CCC2C:0        !!≈ * __@CODE@__ - Extra Strong to Weak consonant
%^CCC2CAllegro:0 !!≈ * __@CODE@__ - Extra Strong to Weak consonant
%^CCC2CC:0       !!≈ * __@CODE@__ - Extra Strong to Strong consonant
%^XYY2XY:0       !!≈ * __@CODE@__ - Shortens consonant cluster
%^XYY2VY:0       !!≈ * __@CODE@__ - Consonant cluster to vowel and consonant sijdd:siid
%^XYY2VYY:0      !!≈ * __@CODE@__ - Consonant cluster to vowel and geminate uvdd:ouʹdd
%^XYY2IY:0       !!≈ * __@CODE@__ - Consonant cluster to i  and consonant kueʹhtt:kueiʹt
%^VY2XYY:0       !!≈ * __@CODE@__ - Consonant cluster i/u to j/v
%^VYY2XYY:0      !!≈ * __@CODE@__ - Consonant cluster i/u to j/v
%^KK2ZERO:0      !!≈ * __@CODE@__ - nââʹer
%^KKK2ZERO:0     !!≈ * __@CODE@__ - jeeʹel
%^KKK2GG:0       !!≈ * __@CODE@__ - Geminate weakening
%^KKK2G:0        !!≈ * __@CODE@__ - Strong Geminate allegro
%^KK2GG:0        !!≈ * __@CODE@__ - Geminate weakening jokk:jooǥǥ
%^KK2GAllegro:0  !!≈ * __@CODE@__ - Geminate weakening in allegro pååss:påz
%^K2GG:0         !!≈ * __@CODE@__ - Geminate weakening čårrõs:čårrõõzz
%^K2GAllegro:0   !!≈ * __@CODE@__ - Geminate weakening in allegro čâustõk

!! Removal of suprasegmentals

%{ʹØ%}:0         !!≈ * __@CODE@__ - This is modifier letter prime
%{ʼØ%}:0         !!≈ * __@CODE@__ - used between l, n, j and subsequent j and elsewhere
%{ˈ1%}:0         !!≈ * __@CODE@__ - This is modifier letter verticle line after diphthongs

!! This introduces vertical bar after diphth before consonants
 %^Allegro:0     !!≈ * @CODE@ This introduces vertical bar after diphth before consonants

!! Various semi-vowel alternations
%^J2I:0          !!≈ * @CODE@ in tuâjj:tuâi
%^I2J:0          !!≈ * @CODE@ in agent names
%^I2Zero:0       !!≈ * @CODE@ in agent names

!! Gradation triggers 2015.01.23
 %^XSt2St:0      !!≈ * @CODE@ Causes weakening from extra strong to strong
 %^XSt2W:0       !!≈ * @CODE@ Causes weakening from extra strong to weak
 %^St2W:0        !!≈ * @CODE@ Causes weakening from strong to weak
 %^W2St:0        !!≈ * @CODE@ Causes strengthening from weak to strong
 %^W2XSt:0       !!≈ * @CODE@ Causes strengthening from weak to extra strong
 %^St2XSt:0      !!≈ * @CODE@ Causes strengthening from strong to extra strong

!! Other vowel length and consonant length will be phased out
 %^VOWXSH:0      !!≈ * @CODE@ Extra shortens vowel in relation to consonant
 %^VOWXL:0       !!≈ * @CODE@ Extra lengthens vowel in relation to consonant in nouns, for example
 %^CXLong:0      !!≈ * @CODE@ Lengthening
 %^CLong:0       !!≈ * @CODE@ Lengthening
 %^CShort:0      !!≈ * @CODE@ Shortening

!! More triggers, possibly realised as a segment:

! Some of the following doccommewnts must be escaped using {% raw %} @CODE@  {% endraw %}, because
! {%..%} is a reserved construct in Jekyll. Jekyll is the publishing tool used by GitHub
! Pages to convert Markdown to HTML.
!! <!-- {% raw %} -->
 X3:0          !!≈ * `@CODE@` causes weak grade
 %{VU%}:u      !!≈ * `@CODE@` used with stuuʹl
 %{Â1%}:0      !!≈ * `@CODE@` used with "mainsted" to tell (a story)
 %{A1%}:0      !!≈ * `@CODE@` used with "vuejted" to drive (causative)
 %{E1%}:0      !!≈ * `@CODE@` used with "kåʹmrded" to bow
 %{%ʼ%}:0      !!  * `@CODE@` archiform for MODIFIER LETTER ACUTE ACCENT used in stems where the lemma does not contain a MODIFIER LETTER ACUTE ACCENT and yet the paradigm requires one.
 %{EÂ%}:e      !!≈ * `@CODE@` archiform for ie:eâ variation
 %^IMPSG2:0    !!≈ * `@CODE@`
 %^INDPRSSG3:0 !!≈ * `@CODE@`
 %^PenVow2a:0  !!≈ * `@CODE@` Changes penultimate vowel to a; e:a â:a
 %^FRICG:0     !!≈ * `@CODE@`

!! Hyphen at compound word boundary
%{%-Ø%}:0    !!  * `@CODE@` Zero versus hyphen in compounding
%^Hyphen:0   !!≈ * `@CODE@` at compound word boundary with %-
%^NoHyphen:0 !!≈ * `@CODE@` at compound word boundary
!! <!-- {% endraw %} -->


!! Literal quotes and angles must be escaped (cf morpheme boundaries below):

 »7     !! * »
 «7     !! * «
 %[%>%] !! * >
 %[%<%] !! * <

!! Morpheme boundaries:
 «  !! * Derivational prefix
 »  !! * Derivational suffix
 %< !! * Inflectional prefix
 %> !! * Inflectional suffix
 #  !! * Word boundary for both lexicalised and dynamic compounds
 %^ !! * (exceptional) soft hyphenation point
 %- !! * Hyphen for question particle
 ∑  !! * Compounding rules

!! End of alphabet definitions
 ;


!! ## Sets
Sets

! Letter =
 Cns    = b c d f g h j k l m n p q r s t v w x z đ ʒ ǯ č ŋ ǩ ǧ ǥ š ž   !!≈ * @CODE@
          B C D F G H J K L M N P Q R S T V W X Z Đ Ʒ Ǯ Č Ŋ Ǩ Ǧ Ǥ Š Ž ; !!≈   @CODE@
 CNoV   = b c d f g h j k l m n p q r s t w x z đ ʒ ǯ č ŋ ǩ ǧ ǥ š ž     !!≈ * @CODE@
          B C D F G H J K L M N P Q R S T W X Z Đ Ʒ Ǯ Č Ŋ Ǩ Ǧ Ǥ Š Ž ;   !!≈   @CODE@

 CNoJ   = b c d f g h k l m n p q r s t v w x z đ ʒ ǯ č ŋ ǩ ǧ ǥ š ž     !!≈ * @CODE@
          B C D F G H K L M N P Q R S T V W X Z Đ Ʒ Ǯ Č Ŋ Ǩ Ǧ Ǥ Š Ž ;   !!≈   @CODE@

 Vow    = a e ẹ i o u å ä â õ         !!≈ * @CODE@
          A E Ẹ I O U Å Ä Â Õ ;       !!≈   @CODE@
 VowNoIU  = a e ẹ o å ä â õ           !!≈ * @CODE@
            A E Ẹ O Å Ä Â Õ ;         !!≈   @CODE@

 CNoJV  = b c d f g h k l m n p q r s t w x z đ ʒ ǯ č ŋ ǩ ǧ ǥ š ž    !!≈ * @CODE@
          B C D F G H K L M N P Q R S T W X Z Đ Ʒ Ǯ Č Ŋ Ǩ Ǧ Ǥ Š Ž ;  !!≈   @CODE@

 CNoDLRZ = b c f g h j k m n p q s t v w x z đ ǯ č ŋ ǩ ǧ ǥ š ž       !!≈ * @CODE@
           B C F G H J K M N P Q S T V W X Z Đ Ǯ Č Ŋ Ǩ Ǧ Ǥ Š Ž ;     !!≈   @CODE@

Ltrs = Vow Cns ;    !!≈ * @CODE@

Dummy = %^PAL %^PALE %^PALẸ %^PALÕ %^PALÄ %^PALK       !!≈ * @CODE@
 %^CC2C %^CCC2C %^CCC2CC %^XYY2IY %^XYY2XY      !!≈   @CODE@
 %^KK2GG %^XYY2VY %^KKK2GG %^KKK2ZERO %^C2CC    !!≈   @CODE@
 %^J2I %^Pen %^V2VV %^VOWLower %^RmVow          !!≈   @CODE@
 %^PenVow2a %^RmCns %^VC %^V2VV                 !!≈   @CODE@
 %^VOWRaise %^VOWSH %^CLong %^K2GG %> ;            !!≈   @CODE@ - triggers in Sg.Nom and Sg.Gen (Note word-final right arrow bracket 2024-01-10)



!! ## Definitions

Definitions

!! ### Short consonant cluster


!! Onset consonant or word boundary
OnSetC = [[%{XC%}:Cns|Cns:Cns] (Cns:|%{XC%}:Cns) |.#.|(∑) #:|%>|»] ;     
!! `OnSetC = [[%{XC%}:Cns\|Cns:Cns] (Cns:\|%{XC%}:Cns) \|.#.\|#:\|%>\](») ;`

!! Penultimate consonant
PenUltCns = [Cns:|%{XC%}:] ;     
!! `PenUltCns = [Cns:\](%{XC%}:) ;`


!! following morpheme or word boundary

!! <!-- {% raw %} -->
!! ```
RBound = [(%^Hyphen: %-|%^NoHyphen:|%{%-Ø%}:) (∑) #:|.#.|%>|»|%-] ; !!≈ * @CODE@
!! ```
!! <!-- {% endraw %} -->

!! ossible triggers before VOWLower and VOWRaise



!! neutral to vowel length
NeutrVowLength = [( %^VV2V: |%^V2VV:)] ;

!! neutral to vowel height and backness
NeutrVowHeight = [(%^VOWLower:|%^VOWRaise:)] ;

NeutrDiphPALTint = [(%^PALÄ:|%^PALE:|%^PALẸ:|%^PALÕ:|%^PALÂ:|%^PALI:)] ;


!! possible triggers before PALE PALÄ
NeutrVowLenghtHeight = [ NeutrVowLength NeutrVowHeight] ;

NeutrVowLenghtHeightDiphTint = [NeutrVowLength NeutrVowHeight NeutrDiphPALTint ] ;

!! possible triggers between stem and PALNo and PAL
BetweenStemAndPALNo = [ NeutrVowLength NeutrVowHeight NeutrDiphPALTint (%^Allegro:) ] ;     


!! possible triggers between vowel length and consonant grade

NeutrVowHeightDiphPalAllegroPAL = [ NeutrVowHeight NeutrDiphPALTint (%^Allegro:) (%^PALNo:| %^PAL:) ] ;     

!! `NeutrVowHeightDiphPalAllegroPAL = [ (%^VOWRaise:\|%^VOWLower:) ( ((%^PALÂ:\|%^PALÕ:) (%^Allegro:) %^PALNo:\|%^VOWLower: %^PALÄ:\|(%^PALÕ:\|%^PALE:\|%^PALÄ:\|%^PALẸ:) (%^Allegro:) %^PAL:)\| (%^Allegro:) (%^PALÕ:\|%^PALE:\|%^PALÄ:\|%^PALẸ:\|%^PALÂ:) (%^PALNo:\](%^PAL:) ) ) ;`

! [ (NeutrVowHeight ((%^PALÂ:|%^PALÕ:) (%^Allegro:) %^PALNo:|%^VOWLower: %^PALÄ:|(%^PALÕ:|%^PALE:|%^PALÄ:|%^PALẸ:) (%^Allegro:) %^PAL:)| (%^Allegro:) (%^PALÕ:|%^PALE:|%^PALÄ:|%^PALẸ:|%^PALÂ:) (%^PALNo:|%^PAL:) ) ] ;

!! possible triggers between word end and consonant grade
NeutrVowLenghtHeightDiphPalAllegroPAL = [ NeutrVowLength NeutrVowHeightDiphPalAllegroPAL ];     


!! possible triggers between vowel length and Palatalization
BetweenVowLengthAndPALNo = [NeutrVowHeight NeutrDiphPALTint ] ;
!! `BetweenVowLengthAndPALNo = [(%^VOWLower:\|%^VOWRaise:) (%^PALÄ:\|%^PALE:\|%^PALẸ:\|%^PALÕ:\](%^PALÂ:) ) ;`

BetweenVowHeightAndConsGrade = [(NeutrDiphPALTint (%^Allegro:) %^PAL:|(%^PALÂ:|%^PALÕ:) (%^Allegro:) %^PALNo:)] ;
!! `BetweenVowHeightAndConsGrade = [((%^PALE:\|%^PALÄ:\|%^PALẸ:\|%^PALÕ:) (%^Allegro:) %^PAL:\|(%^PALÂ:\](%^PALÕ:) (%^Allegro:) %^PALNo:)) ;`

BetweenVowHeightAndMorph = [  (NeutrDiphPALTint (%^Allegro:) %^PAL:|(%^PALÂ:|%^PALÕ:) (%^Allegro:) %^PALNo:) ([(%^Allegro:) %^CC2C:|(%^Allegro:) %^CC2CAllegro:]|%^C2CC:|%^XYY2XY:|%^K2GG:|%^KK2GG:|%^CC2CCC:|%^CCC2C:|%^CCC2CC:|%^XYY2VY:|%^XYY2VYY:|%^KKK2GG:) RBound ] ;


BetweenPALNoAndMorphRightArrow = [([(%^Allegro:) %^CC2C:|(%^Allegro:) %^CC2CAllegro:]|%^C2CC:|%^XYY2XY:|%^K2GG:|%^KK2GG:|%^CC2CCC:|%^CCC2C:|%^CCC2CC:|%^XYY2VY:|%^XYY2VYY:|%^KKK2GG:) ] ;


BetweenPALNoAndMorph = [([(%^Allegro:) %^CC2C:|(%^Allegro:) %^CC2CAllegro:]|%^C2CC:|%^XYY2XY:|%^K2GG:|%^KK2GG:|%^CC2CCC:|%^CCC2C:|%^CCC2CC:|%^XYY2VY:|%^XYY2VYY:|%^KKK2GG:) RBound ] ;

BetweenStemAndRightArrow = [NeutrVowLenghtHeightDiphPalAllegroPAL BetweenPALNoAndMorphRightArrow] ;
!! `BetweenStemAndRightArrow = [NeutrVowLenghtHeightDiphPalAllegroPAL BetweenPALNoAndMorphRightArrow] ;`

!! ### Penultimate vowel centers possible triggers before VOWLower and VOWRaise

PenNeutrVowLength = [(%^Pen: %^VV2V: |%^Pen: %^V2VV:)] ;
PenNeutrVowHeight = [(%^Pen: %^VOWRaise:|%^Pen: %^VOWLower:)] ;
PenNeutrDiphPALTint = [(%^Pen: %^PALÄ:|%^Pen: %^PALE:|%^Pen: %^PALẸ:|%^Pen: %^PALÕ:|%^Pen: %^PALÂ:|%^Pen: %^PALI:)] ;
PenAllegro = [(%^Pen: %^Allegro:|%^Pen: %^CC2CAllegro:|%^Pen: %^CCC2CAllegro:|%^Pen: %^KK2GAllegro:|%^Pen: %^K2GAllegro:)];

PenBetweenStemAndPALNo = [PenNeutrVowLength PenNeutrVowHeight (%^Pen: %^Allegro:) ] ;

PenNeutrVowLenghtHeight  = [ PenNeutrVowLength PenNeutrVowHeight] ;

PenNeutrVowLenghtHeightDiphTint = [PenNeutrVowLength PenNeutrVowHeight PenNeutrDiphPALTint ] ;

PenNeutrVowLenghtHeightDiphPalAllegroPAL = [PenNeutrVowLength PenNeutrVowHeight PenNeutrDiphPALTint (%^Pen: %^Allegro:) (%^Pen: %^PALNo:| %^Pen: %^PAL:) ] ;


PenBetweenStemAndVowelLoss = [PenNeutrVowLength [(PenNeutrVowHeight|%^Pen: %^U2Õ:) (PenNeutrDiphPALTint (%^Pen: %^Allegro:) (%^Pen: %^PALNo:| %^Pen: %^PAL:))] ([%^Pen: %^C2CC:]|%^Pen: [(%^Allegro:) %^CC2C:|%^Allegro:|(%^Allegro:) %^CC2CAllegro:]|%^Pen: %^XYY2XY:|%^Pen: %^CC2CCC:|%^Pen: %^CCC2CC:|%^Pen: %^CCC2C:|%^Pen: %^KKK2GG:)] ;


PenBetweenStemAndStemFinalVoicing = [PenBetweenStemAndVowelLoss (%^RmVow:|%^PenVow2a:)] ;
!! `PenBetweenStemAndStemFinalVoicing = [PenBetweenStemAndVowelLoss (%^RmVow:\](%^PenVow2a:)) ;`

PenBetweenPALNoAndMorph = [(%^Pen: [(%^Allegro:) %^CC2C:|(%^Allegro:) %^CC2CAllegro:]|%^Pen: %^C2CC:|%^Pen: %^XYY2XY:|%^Pen: %^KK2GG:|%^Pen: %^CC2CCC:|%^Pen: %^CCC2C:|%^Pen: %^CCC2CC:|%^Pen: %^XYY2VY:|%^Pen: %^XYY2VYY:|%^Pen: %^KKK2GG:) RBound ] ;
!! `PenBetweenPALNoAndMorph = [(%^Pen: [(%^Allegro:) %^CC2C:\|(%^Allegro:) %^CC2CAllegro:]\|%^Pen: %^C2CC:\|%^Pen: %^XYY2XY:\|%^Pen: %^KK2GG:\|%^Pen: %^CC2CCC:\|%^Pen: %^CCC2C:\|%^Pen: %^CCC2CC:\|%^Pen: %^XYY2VY:\|%^Pen: %^XYY2VYY:\](%^Pen: %^KKK2GG:) RBound ) ;`

!! used in compounding Cmp/SgNom and Cmp/SgGen
SgNomGen = [((%^PALE: %^PAL:) %^CCC2C:|(%^PALE: %^PAL:) %^CCC2CC:|%^PALẸ:|[%^PALE:|%^PALÕ:] %^PAL: %^XYY2IY:|[%^PALẸ:|%^PALE:] %^PAL: %^XYY2XY:|((%^PALE:) %^PAL:) %^KK2GG:|(%^PALE:) %^PAL:| ((%^PALE:) %^PAL:) (%^J2I:) %^CC2C:|%^PAL: %^XYY2VY:)];
!! `SgNomGen = [((%^PALE: %^PAL:) %^CCC2C:\|(%^PALE: %^PAL:) %^CCC2CC:\|%^PALẸ:\|[%^PALE:\|%^PALÕ:] %^PAL: %^XYY2IY:\|[%^PALẸ:\|%^PALE:] %^PAL: %^XYY2XY:\|((%^PALE:) %^PAL:) %^KK2GG:\|(%^PALE:) %^PAL:\| ((%^PALE:) %^PAL:) (%^J2I:) %^CC2C:\](%^PAL: %^XYY2VY:));`


!! neutral to consonant length

CNeutrGrade = [([(%^Allegro:) %^CC2C:|(%^Allegro:) %^CC2CAllegro:] |[%^C2CC:] |%^CC2CCC: |%^KK2GG: |%^KKK2GG: |%^XYY2VY: |%^XYY2VYY:|%^CCC2CC: )] ;
!! `CNeutrGrade = [([(%^Allegro:) %^CC2C:\|(%^Allegro:) %^CC2CAllegro:] \|[%^C2CC:] \|%^CC2CCC: \|%^KK2GG: \|%^KKK2GG: \|%^XYY2VY: \|%^XYY2VYY:\](%^CCC2CC: )) ;`

!! neutral to vowel and consonant length

NeutrGrade = [VNeutrGrade |CNeutrGrade] ; 
!! `NeutrGrade = [VNeutrGrade \](CNeutrGrade) ; `

NoVowRaise = \[ %^VOWRaise: | #]* [(∑) #|.#.] ;
!! `NoVowRaise = \[ %^VOWRaise: \| #]* [#\](.#.) ;`

NoCnsDummy = \[ %^CC2C: | %^CCC2C: | %^CCC2CC: | %^XYY2IY: | %^XYY2XY: | %^KK2GG: | %^XYY2VY: | %^KKK2GG: | %^KKK2ZERO: | %^C2CC: | %^J2I: | %^RmCns: | %^K2GG: | # ]* ;
!! `NoCnsDummy = \[ %^CC2C: \| %^CCC2C: \| %^CCC2CC: \| %^XYY2IY: \| %^XYY2XY: \| %^KK2GG: \| %^XYY2VY: \| %^KKK2GG: \| %^KKK2ZERO: \| %^C2CC: \| %^J2I: \| %^RmCns: \| %^K2GG: \]( # )* ;`

SurfaceDiphthong = [ :e :ä | :e :â | :i :õ | :i :â | :i :e | :i :ẹ | :u :â | :u :õ | :u :å | :u :ä | :u :e | :u :ẹ ] ;
!! `SurfaceDiphthong = [ :e :ä \| :e :â \| :i :õ \| :i :â \| :i :e \| :i :ẹ \| :u :â \| :u :õ \| :u :å \| :u :ä \| :u :e \]( :u :ẹ ) ;`

!! * X3 = C Vx Cx Cx     C Vx Vy Cx ˈ Cx
!! * X2 = C Vx Vx Cx Cx  C Vx Vy Cx Cx
!! * X1 = C Vx Vx Cx     C Vx Vy Cx


!! # Rules
Rules

!! ## Vowel shortening rules



"Vowel shortening â:0"
!! **@RULENAME@** - used in
 â:0 <=>
      [â:â|â:õ] _ (i|u) (ʹ:|%{ʹØ%}:) Cns:+ (PenUltCns) (Vow: (0:) Cns:+ (PenUltCns) %^Pen:)  %^VV2V:  ;
    [OnSetC|PenUltCns|»]   _ (:ʼ|%{ʼØ%}:) [Cns:+|i:|n j|l m|s k:|s ǩ:|s t|š k:|š ǩ:|š m] PenBetweenStemAndVowelLoss %^RmVow: ;

!Not needed here
!! čââʹđ+N+Sg+Ill: **heart/sydän**
!!€ čâ^1VOWđđ>ma
!!€ čâ0đđ>ma
    â: _ [c c| č č| k k|s s|š š] %^VV2V: ;

 _ %^UltRmVow: ;
!! -â
  e: _ (:ʹ|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+|Cns:+ :ˈ Cns:+]  %^VV2V: ;
!! ǩeʹtted+V+Ind+Prs+ConNeg: **cook/keittää**
!!€ ǩeâ{ʹØ}tt^VV2V^VOWRaise^PALI^PAL
!!€ ǩi0ʹtt0000


"Vowel shortening ẹ:0"
!! **@RULENAME@** - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
 ẹ:0 <=>
   OnSetC ẹ: _ (u|i) (ʹ:|%{ʹØ%}:) Cns:+ (PenUltCns) (Vow: (0:) Cns:+ (PenUltCns) %^Pen:) %^VV2V:  ;
!! teevvad+V+Prt+4:
!!€ tẹẹvv^VV2V^VOWRaise>uš
!!€ ti0vv00>uš
   ẹ: _ [c c| č č| k k|s s|š š] %^VV2V: ;


"Vowel shortening e:0"
!! **@RULENAME@** - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
 e:0 <=>
    e:  _ (i|u) (ʹ:|%{ʹØ%}:) Cns:+  (PenUltCns) (Vow: (0:) Cns:+ (PenUltCns) %^Pen:) %^VV2V:  ;

!! eʹǩrded+V+Inf
!!€ eeʹǩǩerd^Pen^VV2V^Pen^CC2C^RmVow>ed
!!€ e0ʹǩ00rd00000>ed

   [OnSetC|PenUltCns]  _ (0:ʼ|%{ʼØ%}:) (ʹ:|%{ʹØ%}:) [Cns:+|i:|n j|l m|s ǩ:|s m|s t|š ǩ:|š m] PenBetweenStemAndVowelLoss %^RmVow: ;
!! Ââvel+N+Prop+Sg+Loc  **Ivalo**
!!€ Ââvel^RmVow>est
!!€ Ââv0l0>est
  e: _ [c c| č č| k k|s s|š š] %^VV2V: ;

 _ %^UltRmVow: ;


!! Jouste

"Vowel shortening å:0"
!! **@RULENAME@** - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
 å:0 <=>
  å: _ (i|u) (ʹ:|%{ʹØ%}:) Cns:+ (PenUltCns) (Vow: (0:) Cns:+ %^Pen:) %^VV2V:  ;
!  å: _ (i|u) (ʹ:|%{ʹØ%}:) Cns:+ (PenUltCns) (Vow: Cns:+ %^Pen:) %^VV2V:  ;
!! jååʹtted+V+Ind+Prt+Pl3 **trekk**
!!€ jåå{ʹØ}tt^VV2V^VOWRaise^PAL>e
!!€ jo0ʹtt000>e
!! låʹǧsted
!!€ lååʹǧǧest^Pen^VV2V^Pen^CC2CAllegro^RmVow
!!€ lå0ʹǧ00st00000

!Not needed here
!! sååbbar+N+Sg+Nom **meeting**
!!€ så^1VOWbbar^Pen^V2VV
!!€ sååbbar00
  å: _ [c c| č č| k k|s s|š š] %^VV2V: ;

 _ %^UltRmVow: ;
!! -å

"Vowel shortening õ:0"
!! **@RULENAME@** - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
 õ:0 <=>
  õ: _ (i|u) (ʹ:|%{ʹØ%}:) PenUltCns+ (Vow: (0:) Cns:+ (PenUltCns) %^Pen:) %^VV2V:  ;
!!€ mõõ{ʹØ}nn^VV2V^PAL>e
!!€ mõ0ʹnn00>e
!!€ mõõ{ʹØ}nn^VV2V^VOWLower^PAL>e
!!€ mâ0ʹnn000>e
!!€ čõõnâst^Pen^VV2V^RmVow>ed
!!€ čõ0n0st000>ed
  õ: _ (i|u) (ʹ:|%{ʹØ%}:) Cns:+ Vow: Cns:+ %^Pen: %^VV2V:  ;
!! tõiŋsǩed+V+Inf
!!€ tõõiŋâsk^Pen^VV2V^RmVow^PALK>ed
!!€ tõ0iŋ0sǩ0000>ed
!!$ tõõiŋâsk^Pen^VV2V^RmVow^PALK>ed
!!$ tõ0iŋ0sk0000>ed

! Is this needed 2016-05-03
      Cns:+ (%{XC%}:)  _ Cns:+ PenBetweenStemAndVowelLoss %^RmVow: ;

!No needed here
!! kõõnjâl+N+Sg+Gen **tear**
!!€ kõ^1VOWnnjâl^Pen^VV2V^Pen^C2CC
!!€ kõ0nnjâl0000
  õ: _ [c c| č č| k k|s s|š š] %^VV2V: ;

 _ %^UltRmVow: ;
!! -õ

"Vowel shortening u:0"
!! **@RULENAME@** - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
 u:0 <=>
  u: _ (ʹ:|%{ʹØ%}:) Cns:+ (PenUltCns) (Vow: (0:) Cns:+ (PenUltCns) %^Pen:) %^VV2V: ;

      Cns:+ (%{XC%}:)  _ Cns:+ PenBetweenStemAndVowelLoss %^RmVow: ;
!! mainstummuš+N+Sg+Ill: **story telling/tarinointi**
!!€ mainstummuš^RmVow>a
!!€ mainstumm0š0>a

!Not needed here
!! juurd+N+Ess **thought**
!!€ ju^1VOWrdd>j>en
!!€ ju0rdd>j>en
   u: _ [c c| č č| k k|s s|š š] %^VV2V: ;

    _ %^UltRmVow: ;
!! Oulu

"Vowel shortening i:0"
!! **@RULENAME@** - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
 i:0 <=>
    i: _ (u) (ʹ:|%{ʹØ%}:) Cns:+ %^VV2V: ;
    i: _ (u) (ʹ:|%{ʹØ%}:) Cns:+ (PenUltCns) Vow: (0:) Cns:+ (PenUltCns) %^Pen: %^VV2V: ;
!! viikkâd+V+Ind+Prs+Pl3
!!€ vii{ʹØ}kk^VV2V^VOWLower^PAL>e
!!€ ve0ʹǩǩ000>e
!! viikkâd+V+Imprt+Pl3
!!€ vii{ʹØ}kk^VV2V^VOWLower>az
!!€ vẹ00kk00>az
!!$ vii{ʹØ}kk^VV2V^VOWLower>az
!!$ vẹẹ0kk00>az
!! iilâsǩ:ilska

        PenUltCns  _ [Cns:+|n j|l m|s ǩ:|s m|s t|š ǩ:|š m] PenBetweenStemAndVowelLoss %^RmVow: ;
!! prääʹzniǩ+N+Sg+Ill: **celebration/juhla**
!!€ prääʹzniǩ^RmVow^PALNo>a
!!€ prääʹzn0k00>a

      [Cns:|Vow:] _ (%^RmVow:) %^I2Zero: ;
!!€ žẹẹvai^RmVow^I2Zero>ǥa
!!€ žẹẹv0000>ǥa

          _ %^UltRmVow: ;
!! +Sg+Ill N_HÕʹPPI
!!€ hõʹppi^UltRmVow>ǥa
!!€ hõʹpp00>ǥa
   i: _ [c c| č č| k k|s s|š š] %^VV2V: ;

"Vowel shortening o:0"
!! **@RULENAME@** - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
 o:0 <=>
    o: _ (i|u) (ʹ:|%{ʹØ%}:) Cns:+ (PenUltCns) (Vow: (0:) Cns:+ (PenUltCns) %^Pen:) %^VV2V: ;
!! šoomm+N+Sg+Ill
!!€ šoomm^VV2V^VOWRaise>u
!!€ šu0mm00>u
!! ponjsted+V+Inf
!!€ poonjâst^Pen^VV2V^RmVow>ed
!!€ po0nj0st000>ed
!! poorrâd+V+Imprt+Pl3
!!€ poorr^VV2V^VOWLower>az
!!€ på0rr00>az
    o: _ [c c| č č| k k|s s|š š] %^VV2V: ;

!! ooccâd+V+Imprt+Pl3
!!€ oocc^VV2V^VOWLower>az
!!€ å0cc00>az

    PenUltCns _ Cns:+ PenBetweenStemAndVowelLoss %^RmVow: ;

!Not needed here
!! ooumaž+N+Sg+Nom
!!€ o^1VOWumm^V2VV^XYY2XY>až
!!€ ooum000>až
 _ %^UltRmVow: ;
!! -o

"Vowel shortening a:0 "
!! **@RULENAME@** - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
 a:0 <=>
  a: _ (i|u) (ʹ:|%{ʹØ%}:) Cns:+ (PenUltCns) (Vow: (0:) Cns:+ (PenUltCns) %^Pen:) %^VV2V: ;
!!€ saa{ʹØ}kk^VV2V^PAL>e
!!€ sa0ʹǩǩ00>e

!!€ maainâst^Pen^VV2V>ed
!!€ ma0inâst00>ed

    [OnSetC|PenUltCns]  _ (:ʼ|%{ʼØ%}:) [Cns:+|i:|n j|l m|s ǩ:|s m|s t|š ǩ:|š m] (%^Pen: %^VV2V:) PenBetweenStemAndVowelLoss %^RmVow: ;
!! Aanar+N+Prop+Sg+Ill: **Inari/Enare**
!!€ Aanar^RmVow>a
!!€ Aan0r0>a

    :Vow+ (%{ˈ1%}:|0:) (ʹ:|%{ʹØ%}:) [Cns:+ - [Cns:* [l|n]]] _ i: %^RmVow:  ;

  PenUltCns _ (:ʼ|%{ʼØ%}:) Cns:+ %^RmVow:  ;
!! mättʼted+V+Inf: **teach/opettaa**
!!€ mätta0t^RmVow>ed
!!€ mätt0ʼt0>ed
!tååpsast^Pen^VV2V^RmVow^PALK
  a: _ [c c| č č| k k|s s|š š] %^VV2V: ;

 _ %^UltRmVow: ;
!! -a

"Vowel shortening ä:0"
!! **@RULENAME@** - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
 ä:0 <=>
  ä: _ (i|u) (ʹ:|%{ʹØ%}:) Cns:+ (PenUltCns) (Vow: (0:) Cns:+ (PenUltCns) %^Pen:) %^VV2V: ;
  ä: _ [c c| č č| k k|s s|š š] %^VV2V: ;

"%{ʼØ%} for modifier letter apostrophe"
!! **@RULENAME@** -
%{ʼØ%}:ʼ <=> [ :n | :l | j] [:0]* (%>) _ :j ;
          :ǩ [:0]* %> _ ǩ ;
          :k [:0]* %> _ k ;
          :ž  [:0]* %> _ ž ;
          :č  [:0]* %> _ č ;
	  š  [:0]* [%> | » ]  _ š ;
	  s  [:0]* [%> | » ]  _ s ;
         l [:0]* %> _ l ;
         v [:0]* %> _ v ;
!! jieʹlli+N+Ess **animal/eläin**
!!€ jieʹll{ʼØ}i^I2J>en
!!€ jieʹllʼj0>en
!mieʹll%>%{ʼØ%}laž

 _ %^UltRmVow: ;

     [ :n | :l ] ([%^C2CC:|%^CCC2CC:]) %> _ j ;
!!€ koll>{ʼØ}j>ed
!!€ koll>ʼj>ed
!!$ koll>{ʼØ}j>ed
!!$ koll>0j>ed
     [ :n | :l ] ([%^C2CC:|%^CCC2CC:]) %> _ (Vow:0) i:j ;
!!€ valmštõõll{ʼØ}i^I2J>i
!!€ valmštõõllʼj0>i
!! piânˈnai+N+Ess: **dog/koira**
!!€ piânˈn^CCC2CC>{ʼØ}j>en
!!€ piân0n0>ʼj>en
     [ :n | :l | j:] _ i: PenBetweenStemAndVowelLoss (%^RmVow:) %^I2J: ;
!!€ jieʹll{ʼØ}i^I2J>e>st
!!€ jieʹllʼj0>e>st


!! -ä

"Vowel shortening y:0"
!! **@RULENAME@** - used in PX
 y:0 <=>
 _ %^UltRmVow: ;
!! -y

"Vowel shortening ö:0"
!! **@RULENAME@** - used in PX
 ö:0 <=>
 _ %^UltRmVow: ;
!! -ö



"ZERO to syllable mark 0:ʼ, same as modifier letter apostrophe"
!! **@RULENAME@** - used in zeeʹtt+N+Sg+Abe: zeeʹttʼtää
 0:ʼ <=>
     m BetweenStemAndRightArrow ( %>) Vow: _ m %^RmVow: ;
     n Vow: _ n %^RmVow: ;
     [s|š] Vow: _ :s %^RmVow: ;
     [s (s:)|š (š:)] Vow: _ :s Cns:* PenNeutrVowLenghtHeightDiphPalAllegroPAL (%^Pen: %^CC2CAllegro:) %^RmVow: ;
!     [s (s:)|š (š:)] Vow: _ :s Cns:* PenNeutrVowLenghtHeightDiphPalAllegroPAL %^RmVow: ;
     t (%>) Vow: _ t %^RmVow: ;
!! mättʼted+V+Inf: **teach/opettaa**
!!€ mätta0t^RmVow>ed
!!€ mätt0ʼt0>ed
!! pueʹtted+V+Der/jed+Der/Caus+Der/NomAct+N+Ess:
!!€ puä{ʹØ}tt^CC2CCC>a0t^RmVow>em^RmVow>en
!!€ puä0tt0>0ʼt0>0m0>en
!!$ puä{ʹØ}tt^CC2CCC>at^RmVow>em^RmVow>en
!!$ puä0tt0>0t0>0m0>en
!! tåpsʼsted+V+Inf:
!!€ tååpsa0st^Pen^VV2V^RmVow^PALK>ed
!!€ tå0ps0ʼst0000>ed

     j Vow: _ j %^RmVow: ;
!!jåʹttʼted+V+Inf
        [ :VowNoIU [i|u]|Cns] (ˈ:) m NeutrVowLenghtHeightDiphPalAllegroPAL (%^CCC2CC:) [%>|»]  _ m ;
        [ :VowNoIU [i|u]|Cns] (ˈ:) n NeutrVowLenghtHeightDiphPalAllegroPAL (%^CCC2CC:) [%>|»]  _ n ;
        [ :VowNoIU [i|u]|Cns] (ˈ:) v NeutrVowLenghtHeightDiphPalAllegroPAL (%^CCC2CC:) [%>|»]  _ v ;
        [ :VowNoIU [i|u]|Cns] (ˈ:) [s|š|%{XC%}:s|%{XC%}:š] NeutrVowLenghtHeightDiphPalAllegroPAL (%^CCC2CC:|%^C2CC:) [%>|»]  _ s ;
        [ :VowNoIU [i|u]|Cns] (ˈ:) t: NeutrVowLenghtHeightDiphPalAllegroPAL (%^CCC2CC:|%^XYY2XY:|%^XYY2VY:) [%>|»]  _ t ;
        [ :VowNoIU [i|u]|Cns] (ˈ:) [Cns:ǩ|k] NeutrVowLenghtHeightDiphPalAllegroPAL (%^CCC2CC:) [%>|»]  _ k ;
       [ :VowNoIU [i|u]|Cns:] (ˈ:) [Cns:ǩ|k] NeutrVowLenghtHeightDiphPalAllegroPAL BetweenPALNoAndMorphRightArrow [%>|»] _ ǩ ;
        [ :VowNoIU [i|u]|Cns:] (ˈ:) [Cns:ž|z|%{XC%}:ž] NeutrVowLenghtHeightDiphPalAllegroPAL (%^C2CC:) BetweenPALNoAndMorphRightArrow [%>|»]  _ ž ;
        [ :VowNoIU [i|u]|Cns:] (ˈ:) [Cns:č|c] NeutrVowLenghtHeightDiphPalAllegroPAL BetweenPALNoAndMorphRightArrow [%>|»]  _ č ;
!!€ ijss>0sa
!!€ ijss>ʼsa

!!€ kåčč>0čem
!!€ kåčč>ʼčem

!!€ a^1VOWutt^V2VV^XYY2XY>0taa
!!€ aaut000>ʼtaa

!!€ taaurõ^1VOWš{XC}^C2CC>0ses
!!€ taaurõ0šš0>ʼses


!! ## Vowel alternation rules

!! ### VOWEL SHIFT

!! ### RAISING
!! deriving +Ind+Prt+Sg1, +Ind+Prt+Sg2 +Ind+Prt+Pl3 in teevvad:tivvu

"Vowel raising o:u"
!! **@RULENAME@** - Adding +Prt+Pl3 for ed verbs, Removing u: and i:  second element due to njeiddad njeiddu
 o:u <=>
       o: _ (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+]  %^VOWRaise:  ;
  OnSetC  _ (o:|%^1VOW:) (i|u) (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+]  NeutrVowLength %^VOWRaise: RBound ;
!!€ šoomm^VV2V^VOWRaise>u
!!€ šu0mm00>u

"Vowel raising %{õu%}:u"
!! **@RULENAME@** - Adding
 %{õu%}:u <=>
       %{õu%}: _ (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+]  %^VOWRaise:  ;
       OnSetC  _ (%{õu%}:|%{õuØ%}:) (i|u) (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+]  NeutrVowLength %^VOWRaise: RBound ;
!! tõlvvad+V+Pass+PrfPrc: 
!!€ t{õu}{õuØ}lvv^VOWRaise>um
!!€ tu0lvv0>um

"Vowel raising å:o"
!! **@RULENAME@** - Adding
 å:o <=>
  OnSetC _ (å:|%^1VOW:) (i|u) (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+]  NeutrVowLength %^VOWRaise: ;

!!€ kåbll^VOWRaise
!!€ kobll0

          OnSetC å: _ (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+]  %^VOWRaise: ;
!!€ kåå{ʹØ}pp^VOWRaise^PAL^CC2C>in
!!€ kooʹv0000>in
!!€ kå^1VOW{ʹØ}ll^V2VV^VOWRaise^PAL>in
!!€ kooʹll000>in
  OnSetC _ (%^1VOW:o|%^1VOW:0|å:0|å:) ((%{ʹØ%}:) [h|:u]|i|j)  (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+] (%^Allegro:) (%^PAL:)  NeutrVowLength %^VOWRaise:  BetweenPALNoAndMorphRightArrow ;


"Vowel raising e:i"
!! **@RULENAME@** - Adding +Prt+Pl3 for ed verbs, Removing u: and i:  second element due to njeiddad njeiddu
 e:i <=>
  OnSetC _ (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+]  %^VOWRaise: ;
  OnSetC _ (%^1VOW:|e:) (u) (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+]  NeutrVowLength %^VOWRaise: ;

  OnSetC  e: _ (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+]  %^VOWRaise: ;
! diphthong raising with e:i at onset - deriving +Ind+Prt+Pl3 in reäkkad > riâkku, veârrad > viõrru

 OnSetC  _ [ä:|â:] (%{ʹØ%}: h:|%{ʹØ%}: v:|u|i) (%{ˈ1%}:|0:) (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:*] %^VOWRaise: ;
! OnSetC  _ [ä:|â:] (%{ʹØ%}: h:|%{ʹØ%}: v:|u|i) (%{ˈ1%}:|0:) (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:*] [%^VOWRaise:|%^PALE:|%^PALẸ:] ;
! OnSetC  _ [ä:ẹ|ä:e|ä:â|ä:õ|â:õ|â:e ] (u) (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+]  [%^VOWRaise:|%^VOWRaise: %^PALE:] ;
!! reâugg+N+Sg+Ill
!!€ reâugg^VOWRaise>u
!!€ riõugg0>u
!! pieʹhssed+V+Inf: ****
!!€ peâ{ʹØ}h{ʹØ}ss^PALE^PAL>ed
!!€ pieʹh0ss00>ed
!! mieʹlǩǩ+N+Sg+Nom: **milk/maito**
!!€ meâ{ʹØ}lkk^VOWRaise^PALE^PAL
!!€ mieʹlǩǩ000

!!€ jeä{ʹØ}gˈg^VOWRaise^PALẸ^PAL^CCC2CC
!!€ jiẹʹǧ0ǧ0000
!!€ beâ{ʹØ}sˈs^VOWRaise^PALE^PAL^CCC2CC
!!€ bieʹs0s0000
!!€ ceägˈg^VOWRaise>u
!!€ ciâgˈg0>u
 OnSetC  _ [ä:|â:] (u|i) (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+] Vow: Cns:* (%^Pen: %^VV2V:)  [%^Pen: %^VOWRaise:|%^Pen: %^PALẸ:] ;
!! cieʹǩǩes+N+Sg+Ill: **trick, type of ear mark/tikki, pykälä**
!!€ ceä{ʹØ}kˈkes^Pen^PALẸ^Pen^PAL^Pen^CCC2CC
!!€ ciẹʹǩ0ǩes000000


"Vowel raising ẹ:i"
!! **@RULENAME@** - used in
 ẹ:i <=>
  OnSetC _ (%^1VOW:|ẹ:) (i:|u) (ʹ:|%{ʹØ%}:) [h: (%{ʹØ%}:)|Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+] NeutrVowLength %^VOWRaise: ;
!!€ pẹ^1VOWss^VOWRaise>u
!!€ pi0ss0>u
!!€ pẹ^1VOWss^V2VV^VOWRaise>už
!!€ piiss00>už
!!€ tẹẹvv^VV2V^VOWRaise>uš
!!€ ti0vv00>uš
!! peigg+N+Sg+Ill
!!$ pẹ^1VOWigg^VOWRaise^VYY2XYY>u
!!$ pẹ0jgg00>u

OnSetC  ẹ: _ (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+]  %^VOWRaise: ;



"Vowel raising â:õ"
!! **@RULENAME@** - Adding +Prt+Pl3 for ed verbs, Removing u: and i:  second element due to njeiddad njeiddu
 â:õ <=>
  â:  _      (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+]  %^VOWRaise: ;
!  [â:â|â:õ]  _      (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+]  %^VOWRaise: ;
  OnSetC _   (â:|%^1VOW:) (u) (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+] NeutrVowLength  %^VOWRaise: ;
!  OnSetC _   (â:0|â:õ|%^1VOW:) (u) (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+] NeutrVowLength  %^VOWRaise: ;
!! mââʹnn+N+Pl+Acc: **egg/muna**
!!€ mââ{ʹØ}nn^VOWRaise^PAL^CC2C>id
!!€ mõõʹn0000>id
!! pââʹjj+N+Pl+Gen
!!€ pââ{ʹØ}jj^VOWRaise^PAL^CC2C>i
!!€ põõ0j000>i
!!$ pââ{ʹØ}jj^VOWRaise^PAL^CC2C>i
!!$ pââ0j000>i
!! čâʹlmm+N+Pl+Acc: **eye/silmä**
!!€ čâ^1VOW{ʹØ}lmm^V2VV^VOWRaise^PAL^XYY2XY>id
!!€ čõõʹlm00000>id
!! kââʹǩǩed+V+Act+PrsPrc+Sg+Nom: **rub/hangata**
!!€ kââ{ʹØ}kk^VOWRaise^PAL>i
!!€ kõõʹǩǩ00>i
!!$ kââ{ʹØ}kk^VOWRaise^PAL>i
!!$ kââʹǩǩ00>i
   u   _    (%{ʹØ%}: h:i |i)  (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+|Cns:+ :ˈ Cns:+ (%{XC%}:)| Cns:+ (ˈ:|:ˈ) :Cns]  %^VOWRaise:  \[ %^PALE: | # ]+ ;
!   u   _    (i)  (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+|Cns:+ :ˈ Cns:+ (%{XC%}:)| Cns:+ (ˈ:|:ˈ) :Cns]  %^VOWRaise:  (%^PALNo:|(%^PALÕ:) %^PAL:) BetweenPALNoAndMorph ;
!muâ{ʹØ}rjj^VOWRaise^PAL^XYY2XY
!muâ{ʹØ}rjj^VOWRaise^PALE^PAL^XYY2XY
!!€ ruâk0k^VOWRaise^CC2CCC>u
!!€ ruõkˈk00>u
!!€ čuâkˈk^VOWRaise>u
!!€ čuõkˈk0>u
!! kuâsˈsad+V+Ind+Prt+Pl3
!!€ kuâsˈs^VOWRaise>u
!!€ kuõsˈs0>u
  e:   _ (u|i) (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+|Cns:+ :ˈ Cns:+] %^VOWRaise: \[ %^PALE: | %^PALẸ: | %^PALI:] ;
!  e:   _ (u) (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+|Cns:+ :ˈ Cns:+] %^VOWRaise: (%^PALNo:|(%^PALÕ:) %^PAL:) BetweenPALNoAndMorph ;
!! reâugg+N+Sg+Ill
!!€ reâugg^VOWRaise>u
!!€ riõugg0>u
!! veârrad+V+Ind+Prt+Pl3
!!€ veâr0r^VOWRaise^CC2CCC>u
!!€ viõrˈr00>u
!! ǩeâikk+N+Sg+Ill: ****
!!€ ǩeâi{ʹØ}kk^VOWRaise>u
!!€ ǩiõi0kk0>u

"Diphthong raising beginning with u ä:å"
!! **@RULENAME@** -
 ä:å <=>
    u  _ (i) (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+] %^VOWRaise: BetweenPALNoAndMorph ;
!! kuärŋŋad+V+Ind+Prt+Pl3
!!€ kuärŋŋ^VOWRaise>u
!!€ kuårŋŋ0>u
!!$ kuärŋŋ^VOWRaise>u
!!$ kuõrŋŋ0>u


"Diphthong raising beginning with e:i ä:â"
!! **@RULENAME@** -
 ä:â <=>
 e:  _ [(i|u)|(ʹ:|%{ʹØ%}:)] [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:*|Cns:+ :ˈ Cns:+] %^VOWRaise: \[ %^PALẸ: | %^PALÕ: | %^PALE:] ;
! e:  _ [(i|u)|(ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+|Cns:+ :ˈ Cns:+]]  NeutrVowLength %^VOWRaise: BetweenPALNoAndMorph ;
!! reäkkad+V+Ind+Prt+Pl3
!!€ reäk0k^VOWRaise^CC2CCC>u
!!€ riâkˈk00>u
!! ǩeäinn+N+Sg+Ill: **road/tie**
!!€ ǩeäinn^VOWRaise>u
!!€ ǩiâinn0>u
!!$ ǩeäinn^VOWRaise>u
!!$ ǩiäinn0>u

"Diphthong raising beginning with e:i ä:e"
!! **@RULENAME@** - used in
 ä:e <=>
  [u|e:i]  _ (i|u|h:u) [:ʹ|%{ʹØ%}:ʹ] [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+|Cns:+ :ˈ Cns:+]  %^VOWRaise: %^PALE: %^PAL: ;
  e  _ (i|u|h:) %{ʹØ%}: [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+|Cns:+ :ˈ Cns:+] Vow: Cns:*   %^Pen: %^PALE: %^Pen: %^PAL: ;
!!€ jeä{ʹØ}kˈkel^Pen^PALE^Pen^PAL^Pen^KKK2ZERO
!!€ jeeʹ000el000000
!!€ mõõnn^VV2V^CC2CAllegro»škuä{ʹØ}t0t^VOWRaise^PALE^PAL^CC2CCC>iǩ
!!€ mõ0n000»škueʹtˈt0000>iǩ


"Diphthong raising beginning with e:i ä:ẹ"
!! **@RULENAME@** - used in
 ä:ẹ <=>
  [u|e:]  _ (i|u) [:ʹ|%{ʹØ%}:ʹ] [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+|Cns:+ :ˈ Cns:+]  (%^VOWRaise:) %^PALẸ: (%^PAL:) ;
!!€ čuä{ʹØ}ckk^PALẸ^PAL^XYY2XY>es
!!€ čuẹʹcǩ0000>es
!!€ suä{ʹØ}kk^PALẸ^PAL^KK2GG
!!€ suẹʹjj000
  [u|e:]  _ (i|u) [:ʹ|%{ʹØ%}:] [Cns:+ (ˈ:|:ˈ) Cns:+|Cns:+ :ˈ Cns:+] Vow: Cns:*  (%^Pen: %^VOWRaise:) %^Pen: %^PALẸ: (%^Pen: %^PAL:) ;
!! vuẹ'ddes+N+Sg+Nom: **root/juuri**
!!€ vuä{ʹØ}dˈdes^Pen^PALẸ^Pen^PAL^Pen^CCC2CC
!!€ vuẹʹd0des000000

"diphthong backing beginning with u ä:õ"
!! **@RULENAME@** - used in
 ä:õ <=>
  u _ (i|u) (:ʹ|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+|Cns:+ :ˈ Cns:+] %^VOWRaise: %^PALÕ: ;
          [u|e:] _ (i|u) (:ʹ|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+|Cns:+ :ˈ Cns:+] %^VOWRaise: %^PALÕ:  ;
!!€ jeä{ʹØ}l0l^VOWRaise^PALÕ^PAL^CC2CCC>e
!!€ jiõʹlˈl0000>e

"Even syllabic verbs I, diphthong raising beginning with ä:a u"
!! **@RULENAME@** - deriving läullad > laullum
 ä:a <=>
  ä:a _ (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+] %^VOWRaise: (%^Allegro:) (%^PAL:) BetweenPALNoAndMorphRightArrow ;
!  ä: _ (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+] %^VOWRaise: (%^Allegro:) (%^PAL:) BetweenPALNoAndMorphRightArrow ;
  OnSetC _ (%^1VOW:a|%^1VOW:0|ä:0|ä:) ((%{ʹØ%}:) [h|:u]|i|j)  (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+] (%^Allegro:) (%^PAL:)  NeutrVowLength %^VOWRaise:  BetweenPALNoAndMorphRightArrow ;
  OnSetC _ ä:a (%{ʹØ%}:) u  (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+] (%^Allegro:) (%^PAL:)  NeutrVowLength %^VOWRaise:  BetweenPALNoAndMorphRightArrow ;
!!€ rääuh^VOWRaise>u
!!€ raauh0>u
!!€ ä^1VOWldd^VOWRaise>u
!!€ a0ldd0>u
!!€ ä^1VOWldd^V2VV^VOWRaise^XYY2XY>u
!!€ aald0000>u
!! čääʹcc+N+Pl+Acc **water**
!!€ čää{ʹØ}cc^VOWRaise^PAL^KK2GG>id
!!€ čaaʹʒʒ00>id
!! mäʹhssed+V+Ind+Prt+Sg3: **pay/maksaa**
!!€ mä^1VOW{ʹØ}h{ʹØ}ss^V2VV^VOWRaise^PAL^XYY2VY>i
!!€ maa0uʹs00000>i
!!$ mä^1VOW{ʹØ}h{ʹØ}ss^V2VV^VOWRaise^PAL^XYY2VY>i
!!$ mää0uʹs00000>i
!! mäʹhssed+V+Ind+Prt+Pl3: **pay/maksaa**
!!€ mä^1VOW{ʹØ}h{ʹØ}ss^VOWRaise^PAL>e
!!€ ma0ʹh0ss00>e
!!$ mä^1VOW{ʹØ%}h%{ʹØ%}ss^VOWRaise^PAL>e
!!$ mä0ʹh0ss00>e
!! ### LOWERING

! Where: VIGGAD +Ind+Prs+Sg3, +Ind+Prs+Pl3
!        TIETTED
! long and single vowels



"Even syllabic verbs I, vowel lowering o:å"
!! **@RULENAME@** - deriving +Ind+Prs+Sg3, +Ind+Prs+Pl3 in poorrâd
 o:å <=> OnSetC    _ o: (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+]  NeutrVowLength %^VOWLower:  ;
!! poorrâd+V+Ind+Prs+Pl3 **eat/syödä**
!!€ poo{ʹØ}rr^VV2V^VOWLower^PAL>e
!!€ på0ʹrr000>e
         OnSetC o: _     (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+]  %^VOWLower:  ;
!! poorrâd+V+Ind+Prs+Sg3 **eat/syödä**
!!€ poo{ʹØ}rr^VOWLower
!!€ påå0rr0
         OnSetC    _  (%^1VOW:)   (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+]   NeutrVowLength %^VOWLower: ;
!! juʹrdded+V+Cond+Sg1: joordčem
         OnSetC _ %^1VOW:å (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+] %^V2VV:   %^VOWLower: ;
!! juʹrdded > joordam
         OnSetC    _ (%^1VOW:) (u:|i:) (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+] NeutrVowLength %^VOWLower: ;
         OnSetC    _ (%^1VOW:) ((ʹ:|%{ʹØ%}:) (v:u|j:i)) (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+] NeutrVowLength %^VOWLower: ;



"Even syllabic verbs I, vowel lowering i:e"
!! **@RULENAME@** -
 i:e <=>
   OnSetC _ (i:|%^1VOW:) (u:|i:) [:ʹ|%{ʹØ%}:ʹ] [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+] NeutrVowLength  %^VOWLower: %^PAL: ;
!! viǯǯâd+V+Ind+Prs+Pl3 **fetch/noutaa**
!!€ vi^1VOW0ǯǯ^VOWLower^PAL>e
!!€ ve0ʹǯǯ00>e
!! viikkâd+V+Ind+Prs+Pl3
!!€ vii0kk^VV2V^VOWLower^PAL>e
!!€ ve0ʹǩǩ000>e


   OnSetC _ (%^1VOW:) (v:u|j:i) [:ʹ|%{ʹØ%}:ʹ] [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+] NeutrVowLength %^VOWLower: %^PAL: ;


   OnSetC _ [õ:|â:|e:] ((%{ʹØ%}:) h:) (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+] [%^VOWLower:  |(e: s:) %^Pen: %^VOWLower: ] ;
!! siõrrâd+V+Ind+Prs+Pl3: **play/leikkiä**
!!€ siõ0r0r^VOWLower^PALÄ^PAL^CC2CCC>e
!!€ seäʹrˈr0000>e
!!€ siõrr^VOWLower
!!€ seârr
!!$ siõrr^VOWLower
!!$ siârr0


  OnSetC _ â: (ʹ:|%{ʹØ%}:) [Cns:+ |Cns:+ (ˈ:|:ˈ) Cns:+] Vow: Cns:* %^Pen: %^VOWLower: ;
!! cieʹǩǩes+N+Sg+Gen: **trick, type of ear mark/tikki, pykälä**
!!€ ciâkˈkes^Pen^VOWLower^PenVow2a^VC
!!€ ceäkˈkaz0000
  OnSetC i: _   [:ʹ|%{ʹØ%}:ʹ] [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+] %^VOWLower: ;


"Even syllabic verbs I, vowel lowering i:ẹ "
!! **@RULENAME@** - deriving +Ind+Prs+Sg3 in viǯǯâd
 i:ẹ <=>
   OnSetC _ (i:|%^1VOW:) (u:|i:) (%{ʹØ%}:0) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+] NeutrVowLength  %^VOWLower: ;
!! viǯǯâd+V+Imprt+Sg2 **fetch/noutaa**
!!€ vi^1VOWǯǯ^V2VV^VOWLower>as
!!€ vẹẹǯǯ00>as
!! viikkâd+V+Imprt+Pl3
!!€ viikk^VV2V^VOWLower>az
!!€ vẹ0kk00>az


           OnSetC _ (%^1VOW:) (v:u|j:i) (%{ʹØ%}:0) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+] NeutrVowLength %^VOWLower: ;

  OnSetC i: _ (%{ʹØ%}:0) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+] %^VOWLower: ;


"Vowel lowering a:ä"
!! **@RULENAME@** -
 a:ä <=>
     a: _     (u|i) (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+] %^VOWLower:  (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) RBound ;
!     a:ä _     (u|i) (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+] %^VOWLower:  (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) RBound ;

  OnSetC    _ (a:) (u|i) (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+] NeutrVowLength  %^VOWLower:  (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) [RBound|BetweenPALNoAndMorph] ;
  OnSetC    _ (%^1VOW:ä|%^1VOW:0) (u|i) (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+] NeutrVowLength  %^VOWLower:  (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) [RBound|BetweenPALNoAndMorph] ;

    OnSetC     _ (%^1VOW:ä|%^1VOW:0) ((ʹ:|%{ʹØ%}:) (v:u|j:i)) (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+] NeutrVowLength %^VOWLower:  (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) RBound ;



"Even syllabic verbs I, vowel lowering u:o"
!! **@RULENAME@** - deriving +Ind+Prs+Sg3, +Ind+Prs+Pl3 in uʹvdded
 u:o <=>
        OnSetC _ (%^1VOW:) (ʹ:|%{ʹØ%}:) v: (%{ʹØ%}:) [n n: |d d:|l l:|r r:|ʒ ʒ:|ǯ ǯ:|j j:] (%^V2VV:) %^VOWLower:  ;

!! uʹvdded+V+Cond+Pl3 **to give**
!!€ u^1VOWvdd^V2VV^VOWLower^XYY2VY>če
!!€ ooud0000>če
!! uʹvdded+V+Ind+Prs+Sg3
!!€ u^1VOWvdd^VOWLower^XYY2VYY
!!€ o0udd00
!! vuʹvll+N+Sg+Ill: voulla
!!€ vu^1VOWvll^VOWLower^XYY2VYY>a
!!€ vo0ull00>a
        OnSetC _ (%^1VOW:) (ʹ:|%{ʹØ%}:) h: (%{ʹØ%}:) [t t: |s s:|c c:|č č:|š š:] (%^V2VV:) %^VOWLower:  ;
!! puʹhtted+V+Cond+Pl3 **to give**
!!€ pu^1VOW{ʹØ}h{ʹØ}tt^VOWLower
!!€ po00h0tt0

  OnSetC _ [u:o|u:0] (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+] (%^VV2V:)  %^VOWLower: BetweenVowHeightAndMorph ;
!! kuullâd+V+Ind+Prs+Pl3 **hear/kuulla**
!!€ kuu0ll^VV2V^VOWLower^PAL^CC2C>e
!!€ ko0ʹl00000>e
       OnSetC _ (%^1VOW:) (ʹ:|%{ʹØ%}:) [CNoV: Cns:* |Cns:+ (ˈ:|:ˈ) Cns:+] NeutrVowLength %^VOWLower: BetweenVowHeightAndMorph ;
!! juʹrdded+V+Ind+Prs+Sg1 **think**
!!€ ju^1VOW{ʹØ}rdd^V2VV^VOWLower^XYY2XY>am
!!€ joo0rd0000>am
!! juʹrdded+V+Ind+Prs+Pl3 **think**
!!€ ju^1VOW0rdd^VOWLower^PAL>e
!!€ jo0ʹrdd00>e
! uʹvdded+V+Ind+Prs+Pl3: ouʹdde

 OnSetC  _ (%^1VOW:) (ʹ:|%{ʹØ%}:)  v:u (%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+] %^VOWLower: BetweenVowHeightAndMorph ;
 OnSetC _ (%^1VOW:) %{VU%}: (ʹ:|%{ʹØ%}:) Cns:+ NeutrVowLength %^VOWLower: BetweenVowHeightAndMorph ;
!! stuuʹl+N+Sg+Ill: **chair/tuoli**
!!€ stu^1VOW{VU}l^V2VV^VOWLower>a
!!€ stooul00>a
!! juʹrdded+V+Cond+Sg1: joordčem
!! juʹrdded > joordam

 OnSetC _ (%^1VOW:) (ʹ:|%{ʹØ%}:) [CNoV: Cns:* |Cns:+ (ˈ:|:ˈ) Cns:+]  Vow: Cns:*  %^Pen: %^VOWLower: ;
!! puuʹttes+A+Sg+Gen: **bright/kirkas**
!!€ pu^1VOWttes^Pen^VOWLower^PenVow2a^VC
!!€ po0ttaz0000
!!$ pu^1VOWttes^Pen^VOWLower^PenVow2a^VC
!!$ pu0ttaz0000

OnSetC u: _   (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+|r d d:]  %^VOWLower:  (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) (%^KK2GG:|[(%^Allegro:) (%^PAL:) %^CC2C:|(%^Allegro:) (%^PAL:) %^CC2CAllegro:]|%^XYY2XY:) RBound ;
!! suukkâd+V+Imprt+Sg3: **row/soutaa**
!!€ suukk^VOWLower^KK2GG>as
!!€ sooǥǥ00>as

"Even syllabic verbs I, vowel lowering u:õ "
!! **@RULENAME@** - deriving
 u:õ <=>
    u: _  (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+] ( Vow: Cns:* %^Pen: ) %^U2Õ: ;
    OnSetC _ (%^1VOW:) (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+] [NeutrVowLength | Vow: Cns:* PenNeutrVowLength %^Pen: ] %^U2Õ: ;
!!€ ču^1VOWkk^V2VV^U2Õ>až
!!€ čõõkk00>až
    OnSetC  _ u: (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+]  %^U2Õ: ;



"Even syllabic verbs I, vowel lowering õ:â "
!! **@RULENAME@** - deriving +Ind+Prs+Sg3, +Ind+Prs+Pl3 in viǯǯâd
 õ:â <=>
       i:e _ ((%{ʹØ%}:) h:) (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+] %^VOWLower: \[ %^PALE:|%^PALÄ:] ;
!       i: _ (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+] %^VOWLower: [ BetweenPALNoAndMorph |%^PAL:|%^PALNo:] ;
!! riõkkâd+V+Ind+Prs+Sg3 **to whip**
!!€ riõkˈk^VOWLower#
!!€ reâkˈk0#
!       õ:â _  (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+] %^VOWLower: ;
       õ: _  (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+] %^VOWLower: ;
!!€ mõõ0nn^VV2V^VOWLower^PAL>e
!!€ mâ0ʹnn000>e
    OnSetC  _ [õ:0|õ:â]  (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+] NeutrVowLength %^VOWLower: ;
!!€ mõõnn^VOWLower^CC2C>as
!!€ mâân000>as

! juʹrdded > joordam
   OnSetC _ (%^1VOW:)  (u:|i:) (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+] (%^V2VV:) %^VOWLower:  (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) ;
   OnSetC _ (%^1VOW:) ((ʹ:|%{ʹØ%}:) (h:|v:u|j:i|i)) (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+] NeutrVowLength %^VOWLower:  (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) ;
!   OnSetC _ (%^1VOW:0|%^1VOW:â)  (u:|i:) (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+] (%^V2VV:) %^VOWLower:  (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) RBound ;
!   OnSetC _ (%^1VOW:0|%^1VOW:â) ((ʹ:|%{ʹØ%}:) (h:|v:u|j:i|i)) (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+] NeutrVowLength %^VOWLower:  (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) RBound ;
! diphthong opening after u
!! vââid+N+Sg+Nom: ****
!!€ võ^1VOWidd^V2VV^VOWLower^XYY2XY
!!€ vââid0000
        u _ (i) (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+] %^VOWLower: [ BetweenPALNoAndMorph |%^PAL:|%^PALNo:] ;
!! kuõskkâd+V+Ind+Prs+Sg3
!!€ kuõskk^VOWLower
!!€ kuâskk0
!! juõiggâd+V+Ind+Prs+Sg3
!!€ kuõigg^VOWLower
!!€ kuâigg0


!! ### Diphthongs

"Even syllabic verbs I, diphthong opening after u å:ä"
!! **@RULENAME@** - deriving +Ind+Prs+Sg3, in kuåccâd kuäʹcce
!! = a>ä lowering clockwise
 å:ä <=> u _ (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+] NeutrVowLength %^VOWLower: (%^PALNo:|%^PAL:) BetweenPALNoAndMorph ;
!!€ kuå0c0c^VOWLower^PAL^CC2CCC>e
!!€ kuäʹcˈc000>e
!! puäj+N+Sg+Nom
!!€ puåjˈj^VOWLower^CCC2C
!!€ puåj0000
!!$ puåjˈj^VOWLower^CCC2C
!!$ puäj0j00


"Vowel in second syllable e:a"
!! **@RULENAME@** - deriving cieʹǩǩes+N+Sg+Gen: ceäkˈkaz
 e:a <=>
  OnSetC _ (ʹ:|%{ʹØ%}:) [Cns:*] PenBetweenStemAndVowelLoss %^PenVow2a: ;
!! cieʹǩǩes+N+Sg+Gen: **trick, type of ear mark/tikki, pykälä**
!!€ ciâkˈkes^Pen^VOWLower^PenVow2a^VC
!!€ ceäkˈkaz0000
!! puuʹttes+A+Sg+Gen



"Even syllabic verbs I, diphthong opening i:e â:ä for â:ä "
!! **@RULENAME@** - deriving +Ind+Prs+Sg3, in čiõkkâd
 â:ä <=>
 u _ i (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+] %^VOWLower: ;
!!vueiʹnned+V+Ind+Prs+Sg3
!!€ vuâinn^VOWLower
!!€ vuäinn0
 i:e _ (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+] %^VOWLower: (%^PALNo:| %^PAL:)  ;
 i:e _ (ʹ:|%{ʹØ%}:) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+] Vow: Cns:+ %^Pen: %^VOWLower: (%^Pen: %^PALNo:| %^Pen: %^PAL:)  ;
 [u|e] _ (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+] %^PALÄ:  ;
!! vueʹlǧǧed+Use/NG+V+Ind+Prs+Pl3 **leave/lähteä**
!!€ vuâ0lgg^PALÄ^PAL>a
!!€ vuäʹlǧǧ00>a
!! tieʹtted+V+Ind+Prs+Pl3 **know/tietää**
!!€ t e â {ʹØ} t ˈ t ^PALÄ ^PAL ^CC2CCC > e
!!€ t e ä ʹ t ˈ t 0 0 0 > e

"Even syllabic verbs I, diphthong opening after i:e e:â"
!! **@RULENAME@** - deriving +Ind+Prs+Sg3, in pi%{EÂ%}ʹǩǩ:peâkka
 %{EÂ%}:â <=> i:e _ (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+] %^VOWLower: %^PALNo:  RBound ;


!! ### Vowel backing

" u å:õ"
!! **@RULENAME@** - used in
!! = a>ä lowering clockwise
 å:õ <=> u _ (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+] %^VOWRaise: ;

!!€ puått^VOWRaise>u
!!€ puõtt0>u



!! ### Vowel Palatalization
"diphthong allophonic realization in palatalization u å:e"
!! **@RULENAME@** - deriving e from å
 å:e <=> u _ [:ʹ|%{ʹØ%}:ʹ]  [Cns:+ (%{XC%}:)|Cns:+ :ˈ Cns:+]  %^PALE: %^PAL: ;

!!€ puå0v0v^PALE^PAL^CC2CCC>e
!!€ pueʹvˈv000>e


"diphthong allophonic realization in palatalization u å:ẹ"
!! **@RULENAME@** - deriving
 å:ẹ <=> u _ [:ʹ|%{ʹØ%}:ʹ]  [Cns:+ (%{XC%}:)|Cns:+ :ˈ Cns:+] %^PALẸ: %^PAL: ;

!! ### Vowel Lowering and Fronting

"Even syllabic verbs I, diphthong opening õ:ä after i:e"
!! **@RULENAME@** - deriving +Ind+Prs+Sg3, in čiõkkâd
 õ:ä <=>
 i:e _ (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+]  NeutrVowLength %^VOWLower: %^PALÄ: ;
 u _ (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+]  NeutrVowLength %^VOWLower: %^PALÄ: ;
!! siõrrâd+V+Ind+Prs+Pl3: **play/leikkiä**
!!€ siõ0r0r^VOWLower^PALÄ^PAL^CC2CCC>e
!!€ seäʹrˈr0000>e



!! ### SECONDARY FRONTING



"Even syllabic verbs I, secondary vowel fronting with PAL u õ:e"
!! **@RULENAME@** - deriving +Ind+Prs+Pl3 in VIQQAD: kuõskkâd >kueʹsǩǩe
 õ:e <=>
  u _ (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+] %^VOWLower: %^PALE: ; ! deriving +V+Inf in vueʹlǧǧed

         i _ (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+] %^VOWLower: %^PALE: ;
! - deriving +Ind+Prs+Sg3, in kuõskkâd kueʹsǩǩe CHECK THIS
!!€ kuõ0skk^VOWLower^PALE^PAL>e
!!€ kueʹsǩǩ000>e


"Even syllabic verbs I, secondary u > v "
!! **@RULENAME@**
 u:v <=> u _ (ʹ:|%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+] %^VY2XYY: ;


!! ### RELATIVE VOWEL LENGTHENING

!! vowel lengthening and consonant shortening, %^Pen: %^V2VV and %^CShort

"Even syllabic verbs I, relative vowel lengthening %^1VOW:â"
!! **@RULENAME@** - deriving +V+Inf in TIETTED: uudd > uʹvdded
 %^1VOW:â <=>
   OnSetC [â|Â] _ (u:|i:|%{VU%}:) (ʹ:|%{ʹØ%}:) Cns:+ (%{XC%}:) %^V2VV: ( %^VOWLower: ) ;
   OnSetC [õ:â|Õ:Â] _ (u:|i:|%{VU%}:) (ʹ:|%{ʹØ%}:) Cns:+ (%{XC%}:) %^V2VV: %^VOWLower: ;
!   OnSetC [â|õ:â|Â|Õ:Â] _ (u:|i:|%{VU%}:) (ʹ:|%{ʹØ%}:)  Cns:+ (%{XC%}:) %^V2VV: ;
!! šõddâd+V+Imprt+Sg3:
!!€ šõ^1VOWdd^V2VV^VOWLower>as
!!€ šââdd00>as

!Distinguishing mainstam and maainstam
      OnSetC [â|Â] _ (u:|i:) (ʹ:|%{ʹØ%}:)  Cns:+ (%{XC%}:)  Vow: Cns:+ (%{XC%}:)  %^Pen: %^V2VV: ;
!! nââʹer+N+Sg+Nom **sleep**
!!€ nâ^1VOW0kker^Pen^V2VV^Pen^PAL^Pen^KK2ZERO
!!€ nââʹ00er000000
      [e|E] _ [u (%{ʹØ%}:) |(%{ʹØ%}:) Cns:] Cns:+ NeutrVowLength %^PALÂ: %^PALNo: ;

  â _ %^V2VV: ;
!! radio+N+Sg+Ill
!!€ radio^1VOW^V2VVʹje
!!€ radioo0ʹje

"%^1VOW:ẹ relative vowel lengthening"
!! **@RULENAME@** -
 %^1VOW:ẹ <=>   OnSetC [:ẹ|:Ẹ] _ (i|u) (%{ʹØ%}:0) Cns:+ (%{XC%}:) %^V2VV: (%^VOWLower:) BetweenPALNoAndMorphRightArrow RBound  ;
!  OnSetC [:ẹ|:Ẹ] _ (i|u) (%{ʹØ%}:0) Cns:+ (%{XC%}:) %^V2VV: (%^VOWLower:) BetweenPALNoAndMorphRightArrow RBound ;
   OnSetC [ẹ|Ẹ] _ (i|u) (%{ʹØ%}:0) Cns:+ (%{XC%}:) %^V2VV: (%^VOWLower:) BetweenPALNoAndMorphRightArrow  ;
!! sẹẹr+N+Pl+Nom: ****
!!€ sẹ^1VOWrr^CC2C
!!€ sẹẹr00
!! pess+N+Sg+Acc: **rifle/pyssy**
!!€ pẹ^1VOWss^V2VV
!!€ pẹẹss0
!! veʹrǧǧ+N+Der/lazh+A+Sg+Nom
!!€ vẹ^1VOW{ʹØ}rgg^V2VV^XYY2XY>laž
!!€ vẹẹ0rǥ000>laž
     OnSetC [:ẹ|:Ẹ] _ (i|u) (%{ʹØ%}:) Cns:+ Vow: Cns: %^Pen: %^V2VV: [%^Pen:0 %^PAL:0| %^Pen:0 %^PALNo:0] ;
!! čẹẹuʹres+N+Sg+Nom = otter
!!€ čẹ^1VOWu{ʹØ}rres^Pen^V2VV^Pen^PAL^Pen^XYY2XY
!!€ čẹẹuʹr0es000000
!! viǯǯâd+V+Imprt+Sg2 **fetch/noutaa**
!!€ vi^1VOWǯǯ^V2VV^VOWLower>as
!!€ vẹẹǯǯ00>as
         OnSetC [:ẹ|:Ẹ] _ (i|u) (%{ʹØ%}:0) Cns:+ Vow: Cns: %^Pen: %^V2VV: (%^Pen: %^VOWLower:) PenBetweenPALNoAndMorph ;
!         OnSetC [:ẹ|:Ẹ] _ (i|u) (%{ʹØ%}:0) Cns:+ Vow: Cns: %^Pen: %^V2VV: (%^Pen: %^VOWLower:) PenBetweenPALNoAndMorphRightArrow RBound ;
!! eelas+N+Sg+Nom:
!!€ ẹ^1VOWllas^Pen^V2VV^Pen^CC2C
!!€ ẹẹl0as0000

"Even syllabic verbs I, relative vowel lengthening %^1VOW:e"
!! **@RULENAME@** - deriving +V+Inf in TIETTED: uudd > uʹvdded
 %^1VOW:e <=> OnSetC [e|E|ẹ:e] _  [:ʹ|%{ʹØ%}:ʹ]  Cns:+ [%^V2VV:  %^PAL: |Vow: Cns:* %^Pen: %^V2VV: %^Pen: %^PAL:] ;
!!€ čââʹlmtẹ^1VOW0m^V2VV^PAL
!!€ čââʹlmteeʹm00
!! reeʹǧǧes+A+Sg+Nom: **rich/rikas**
!!€ rẹ^1VOW0gges^Pen^V2VV^Pen^PAL
!!€ rẹẹʹǧǧes0000
        OnSetC [Vow:e|Vow:E] _ (i|u) [:ʹ|%{ʹØ%}:ʹ]  Cns:+ (%{XC%}:) %^V2VV:  %^PAL: ;
!!€ pẹ^1VOWi0vv^V2VV^PAL^XYY2XY
!!€ peeiʹv0000
         OnSetC i:e _  (ʹ:|%{ʹØ%}:) Cns:+ %^V2VV: %^VOWLower: ;

!Distinguishing mainstam and maainstam
      Cns: Vow:e _ (u:|i:) (ʹ:|%{ʹØ%}:)  Cns:+ (Vow:) Cns:+  %^Pen: %^V2VV: ;
      [ n | š ] Vow:e _ (ʹ:|%{ʹØ%}:) ǩ %{XC%}: %^V2VV: %^PAL: ;
!!€ kaađnẹ^1VOW0ǩ{XC}^V2VV^PAL^C2CC
!!€ kaađneeʹǩǩ000

  e _ %^V2VV: ;
!! Jouste+N+Prop+Sg+Ill
!!€ Jouste^1VOW^V2VVʹje
!!€ Joustee0ʹje


"Even syllabic verbs I, relative vowel lengthening %^1VOW:å"
!! **@RULENAME@** - deriving
 %^1VOW:å <=>
   OnSetC [o:å|Å|O:Å] _ (u:|i:|h:u|v:u) (ʹ:|%{ʹØ%}:) Cns:+ %^V2VV:  %^VOWLower:  ;
!   OnSetC [å|o:å|Å|O:Å] _ (u:|i:|h:u|v:u) (ʹ:|%{ʹØ%}:) Cns:+ %^V2VV: ( %^VOWLower: ) ;
   OnSetC [å|Å] _ (u:|i:|h:u|v:u) (ʹ:|%{ʹØ%}:) Cns:+ (Vow: [Cns:* (%{XC%}:)] %^Pen:) %^V2VV: NoVowRaise ;
!! sååbbar+N+Sg+Nom
!!€ så^1VOWbbar^Pen^V2VV
!!€ sååbbar00
!!$ så%^1VOWbbar^Pen^V2VV
!!$ så0bbar00
  å _ %^V2VV: ;
!! radio+N+Sg+Ill
!!€ radio^1VOW^V2VVʹje
!!€ radioo0ʹje
   OnSetC å _ (%{ʹØ%}:) h: (ʹ:|%{ʹØ%}:) Cns:+ %^V2VV: BetweenVowHeightAndConsGrade %^XYY2VY: ;
!!€ jå^1VOW{ʹØ}h{ʹØ}ss^V2VV^PAL^XYY2VY
!!€ jåå0uʹs0000


"Even syllabic verbs I, relative vowel lengthening %^1VOW:õ"
!! **@RULENAME@** - deriving +V+Inf in TIETTED: uudd > uʹvdded
 %^1VOW:õ <=>
     OnSetC [õ|â:õ|u:õ] _ (u:|i:) (ʹ:|%{ʹØ%}:)  Cns:+ (%{XC%}:) %^V2VV: ;
     e õ _ (%{ʹØ%}:)  Cns:+ (%{XC%}:) %^V2VV: ;
!     OnSetC Vow:õ _ (u:|i:) (ʹ:|%{ʹØ%}:)  Cns:+ (%{XC%}:) %^V2VV: ;
!! âʹlǧǧ+N+Pl+Gen: **boy/poika**
!!€ â^1VOW{ʹØ}lgg^V2VV^VOWRaise^PAL^XYY2XY>i
!!€ õõʹlj00000>i
!! čâʹlmm+N+Pl+Acc: **eye/silmä**
!!€ čâ^1VOW{ʹØ}lmm^V2VV^VOWRaise^PAL^XYY2XY>id
!!€ čõõʹlm00000>id
     OnSetC [u:õ|õ|â:õ] _ (ʹ:|%{ʹØ%}:)  Cns:+ (%{XC%}:) [ %^V2VV:  | %^Pen: %^V2VV:] ;
!     OnSetC [u:õ|õ] _ (ʹ:|%{ʹØ%}:)  Cns:+ (%{XC%}:) [ %^V2VV:  | %^Pen: %^V2VV:] ;
!!€ ču^1VOWkk^V2VV^U2Õ>až
!!€ čõõkk00>až

!Distinguishing mainstam and maainstam
      OnSetC õ _ (u:|i:) (ʹ:|%{ʹØ%}:)  Cns: Cns:* Vow: [Cns:+| %{XC%}:Cns]  %^Pen: %^V2VV: ;

   OnSetC [õ|Õ] _ (ʹ:|%{ʹØ%}:) Cns:+ (%{XC%}:) %^V2VV: ;
!!€ võ^1VOWrr^V2VV^CC2C
!!€ võõr000
!!€ sõ^1VOWll^V2VV^CC2C
!!€ sõõl000
!!€ mättʼtõ^1VOWs{XC}^V2VV^K2GG
!!€ mättʼtõõzz00
 OnSetC õ _ (%{ʹØ%}:) h: (ʹ:|%{ʹØ%}:) Cns:+ %^V2VV: BetweenVowHeightAndConsGrade %^XYY2VY: ;
!!€ sõ^1VOWhss^V2VV^XYY2VY
!!€ sõõus000


"relative vowel lengthening %{õuØ%}:õ"
!! **@RULENAME@**
 %{õuØ%}:õ <=>
     OnSetC %{õu%}:õ _ (u:|i:) (ʹ:|%{ʹØ%}:)  Cns:+ (%{XC%}:) %^V2VV: ;

  õ _ %^V2VV: ;
!! radio+N+Sg+Ill
!!€ radio^1VOW^V2VVʹje
!!€ radioo0ʹje

   OnSetC [u:õ|õ] _ (%{ʹØ%}:)  Cns:+ Vow: Cns:* %^Pen: %^V2VV: ;



"Even syllabic verbs I, relative vowel lengthening %^1VOW:ä"
!! **@RULENAME@** - deriving +V+Inf in TIETTED: uudd > uʹvdded
 %^1VOW:ä <=>
 OnSetC [ä|Ä] _ (i|:u) (ʹ:|%{ʹØ%}:) Cns:+ %^V2VV: BetweenVowHeightAndMorph ;
! OnSetC [ä|Ä] _ (i|:u) (ʹ:|%{ʹØ%}:) Cns:+ %^V2VV: ;
 OnSetC ä _ (ʹ:|%{ʹØ%}:) Cns:+ %^V2VV: ([(%^Allegro:) (%^PAL:) %^CC2C:|(%^Allegro:) (%^PAL:) %^CC2CAllegro:|%^XYY2XY:]) RBound ;
!!€ ä^1VOWldd^V2VV^CC2C
!!€ ääld000
!!€ jä^1VOWu0rr^V2VV^PAL^CC2C
!!€ jääuʹr0000
   OnSetC ä _ (%{ʹØ%}:) h: (ʹ:|%{ʹØ%}:) Cns:+ %^V2VV: BetweenVowHeightAndConsGrade %^XYY2VY: ;
!! mäʹhssed+V+Ind+Prs+Sg1: **pay/maksaa**
!!€ mä^1VOW{ʹØ}h{ʹØ}ss^V2VV^XYY2VY>am
!!€ mää0u0s000>am
!! mäʹhssed+V+Ind+Prs+Sg4: **pay/maksaa**
!!€ mä^1VOW{ʹØ}h{ʹØ}ss^V2VV^PAL^XYY2VY>et
!!€ mää0uʹs0000>et


 OnSetC [ä|a:ä] _ (u|i) (ʹ:|%{ʹØ%}:) Cns:+ %^V2VV: ( %^VOWLower: )  BetweenVowHeightAndMorph ;
 OnSetC [ä|a:ä] _ (%{ʹØ%}:) (h:) (ʹ:|%{ʹØ%}:) Cns:+ %^V2VV: ( %^VOWLower: )  BetweenVowHeightAndMorph ;
! OnSetC [å:ä|ä|a:ä] _ (h:u|u|i) (ʹ:|%{ʹØ%}:) Cns:+ %^V2VV: ( %^VOWLower: )  ;
!Distinguishing mainstam and maainstam
!- kååʹmmerd, maainâst Vowel lengthening in INDPRSSG3 and IMPSG2
    OnSetC ä _ (u|i) (ʹ:|%{ʹØ%}:) Cns:+ Vow: Cns:+ (%{XC%}:) [%^Pen: %^V2VV:|%^IMPSG2:|%^INDPRSSG3:] ;
    OnSetC ä _ (%{ʹØ%}:) (h:) (ʹ:|%{ʹØ%}:) Cns:+ Vow: Cns:* %^Pen: %^V2VV: ;
    OnSetC ä _ (u|i) (ʹ:|%{ʹØ%}:) Cns:+ Vow: Cns:+ (%{XC%}:) [%^Pen: %^V2VV:|%^IMPSG2:|%^INDPRSSG3:] ;
    OnSetC ä _ (%{ʹØ%}:) (h:) (ʹ:|%{ʹØ%}:) Cns:+ Vow: Cns:* %^Pen: %^V2VV: ;

  ä _ %^V2VV: BetweenVowHeightAndMorph ;
!! Määttä+N+Prop+Sg+Ill
!!€ Määttä^1VOW^V2VVʹje
!!€ Määttää0ʹje


"Even syllabic verbs I, relative vowel lengthening %^1VOW:a"
!! **@RULENAME@** - deriving +V+Inf in MAINSTED: maainstam, mainstam
 %^1VOW:a <=>
  OnSetC (i) a _ (i|:u) (ʹ:|%{ʹØ%}:) Cns:+ (%{XC%}:) %^V2VV: ;
!!€ dura^1VOWk^V2VV
!!€ duraak0
  OnSetC ä: _ (i|:u) (ʹ:|%{ʹØ%}:) Cns:+ %^V2VV: %^VOWRaise: ;
!!€ ä^1VOWldd^V2VV^VOWRaise^CC2C>už
!!€ aald0000>už
!! täiʹdded+V+Ind+Prt+Sg3:
!!€ tä^1VOWi{ʹØ}dd^V2VV^VOWRaise^PAL^XYY2XY

 OnSetC a _ (%{ʹØ%}:) h: (ʹ:|%{ʹØ%}:) Cns:+ %^V2VV: BetweenVowHeightAndConsGrade %^XYY2VY: ;
 OnSetC ä: _ (%{ʹØ%}:) h: (ʹ:|%{ʹØ%}:) Cns:+ %^V2VV: %^VOWRaise: BetweenVowHeightAndConsGrade %^XYY2VY: ;

!! mäʹhssed+V+Ind+Prt+Sg3: **pay/maksaa**
!!€ mä^1VOW{ʹØ}h{ʹØ}ss^V2VV^VOWRaise^PAL^XYY2VY>i
!!€ maa0uʹs00000>i

!
  [a|A] _ (i|u)  (ʹ:|%{ʹØ%}:) Cns:+ Vow: Cns:* (%{XC%}:)  %^Pen: %^V2VV: ;
!! taalkâs+N+Sg+Nom

!2016-04   [ b | ʒ | (ǯ) ǯ | (g) g | i b | i n |(k) k |l|m |(n) n | n v|r g|r| r t | u d|u l| u n | u ŋ | u r] _ s t (%^Pen: %^V2VV:) %^IMPSG2: ;
!2016-04   [ ʹ đ đ | ʹ ǩ |ʹ l|ʹ l ǩ |ʹ m m |ʹ p] _ s t %^IMPSG2: ;
   Cns: a _  i  (ʹ:|%{ʹØ%}:) Cns: Vow: Cns:+ (%{XC%}:)  %^Pen: %^V2VV: ;
   Cns: a _  i  Cns: Vow: Cns:+ (%{XC%}:)  %^Pen: %^V2VV: ;
   Cns: a _ (u:|i:) (ʹ:|%{ʹØ%}:)  Cns:+ Vow: Cns:+ (%{XC%}:)  [%^Pen: %^V2VV:|%^IMPSG2:|%^INDPRSSG3:] ;


  a _ %^V2VV: ;
!! biologia+N+Sg+Ill
!!€ biologia^1VOW^V2VVʹje
!!€ biologiaa0ʹje


"Relative vowel lengthening %^1VOW:o"
!! **@RULENAME@** -  simultaneous lengthening and lowering: juʹrdded > joordam juʹrdded+V+Ind+Prs+Sg1
 %^1VOW:o <=>
   OnSetC u:o _ (%{ʹØ%}:) [Cns: - v: ] [Cns:* |:Cns*] %^V2VV:  %^VOWLower: ;
   OnSetC u:o _ [%{VU%}:u|(ʹ:|%{ʹØ%}:) v:] (ʹ:|%{ʹØ%}:) [d|l|r|ʒ] %^V2VV:  %^VOWLower: ;
   OnSetC u:o _ [ (ʹ:|%{ʹØ%}:) [ v:|h:] (%{ʹØ%}:) [d d:|l l:|r r:|ʒ ʒ:|ǯ ǯ:|j j:|s s:|t t:| č č: |c c: ] ] %^V2VV:  %^VOWLower: ;

!!€ u^1VOWvdd^V2VV^VOWLower^XYY2VY>če
!!€ ooud0000>če
!! juʹrdded+V+Ind+Prs+Sg1 **think**
!!€ ju^1VOWrdd^V2VV^VOWLower^XYY2XY>am
!!€ joord0000>am
 OnSetC å: _ (%{ʹØ%}:) h: (ʹ:|%{ʹØ%}:) Cns:+ %^V2VV: %^VOWRaise: BetweenVowHeightAndConsGrade %^XYY2VY: ;
  å:o _  (i|u) (ʹ:|%{ʹØ%}:) Cns:+  %^V2VV: %^VOWRaise: ;
!!€ kå^1VOW0ll^V2VV^VOWRaise^PAL>in
!!€ kooʹll000>in
!!€ tå^1VOWidd^V2VV^VOWRaise^XYY2XY>čed
!!€ tooid0000>čed
   o _ (%{ʹØ%}:) h: (ʹ:|%{ʹØ%}:) Cns:+ (%{XC%}:) (Vow: Cns:+ (PenUltCns) %^Pen:)  %^V2VV: ;
   o _ (i|u) (ʹ:|%{ʹØ%}:) Cns:+ (%{XC%}:) (Vow: Cns:+ (PenUltCns) %^Pen:)  %^V2VV: ;
!! oolmaž+N+Sg+Nom: **person/henkilö**
!!€ o^1VOWlmm^V2VV^XYY2XY>až
!!€ oolm000>až
!! Sajos+N+Prop+Sg+Gen:Sajoozz
!!€ Sajo^1VOW{ʹØ}s{XC}^V2VV^K2GG
!!€ Sajoo0zz00
!!$ Sajo^1VOW{ʹØ}s{XC}^V2VV^K2GG
!!$ Sajo00zz00
!! toll+N+Sg+Gen: **fire/tuli**
!!€ to^1VOWll^V2VV^CC2C
!!€ tool000
!!€ to^1VOW0ll^V2VV^PAL^CC2C>e
!!€ tooʹl0000>e
!!€ ko^1VOWnttâr^Pen^V2VV^Pen^XYY2XY
!!€ koont0âr0000
  o _ %^V2VV: ;
!! radio+N+Sg+Ill
!!€ radio^1VOW^V2VVʹje
!!€ radioo0ʹje

"Even syllabic nouns I, relative vowel lengthening i"
!! **@RULENAME@** - ǩiđđ:ǩiiđ
 %^1VOW:i <=> OnSetC [ e:i| ẹ:i ] _ (%{ʹØ%}:)  [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+] [%^V2VV: %^VOWRaise:| Vow: Cns:+ %^Pen: %^V2VV: %^Pen: %^VOWRaise:]  ;

OnSetC [i|I] _ ((%{ʹØ%}:) h:|u) (%{ʹØ%}:) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+] %^V2VV: BetweenVowHeightAndMorph   ;
  OnSetC [Vow:i|Vow:I] _  [(ʹ:|%{ʹØ%}:) CNoJ:|(%{ʹØ%}:) [h:|u] (ʹ:|%{ʹØ%}:) Cns:+] Vow: Cns:+ (%{XC%}:)  %^Pen: %^V2VV: ;
!Not needed here
!! pirsted+V+Ind+Prs+ConNeg
!!€ piirâst
!!€ piirâst
!!€ pẹ^1VOWss^V2VV^VOWRaise>už
!!€ piiss00>už

   OnSetC [i|I] _ (ʹ:|%{ʹØ%}:) CNoJ: Cns:* [ %^V2VV:| Vow: Cns:* %^Pen: %^V2VV:]  ;
!!€ ǩi^1VOWđđ^V2VV^CC2C
!!€ ǩiiđ000

  i _ %^V2VV: ;
!! Terhi+N+Prop+Sg+Ill
!!€ Terhi^1VOW^V2VVʹje
!!€ Terhii0ʹje


"Even syllabic nouns, relative vowel extra lengthening u not followed by v"
!! **@RULENAME@** - declension of nouns kunn > kuun
 %^1VOW:u <=> [OnSetC|PenUltCns] [o:u|u] _ (ʹ:|%{ʹØ%}:) Cns:+  (%{XC%}:) (%^Pen: [(%^Allegro: %^Pen:) (%^PAL: %^Pen:) %^CC2C:|(%^Allegro: %^Pen:) (%^PAL: %^Pen:) %^CC2CAllegro:]) %^V2VV: ((%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:|%^PALNo:) BetweenPALNoAndMorph ;
!! mainstummuš+N+Err/Orth+Sg+Gen: **story telling/tarinointi**
!!€ mainstummu^1VOWš{XC}^Pen^CC2C^V2VV^K2GG
!!€ mainstum0uužž0000

 OnSetC [o:u|u] _ (ʹ:|%{ʹØ%}:) Cns: Cns:* (%^Pen: [(%^Allegro:) (%^PAL:) %^CC2C:|(%^Allegro:) (%^PAL:) %^CC2CAllegro:]) %^V2VV: ((%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:|%^PALNo:) [%^C2CC: | %^K2GG:] ;
 OnSetC o:u     _ (i) (ʹ:|%{ʹØ%}:) Cns: Cns:*  %^V2VV:  %^VOWRaise:  ;
 OnSetC u (i)   _ (ʹ:|%{ʹØ%}:) Cns: Cns:*  %^V2VV: BetweenVowHeightAndMorph ;
         Cns: u _ (ʹ:|%{ʹØ%}:)  CNoV: Vow: Cns:+ (%{XC%}:)  %^Pen: %^V2VV: [%>|(%^Hyphen: %- |%^NoHyphen:|%{%-Ø%}:) (∑) #:|.#.|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:] ;
              u _ (ʹ:|%{ʹØ%}:) (i) [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+]  Vow: Cns:* %^Pen: %^V2VV: ;

!! tuuibâl+N+Sg+Nom: ****
!!€ tu^1VOWibbâl^Pen^V2VV^Pen^XYY2XY
!!€ tuuib0âl0000
!!€ pu^1VOW0ttes^Pen^V2VV^Pen^PAL
!!€ puuʹttes0000
!!$ pu%^1VOW0ttes^Pen^V2VV^Pen^PAL
!!$ pu0ʹttes0000

      OnSetC u _ (ʹ:|%{ʹØ%}:) v %{ʹØ%}:0 Cns:*  %^V2VV: BetweenVowHeightAndMorph ;
!! vuʹvdd+N+Err/Orth+Sg+Gen: **area/alue**
!!€ vu^1VOW{ʹØ}v{ʹØ}dd^V2VV^PAL^XYY2XY
!!€ vuuʹv0d0000
            u _ %^V2VV: ;
!! Oulu+N+Prop+Sg+Ill
!!€ Oulu^1VOW^V2VVʹje
!!€ Ouluu0ʹje



"Word-final vowel ö"
!! **@RULENAME@** - 
 %^1VOW:ö <=>  ö _ %^V2VV: ;
!! Enontekiö+N+Prop+Sg+Ill
!!€ Enontekiö^1VOW^V2VVʹje
!!€ Enontekiöö0ʹje


!! simultaneous lengthening and raising, hmm: xfst ordering might be easier



!! ### VOWEL DUMMY LOSS


!! ### SEMI VOWELS


"Even syllabic nouns, for j>i"
!! **@RULENAME@** - sijdd > siid This will need a special extra-lengthening rule
 j:i <=> OnSetC i: (%{ʹØ%}:) _ (ʹ:|%{ʹØ%}:) [ d d:|s s:|n n:| p p:] [NeutrVowLenghtHeightDiphTint | Vow: Cns:+ PenNeutrVowLenghtHeightDiphTint %^Pen: ] %^XYY2VY: ;
!!€ ijss^XYY2VY>âs
!!€ iis00>âs
!! fiinâs+N+Sg+Nom: **fine/hieno**
!!€ fijnnâs>^Pen^XYY2VY
!!€ fiin0âs>00

  Vow: (%^1VOW:) (ʹ:|:ʹ|%{ʹØ%}:) _ j: NeutrVowLenghtHeightDiphPalAllegroPAL %^J2I: [(%^Allegro:) (%^PAL:) %^CC2C:|(%^Allegro:) (%^PAL:) %^CC2CAllegro:|%^XYY2XY:] ;

!!€ sââjj^J2I^CC2C
!!€ sââi000
!! tuâjj+N+Sg+Acc **work**
!!€ tuâjj^J2I^CC2C
!!€ tuâi000

"Even syllabic verbs,  for v>u"
!! **@RULENAME@** - uvdd > ouʹdde
 v:u <=> u: (%^1VOW:) (ʹ:|%{ʹØ%}:) _ (%{ʹØ%}:) [ǯ ǯ: |ʒ ʒ:|d d:|l l:|r r:|j j:] NeutrVowLenghtHeightDiphTint (%^PAL:) [%^XYY2VY: |%^CCC2CC:|%^XYY2VYY:] ;
! u: (%^1VOW:) (ʹ:|%{ʹØ%}:) _ (%{ʹØ%}:) [ǯ ǯ: |ʒ ʒ:|d d:|l l:|r r:] NeutrVowLenghtHeightDiphPalAllegroPAL [%^XYY2VY: |%^CCC2CC:|%^XYY2VYY:] ;
!!€ u^1VOWv0dd^PAL^XYY2VY
!!€ u0uʹd000
!!€ u^1VOWvdd^VOWLower^XYY2VYY
!!€ o0udd00
!! kuʹvǯǯ+N+Sg+Gen
!!€ ku^1VOWv0ǯǯ^VV2V^PAL^XYY2VY
!!€ ku0uʹǯ0000
      OnSetC u %^1VOW: %{ʹØ%}: _ %{ʹØ%}: Cns:*  (%^PAL:) %^XYY2VY: BetweenVowHeightAndMorph ;
!! kuʹvǯǯ+N+Sg+Gen: **/**
!!€ ku^1VOW{ʹØ}v{ʹØ}ǯǯ
!!€ kuuʹ00ǯ0
  u: (%^1VOW:) (ʹ:|%{ʹØ%}:) _ (%{ʹØ%}:) [ǯ ǯ: |ʒ ʒ:|d d:|l l:|r r:] Vow: Cns:* PenNeutrVowLenghtHeightDiphTint %^Pen: [%^XYY2VY: |%^CCC2CC:|%^XYY2VY:] ;
!  u: (%^1VOW:) (ʹ:|%{ʹØ%}:) _ (%{ʹØ%}:) [ǯ ǯ: |ʒ ʒ:|d d:|l l:|r r:] Vow: Cns:* PenNeutrVowLenghtHeightDiphPalAllegroPAL %^Pen: [%^XYY2VY: |%^CCC2CC:|%^XYY2VY:] ;  
!! uudâs+N+Sg+Nom: **creature/luomus**
!!€ uvddâs^Pen^XYY2VY
!!€ uud0âs00

"Even syllabic nouns, for h>u"
!! **@RULENAME@** - luhtt  s s: ...
 h:u <=>
 [Vow: - u:] (%^1VOW:) (%{ʹØ%}:) _ (:ʹ|%{ʹØ%}:) [s s:|š š:|t t:|c c:|č č:] NeutrVowLenghtHeightDiphPalAllegroPAL %^XYY2VY: ;
 u: (%^1VOW:) (%{ʹØ%}:) _ (:ʹ|%{ʹØ%}:) [s s:|š š:|t t:|c c:|č č:] (%^V2VV:) NeutrVowHeightDiphPalAllegroPAL  %^XYY2VY: ;
!! uhss+N+Der/Dim+N+Sg+Gen **door**
!!€ uh0ss^PAL^XYY2VY>e
!!€ uuʹs000>e

!!€ sä^1VOWhss^V2VV^XYY2VY>am
!!€ sääus000>am

 Vow: (%^1VOW:) (%{ʹØ%}:) _ (:ʹ|%{ʹØ%}:) [s s:|š š:|t t:|c c:|č č:] Vow: Cns:* PenNeutrVowLenghtHeightDiphPalAllegroPAL %^Pen: %^XYY2VY: ;
!! piiutâs+N+Sg+Nom **clothing/vaate**
!!€ pi^1VOWhttâs^Pen^V2VV^Pen^XYY2VY
!!€ piiut0âs0000


"Even syllabic nouns, for h>i"
!! **@RULENAME@** - kueʹhtt
h:i <=> Vow: (%^1VOW:) %{ʹØ%}: _ (:ʹ|%{ʹØ%}:) t t: NeutrVowLenghtHeightDiphPalAllegroPAL %^XYY2IY: ;
!! kueiʹt+Num+Sg+Gen **two/kaksi**
!!€ kuâh{ʹØ}tt^PALE^PAL^XYY2IY
!!€ kueiʹt0000
!!$ kuâh{ʹØ}tt^PALE^PAL^XYY2IY
!!$ kuâhʹt0000

"trisyllabic verbs and doer derivations, i>j"
!! **@RULENAME@** - used in
 i:j <=> [Vow: |Cns:] (0:ʼ|%{ʼØ%}:) _ [PenBetweenStemAndVowelLoss|NeutrVowLenghtHeightDiphPalAllegroPAL BetweenPALNoAndMorphRightArrow] (%^RmVow:) %^I2J:  ;
   Vow:i (%^1VOW:) (%{ʹØ%}:) _ [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+]  %^VOWRaise:  %^VYY2XYY: ;
!   Vow:i (%^1VOW:) (%{ʹØ%}:) _ [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+] NeutrVowLength %^VOWRaise:  %^VYY2XYY: ;
!!
!!€ valmštõõll{ʼØ}i^I2J>i
!!€ valmštõõllʼj0>i
!! piânˈnai+N+Ess: **dog/koira**
!!€ piânˈn^CCC2CC>{ʼØ}j>en
!!€ piân0n0>ʼj>en

!! peigg+N+Sg+Ill
!!€ pẹ^1VOWigg^VOWRaise^VYY2XYY>u
!!€ pi0jgg00>u

!! ### VOWELS TENSE vs LAX 2012-11-28

"Vowels for â:i"
!! **@RULENAME@** -
 â:i <=>
  e:  _ (i|u) [:ʹ|%{ʹØ%}:ʹ]  [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+|Cns:+ :ˈ Cns:+]  [%^VOWRaise: %^PALI:| Vow: Cns:* %^Pen: %^VOWRaise: %^Pen: %^PALI: ]  ;
!! ǩeʹtted+V+Ind+Prs+ConNeg: **cook/keittää**
!!€ ǩeâ0tt^VOWRaise^PALI^PAL
!!€ ǩiiʹtt000



"Vowels for â:e"
!! **@RULENAME@** -
 â:e <=>
   [i|e:|u] _ (%{ˈ1%}:|0:) (( (%{ʹØ%}:) h:|i|u) [:ʹ|%{ʹØ%}:] ) [Cns:+ (%{XC%}:)|Cns:+ (:ˈ|ˈ:)  Cns:+]  (%^VOWRaise:) %^PALE: %^PAL: ;
!   [i|e:i|u] _ ((h:i|i|u) [:ʹ|%{ʹØ%}:ʹ] ) [Cns:+ (%{XC%}:)|Cns:+ (:ˈ|ˈ:)  Cns:+]  NeutrVowLength (%^VOWRaise:) %^PALE: %^PAL: ;
!! miârr+N+Sg+Ill
!!€ miâ0rr^PALE^PAL>e
!!€ mieʹrr00>e
!! mieʹlǩǩ+N+Sg+Nom
!!€ meâ0lkk^VOWRaise^PALE^PAL
!!€ mieʹlǩǩ000
!!€ piâ0ss^PALE^PAL
!!€ pieʹss00
!!€ ǩiâ0p0p^PALE^PAL^CC2CCC>e
!!€ ǩieʹpˈp000>e
!!€ kuâ0ll^PALE^PAL
!!€ kueʹll00
!! tieʹtted+V+Imprt+Sg2: **know/tietää**
!!€ teâ0tt^VOWRaise^PALE^PAL^CC2C
!!€ tieʹđ00000
   [i|e:|u] _ ((h:i|i|u) [:ʹ|%{ʹØ%}:] ) [Cns:+ (%{XC%}:)|Cns:+ (:ˈ|ˈ:)  Cns:+]  Vow: Cns:* (%^Pen: %^VOWRaise:) %^Pen: %^PALE: (%^Pen: %^PAL:) ;
!! mueʹles+N+Sg+Nom
!!€ muâ{ʹØ}lles^Pen^PALE^Pen^PAL^Pen^CC2C
!!€ mueʹl0es000000
!! kueʹtǩes+N+Sg+Nom
!!€ kuâ{ʹØ}tkkes^Pen^PALE^Pen^PAL^Pen^XYY2XY
!!€ kueʹtǩ0es000000

e:  _ (%{ʹØ%}: h:|%{ʹØ%}: v:|i|u) [:ʹ|%{ʹØ%}:ʹ]  [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:*|Cns:+ :ˈ Cns:+]  [(%^VOWRaise:) %^PALE:| Vow: Cns:* (%^Pen: %^VOWRaise:) %^Pen: %^PALE: ]  ;
!! pieʹhssed+V+Inf: ****
!!€ peâ{ʹØ}h{ʹØ}ss^PALE^PAL>ed
!!€ pieʹh0ss00>ed
!!€ ǩeâ0sttes^Pen^PALE^Pen^PAL^Pen^XYY2XY
!!€ ǩeeʹst0es000000
!! ǩeʹtted+V+Ind+Prs+ConNeg: **cook/keittää**
!!€ ǩeâ0tt^PALE^PAL
!!€ ǩeeʹtt00

"Even-syllabic nouns, for â:ẹ"
!! **@RULENAME@** - used in
 â:ẹ <=> i _ [:ʹ|%{ʹØ%}:ʹ]  [Cns:+ (%{XC%}:)|Cns:+ :ˈ Cns:+]  %^PALẸ: %^PAL: ;
!Not needed here
!! pieʹll+N+Sg+Nom: **half**
!!€ peä0ll^VOWRaise^PALẸ^PAL
!!€ piẹʹll000
    u _ j j: %^PALẸ: ;
!!€ tuâjj^PALẸ
!!€ tuẹjj0

"Palatalization for ẹ:e"
!! **@RULENAME@** - used in
 ẹ:e <=>
 OnSetC ẹ: _ (i|u) [:ʹ|%{ʹØ%}:ʹ]  [Cns:+ (%{XC%}:)|Cns:+ (:ˈ|ˈ:0) Cns:+]  %^PAL:  ;
 OnSetC _ (%^1VOW:) [(i|u) [:ʹ|%{ʹØ%}:ʹ]| %{ʹØ%}: h: %{ʹØ%}: ]  [Cns:+ (%{XC%}:)|Cns:+ (:ˈ|ˈ:0) Cns:+] NeutrVowLength [%^PAL:|%^PALE:]  ;
!! reʹhtt+N+Pl+Nom
!!€ rẹ^1VOW{ʹØ}h{ʹØ}tt^V2VV^PAL^XYY2VY
!!€ ree0uʹt0000
!! peiʹvv+N+Sg+Gen: **day/päivä**
!!€ pẹ^1VOWi{ʹØ}vv^V2VV^PAL^CC2C
!!€ peeiʹv0000
!! karies+N+Sg+Nom: **caries/hammasmätä**
!!€ kariẹ^1VOW{ʹØ}s^PALE
!!€ karie00s0
         OnSetC _ ẹ: (i|u) [:ʹ|%{ʹØ%}:ʹ]  [Cns:+ (%{XC%}:)|Cns:+ (:ˈ|ˈ:0) Cns:+]  %^PAL:  ;

!! ### VOWEL and ZERO ALTERNATION


"Realization for â in a"
!! **@RULENAME@** - used in
 â:a <=> OnSetC _ (ʹ:|%{ʹØ%}:) [Cns:+] (%^Pen: %^V2VV:) %^PenVow2a: ;
!!ǩiõrggned+V+Ind+Prs+Sg3
!!€ ǩiõrggân^PenVow2a
!!€ ǩiõrggan0

!! ### THE NON-ORTHOGRAPHIC SYLLABLE


"Loss of ʼ when preceded by vowel"
!! **@RULENAME@** - This is a temporary solution to  "ʼ" in võʹllʼjed, it deletes softmark when preceded by vowel

 ʼ:0 <=> Vow:Vow _ Cns: ;

"%{A1%}:ʼ when subseqent syllable has vowel v"
!! **@RULENAME@** - This is a temporary solution to  "ʼ" in võʹllʼjed 2013-08-29
%{A1%}:ʼ <=> [l l|n n] _ i: %> Vow: ;



!! ### PALATALIZATION




!2024-01-11"0:ʹ as transfer from left of v:u"
!2024-01-11!! **@RULENAME@** - used +Ind+Prs+Pl3, uʹvdded+V+Ind+Prs+Pl3: ouʹdde
!2024-01-110:ʹ <=> v: (:ˈ) _ [ʒ ʒ:|ǯ ǯ:|d d:|l l:|r r:|j j:]       BetweenStemAndPALNo  %^PAL: [%^XYY2VY:|%^XYY2VYY:] ;
!2024-01-11!! uʹvdded+V+Ind+Prs+Pl3: **give/antaa**
!2024-01-11!!€ u^1VOW{ʹØ}v{ʹØ}dd^VOWLower^PAL^XYY2VYY>e
!2024-01-11!!€ o00uʹdd000>e
!2024-01-11!!€ u^1VOW{ʹØ}v{ʹØ}dd^PAL^XYY2VY
!2024-01-11!!€ u00uʹd000
!2024-01-11
!2024-01-11          v:u (:ˈ) _ [ʒ ʒ:|ǯ ǯ:|d d:|l l:|r r:|j j:]       BetweenStemAndPALNo  ( %^PAL:) RBound ;
!2024-01-11              h: (:ˈ) _ [c c:|č č:|s s:|š š:|t t:]  BetweenStemAndPALNo  %^PAL: [ %^XYY2VY: ] ;
!2024-01-11!              h: (:ˈ) _ [c c:|č č:|s s:|š š:|t t:]  BetweenStemAndPALNo  %^PAL: [ %^XYY2VY: |%^XYY2IY: ] ;
!2024-01-11!! täʹhtt+N+Pl+Nom: **bone/luu**
!2024-01-11!!€ tä^1VOW{ʹØ}h{ʹØ}tt^V2VV^PAL^XYY2VY
!2024-01-11!!€ tääuʹt0000
!2024-01-11!! täʹhtt+N+Pl+Gen: **bone/luu**
!2024-01-11!!€ tä^1VOW{ʹØ}h{ʹØ}tt^V2VV^VOWRaise^PAL^XYY2VY>i
!2024-01-11!!€ taauʹt00000>i
!2024-01-11!! kueiʹt+Num+Sg+Gen **two/kaksi**
!2024-01-11!!€ kuâ{ʹØ}h{ʹØ}tt^PALE^PAL^XYY2IY
!2024-01-11!!€ kue0iʹt0000
!2024-01-11             [Vow:|:Vow] (%^1VOW:) (i|u) (:ˈ) _ Cns:+ [Cns:*|%{XC%}:Cns|Cns:* (ˈ:|:ˈ) Cns:*] (i:) BetweenStemAndPALNo  %^PAL: ;
!2024-01-11!! siõrrâd+V+Ind+Prs+Pl3: **play/leikkiä**
!2024-01-11!!€ siõ{ʹØ}r0r^VOWLower^PALÄ^PAL^CC2CCC>e
!2024-01-11!!€ seäʹrˈr0000>e
!2024-01-11!!€ saa{ʹØ}kk^VV2V^PAL>e
!2024-01-11!!€ sa0ʹǩǩ00>e
!2024-01-11!! tääʹss+N+Sg+Gen: **level/taso**
!2024-01-11!!$ tää{ʹØ}ss^PAL^KK2GG
!2024-01-11!!$ tää0zz00
!2024-01-11!!€ mõõ{ʹØ}nn^VV2V^VOWLower^PAL>e
!2024-01-11!!€ mâ0ʹnn000>e
!2024-01-11!!€ jåå{ʹØ}tt^VV2V^VOWRaise^PAL>e
!2024-01-11!!€ jo0ʹtt000>e
!2024-01-11!!€ kuâ{ʹØ}ll^PALE^PAL
!2024-01-11!!€ kueʹll00
!2024-01-11!!€ stu^1VOW{VU}{ʹØ}l^PAL
!2024-01-11!!€ stu0uʹl0
!2024-01-11!!€ čââʹlmtẹ^1VOW{ʹØ}m^PAL
!2024-01-11!!€ čââʹlmte0ʹm0
!2024-01-11             [Vow:|:Vow] (%^1VOW:) (i|u|h:u|h:i) (:ˈ)  _ Cns:+ [Cns:*|%{XC%}:Cns|Cns:* (ˈ:|:ˈ) Cns:*] Vow: Cns:* PenBetweenStemAndPALNo (%^Pen: %^PALE:)  %^Pen: %^PAL: ;
!2024-01-11!!€ kä^1VOW{ʹØ}lles^Pen^V2VV^Pen^PAL
!2024-01-11!!€ kääʹlles0000
!2024-01-11!!€ jeä{ʹØ}kˈkel^Pen^PALE^Pen^PAL^Pen^KKK2ZERO
!2024-01-11!!€ jeeʹ000el000000
!2024-01-11!!€ seäh{ʹØ}tter^Pen^PALE^Pen^PAL^Pen^XYY2VY
!2024-01-11!!€ seeuʹt0er000000
!2024-01-11!! The left context allows for:
!2024-01-11!! Vowel shortening, and j/v > Vow

"%{ʹØ%}:ʹ as transfer from left of v:u and all instances of modifier letter prime"
!! **@RULENAME@** - used +Ind+Prs+Pl3, uʹvdded+V+Ind+Prs+Pl3: ouʹdde
%{ʹØ%}:ʹ <=> [v:0|v:u] (:ˈ) _ [ʒ ʒ:|ǯ ǯ:|d d:|g: g:|l l:|n n:|r r:|j j:]       NeutrVowLenghtHeightDiphTint (%^Allegro:) %^PAL: [%^XYY2VY:|%^XYY2VYY:] ;
!!j u {ʹØ}:ʹ v:0 {ʹØ}:0 j j:0 ^Allegro:0 ^PAL:0 ^XYY2VY:0

    Cns: :e %^1VOW: _ d ; ! Teäpˈpnẹ%^1VOW%{ʹØ%}d
!! uʹvdded+V+Ind+Prs+Pl3: **give/antaa**
!!€ u^1VOWv{ʹØ}dd^VOWLower^PAL^XYY2VYY>e
!!€ o0uʹdd000>e
!!€ u^1VOWv{ʹØ}dd^PAL^XYY2VY
!!€ u0uʹd000
!!€ Teäpˈpnẹ%^1VOW%{ʹØ%}d
!!€ Teäpˈpne0ʹd

          v:u (:ˈ) _ [ʒ ʒ:|ǯ ǯ:|d d:|g: g:|l l:|r r:|j j:]       NeutrVowLenghtHeightDiphTint   %^PAL: %^XYY2VY: ;
!          v:u (:ˈ) _ [ʒ ʒ:|ǯ ǯ:|d d:|g: g:|l l:|r r:|j j:]       BetweenStemAndPALNo   (%^PAL:)  RBound ;
           h: (:ˈ) _ [c c:|č č:|s s:|š š:|t t:]  NeutrVowLenghtHeightDiphTint  %^PAL: [ %^XYY2VY: |%^XYY2IY: ] ;

!! täʹhtt+N+Pl+Nom: **bone/luu**
!!€ tä^1VOWh{ʹØ}tt^V2VV^PAL^XYY2VY
!!€ tääuʹt0000
!! täʹhtt+N+Pl+Gen: **bone/luu**
!!€ tä^1VOW{ʹØ}h{ʹØ}tt^V2VV^VOWRaise^PAL^XYY2VY>i
!!€ taa0uʹt00000>i
!! kueiʹt+Num+Sg+Gen **two/kaksi**
!!€ kuâ{ʹØ}h{ʹØ}tt^PALE^PAL^XYY2IY
!!€ kue0iʹt0000
    _ v  %{ʹØ%}:0 [ʒ ʒ:|ǯ ǯ:|d d:|l l:| n n:|r r:|j j:]       NeutrVowLenghtHeightDiphTint  %^PAL: [%>:| (∑) #] ;
!! uʹvdded+V+Ind+Prs+Pl3: **give/antaa**
!!€ u^1VOW{ʹØ}v{ʹØ}dd^PAL>ed
!!€ u0ʹv0dd0>e
!! tuʹvnn+N+Sg+Nom: ****
!! € tu^1VOW{ʹØ}v{ʹØ}nn^PAL
!! € tu0ʹv0nn0
            _  h: (:ˈ) %{ʹØ%}: [c c:|č č:|s s:|š š:|t t:]  NeutrVowLenghtHeightDiphTint  %^PAL: [%>| (∑) #] ;
!! täʹhtt+N+Pl+Nom: **bone/luu**
!!€ tä^1VOW{ʹØ}h{ʹØ}tt^PAL
!!€ tääuʹt0000


             [Vow:|:Vow] (%^1VOW:) (i|u) (:ˈ) _ Cns:+ [Cns:*|%{XC%}:Cns|Cns:* (ˈ:|:ˈ) Cns:*] (i:) NeutrVowLenghtHeightDiphTint  %^PAL: ;
!! čẹẹuʹres+N+Sg+Nom = otter
!!€ čẹ^1VOWu{ʹØ}rres^Pen^V2VV^Pen^PAL^Pen^XYY2XY
!!€ čẹẹuʹr0es000000
!! siõrrâd+V+Ind+Prs+Pl3: **play/leikkiä**
!!€ siõ{ʹØ}r0r^VOWLower^PALÄ^PAL^CC2CCC>e
!!€ seäʹrˈr0000>e
!!€ saa{ʹØ}kk^VV2V^PAL>e
!!€ sa0ʹǩǩ00>e
!! tää%{ʹØ}ss+N+Sg+Gen: **level/taso**
!!$ tää0ss^PAL^KK2GG
!!$ tää0zz00
!!€ mõõ{ʹØ}nn^VV2V^VOWLower^PAL>e
!!€ mâ0ʹnn000>e
!!€ jåå{ʹØ}tt^VV2V^VOWRaise^PAL>e
!!€ jo0ʹtt000>e
!!€ kuâ{ʹØ}ll^PALE^PAL
!!€ kueʹll00
!!€ veä{ʹØ}kˈk^PAL
!!€ veäʹǩˈǩ0
!!$ veä{ʹØ}kˈk^PAL
!!$ veäʹkˈk0
!!€ stu^1VOW{VU}{ʹØ}l^PAL
!!€ stu0uʹl0
!!€ čââʹlmtẹ^1VOW{ʹØ}m^PAL
!!€ čââʹlmte0ʹm0
             [Vow:|:Vow] (%^1VOW:) (i|u|h:u|h:i) (:ˈ)  _ Cns:+ [Cns:*|%{XC%}:Cns|Cns:* (ˈ:|:ˈ) Cns:*] Vow: Cns:* PenNeutrVowLenghtHeight (%^Pen: %^PALE:|%^Pen: %^PALẸ:)  %^Pen: %^PAL: ;
!!€ kä^1VOW{ʹØ}lles^Pen^V2VV^Pen^PAL
!!€ kääʹlles0000
!!€ jeä{ʹØ}kˈkel^Pen^PALE^Pen^PAL^Pen^KKK2ZERO
!!€ jeeʹ000el000000
!!€ seäh{ʹØ}tter^Pen^PALE^Pen^PAL^Pen^XYY2VY
!!€ seeuʹt0er000000
    _ v  %{ʹØ%}: [ʒ ʒ:|ǯ ǯ:|d d:|g: g:|l l:|r r:|j j:] Vow: Cns* PenNeutrVowLenghtHeight %^Pen: %^PAL: [%>:| (∑) #] ;


    _  h: %{ʹØ%}:0 [c c:|č č:|s s:|š š:|t t:] Vow: Cns*  PenNeutrVowLenghtHeight %^Pen: %^PAL: [%>|(∑) #] ;
!! huʹvǧǧi+N+Sg+Nom: **rattle/suhistin**
!!€ hu^1VOW{ʹØ}v{ʹØ}ggi^Pen^PAL
!!€ hu0ʹv0ǧǧi00
!! The left context allows for:
!! Vowel shortening, and j/v > Vow

"d:đ in weak grade"
!! **@RULENAME@** - used in
 d:đ <=> Vow: (%^1VOW:) (:ʹ|%{ʹØ%}:) _ [g: g:| v v:] NeutrVowLenghtHeightDiphPalAllegroPAL [%^XYY2XY:] ;
!!€ ǩeä0dgg^VOWRaise^PALẸ^PAL^XYY2XY
!!€ ǩiẹʹđj00000
         Vow: (%^1VOW:) (:ʹ|%{ʹØ%}:) _ [g: g:| v v:] Vow: Cns* PenNeutrVowLenghtHeightDiphPalAllegroPAL [%^Pen: %^XYY2XY:] ;


"Even-syllabic verbs I, Palatalization of g:ǧ"
!! **@RULENAME@** - used in
 g:ǧ <=>
         Vow: %^1VOW: (ʹ:|%{ʹØ%}:)      _ (ˈ:|:ˈ) [g:ǧ|g:0] NeutrVowLenghtHeight (%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL: (%^CCC2CC:|%^CCC2CAllegro:|%^CC2CCC:|%^XYY2XY:) RBound ;
         [Vow: - i] (ʹ:|%{ʹØ%}:)        _ (ˈ:|:ˈ) [g:ǧ|g:0] NeutrVowLenghtHeight (%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL: (%^CCC2CC:|%^CCC2CAllegro:|%^CC2CCC:|%^XYY2XY:) RBound ;
         Vow: (%^1VOW:) i (ʹ:|%{ʹØ%}:)  _ (ˈ:|:ˈ) [g:ǧ|g:0] NeutrVowLenghtHeight (%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL: RBound ;
!! reäiʹǧǧ+N+Sg+Nom: **hole/reikä**
!!€ reäi0gg^PAL
!!€ reäiʹǧǧ0
         Vow: (%^1VOW:) (ʹ:|%{ʹØ%}:) [Cns: - [l|r|d:]] _ (ˈ:|:ˈ) g:0 NeutrVowLenghtHeight (%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL: (%^CCC2CC:|%^CC2CCC:|%^XYY2XY:) RBound ;
!! šäʹŋǧǧ+N+Sg+Com: **pasty/piirakka**
!!€ šä^1VOW0ŋgg^V2VV^VOWRaise^PAL^XYY2XY>in
!!€ šaaʹŋǧ00000>in
         Vow: (%^1VOW:) (ʹ:|%{ʹØ%}:) Cns:     _ (ˈ:|:ˈ) g:ǧ NeutrVowLenghtHeight (%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL: (%^CCC2CC:|%^CC2CCC:) RBound ;
!! vueʹlǧǧed+Use/NG+V+Ind+Prs+Pl3 **leave/lähteä**
!!€ vuâ0lgg^PALÄ^PAL>a
!!€ vuäʹlǧǧ00>a

           Vow: (%^1VOW:) (ʹ:|%{ʹØ%}:) (Cns:) g: (ˈ:|:ˈ) _   NeutrVowLenghtHeight (%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL: (%^CCC2CC:|%^CC2CCC:) RBound ;

         Vow: (%^1VOW:) (ʹ:|%{ʹØ%}:) Cns:     _ (ˈ:|:ˈ) g: NeutrVowLenghtHeightDiphTint (%^PAL:) (%^CCC2CC:|%^CC2CCC:) %^PALK: ;
           Vow: (%^1VOW:) (ʹ:|%{ʹØ%}:) (Cns:) g: (ˈ:|:ˈ) _  NeutrVowLenghtHeightDiphTint (%^PAL:) (%^CCC2CC:|%^CC2CCC:) %^PALK: ;
!!€ veâ{ʹØ}rǧǧ^VOWRaise^PALE^PAL^PALK
!!€ vieʹrǧǧ0000

    Vow: (%^1VOW:) 0: _ NeutrVowLenghtHeight %^PAL: ;
!! bioloog+N+Sg+Ill **biologist**
!!€ bioloo0g^PAL>e
!!€ biolooʹǧ0>e

         Vow: (%^1VOW:) (ʹ:|%{ʹØ%}:) Cns: (%{ʹØ%}:)    _ (ˈ:|:ˈ) g:ǧ Vow: Cns* PenNeutrVowLenghtHeight (%^Pen: %^PALE:|%^Pen: %^PALÄ:|%^Pen: %^PALẸ:) %^Pen: %^PAL: (%^Pen: %^CCC2CC:|%^Pen: %^CC2CCC:) RBound ;

           Vow: (%^1VOW:) (ʹ:|%{ʹØ%}:) (Cns:) (%{ʹØ%}:) g: (ˈ:|:ˈ) _   Vow: Cns* PenNeutrVowLenghtHeight (%^Pen: %^PALE:|%^Pen: %^PALÄ:|%^Pen: %^PALẸ:) %^Pen: %^PAL: (%^Pen: %^CCC2CC:|%^Pen: %^CC2CCC:) RBound ;
!! huʹvǧǧi+N+Sg+Nom: **rattle/suhistin**
!!€ hu^1VOW{ʹØ}v{ʹØ}ggi^Pen^PAL
!!€ hu0ʹv0ǧǧi00

         Vow: (%^1VOW:) (ʹ:|%{ʹØ%}:) (Cns:) (%{ʹØ%}:)    _ (ˈ:|:ˈ) g: Vow: Cns* PenNeutrVowLenghtHeightDiphTint (%^Pen: %^PAL:) (%^Pen: %^CCC2CC:|%^Pen: %^CC2CCC:) %^Pen: %^PALK: ;

           Vow: (%^1VOW:) (ʹ:|%{ʹØ%}:) (Cns:) (%{ʹØ%}:) g: (ˈ:|:ˈ) _   Vow: Cns* PenNeutrVowLenghtHeightDiphTint (%^Pen: %^PAL:) (%^Pen: %^CCC2CC:|%^Pen: %^CC2CCC:) %^Pen: %^PALK: ;
!! ääʹǧǧes+N+Sg+Nom: ****
!!€ ä^1VOW{ʹØ}gges^Pen^V2VV^Pen^PAL^Pen^PALK
!!€ ääʹǧǧes000000
!!
!!€ a^1VOW{ʹØ}lggi^Pen^PAL^Pen^PALK
!!€ a0ʹlǧǧi0000


"Even-syllabic verbs I, Palatalization of k:ǩ here"
!! **@RULENAME@** - used in
 k:ǩ <=>
 Vow: (%^1VOW:) (ʹ:|%{ʹØ%}:) (Cns:)   _ (0:|ˈ:) [k: |%{XC%}: ] NeutrVowLenghtHeightDiphTint %^PAL: ([(%^Allegro:) (%^PAL:) %^CC2C:|(%^Allegro:) (%^PAL:) %^CC2CAllegro:|%^CCC2CC:|%^XYY2XY:]) (%^VC:) RBound ;
 Vow: (%^1VOW:) (ʹ:|%{ʹØ%}:) (Cns:)   _ (0:|ˈ:) [k: |%{XC%}: ] Vow: Cns:* PenNeutrVowLenghtHeightDiphTint %^Pen: %^PAL: ([(%^Pen: %^Allegro:) (%^Pen: %^PAL:) %^CC2C:|(%^Pen: %^Allegro:) (%^Pen: %^PAL:) %^Pen: %^CC2CAllegro:|%^Pen: %^CCC2CC:|%^Pen: %^XYY2XY:]) (%^VC:) RBound ;

 Vow: (%^1VOW:) (ʹ:|%{ʹØ%}:) [Cns: - k:]   _ (k: |%{XC%}:) [NeutrVowLenghtHeightDiphTint |Vow: Cns:* PenNeutrVowLenghtHeightDiphTint %^Pen: ] %^PAL: ([ %^CC2C:| %^CC2CAllegro:|%^XYY2XY:| %^Pen: %^CC2C:| %^Pen: %^CC2CAllegro:| %^Pen: %^XYY2XY:]) (%^VC:) RBound ;
!! hääʹsǩ Perhaps the stem should simply be **häskk**
!!€ hää0sk^PAL
!!€ hääʹsǩ0
 Vow: (%^1VOW:) (ʹ:|%{ʹØ%}:) (Cns:)   _ (0:|ˈ:) [k: |%{XC%}: ] NeutrVowLenghtHeightDiphTint (%^PAL:) ([ %^CC2C:| %^CC2CAllegro:|%^CCC2CC:|%^XYY2XY:])  %^PALK: ;
 Vow: (%^1VOW:) (ʹ:|%{ʹØ%}:) (Cns:)   _ (0:|ˈ:) [k: |%{XC%}: ] Vow: Cns:* PenNeutrVowLenghtHeightDiphTint (%^Pen: %^PAL:) ([ %^Pen: %^CC2C:| %^Pen: %^CC2CAllegro:|%^Pen: %^CCC2CC:|%^Pen: %^XYY2XY:]) (%^VC:) %^Pen: %^PALK: ;

 Vow: (%^1VOW:) (ʹ:|%{ʹØ%}:) (Cns:)   k: (0:|ˈ:) _ NeutrVowLenghtHeightDiphTint (%^PAL:) (%^CC2CCC:|%^CCC2CC:)  %^PALK: ;
 Vow: (%^1VOW:) (ʹ:|%{ʹØ%}:) (Cns:)   k: (0:|ˈ:) _ Vow: Cns:* PenNeutrVowLenghtHeightDiphTint (%^Pen: %^PAL:) ( %^Pen: %^CC2CCC:|%^Pen: %^CCC2CC:) (%^VC:) %^Pen: %^PALK: ;




 Vow: (%^1VOW:) (ʹ:|%{ʹØ%}:) (Cns:)   _ (0:|ˈ:) [k: |%{XC%}:] Vow: Cns:+ PenNeutrVowLenghtHeightDiphTint %^Pen: %^PAL: ( [ %^Pen: %^CC2C:| %^Pen: %^CC2CAllegro:|%^Pen: %^XYY2XY:]) (%^VC:) RBound ;

!! cieʹǩǩes+N+Sg+Ill: **trick, type of ear mark/tikki, pykälä**
!!€ ceä{ʹØ}kˈkes^Pen^PALẸ^Pen^PAL^Pen^CCC2CC
!!€ ciẹʹǩ0ǩes000000

!! mieʹlǩǩ+N+Sg+Acc: **milk/maito**
!!€ meâ0lkk^VOWRaise^PALE^PAL^CC2C
!!€ mieʹlǩ00000
 Vow: (%^1VOW:) (ʹ:|%{ʹØ%}:)        _ (ˈ:|:ˈ) [k: |%{XC%}:] NeutrVowLenghtHeight (%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL: (%^CC2CCC:|%^CCC2CC:|%^C2CC:) RBound ;
  Vow: (%^1VOW:) (ʹ:|%{ʹØ%}:) (Cns:) k: (ˈ:|:ˈ) _   NeutrVowLenghtHeight (%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL: (%^CC2CCC:|%^CCC2CC:) RBound ;
  Vow: (%^1VOW:) (ʹ:|%{ʹØ%}:) (Cns:) k: (ˈ:|:ˈ) _  Vow: Cns:* PenNeutrVowLenghtHeight (%^Pen: %^PALE:|%^Pen: %^PALÄ:|%^Pen: %^PALẸ:) %^Pen: %^PAL: (%^Pen: %^CC2CCC:|%^Pen: %^CCC2CC:) RBound ;

!! rääʹǩǩes+A+Sg+Nom **beloved/rakas**
!!€ rä^1VOW0kkes^Pen^V2VV^Pen^PAL
!!€ rääʹǩǩes0000
!!€ jo^1VOW0kk^PAL>e
!!€ jo0ʹǩǩ0>e
!!€ saa0kk^VV2V^PAL>e
!!€ sa0ʹǩǩ00>e
  Vow: (%^1VOW:|s|š) _ (PenBetweenStemAndVowelLoss) (%^RmVow:) %^PALK: ;
!! kåålvak+N+Sg+Loc
!!€ kåålvak^RmVow^PALK>est
!!€ kåålv0ǩ00>est
!^Pen^VV2V^RmVow^PALK

!! tõiŋsǩed+V+Inf
!!€ tõõiŋâsk^Pen^VV2V^RmVow^PALK>ed
!!€ tõ0iŋ0sǩ0000>ed
!!$ tõõiŋâsk^Pen^VV2V^RmVow^PALK>ed
!!$ tõ0iŋ0sk0000>ed

         Vow: (%^1VOW:) (ʹ:|%{ʹØ%}:) Cns: (%{ʹØ%}:)    _ (ˈ:|:ˈ) k: Vow: Cns* PenNeutrVowLenghtHeightDiphTint (%^Pen: %^PAL:) (%^Pen: %^CCC2CC:|%^Pen: %^CC2CCC:) %^Pen: %^PALK: ;

           Vow: (%^1VOW:) (ʹ:|%{ʹØ%}:) (Cns:) (%{ʹØ%}:) k: (ˈ:|:ˈ) _   Vow: Cns* PenNeutrVowLenghtHeightDiphTint (%^Pen: %^PAL:) (%^Pen: %^CCC2CC:|%^Pen: %^CC2CCC:) %^Pen: %^PALK: ;
!!



"Even-syllabic nouns I, Depalatalization of ǩ:k"
!! **@RULENAME@** - used in
 ǩ:k <=>
!Not needed here
!!mäʹtǩǩ+N+Sg+Ill
!!€ mätkk>a
!!€ mätkk>a
   Vow: (%^1VOW:) (%{ʹØ%}:) _ %{XC%}: %^PALNo: ;
!! kaađneǩ+N+Sg+Ill:
!!€ kaađnẹ^1VOW{ʹØ}ǩ{XC}^PALNo^C2CC>a
!!€ kaađnẹ00kk00>a

 Vow: (%^1VOW:) (ˈ:|:ˈ) (Cns) _ PenBetweenStemAndVowelLoss (%^RmVow:) %^PALNo: ;
!! prääʹzniǩ+N+Sg+Ill: **celebration/juhla**
!!€ prääʹzniǩ^RmVow^PALNo>a
!!€ prääʹzn0k00>a
!!€ kaaddâšǩ^Pen^VV2V^Pen^CC2C^RmVow^PALNo>a
!!€ ka0d00šk000000>a
!20240419 Cns: (ˈ:|:ˈ) _ Vow: Cns:* PenNeutrVowLenghtHeightDiphPalAllegroPAL (%^RmVow:) %^PALNo: (%^VC:) RBound ;
!!$ vieʹǩǩet%^PALNo
!!$ vieʹǩket0


"Even-syllabic nouns, removing palatalization in -est +Loc nouns"
!! **@RULENAME@** - removing palatalization in +Sg+Ill,  pieʹss:peässa
   ʹ:0 <=>  [Vow:|:Vow] (%^1VOW:) _ [:Cns :Cns+|Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+] NeutrVowLenghtHeight %^PALNo: [RBound|%^UltRmVow:|%^VC:] ;
!Not needed here
!!€ čââʹlmtẹ^1VOWm{XC}^C2CC>a
!!€ čââʹlmtẹ0mm0>a
   Vow: (%^1VOW:)  _ [:Cns :Cns+|Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+] NeutrVowLenghtHeight %^PALNo: ;
   [Vow:|:Vow] (%^1VOW:) _ [:Cns :Cns+|Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+] %^RmVow: %^PALNo: [RBound|%^UltRmVow:|%^VC:] ;
!CHECKME
!! stroitteʹl+N+Sg+Ill: **plant/laitos**
!!€ stroitteʹl^RmVow^PALNo>a
!!€ stroitt00l00>a
  [Vow:|:Vow] (%^1VOW:) _ [:Cns :Cns+|Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+] Vow: Cns:* PenNeutrVowLenghtHeight %^Pen: %^PALNo:  ;
!! prääʹzniǩ+N+Sg+Ill: **celebration/juhla**
!!€ prääʹzniǩ^RmVow^PALNo>a
!!€ prääʹzn0k00>a

!jurdded
                    Vow: (%^1VOW:) _ CNoV: Cns:+ NeutrVowLenghtHeight %^PALNo: ;
                    Vow: (%^1VOW:) _ v:u ;



!! ## Consonant QUANTITY CHANGE gradation rules

!! ### Weakening Consonant Cluster

!! dealing with relative length changes
!! mõõnnâd : mõʹnne : mõõn

!"Even syllabic verbs I, cg in relation to Vowel length"
! !Vow Cx Cx:0

"Even syllabic verbs I, cg m:0"
!! **@RULENAME@** - used with  +Imp+Sg2, +Ind+Prs+ConNeg,
 m:0 <=>
 Vow: (%^1VOW:|%{ˈ1%}:|0:) (ʹ:|%{ʹØ%}:) (s|r|l|p|d:)  m: (ˈ:) _ NeutrVowLenghtHeightDiphPalAllegroPAL [(%^Allegro:) (%^PAL:) %^CC2C:|(%^Allegro:) (%^PAL:) %^CC2CAllegro:|%^CCC2C:|%^XYY2XY:] ;
!! oolmaž+N+Sg+Nom: **person/henkilö**
!!€ o^1VOWlmm^V2VV^XYY2XY>až
!!€ oolm000>až
!!€ sää0mm^PAL^CC2C
!!€ sääʹm000
         Vow: (%^1VOW:|%{ˈ1%}:|0:) (ʹ:|%{ʹØ%}:) h: (ʹ:|%{ʹØ%}:)      m: (ˈ:) _ NeutrVowLenghtHeightDiphPalAllegroPAL [[(%^Allegro:) (%^PAL:) %^CC2C:|(%^Allegro:) (%^PAL:) %^CC2CAllegro:] |%^CCC2C:|%^XYY2XY:] ;
         m: (ˈ:) _ Vow: (%^1VOW:) Cns:+ (%{XC%}:) PenNeutrVowLenghtHeightDiphPalAllegroPAL [%^Pen: [(%^Allegro:) (%^PAL:) %^CC2C:|(%^Allegro:) (%^PAL:) %^CC2CAllegro:]|%^Pen: %^K2GG:|%^Pen: %^CCC2C:|%^Pen: %^XYY2XY:] ;
!Not needed here
!! kåʹmrded+V+Ind+Prs+Sg3 **bow**
!!€ kååʹmmerd^PenVow2a
!!€ kååʹmmard0
!! mainstummuš+N+Err/Orth+Sg+Gen: **story telling/tarinointi**
!!$ mainstummu%^1VOWš{XC}^Pen^CC2C^V2VV%^K2GG
!!$ mainstummuužž0000

"Even syllabic verbs I, cg for b"
!! **@RULENAME@** - used
 b:0 <=> Vow: (%^1VOW:)  [i (ʹ:|%{ʹØ%}:)|(ʹ:|%{ʹØ%}:) Cns: ] b _   NeutrVowLenghtHeightDiphPalAllegroPAL [(%^Allegro:) (%^PAL:) %^CC2C:|(%^Allegro:) (%^PAL:) %^CC2CAllegro:|%^XYY2XY:] ;
  Vow: (:ˈ) %{ʹØ%}: b ˈ: _ NoCnsDummy %^CC2CAllegro: ;
!! neiʹbb+N+Sg+Gen: **knife/veitsi**
!!€ nẹ^1VOWi0bb^V2VV^PAL^CC2C
!!€ neeiʹb0000
!! vuäʹbb+N+Sg+Loc+PxSg2
!!€ vuä{ʹØ}bˈb^VOWRaise^PALẸ^Allegro^PAL^CCC2CAllegro>stad
!!€ vuẹʹb0000000>stad
!! äʹrbb+N+Sg+Gen: **heritage/perintö**
!!€ ä^1VOW0rbb^V2VV^PAL^XYY2XY
!!€ ääʹrb0000
 Vow: (%^1VOW:)  (i (ʹ:|%{ʹØ%}:)|%{ˈ1%}:|(ʹ:|%{ʹØ%}:) Cns: ) b _ Vow: Cns:*   PenNeutrVowLenghtHeightDiphPalAllegroPAL [(%^Pen: %^Allegro:) (%^Pen: %^PAL:) %^Pen: %^CC2C:|(%^Pen: %^Allegro:) (%^Pen: %^PAL:) %^Pen: %^CC2CAllegro:|%^Pen: %^XYY2XY:] ;
!! joorbâs+A+Sg+Nom
!!€ jo^1VOWrbbâs^Pen^V2VV^Pen^XYY2XY
!!€ joorb0âs0000
!! kuâbsted
!!€ kuâ{ˈ1}bbast^Pen^CC2CAllegro^RmVow
!!€ kuâˈb00st000

"Even syllabic verbs I, second consonant loss p:0"
!! **@RULENAME@** - used in
 p:0 <=> Vow: (%^1VOW:|%{ˈ1%}:|0:) (ʹ:|%{ʹØ%}:) (Cns:) p: _ NeutrVowLenghtHeightDiphPalAllegroPAL [(%^Allegro:) (%^PAL:) %^CC2C:|(%^Allegro:) (%^PAL:) %^CC2CAllegro:|%^XYY2XY:] ;
!!€ ǩiâpp^CC2C
!!€ ǩiâv00
          Vow: (%^1VOW:|%{ˈ1%}:|0:) (ʹ:|%{ʹØ%}:)       (Cns:)      p: (ˈ:) _ Vow: [Cns:+|n j|l m|s ǩ:|s m|s t|š ǩ:|š m] PenNeutrVowLenghtHeightDiphPalAllegroPAL  [%^Pen: [(%^Allegro:) (%^PAL:) %^CC2C:|(%^Allegro:) (%^PAL:) %^CC2CAllegro:]|%^Pen: %^CCC2C: |%^Pen: %^XYY2XY:] ;
!!€ vuäʹppest^Pen^CC2CAllegro^RmVow>ed
!!€ vuäʹp00st000>ed
!!€ ka^1VOWmppâr^Pen^V2VV^Pen^XYY2XY
!!€ kaamp0âr0000
!!$ ka%^1VOWmppâr^Pen^V2VV^Pen^XYY2XY
!!$ kaamppâr0000
  i j:   p _ Vow: Cns:* %^Pen: %^XYY2VY: ;
!! riipâs+N+Sg+Nom: **snail/etana**
!!€ rijppâs ^Pen:0 ^XYY2VY:0
!!€ riip0âs ^Pen:0 ^XYY2VY:0

"Even syllabic verbs I, cg v:0"
!! **@RULENAME@** - used with  +Imp+Sg2, +Ind+Prs+ConNeg, teevvad > teev
 v:0 <=>
  v _    NeutrVowLenghtHeightDiphPalAllegroPAL [(%^Allegro:) (%^PAL:) %^CC2C:|(%^Allegro:) (%^PAL:) %^CC2CAllegro:|%^XYY2XY:] ;
!!peiʹvv+N+Sg+Gen+Indef
!!€ pẹ^1VOWi0vv^V2VV^PAL^CC2C
!!€ peeiʹv0000
 v (ˈ:) _  Vow: Cns:+ (%{XC%}:)  PenNeutrVowLenghtHeightDiphPalAllegroPAL %^Pen: [%^CC2C:|%^CC2CAllegro:|%^XYY2XY:| %^CCC2C:] ;
!! aasvâs+N+Sg+Nom: ****
!!€ a^1VOWsvvâs^Pen^V2VV^Pen^XYY2XY
!!€ aasv0âs0000
!! tuõvâs+N+Sg+Nom: **candle/tuohus, kynttilä**
!!€ tuõvˈvâs^Pen^CCC2C
!!€ tuõv00âs00
! u: %^1VOW: (:ʹ|%{ʹØ%}:) _ Cns:+ %^VV2V: %^PAL: %^XYY2VY: ; 2017-08-21
!! juʹvjj+N+Sg+Loc+PxSg3:
!!€ ju^1VOWv0jj^VV2V^PAL^XYY2VY>stes
!!€ ju0uʹj0000>stes
 u %^1VOW: %{ʹØ%}: _ %{ʹØ%}: Cns:+ %^Allegro: %^PAL: %^XYY2VY: ; !2024-05-20
!!€ ju{ʹØ}v{ʹØ}jj^VV2V^PAL^XYY2VY>stes
!!€ juʹ00j0000>stes

!j u ^1VOW:0 {ʹØ}:0 v:0 {ʹØ}:ʹ j j:0 ^Allegro:0 ^PAL:0 ^XYY2VY:0 > s t e s


"f:0"
!! **@RULENAME@** - used in

 f:0 <=>
      f: _    NeutrVowLenghtHeightDiphPalAllegroPAL [(%^Allegro:) (%^PAL:) %^CC2C:|(%^Allegro:) (%^PAL:) %^CC2CAllegro:|%^XYY2XY:] ;
!! kaaʹff+N+Sg+Gen **coffee**
!!€ kaa0ff^PAL^CC2C
!!€ kaaʹf000


"Even syllabic verbs I, cg n:0"
!! **@RULENAME@** - used with  +Imp+Sg2, +Ind+Prs+ConNeg, jiõnn:jiõn
 n:0 <=>
      n: (ˈ:)  _  (j)  NeutrVowLenghtHeightDiphPalAllegroPAL [(%^Allegro:) (%^PAL:) %^CC2C:|(%^PALE: %^PAL:) %^CCC2C:|(%^Allegro:) ((%^PALE:|%^PALẸ:) %^PAL:) %^CC2CAllegro:|%^CCC2CAllegro:|%^XYY2XY:|%^XYY2VY:] ;
!!päʹrnn
!! vueʹn+N+Sg+Nom: **mother-in-law/anoppi**
!!€ vuâ0nˈn^PALE^PAL^CCC2C
!!€ vueʹn00000
!! čuânj+N+Sg+Nom: **goose/hanhi**
!!€ čuõnˈnj^VOWLower^CCC2C
!!€ čuân00j00
!! jeäʹnn+N+Sg+Loc+PxSg1: **mother/äiti**
!!€ jeä0{ʹØ}nˈn^PALẸ^PAL^CCC2CAllegro>stan
!!€ jiẹˈʹn00000>stan
         Vow: (%^1VOW:)  (ʹ:|%{ʹØ%}:) h: (ʹ:|%{ʹØ%}:) n: _ (j) NeutrVowLenghtHeightDiphPalAllegroPAL [(%^Allegro:) (%^PAL:) %^CC2C:|(%^Allegro:) (%^PAL:) %^CC2CAllegro:|%^XYY2XY:] ;
      n: (ˈ: ) _ (j) Vow: Cns:*  PenNeutrVowLenghtHeightDiphPalAllegroPAL [(%^Pen: %^Allegro:) (%^Pen: %^PAL:) %^Pen: %^CC2C:|(%^Pen: %^Allegro:) (%^Pen: %^PAL:) %^Pen: %^CCC2C:|(%^Pen: %^Allegro:) (%^Pen: %^PAL:) %^Pen: %^CC2CAllegro:|%^Pen: %^XYY2XY:|%^Pen: %^XYY2VY:] ;
!! huânas+N+Sg+Nom: ****
!!€ huânˈnas^Pen^CCC2C
!!€ huân00as00

"Even syllabic nouns I, with extra lengthening of vowel ij>ii/uv>uu and dd>d"
!! **@RULENAME@** - used with  +N+Sg+Nom > +N+Sg+Gen, sijdd > siid

 d:0 <=> d _ NeutrVowLenghtHeightDiphPalAllegroPAL [(%^Allegro:) (%^PAL:) %^CC2C:|(%^Allegro:) (%^PAL:) %^CC2CAllegro:|%^XYY2XY:] ;
!!€ ä^1VOWldd^V2VV^XYY2XY
!!€ ääld000
!!€ ä^1VOWldd^V2VV^VOWRaise^XYY2XY>už
!!€ aald0000>už
 [v:| j:] (%{ʹØ%}:) d _  NeutrVowLenghtHeightDiphPalAllegroPAL  [%^XYY2VY:] ;
!!€ u^1VOWv0dd^PAL^XYY2VY
!!€ u0uʹd000
!! juʹrdded+V+Ind+Prs+Sg1 **think**
!!€ ju^1VOWrdd^V2VV^VOWLower^XYY2XY>am
!!€ joord0000>am

!! uʹvdded+V+Ind+Prs+4:
!!€ u^1VOWv0dd^PAL^XYY2VY>et
!!€ u0uʹd000>et


! njeiddad > njeeid
! 2012-06-12 v:u allows for uʹvdded
!! - idd:id, +Imp+Sg2, +Ind+Prs+ConNeg double consonants following vowel and "i" at coda that become single consonants in gradation
         d _ Vow: [Cns:+|n j|l m|s ǩ:|s m|s t|š ǩ:|š m]  PenNeutrVowLenghtHeightDiphPalAllegroPAL [(%^Pen: %^Allegro:) (%^Pen: %^PAL:) %^Pen: %^CC2C:|(%^Pen: %^Allegro:) (%^Pen: %^PAL:) %^Pen: %^CC2CAllegro: |%^Pen: %^XYY2XY:|%^Pen: %^XYY2VY:] ;
!! uudâs+N+Sg+Nom: **creature/luomus**
!!€ uvddâs^Pen^XYY2VY
!!€ uud0âs00





"Even syllabic verbs I, cg for ʒ"
!! **@RULENAME@** - used with  pääʹʒʒelm+N+Sg+Ill: päʹʒlma
 ʒ:0 <=>
      u: (ʹ:|%{ʹØ%}:) v:u (%{ʹØ%}:) ʒ _  NeutrVowLenghtHeightDiphPalAllegroPAL %^XYY2VY: ;
      [Vow - u] (%^1VOW:) (0:) (i|u) (%{ʹØ%}:) ʒ _  NeutrVowLenghtHeightDiphPalAllegroPAL [%^XYY2XY:|[(%^Allegro:) (%^PAL:) %^CC2C:|(%^Allegro:) (%^PAL:) %^CC2CAllegro:]] ;
!! sauʒʒ+N+Pl+Nom **sheep/lammas**
!!€ sa^1VOWuʒʒ^V2VV^XYY2XY
!!€ saauʒ000

        Vow: (%^1VOW:|%{ˈ1%}:|0:) (ʹ:|%{ʹØ%}:) ʒ _ Vow: [Cns:+|n j|l m|s ǩ:|s m|s t|š ǩ:|š m]  PenNeutrVowLenghtHeightDiphPalAllegroPAL  [%^Pen: [(%^Allegro:) (%^PAL:) %^CC2C:|(%^Allegro:) (%^PAL:) %^CC2CAllegro:]|%^Pen: %^XYY2XY:] ;
      [v:| j:] (%{ʹØ%}:) ʒ _  NeutrVowLenghtHeightDiphPalAllegroPAL  [%^XYY2VY:] ;


"ǯ:0"
!! **@RULENAME@** - used in
 ǯ:0 <=>
      Vow: (%^1VOW:|%{ˈ1%}:|0:) (ʹ:|:ʹ|%{ʹØ%}:) [ v: (%{ʹØ%}:)|Cns:] ǯ _  NeutrVowLenghtHeightDiphPalAllegroPAL [(%^Allegro:) (%^PAL:) %^CC2C:|(%^Allegro:) (%^PAL:) %^CC2CAllegro:|(%^PAL:) %^XYY2XY:|(%^PAL:)  %^XYY2VY: ] ;
!! kuʹvǯǯ+N+Sg+Gen
!!€ ku^1VOW{ʹØ}v{ʹØ}ǯǯ^PAL^XYY2VY
!!€ ku00uʹǯ000
        Vow: (%^1VOW:|%{ˈ1%}:|0:) (ʹ:|%{ʹØ%}:) (Cns:) ǯ _ Vow: [Cns:+|n j|l m|s ǩ:|s m|s t|š ǩ:|š m] PenNeutrVowLenghtHeightDiphPalAllegroPAL  [%^Pen: [(%^Allegro:) (%^PAL:) %^CC2C:|(%^Allegro:) (%^PAL:) %^CC2CAllegro:]| (%^Pen: %^PAL:) %^Pen: %^XYY2XY:|(%^Pen: %^PAL:) %^Pen: %^XYY2VY:] ;


"č:0"
!! **@RULENAME@** - used in
 č:0 <=>
        Vow: (%^1VOW:|%{ˈ1%}:|0:) (ʹ:|%{ʹØ%}:) Cns: č _ Vow: [Cns:+|n j|l m|s ǩ:|s m|s t|š ǩ:|š m] PenNeutrVowLenghtHeightDiphPalAllegroPAL  [%^Pen: [(%^Allegro:) (%^PAL:) %^CC2C:|(%^Allegro:) (%^PAL:) %^CC2CAllegro:]|%^Pen: %^XYY2XY:] ;

       Vow: (%^1VOW:) [ (ʹ:|%{ʹØ%}:)  Cns: |(ʹ:|%{ʹØ%}:) [h:|j:] (ʹ:|%{ʹØ%}:) | [u|i] (ʹ:|%{ʹØ%}:) ] č: _ [NeutrVowLenghtHeightDiphPalAllegroPAL | Vow: Cns:+ PenNeutrVowLenghtHeightDiphPalAllegroPAL %^Pen:] [(%^Allegro:) (%^PAL:) %^CC2C:|(%^Allegro:) (%^PAL:) %^CC2CAllegro:|%^XYY2XY:|%^XYY2VY:] ;
       Vow: (%^1VOW:|%{ˈ1%}:|0:) (ʹ:|%{ʹØ%}:) č:  _ NeutrVowLenghtHeightDiphPalAllegroPAL %^XYY2VY: ;
    Vow: (%^1VOW:|%{ˈ1%}:|0:) (ʹ:|%{ʹØ%}:) č _ Vow: [Cns:+|n j|l m|s ǩ:|s m|s t|š ǩ:|š m] PenNeutrVowLenghtHeightDiphPalAllegroPAL %^Pen: (%^Allegro:) (%^PAL:) %^CC2CAllegro: ;

    Vow: (%^1VOW:) ( [u|i] (ʹ:|%{ʹØ%}:) ) č: _ [NeutrVowLenghtHeightDiphPalAllegroPAL | Vow: Cns:+ PenNeutrVowLenghtHeightDiphPalAllegroPAL %^Pen:] (%^Allegro:) (%^PAL:) %^CC2CAllegro: ;


"c:0"
!! **@RULENAME@** - used in
 c:0 <=>
        (ʹ:|%{ʹØ%}:) Cns: c _ Vow: [Cns:+|n j|l m|s ǩ:|s m|s t|š ǩ:|š m] PenNeutrVowLenghtHeightDiphPalAllegroPAL  [%^Pen: [(%^Allegro:) (%^PAL:) %^CC2C:|(%^Allegro:) (%^PAL:) %^CC2CAllegro:]|%^Pen: %^XYY2XY:|%^Pen: %^XYY2VY:] ;
!!
!!€ kä^1VOW{ʹØ}h{ʹØ}cces^Pen^V2VV^Pen^PAL^Pen^XYY2VY
!!€ kä00uʹc0es000000
       Vow: (%^1VOW:) [ (ʹ:|%{ʹØ%}:)  Cns: | (ʹ:|%{ʹØ%}:) [h:|j:] (ʹ:|%{ʹØ%}:) | [u|i] (ʹ:|%{ʹØ%}:) ] c: _ [NeutrVowLenghtHeightDiphPalAllegroPAL|Vow: Cns:+ PenNeutrVowLenghtHeightDiphPalAllegroPAL %^Pen:] [(%^Allegro:) (%^PAL:) %^CC2C:|(%^Allegro:) (%^PAL:) %^CC2CAllegro:|%^XYY2XY:|%^XYY2VY:] ;
       Vow: (%^1VOW:) (%{ˈ1%}:|0:) (ʹ:|%{ʹØ%}:) c:  _ [NeutrVowLenghtHeightDiphPalAllegroPAL %^KK2GAllegro:|Vow: Cns:+ PenNeutrVowLenghtHeightDiphPalAllegroPAL [(%^Pen: %^Allegro:) (%^Pen: %^PAL:) %^Pen: %^CC2C:|(%^Pen: %^Allegro:) (%^Pen: %^PAL:) %^Pen: %^CC2CAllegro:] ] ;

"ž:0"
!! **@RULENAME@** - used in
 ž:0 <=>
      Vow: (%^1VOW:|%{ˈ1%}:|0:) (ʹ:|%{ʹØ%}:) (Cns:) ž _  NeutrVowLenghtHeightDiphPalAllegroPAL [(%^Allegro:) (%^PAL:) %^CC2C:|(%^Allegro:) (%^PAL:) %^CC2CAllegro:|%^XYY2XY:] ;
         (ʹ:|%{ʹØ%}:) Cns: ž _ Vow: [Cns:+|n j|l m|s ǩ:|s m|s t|š ǩ:|š m] PenNeutrVowLenghtHeightDiphPalAllegroPAL  [%^Pen: [(%^Allegro:) (%^PAL:) %^CC2C:|(%^Allegro:) (%^PAL:) %^CC2CAllegro:]|%^Pen: %^XYY2XY:] ;

      Vow: (%^1VOW:|%{ˈ1%}:|0:) (ʹ:|%{ʹØ%}:) (Cns:) ž _ Vow: Cns:+  PenNeutrVowLenghtHeightDiphPalAllegroPAL [(%^Pen: %^Allegro:) (%^Pen: %^PAL:) %^Pen: %^CC2C:|(%^Pen: %^Allegro:) (%^Pen: %^PAL:) %^Pen: %^CC2CAllegro:|%^Pen: %^XYY2XY:] ;
!! väžsted+V+Inf
!!€ vääžžast^Pen^VV2V^Pen^CC2CAllegro^RmVow^PALK>ed
!!€ vä0ž00st000000>ed

"z:0"
!! **@RULENAME@** - used in
 z:0 <=>
      Vow: (%^1VOW:|%{ˈ1%}:|0:) (ʹ:|%{ʹØ%}:) (Cns:) z _  NeutrVowLenghtHeightDiphPalAllegroPAL [(%^Allegro:) (%^PAL:) %^CC2C:|(%^Allegro:) (%^PAL:) %^CC2CAllegro:|%^XYY2XY:] ;
        (ʹ:|%{ʹØ%}:) Cns: z _ Vow: [Cns:+|n j|l m|s ǩ:|s m|s t|š ǩ:|š m] PenNeutrVowLenghtHeightDiphPalAllegroPAL  [%^Pen: [(%^Allegro:) (%^PAL:) %^CC2C:|(%^Allegro:) (%^PAL:) %^CC2CAllegro:]|%^Pen: %^XYY2XY:] ;
      Vow: _ PenBetweenStemAndVowelLoss (%^PenVow2a:) %^RmCns: ;
!!€ ǩeâsttez^PenVow2a^RmCns
!!€ ǩeâstta000
        (ʹ:|%{ʹØ%}:) (Cns:) z _ Vow: [Cns:+|n j|l m|s ǩ:|s m|s t|š ǩ:|š m] PenNeutrVowLenghtHeightDiphPalAllegroPAL  [(%^Pen: %^Allegro:) (%^Pen: %^PAL:) %^Pen: %^CC2C:|(%^Pen: %^Allegro:) (%^Pen: %^PAL:) %^Pen: %^CC2CAllegro:|%^Pen: %^XYY2XY:] ;
!! juzrdâd+V+Inf:
!!€ juuzzârd^Pen^VV2V^Pen^CC2C^RmVow
!!€ ju0z00rd00000

"Even syllabic verbs I, cg for đ"
!! **@RULENAME@** - used with  ǩiđđ:ǩiiđ
 đ:0 <=>
 Vow: (%^1VOW:|%{ˈ1%}:|0:) (ʹ:|%{ʹØ%}:) đ: _    NeutrVowLenghtHeightDiphPalAllegroPAL [(%^Allegro:) (%^PAL:) %^CC2C:|(%^Allegro:) (%^PAL:) %^CC2CAllegro:] ;
!!€ ǩi^1VOWđđ^V2VV^CC2C
!!€ ǩiiđ000
          đ: (ˈ:) _ Vow: Cns:*  PenNeutrVowLenghtHeightDiphPalAllegroPAL [(%^Pen: %^Allegro:) (%^Pen: %^PAL:) %^Pen: %^CC2C:|(%^Pen: %^Allegro:) (%^Pen: %^PAL:) %^Pen: %^CC2CAllegro:| %^Pen: %^CCC2C:|%^Pen: %^XYY2XY:] ;
!!€ o^1VOWđđâs^Pen^V2VV^Pen^CC2C
!!€ oođ0âs0000

"Even syllabic verbs I, cg for r"
!! **@RULENAME@** - used with  võrr:võõr
 r:0 <=>
 Vow: (%^1VOW:) (i|u) (ʹ:|%{ʹØ%}:) (Cns:)  r: (ˈ:) _    NeutrVowLenghtHeightDiphPalAllegroPAL [(%^Allegro:) (%^PAL:) %^CC2C:|(%^Allegro:) (%^PAL:) %^CC2CAllegro:|%^CCC2C:|%^XYY2XY:|%^XYY2VY:] ;
!!€ võ^1VOWrr^V2VV^CC2C
!!€ võõr000
!!€ paarr^CC2C
!!€ paar00
!!€ jä^1VOWu0rr^V2VV^PAL^CC2C
!!€ jääuʹr0000
 Vow: (%^1VOW:) (i|u) (ʹ:|%{ʹØ%}:) (Cns:) r: (ˈ:) _  Vow: Cns:+  PenNeutrVowLenghtHeightDiphPalAllegroPAL [%^Pen: [(%^Allegro:) (%^PAL:) %^CC2C:|(%^Allegro:) (%^PAL:) %^CC2CAllegro:]|%^Pen: %^CCC2C:|%^Pen: %^XYY2XY:|%^Pen: %^XYY2VY:] ;
!! čårsted+V+Inf: **descriminate/syrjiä**
!!€ čåårrast^Pen^VV2V^Pen^CC2C^RmVow>ed
!!€ čå0r00st00000>ed
!!$ čåårrast^Pen^VV2V^Pen^CC2C^RmVow>ed
!!$ čå0rr0st00000>ed
      [v:| j:] (%{ʹØ%}:) r _  NeutrVowLenghtHeightDiphPalAllegroPAL  [%^XYY2VY:] ;


"Even syllabic verbs I, cg for l"
!! **@RULENAME@** - used with  vuʹvll+N+Sg+Gen: vuuʹl
 l:0 <=>
 Vow: (%^1VOW:|%{ˈ1%}:|0:) (ʹ:|%{ʹØ%}:) (Cns:) l: (ˈ:) _ (j|%{ʼØ%}: i:)   NeutrVowLenghtHeightDiphPalAllegroPAL [(%^Allegro:) (%^PAL:) %^CC2C:|(%^Allegro:) (%^PAL:) %^CC2CAllegro:|%^CCC2C:|%^XYY2XY:|%^XYY2VY:] ;
! Vow: (%^1VOW:|%{ˈ1%}:|0:) (ʹ:|%{ʹØ%}:) (Cns:) l: (ˈ:) _ (j)   NeutrVowLenghtHeightDiphPalAllegroPAL [(%^Allegro:) (%^PAL:) %^CC2C:|(%^Allegro:) (%^PAL:) %^CC2CAllegro:|%^CCC2C:|%^XYY2XY:|%^XYY2VY:] ;
!! deriving
!!€ to^1VOWll^V2VV^CC2C
!!€ tool000
!!€ stä^1VOW0llj^V2VV^PAL^CC2C
!!€ stääʹl0j000
!! alttõõlli+N+NomAg+Pl+Gen
!!€ alttõõll{ʼØ}i^CC2C
!!€ alttõõl00i0

 Vow: (%^1VOW:|%{ˈ1%}:|0:) (ʹ:|%{ʹØ%}:) (Cns:) l: (ˈ:) _ (j) Vow: [Cns:+|n j|l m|s ǩ:|s m|s t|š ǩ:|š m]  PenNeutrVowLenghtHeightDiphPalAllegroPAL [(%^Pen: %^Allegro:) (%^Pen: %^PAL:) %^Pen: %^CC2C:|(%^Pen: %^Allegro:) (%^Pen: %^PAL:) %^Pen: %^CC2CAllegro:|%^Pen: %^CCC2C:|%^Pen: %^XYY2XY:|%^Pen: %^XYY2VY:] ;
! Vow: (%^1VOW:|%{ˈ1%}:|0:) (ʹ:|%{ʹØ%}:) (Cns:) l: (ˈ:) _ (j)   PenNeutrVowLenghtHeightDiphPalAllegroPAL [(%^Pen: %^Allegro:) (%^Pen: %^PAL:) %^Pen: %^CC2C:|(%^Pen: %^Allegro:) (%^Pen: %^PAL:) %^Pen: %^CC2CAllegro:|%^Pen: %^CCC2C:|%^Pen: %^XYY2XY:|%^Pen: %^XYY2VY:] ;
!! pääʹljes+N+Sg+Nom: **path/polku**
!!€ pä^1VOW0lljes^Pen^V2VV^Pen^PAL^Pen^CC2C
!!€ pääʹl0jes000000
!! talsted+V+Inf:
!!€ taallâst^Pen^VV2V^Pen^CC2C^RmVow>ed
!!€ ta0l00st00000>ed

   u: (ʹ:|%{ʹØ%}:) v:u (%{ʹØ%}:) l: _  %^V2VV: ((%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:|%^PALNo:) [(%^Allegro:) (%^PAL:) %^CC2C:|(%^Allegro:) (%^PAL:) %^CC2CAllegro:|%^CCC2C:|%^XYY2XY:|%^XYY2VY:] ;
!!  vuʹvll+N+Sg+Gen:
!!€ vu^1VOWv0ll^PAL^XYY2VY
!!€ vu0uʹl000
      [v:| j:] (%{ʹØ%}:) l _  NeutrVowLenghtHeightDiphPalAllegroPAL  [%^XYY2VY:] ;

!! deriving kueʹll+N+Sg+Gen: kueʹl
!!€ kuâ0ll^V2VV^PALE^PAL^CC2C
!!€ kueʹl00000

"j:0 "
!! **@RULENAME@** - used in
 j:0 <=>
     j: (ˈ:) _ [ NeutrVowLenghtHeightDiphPalAllegroPAL (%^J2I:) | Vow: Cns:+ PenNeutrVowLenghtHeightDiphPalAllegroPAL %^Pen: ] [(%^Allegro:) (%^PAL:) %^CC2C:|(%^Allegro:) (%^PAL:) %^CC2CAllegro:| %^CCC2C:|%^XYY2XY:|%^XYY2VY:] ;

!! ǩeʹrjj+N+Pl+Nom: **book/kirja**
!!€ ǩẹ^1VOW0rjj^V2VV^PAL^XYY2XY
!!€ ǩeeʹrj0000
!! tuâjj+N+Sg+Acc: **work/työ**
!!€ tuâjj^CC2C
!!€ tuâj00
!! tuâjj+N+Sg+Gen:tuâi
!!€ tuâjj^J2I^CC2C
!!€ tuâi000
!    j: _ Vow: Cns:+ PenNeutrVowLenghtHeightDiphPalAllegroPAL [%^Pen: [(%^Allegro:) (%^PAL:) %^CC2C:|(%^Allegro:) (%^PAL:) %^CC2CAllegro:]|%^Pen: %^XYY2XY:|%^Pen: %^XYY2VY:] ;
!!€ po^1VOWjjâs^Pen^V2VV^Pen^CC2C
!!€ pooj0âs0000
      v: (%{ʹØ%}:) j _  NeutrVowLenghtHeightDiphPalAllegroPAL  [%^XYY2VY:] ;
  i %{ʹØ%}: _ %{ʹØ%}: Cns:+ %^Allegro: %^XYY2VY: ; !2024-05-20

"Even syllabic verbs I, cg for g"
!! **@RULENAME@** - used in
 g:0 <=>  Vow: (%^1VOW:|%{ˈ1%}:|0:) (ʹ:|%{ʹØ%}:) (Cns:+) g: (ˈ:) _ Vow: [Cns:+|n j|l m|s ǩ:|s m|s t|š ǩ:|š m] PenNeutrVowLenghtHeightDiphPalAllegroPAL [(%^Pen: %^Allegro:) (%^Pen: %^PAL:) %^Pen: %^CC2C:|(%^Pen: %^Allegro:) (%^Pen: %^PAL:) %^Pen: %^CC2CAllegro:|%^Pen: %^CCC2CAllegro:|%^Pen: %^XYY2XY:] ;
  Vow: (%^1VOW:|%{ˈ1%}:|0:) (ʹ:|%{ʹØ%}:) ŋ g  _ Vow: Cns:+ PenNeutrVowLenghtHeightDiphPalAllegroPAL [(%^Pen: %^Allegro:) (%^Pen: %^PAL:) %^Pen: %^CC2C:|(%^Pen: %^Allegro:) (%^Pen: %^PAL:) %^Pen: %^CC2CAllegro:|%^Pen: %^CCC2CAllegro:|%^Pen: %^XYY2XY:] ;
!! cõõggâlm+N+Sg+Ill
!!€ cõõggâlm^Pen^VV2V^Pen^CC2C^RmVow>a
!!€ cõ0g00lm00000>a
!!€ ǩeâlggal^Pen^XYY2XY
!!€ ǩeâlǥ0al00

          Vow: (%^1VOW:)  [i (%{ʹØ%}:)|u (%{ʹØ%}:) |(%{ʹØ%}:)  (ŋ|r|l|d:)] g: (ˈ:) _    NeutrVowLenghtHeightDiphPalAllegroPAL [(%^Allegro:) (%^PAL:) %^CC2C:|(%^Allegro:) (%^PAL:) %^CC2CAllegro:|%^CCC2CAllegro:|%^XYY2XY:] ;
!! äiʹǧǧ+N+Sg+Gen: **time/aika**
!!€ ä^1VOWi0gg^V2VV^PAL^XYY2XY
!!€ ääiʹj0000
!! reäiʹǧǧ+N+Pl+Nom: **hole/reikä**
!!€ reäi0gg^PAL^XYY2XY
!!€ reäiʹj000


"Allegro loss ǧ"
!! **@RULENAME@** - used in
ǧ:0 <=> Vow: (%{ˈ1%}:|0:) (%{ʹØ%}:|ʹ) ǧ _ Vow: Cns:+ PenNeutrVowLenghtHeightDiphPalAllegroPAL [(%^Pen: %^Allegro:) (%^Pen: %^PAL:) %^Pen: %^CC2C:|(%^Pen: %^Allegro:) (%^Pen: %^PAL:) %^Pen: %^CC2CAllegro:|%^Pen: %^CCC2CAllegro:|%^Pen: %^XYY2XY:] ;
!!€ lååʹǧǧest^Pen^VV2V^Pen^CC2CAllegro^RmVow
!!€ lå0ʹǧ00st00000

!! GEMINATE TO WEAK QUALITY GEMINATE
"Even syllabic nouns I, with extra lengthening of vowel V>VV and KK>ǤǤ"
!! **@RULENAME@**  - used in
!! used with  +N+Sg+Nom > +N+Sg+Gen for cases like
!! lookki > looǥǥi.
 k:ǥ <=>
    Vow: (%^1VOW:) (%{ʹØ%}:0)  k: _ NeutrVowLenghtHeight %^KK2GG: ;
    Vow: (%^1VOW:) (%{ʹØ%}:0)  _ [k: | %{XC%}:]  NeutrVowLenghtHeight [%^KK2GG: | %^K2GG:] ;
    
    Vow: (%^1VOW:) (%{ʹØ%}:0)  k: _ Vow: Cns:*  PenNeutrVowLenghtHeight %^Pen: %^KK2GG: ;
    Vow: (%^1VOW:) (%{ʹØ%}:0)  _ k: Vow: Cns:*  PenNeutrVowLenghtHeight %^Pen: [%^KK2GG: | %^K2GG:] ;

!! čâustõk+N+Sg+Gen
!!€ čâustõ^1VOW{ʹØ}k{XC}^V2VV^K2GG
!!€ čâustõõ0ǥǥ00
       Vow: (%^1VOW:) (%{ʹØ%}:0)  _ NeutrVowLenghtHeight %^K2GAllegro: ;
!! with allegro
!! čâustõk+N+Sg+Ela+PxSg3
!!€ čâustõ{ʹØ}k^K2GAllegro>stes
!!€ čâustõ0ǥ0>stes

    k: (ˈ:) _ NeutrVowLenghtHeight [%^KK2GG: | %^KKK2GG:] ;
    Vow:Vow (%^1VOW:) (%{ʹØ%}:0) _ (ˈ:) k: NeutrVowLenghtHeight [%^KK2GG: | %^KKK2GG:] ;
!!€ jo^1VOWkk^V2VV^KK2GG
!!€ jooǥǥ00
!! puâǥǥ+N+Sg+Nom: ****
!!€ puõkˈk^VOWLower^KKK2GG
!!€ puâǥ0ǥ00

!! čuâǥǥas+N+Sg+Nom  **road**
!!€ čuâkˈk^KKK2GG>as
!!€ čuâǥ0ǥ0>as

  Vow:Vow (%^1VOW:|Vow:0) (%{ʹØ%}:0) _ k: NeutrVowLenghtHeight %^KK2GAllegro: ;
!! loǥškueʹtted **begin to read**
!!€ loo{ʹØ}kk^VV2V^KK2GAllegro»škueʹtted
!!€ lo00ǥ000»škueʹtted

  Vow:Vow (%^1VOW:) (%{ʹØ%}:0) k: (ˈ:) _ Vow: Cns:* PenNeutrVowLenghtHeight %^Pen: [%^KKK2GG: | %^KK2GG: ] ;
  Vow:Vow (%^1VOW:) (%{ʹØ%}:0)  _ (ˈ:) k: Vow: Cns:* PenNeutrVowLenghtHeight %^Pen: [%^KKK2GG: | %^KK2GG: ] ;
!! čõõǥǥâs
!!€ čõ^1VOWkkâs^Pen^V2VV^Pen^KK2GG
!!€ čõõǥǥâs0000



"Even syllabic nouns I, with extra lengthening of vowel V>VV and k:j"
!! **@RULENAME@** - used in
!! used with
!!  > .
 k:j <=>
 Vow: (%^1VOW:) [:ʹ|%{ʹØ%}:ʹ]  k: _  (i:) NeutrVowLenghtHeight (%^PALÕ:|%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL: %^KK2GG: ;
         Vow: (%^1VOW:) (:ˈ) [:ʹ|%{ʹØ%}:ʹ]     _ [ k: (i:) |%{XC%}:] NeutrVowLenghtHeight (%^PALÕ:|%^PALE:|%^PALÄ:|%^PALẸ:) [%^PAL: %^KK2GG:| %^Allegro: %^PAL: %^KK2GAllegro:| %^PAL: %^K2GG: ] ;
!         Vow: (%^1VOW:) (:ˈ) [:ʹ|%{ʹØ%}:ʹ]     _ [ k: (i:) |0:|%{XC%}:] NeutrVowLenghtHeight (%^PALE:|%^PALÄ:|%^PALẸ:) [%^PAL: %^KK2GG:| %^Allegro: %^PAL: %^KK2GAllegro:| %^PAL: %^K2GG: ] ;
!! tuʹmstõk+N+Der/Dimin+N+Pl+Nom: **decision/päätös, mietintö**
!!€ tuʹmstõ^1VOW0k{XC}^V2VV^PAL^K2GG>e
!!€ tuʹmstõõʹjj000>e

     Vow: (%^1VOW:) (:ˈ) [:ʹ|%{ʹØ%}:ʹ]   _  NeutrVowLenghtHeight  %^PAL: %^K2GAllegro: ;
!! with allegro

!!2015-10-05
  Vow: (%^1VOW:) [:ʹ|%{ʹØ%}:ʹ]  k: (ˈ:)  _ (i:) NeutrVowLenghtHeight (%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL: [ %^KK2GG:| %^KKK2GG:] ;
  Vow: (%^1VOW:) [:ʹ|%{ʹØ%}:ʹ]   _ (ˈ:) k: (i:) NeutrVowLenghtHeight (%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL: [%^KK2GG: | %^KKK2GG:] ;
!!€ suä0kk^PALẸ^PAL^KK2GG
!!€ suẹʹjj000

  Vow (%^1VOW:) [:ʹ|%{ʹØ%}:ʹ]  k:  (ˈ:)   _ Vow: Cns:* PenNeutrVowLenghtHeight (%^Pen: %^PALE:|%^Pen: %^PALÄ:|%^Pen: %^PALẸ:) %^Pen: %^PAL: %^Pen: %^KKK2GG: ;
   Vow: (%^1VOW:) [:ʹ|%{ʹØ%}:ʹ]   _  (ˈ:)  k: Vow: Cns:* PenNeutrVowLenghtHeight (%^Pen: %^PALE:|%^Pen: %^PALÄ:|%^Pen: %^PALẸ:) %^Pen: %^PAL: %^Pen: %^KKK2GG: ;
!!€ jeä0kˈkel^Pen^PALE^Pen^PAL^Pen^KKK2GG^RmVow>až
!!€ jeeʹj0j0l0000000>až


"Even syllabic nouns I, with extra lengthening of vowel V>VV and ǩ:j"
!! **@RULENAME@** - used in
!! used with
!!  > .
 ǩ:j <=>
 Vow: (%^1VOW:) (ʹ:|%{ʹØ%}:) ǩ: _  (i:)  NeutrVowLenghtHeight ((%^PALÄ:|%^PALẸ:|%^PALE:) %^PAL:) %^KK2GG: ;
         Vow: (%^1VOW:) (ʹ:|%{ʹØ%}:)    _ ǩ: (i:) NeutrVowLenghtHeight ((%^PALÄ:|%^PALẸ:|%^PALE:) %^PAL:) %^KK2GG: ;

  Vow: (%^1VOW:) (ʹ:|%{ʹØ%}:) ǩ:   _ (i:) NeutrVowLenghtHeight ((%^PALÄ:|%^PALẸ:|%^PALE:) %^PAL:) %^KK2GG: ;
  Vow: (%^1VOW:) (ʹ:|%{ʹØ%}:)  _  ǩ: (i:) NeutrVowLenghtHeight ((%^PALÄ:|%^PALẸ:|%^PALE:) %^PAL:) %^KK2GG: ;

  Vow: (%^1VOW:)   (ʹ:|%{ʹØ%}:) ǩ: (ˈ:) _ (i:) PALAllo ((%^PALÄ:|%^PALẸ:|%^PALE:) %^PAL:)  %^KKK2GG: ;
  Vow: (%^1VOW:)   (ʹ:|%{ʹØ%}:)          _ (ˈ:) ǩ: (i:) PALAllo ((%^PALÄ:|%^PALẸ:|%^PALE:) %^PAL:) %^KKK2GG: ;


"š:0"
!! **@RULENAME@** - used in Allegro
 š:0 <=>
      Vow: (%^1VOW:|%{ˈ1%}:|0:) (ʹ:|%{ʹØ%}:) Cns: š _  NeutrVowLenghtHeightDiphPalAllegroPAL [(%^Allegro:) (%^PAL:) %^CC2C:|(%^Allegro:) (%^PAL:) %^CC2CAllegro:|%^XYY2XY:] ;
         (ʹ:|%{ʹØ%}:) Cns: š _ Vow: [Cns:+|n j|l m|s ǩ:|s m|s t|š ǩ:|š m] PenNeutrVowLenghtHeightDiphPalAllegroPAL  [%^Pen: [(%^Allegro:) (%^PAL:) %^CC2C:|(%^Allegro:) (%^PAL:) %^CC2CAllegro:]|%^Pen: %^XYY2XY:] ;

      Vow: (%^1VOW:|%{ˈ1%}:|0:) (ʹ:|%{ʹØ%}:) Cns: š _ Vow: Cns:+  PenNeutrVowLenghtHeightDiphPalAllegroPAL [(%^Pen: %^Allegro:) (%^Pen: %^PAL:) %^Pen: %^CC2C:|(%^Pen: %^Allegro:) (%^Pen: %^PAL:) %^Pen: %^CC2CAllegro:|%^Pen: %^XYY2XY:] ;

!! šapšš+N+Sg+Gen **white fish/siika**
!!€ ša^1VOWpšš^V2VV^XYY2XY
!!€ šaapš000
       Vow: (%^1VOW:| %{ˈ1%}:|0:) [ (ʹ:|%{ʹØ%}:)  (Cns:) |(ʹ:|%{ʹØ%}:) [h:|j:] (ʹ:|%{ʹØ%}:) | [u|i] (ʹ:|%{ʹØ%}:) ] š: (ˈ:) _ NeutrVowLenghtHeightDiphPalAllegroPAL (%^Allegro:) (%^PAL:) [%^CC2C:| %^CC2CAllegro:| %^CCC2CAllegro:|%^XYY2XY:|%^XYY2VY:] ;
       Vow: (%^1VOW:| %{ˈ1%}:|0:) [ (ʹ:|%{ʹØ%}:)  (Cns:) |(ʹ:|%{ʹØ%}:) [h:|j:] (ʹ:|%{ʹØ%}:) | [u|i] (ʹ:|%{ʹØ%}:) ] š: (ˈ:) _ Vow: Cns:* PenNeutrVowLenghtHeightDiphPalAllegroPAL (%^Pen: %^Allegro:) (%^Pen: %^PAL:) [%^Pen: %^CC2C:| %^Pen: %^CC2CAllegro:| %^Pen: %^CCC2CAllegro:|%^Pen: %^XYY2XY:|%^Pen: %^XYY2VY:] ;
       Vow: (%^1VOW:| %{ˈ1%}:|0:)  (ʹ:|%{ʹØ%}:)  š:  _ NeutrVowLenghtHeightDiphPalAllegroPAL %^KK2GAllegro: ;
!! ruõšsted+V+Inf: ****
!!€ ruõ{ˈ1%}ššâst^Pen^CC2CAllegro^RmVow>ed
!!€ ruõˈš00st000>ed


"Even syllabic verbs I, Voicing š:ž"
!! **@RULENAME@** - ss:zz, +Imp+Sg2, +Ind+Prs+ConNeg, double consonants at coda become voiced in gradation
 š:ž <=>
 Vow: (%^1VOW:) (ʹ:|%{ʹØ%}:) š: _   (i:) NeutrVowLenghtHeightDiphPalAllegroPAL (%^I2J:) %^KK2GG: ;
         Vow: (%^1VOW:) (ʹ:|%{ʹØ%}:)    _ [š: (i:) |%{XC%}:] [(%^Pen: %^CC2C:) NeutrVowLenghtHeightDiphPalAllegroPAL|PenNeutrVowLenghtHeightDiphPalAllegroPAL] (%^I2J:) [ %^KK2GG: | %^K2GG: ] ;
!! lookkmõš+N+Sg+Gen
!!€ lookkmõ^1VOWš{XC}^V2VV^K2GG
!!€ lookkmõõžž00
       Vow: (%^1VOW:) 0:*  _  NeutrVowLenghtHeightDiphPalAllegroPAL %^K2GAllegro: ;
!! with allegro


          Vow: (%^1VOW:)      (ʹ:|%{ʹØ%}:) š:  ˈ:  _ (i:)  NeutrVowLenghtHeightDiphPalAllegroPAL (%^I2J:) %^KKK2GG: ;
          Vow: (%^1VOW:)      (ʹ:|%{ʹØ%}:) _   ˈ:  š: (i:)  NeutrVowLenghtHeightDiphPalAllegroPAL (%^I2J:) %^KKK2GG: ;
       Vow: (%^1VOW:)  _ š: (%^VV2V: ) %^KK2GAllegro: ;
!!€ pååss^VV2V^KK2GAllegro»škuätt
!!€ på0z000»škuätt

      Vow: (%^1VOW:)   _ PenBetweenStemAndStemFinalVoicing %^VC: ;
!! mainstummuš+N+Sg+Gen:
!!€ mainstummuš^VC
!!€ mainstummuž0

         Vow: (%^1VOW:) (ʹ:|%{ʹØ%}:) š: (ˈ:) _  Vow: Cns:* PenNeutrVowLenghtHeightDiphPalAllegroPAL (%^I2J:) %^Pen: [ %^KKK2GG: | %^KK2GG: ] ;
         Vow: (%^1VOW:) (ʹ:|%{ʹØ%}:)    _ (ˈ:) š: Vow: Cns:* PenNeutrVowLenghtHeightDiphPalAllegroPAL (%^I2J:) %^Pen: [%^KKK2GG:| %^KK2GG: |%^KKK2G:] ;



"Even syllabic verbs I, Voicing c:ʒ"
!! **@RULENAME@** - cc:zz, +Imp+Sg2, +Ind+Prs+ConNeg, double consonants at coda become voiced in gradation
 c:ʒ <=> Vow: (%^1VOW:) (ʹ:|%{ʹØ%}:) c: _   (i:)  NeutrVowLenghtHeightDiphPalAllegroPAL (%^I2J:) %^KK2GG: ;
         Vow: (%^1VOW:) (ʹ:|%{ʹØ%}:)    _ c: (i:) NeutrVowLenghtHeightDiphPalAllegroPAL (%^I2J:) [%^KK2GG:|%^KK2GAllegro:] ;
!!€ čää0cc^PAL^KK2GG
!!€ čääʹʒʒ00
!! čääʹcc+N+Sg+Loc+PxSg3
!!€ čää0cc^VV2V^PAL^KK2GAllegro>stes
!!€ čä0ʹʒ0000>stes
!!€ čää0cc^PAL^KK2GG>stes
!!€ čääʹʒʒ00>stes
         Vow: (%^1VOW:) (ʹ:|%{ʹØ%}:) c: ˈ: _    (i:) NeutrVowLenghtHeightDiphPalAllegroPAL (%^I2J:) %^KKK2GG: ;
         Vow: (%^1VOW:) (ʹ:|%{ʹØ%}:)    _ ˈ: c: (i:) NeutrVowLenghtHeightDiphPalAllegroPAL (%^I2J:) [%^KKK2GG:|%^KKK2G:] ;
!!€ puõcˈc^VOWLower^PALÄ^KKK2GG
!!€ puäʒ0ʒ000
                                   _ %^VC: ;
       Vow: (%^1VOW:)  _ c: (%^VV2V: ) %^KK2GAllegro: ;
!!€ pååss^VV2V^KK2GAllegro»škuätt
!!€ på0z000»škuätt

         Vow: (%^1VOW:) (ʹ:|%{ʹØ%}:) c: (ˈ:) _  Vow: Cns:* PenNeutrVowLenghtHeightDiphPalAllegroPAL (%^I2J:) %^Pen: [ %^KKK2GG: | %^KK2GG: ] ;
         Vow: (%^1VOW:) (ʹ:|%{ʹØ%}:)    _ (ˈ:) c: Vow: Cns:* PenNeutrVowLenghtHeightDiphPalAllegroPAL (%^I2J:) %^Pen: [%^KKK2GG:| %^KK2GG: |%^KKK2G:] ;
!! õõʒʒâs+N+Sg+Nom: **high water/vuoksi**
!!€ õ^1VOWccâs^Pen^V2VV^Pen^KK2GG
!!€ õõʒʒâs0000



"Even syllabic verbs I, Voicing č:j"
!! **@RULENAME@** - ss:zz, +Imp+Sg2, +Ind+Prs+ConNeg, double consonants at coda become voiced in gradation
 č:j <=> Vow: (%^1VOW:) (ʹ:|%{ʹØ%}:) č: _    (i:) NeutrVowLenghtHeightDiphPalAllegroPAL (%^I2J:) %^KK2GG: ;
         Vow: (%^1VOW:) (ʹ:|%{ʹØ%}:)    _ č: (i:) NeutrVowLenghtHeightDiphPalAllegroPAL (%^I2J:) [ %^KK2GG: | [(%^Allegro:) (%^PAL:) %^CC2C:|(%^Allegro:) (%^PAL:) %^CC2CAllegro:|(%^Allegro:) (%^PAL:) %^KK2GAllegro:] ] ;
                                             _ %^VC: ;

       Vow: (%^1VOW:)  _ č: (%^VV2V: ) %^KK2GAllegro: ;
!!€ pååss^VV2V^KK2GAllegro»škuätt
!!€ på0z000»škuätt
     Vow: (%^1VOW:) (ʹ:|%{ʹØ%}:) č: ˈ: _    (i:) NeutrVowLenghtHeightDiphPalAllegroPAL (%^I2J:) %^KKK2GG: ;
     Vow: (%^1VOW:) (ʹ:|%{ʹØ%}:)    ˈ: _ č: (i:) NeutrVowLenghtHeightDiphPalAllegroPAL (%^I2J:) %^KKK2GG: ;

         Vow: (%^1VOW:) (ʹ:|%{ʹØ%}:) č: (ˈ:) _  Vow: Cns:* PenNeutrVowLenghtHeightDiphPalAllegroPAL (%^I2J:) %^Pen: [ %^KKK2GG: | %^KK2GG: ] ;
         Vow: (%^1VOW:) (ʹ:|%{ʹØ%}:)    _ (ˈ:) č: Vow: Cns:* PenNeutrVowLenghtHeightDiphPalAllegroPAL (%^I2J:) %^Pen: [%^KKK2GG:| %^KK2GG: |%^KKK2G:] ;

"Even syllabic verbs I, Voicing after long vowel or diphthong s:z s:z"
!! **@RULENAME@** - ss:zz, +Imp+Sg2, +Ind+Prs+ConNeg, double consonants at coda become voiced in gradation
 s:z <=> Vow: (%^1VOW:) (ʹ:|%{ʹØ%}:) s:  _  (i:) NeutrVowLenghtHeightDiphPalAllegroPAL (%^I2J:)  %^KK2GG: ;
         Vow: (%^1VOW:) (:ˈ) (ʹ:|%{ʹØ%}:) _   [s: (i:) |%{XC%}: ] NeutrVowLenghtHeightDiphPalAllegroPAL (%^I2J:)  [%^KK2GG: |%^K2GG: ] ;
!! tääʹss+N+Sg+Gen: **level/taso**
!!€ tää0ss^PAL^KK2GG
!!€ tääʹzz00
!!€ mättʼtõ^1VOWs{XC}^V2VV^K2GG
!!€ mättʼtõõzz00
       Vow: (%^1VOW:) (:ˈ) (ʹ:|%{ʹØ%}:)  _  NeutrVowLenghtHeightDiphPalAllegroPAL %^K2GAllegro: ;
!! with allegro

         Vow: (%^1VOW:)  (ʹ:|%{ʹØ%}:) s:  ˈ:  _  (i:) NeutrVowLenghtHeightDiphPalAllegroPAL (%^I2J:) %^KKK2GG: ;
         Vow: (%^1VOW:)  (ʹ:|%{ʹØ%}:)  _ ˈ:   s: (i:) NeutrVowLenghtHeightDiphPalAllegroPAL (%^I2J:) %^KKK2GG: ;
!!
    [Cns:+ (%{XC%}:)|Cns:+ (ˈ:|:ˈ) Cns:+] Vow: _ PenBetweenStemAndStemFinalVoicing (%^PenVow2a:) %^VC: ;
!! cieʹǩǩes+N+Sg+Gen: **trick, type of ear mark/tikki, pykälä**
!!$ ciâkˈkes^Pen^VOWLower%^PenVow2a%^VC
!!$ ceäkˈkas0000
       Vow: (%^1VOW:)  _ s: (%^VV2V: ) %^KK2GAllegro: ;
!!€ pååss^VV2V^KK2GAllegro»škuätt
!!€ på0z000»škuätt

   Vow (%^1VOW:) (ʹ:|%{ʹØ%}:) _ %{XC%}: NeutrVowLenghtHeightDiphPalAllegroPAL %^K2GG: ;

!! čårrõs+N+Sg+Gen
!!€ čårrõ^1VOW{ʹØ}s{XC}^V2VV^K2GG
!!€ čårrõõ0zz00

         Vow: (%^1VOW:) (ʹ:|%{ʹØ%}:) s: (ˈ:) _  Vow: Cns:* PenNeutrVowLenghtHeightDiphPalAllegroPAL (%^I2J:) %^Pen: [ %^KKK2GG: | %^KK2GG: ] ;
         Vow: (%^1VOW:) (ʹ:|%{ʹØ%}:)    _ (ˈ:) s: Vow: Cns:* PenNeutrVowLenghtHeightDiphPalAllegroPAL (%^I2J:) %^Pen: [%^KKK2GG:| %^KK2GG: |%^KKK2G:] ;



"Even syllabic verbs I, second consonant loss t:0"
!! **@RULENAME@** - used in
 t:0 <=> Vow: (%^1VOW:)  (i|u)   (ʹ:|%{ʹØ%}:) ((Cns: )[b|p|v|f|n|s|c|r|l|š|č|j|k]) t: _ (i:) NeutrVowLenghtHeightDiphPalAllegroPAL  (%^I2J:) [(%^Allegro:) (%^PAL:) %^CC2C:|(%^Allegro:) (%^PAL:) %^CC2CAllegro:|%^XYY2XY:] ;
!! autt+N+Pl+Nom **car/auto**
!!€ a^1VOWutt^V2VV^CC2C
!!€ aaut000
!! põrtt+N+Pl+Nom **house/talo**
!!€ põ^1VOW{ʹØ}rtt^V2VV^XYY2XY
!!€ põõ0rt000
         Vow: (%^1VOW:) (ʹ:|%{ʹØ%}:) h: (ʹ:|%{ʹØ%}:)  t: _ NeutrVowLenghtHeightDiphPalAllegroPAL  [%^XYY2VY: | %^XYY2IY: ] ;

 t: _ Vow: (%^1VOW:) Cns:+ PenNeutrVowLenghtHeightDiphPalAllegroPAL [%^Pen: %^XYY2XY:|%^Pen: %^XYY2VY: | %^Pen: %^CC2C:0 | %^Pen: %^CC2CAllegro:0 ] ;
!!€ ko^1VOWnttâr^Pen^V2VV^Pen^XYY2XY
!!€ koont0âr0000
!! puʹtsted+V+Inf: ****
!!€ puuʹttest^Pen^VV2V^Pen^CC2CAllegro^RmVow
!!€ pu0ʹt00st00000
!! piiutâs+N+Sg+Nom **clothing/vaate**
!!€ pi^1VOWhttâs^Pen^V2VV^Pen^XYY2VY
!!€ piiut0âs0000
!! kääđas+N+Sg+Nom
!!€ kä^1VOWttas^Pen^V2VV^Pen^CC2C
!!€ kääđ0as0000



"Consonant loss s:0"
!! **@RULENAME@** - used in
 s:0 <=>
       Vow: (%^1VOW:| %{ˈ1%}:|0:) [ (ʹ:|%{ʹØ%}:)  Cns: |(ʹ:|%{ʹØ%}:) [h:|j:] (ʹ:|%{ʹØ%}:) | [u|i] (ʹ:|%{ʹØ%}:) ] s: (ˈ:) _ NeutrVowLenghtHeightDiphPalAllegroPAL (%^Allegro:) (%^PAL:) [%^CC2C:| %^CC2CAllegro:| %^CCC2CAllegro:|%^XYY2XY:|%^XYY2VY:] ;
       Vow: (%^1VOW:| %{ˈ1%}:|0:) [ (ʹ:|%{ʹØ%}:)  (Cns:) |(ʹ:|%{ʹØ%}:) [h:|j:] (ʹ:|%{ʹØ%}:) | [u|i] (ʹ:|%{ʹØ%}:) ] s: (ˈ:) _ Vow: (0:) Cns:* PenNeutrVowLenghtHeightDiphPalAllegroPAL (%^Pen: %^Allegro:) (%^Pen: %^PAL:) [%^Pen: %^CC2C:|%^Pen: %^CC2CAllegro:| %^Pen: %^CCC2CAllegro:|%^Pen: %^XYY2XY:|%^Pen: %^XYY2VY: |%^Pen: %^KK2GAllegro: ] ;
       Vow: (%^1VOW:| %{ˈ1%}:|0:)  (ʹ:|%{ʹØ%}:)  s:  _ NeutrVowLenghtHeightDiphPalAllegroPAL %^KK2GAllegro: ;

!!€ sä^1VOWhss^V2VV^XYY2VY>am
!!€ sääus000>am
       Cns: _ Vow:0 Cx ;


!! uhss+N+Sg+Gen **door**
!!€ uhss^XYY2VY
!!€ uus00

       Vow: (%^1VOW:) (%{ˈ1%}:|ʹ) Cns _ [Vow: s Cns:+ PenNeutrVowLenghtHeightDiphPalAllegroPAL] [(%^Pen: %^Allegro:) (%^Pen: %^PAL:) %^Pen: %^CC2CAllegro: %^RmVow: | %^RmVow: ] ;

!! ǩeäsʼsted+V+Inf:
!!€ ǩeä{ˈ1}ssa0st^Pen^CC2CAllegro^RmVow^PALK>ed
!!€ ǩeäˈs00ʼst0000>ed
!! âʹpsted+V+Inf: **smell a little/haiskahtaa**
!!€ ââʹpsest^Pen^VV2V^RmVow
!!€ â0ʹp00st000

"Consonant loss ŋ:0"
!! **@RULENAME@** - used in
 ŋ:0 <=>
 Vow: (%^1VOW:|%{ˈ1%}:|0:) (ʹ:|%{ʹØ%}:) (r|l) ŋ: (ˈ:0) _   [(i:) NeutrVowLenghtHeightDiphPalAllegroPAL |Vow: Cns:*  PenNeutrVowLenghtHeightDiphPalAllegroPAL %^Pen: ] [(%^Allegro:) (%^PAL:) %^CC2C:|(%^Allegro:) (%^PAL:) %^CC2CAllegro:|(%^Allegro:) (%^PAL:) %^CCC2C:|%^XYY2XY:] ;
! Vow: (%^1VOW:|%{ˈ1%}:|0:) (ʹ:|%{ʹØ%}:) ([Cns - ŋ]) ŋ: (ˈ:0) _  Vow: Cns:*  PenNeutrVowLenghtHeightDiphPalAllegroPAL [%^Pen: [(%^Allegro:) (%^PAL:) %^CC2C:|(%^Allegro:) (%^PAL:) %^CC2CAllegro:]|%^Pen: %^XYY2XY:] ;


"Consonant loss h:0"
!! **@RULENAME@** - used in
 h:0 <=>
 OnSetC u: %^1VOW: (:ʹ|%{ʹØ%}:) _ Cns:+ %^VV2V: %^PAL: %^XYY2VY: ;
!! ruʹhss+N+Sg+Loc+PxSg3:
!!€ ru^1VOW0hss^VV2V^PAL^XYY2VY>stes
!!€ ru0ʹ0s0000>stes
   h _ Vow: Cns:+ PenNeutrVowLenghtHeightDiphTint %^Pen:0 %^CC2CAllegro: ;
!! påʹhlded+V+Inf: ****
!!€ pååʹhheld^Pen^VV2V^Pen^CC2CAllegro^RmVow
!!€ på0ʹh00ld00000

"Even syllabic verbs I, t>đ"
!! **@RULENAME@** - tt:đ
 t:đ  <=>
 Vow: %^1VOW: (:ˈ) (ʹ:|%{ʹØ%}:) _ t:0 (i:) NeutrVowLenghtHeightDiphPalAllegroPAL [%^CC2C:|%^CCC2C:] (%^I2J:) ;
 Vow: [Vow: - [i|u]] (:ˈ) (ʹ:|%{ʹØ%}:) _ t:0 (i:) NeutrVowLenghtHeightDiphPalAllegroPAL [%^CC2C:|%^CCC2C:| %^CC2CAllegro:] (%^I2J:) ;
!! ǩiõtt+N+Sg+Loc+PxSg1 **hand,arm/käsi**
!!€ ǩiõ0tt^Allegro^PAL^CC2CAllegro>stan
!!€ ǩiõʹđ0000>stan
!! autt+N+Pl+Nom **car/auto**
!!$ a^1VOWutt^V2VV^CC2C
!!$ aauđ000
 u u: (:ˈ) (ʹ:|%{ʹØ%}:) _ t:0 (i:) NeutrVowLenghtHeightDiphPalAllegroPAL [%^CC2C:|%^CCC2C:|%^CC2CAllegro:] (%^I2J:) ;
!! puuttâd+V+Ind+Prt+Sg3: **end/loppua**
!!€ puutt^CC2C>i
!!€ puuđ00>i
 Vow: (%^1VOW:) (:ˈ) (ʹ:|%{ʹØ%}:) _ t:0 Vow: Cns: PenNeutrVowLenghtHeightDiphPalAllegroPAL %^Pen: [%^CC2C:|%^CCC2C:] (%^I2J:) ;
!! kääđas+N+Sg+Nom:
!!€ kä%^1VOWttas^Pen^V2VV^Pen^CC2C
!!€ kääđ0as0000
!!€ čuâ{ʹØ}ttes^Pen^PALE^Pen^PAL^Pen^CC2C
!!€ čueʹđ0es000000

"Even syllabic nouns I, p>v"
!! **@RULENAME@** - pp:v
 p:v  <=> Vow: (%^1VOW:) (:ˈ) (ʹ:|%{ʹØ%}:) _ (ˈ:) p:0 (i:) NeutrVowLenghtHeightDiphPalAllegroPAL  [%^CC2C: | %^CCC2C: ] (%^I2J:) ;
!!€ ǩiâpp^CC2C
!!€ ǩiâv00
 Vow: (%^1VOW:) (:ˈ) (ʹ:|%{ʹØ%}:) _ (ˈ:) p:0 Vow: Cns:* PenNeutrVowLenghtHeightDiphPalAllegroPAL [%^Pen: %^CC2C:|%^Pen: %^CCC2C:] ;
!!€ heäpˈpaš^Pen^CCC2C
!!€ heäv00aš00

"Consonant quality change ǥ:j"
!! **@RULENAME@** - used in
 ǥ:j <=>
  Vow: (%^1VOW:)  i (%{ʹØ%}:)  _ %^PAL: ;
  Vow: (%^1VOW:)  (i|u) (%{ʹØ%}:) [l|r|ŋ] _ NeutrVowLenghtHeight %^PAL: ;
!!€ lääi0ǥ^PAL
!!€ lääiʹj0
!! čiõʹlj+N+Sg+Nom: **back/selkä**
!!€ čeâ{ʹØ}lǥ^VOWRaise^PAL
!!€ čiõʹlj00

  Vow: (%^1VOW:) Cns _ (%^Pen: %^VV2V:) (%^RmVow:) %^PALK: ;
!!€ jäämalǥ^Pen^VV2V^RmVow^PALK>ed
!!€ jä0m0lj0000>ed

"Even syllabic verbs I, second consonant loss k:0"
!! **@RULENAME@** - used in
 k:0 <=>   k:  _ (i:) NeutrVowLenghtHeightDiphPalAllegroPAL  [ [(%^Allegro:) (%^PAL:) %^CC2C:|(%^Allegro:) (%^PAL:) %^CC2CAllegro:] | %^XYY2XY: ] ;
!! loǥškueʹtted **begin to read**
!!€ lookk^VV2V^KK2GAllegro»škueʹtted
!!€ lo0ǥ000»škueʹtted
!! mieʹlǩǩ+N+Sg+Acc: **milk/maito**
!!€ meâ0lkk^VOWRaise^PALE^PAL^CC2C
!!€ mieʹlǩ00000
       Vow: (%^1VOW:) k:  _ (%^VV2V: ) %^KK2GAllegro: ;
!!€ pååss^VV2V^KK2GAllegro»škuätt
!!€ på0z000»škuätt
           k: (ˈ:)  _ Vow: Cns:+ PenNeutrVowLenghtHeightDiphPalAllegroPAL  %^Pen: [[(%^Allegro:) (%^PAL:) %^CC2C:|(%^Allegro:) (%^PAL:) %^CC2CAllegro:] | %^XYY2XY: | %^KKK2ZERO: | %^KK2ZERO: ] ;
!! taalkâs+N+Sg+Nom
!!€ ta^1VOWlkkâs^Pen^V2VV^Pen^XYY2XY
!!€ taalk0âs0000

 Vow: (%^1VOW:) (%{ʹØ%}:) _ (ˈ:) k: Vow: Cns:+ PenNeutrVowLenghtHeightDiphPalAllegroPAL  %^Pen: [%^KKK2ZERO: | %^KK2ZERO:] ;
!!€ jeä{ʹØ}kˈkel^Pen^PALE^Pen^PAL^Pen^KKK2ZERO
!!€ jeeʹ000el000000
!! nââʹer+N+Sg+Nom sleep
!!€ nâ^1VOW0kker^Pen^V2VV^Pen^PAL^Pen^KK2ZERO
!!€ nââʹ00er000000

"Even syllabic verbs I, second consonant loss ǩ:0"
!! **@RULENAME@** - used in
 ǩ:0 <=>

    ǩ:  _ Vow: Cns:+ PenNeutrVowLenghtHeightDiphPalAllegroPAL  [%^Pen: [(%^Allegro:) (%^PAL:) %^CC2C:|(%^Allegro:) (%^PAL:) %^CC2CAllegro:]|%^Pen: %^XYY2XY:] ;
!! eʹǩrded+V+Inf
!!€ eeʹǩǩerd^Pen^VV2V^Pen^CC2C^RmVow>ed
!!€ e0ʹǩ00rd00000>ed

"Even syllabic verbs I, second consonant loss ǥ:0"
!! **@RULENAME@** - used in
 ǥ:0 <=>
  Vow: (%^1VOW:)  (ʹ:|%{ʹØ%}:)  [i|u] (ʹ:|%{ʹØ%}:)  ǥ:  _ (i:) NeutrVowLenghtHeightDiphPalAllegroPAL  [(%^Allegro:) (%^PAL:) %^CC2C:|(%^Allegro:) (%^PAL:) %^CC2CAllegro:] ;

    ǥ:  _ Vow: Cns:+ PenNeutrVowLenghtHeightDiphPalAllegroPAL  %^Pen: [(%^Allegro:) (%^PAL:) %^CC2C:|(%^Allegro:) (%^PAL:) %^CC2CAllegro:] ;
!! påǥsted+V+Inf
!!€ pååǥǥast^Pen^VV2V^Pen^CC2C^RmVow>ed
!!€ på0ǥ00st00000>ed


"Even syllabic verbs I, lgg>lǥ"
!! **@RULENAME@**
 g:ǥ  <=> Vow: (%^1VOW:) [i (%{ʹØ%}:0) | u (%{ʹØ%}:0)| (ʹ:0|%{ʹØ%}:0) [l|r|d:] ]  _ g: (i:) NeutrVowLenghtHeight [(%^Allegro:) %^CC2C:|(%^Allegro:) %^CC2CAllegro:| %^XYY2XY: ] ;
!! vueʹlǧǧed+V+Ind+Prs+Sg2
!!€ vuâ{ʹØ}lgg^XYY2XY>ak
!!€ vuâ0lǥ00>ak

!!$ vuâ{ʹØ}lgg^XYY2XY>ak
!!$ vuâ0lg00>ak
!! õlggâd+V+Cond+Sg3
!!€ õ^1VOW{ʹØ}lgg^V2VV^XYY2XY>či
!!€ õõ0lǥ000>či
!!$ õ^1VOW{ʹØ}lgg^V2VV^XYY2XY>či
!!$ õõ0lg000>či

 Vow: (%^1VOW:) (ʹ:0|%{ʹØ%}:0) (Cns:+)  _ g: Vow: Cns:+ PenNeutrVowLenghtHeight [%^Pen: %^XYY2XY:] ;
!!€ ǩeâlggal^Pen^XYY2XY
!!€ ǩeâlǥ0al00
!!€ aaggâst^Pen^VV2V^Pen^CC2C^RmVow>ed
!!€ a0g00st00000>ed

"even syllabic verbs I, 'lgg>'lj"
!! **@RULENAME@** - used in
 g:j  <=> Vow: (%^1VOW:)  [ i [:ʹ|%{ʹØ%}:ʹ] |[:ʹ|%{ʹØ%}:ʹ] [l|r|d:]]  _ g: NeutrVowLenghtHeight %^PAL: %^XYY2XY: ;
!! vueʹlǧǧed+V+Ind+Prt+Pl1
!!€ vuâ0lgg^VOWRaise^PAL^XYY2XY>im
!!€ vuõʹlj0000>im
!!€ ä^1VOWi0gg^V2VV^PAL^XYY2XY
!!€ ääiʹj0000
!! reäiʹǧǧ+N+Pl+Nom
!!€ reäi0gg^PAL^XYY2XY
!!€ reäiʹj000
   Vow: (%^1VOW:)  [ i [:ʹ|%{ʹØ%}:ʹ] |[:ʹ|%{ʹØ%}:ʹ] [l|r|d:]]  _ g: Vow: Cns:* PenNeutrVowLenghtHeight %^Pen: %^PAL: %^Pen: %^XYY2XY: ;




!! ## Rules for cleaning up and composing end result

!! * Strengthening Consonant Cluster
!! * V shortening and C lengthening
!! * %^C2CC is generally used in conjunction with a preceding %^VV2V
!! * In verbs this will be observed in finite forms
!! * In derivational morphology, e.g. -škuetted, this is not the case

"Orthographic Consonant lengthening Weak to strong %{XC%}:Cx "
!! **@RULENAME@** - used in
 %{XC%}:Cx <=>  Vow: (%^1VOW:) (ʹ:|%{ʹØ%}:) Cns:Cx _ [(%^Pen: %^CC2C:) NeutrVowLenghtHeightDiphPalAllegroPAL|PenNeutrVowLenghtHeightDiphPalAllegroPAL] [%^C2CC:|%^K2GG:] ;
        where Cx in (b c ʒ ǯ d đ ǥ g ǧ j k ǩ m ŋ p r s š t v z ž) ;
!!€ čââʹlmtẹ^1VOWm{XC}^C2CC>a
!!€ čââʹlmtẹ0mm0>a
!! kaađneǩ+N+Sg+Gen 
!!€ kaađnẹ^1VOW{ʹØ}ǩ{XC}^PALNo^C2CC>a
!!€ kaađnẹ00kk00>a



"Orthographic Consonant lengthening Weak to strong %{XC%}:Cx for n and l"
!! **@RULENAME@** - used in
 %{XC%}:Cx <=>  Vow: (%^1VOW:) (ʹ:|%{ʹØ%}:) Cns:Cx _ (j) NeutrVowLenghtHeightDiphPalAllegroPAL [%^C2CC:] ;
        where Cx in (l n) ;



!Not needed here
!!€ ma^1VOWnn^V2VV^VOWLower^CC2C
!!€ mään0000

!Not needed here
!!€ puärˈr^VOWLower^CCC2C
!!€ puär0000

!Not needed here
!! suâl+N+Sg+Nom **island**
!!€ suõlˈl^VOWLower^CCC2C
!!€ suâl0000




!"Orthographic Consonant lengthening in INDPRSSG3 and IMPSG2 0:m"
!!! __@RULENAME@__ - kåʹmrded+V+Imp+Sg2: kååʹmmerd
! 0:m <=> Vow:Vow (^1VOW:) (ʹ:|{ʹØ}:)  m _ Vow: Cns: PenNeutrVowLenghtHeightDiphPalAllegroPAL ^Pen: ^C2CC:  ;

!Not needed here
!!€ čââʹlmtẹ^1VOWm{XC}^C2CC>a
!!€ čââʹlmtẹ0mm0>a

!Not needed here
!! kååvas+N+Sg+Nom: **kota**
!!€ kå^1VOWvvas^Pen^V2VV^Pen^CC2C
!!€ kååv0as0000


!Not needed here
!! kõõnjâl+N+Sg+Nom **tear**
!!€ kõ^1VOWnnjâl^Pen^V2VV^Pen^CC2C
!!€ kõõn0jâl0000



!Not needed here
!! suâl+N+Nom **island/saari**
!!€ suõlˈl^VOWLower^CCC2C
!!€ suâl0000


!Not needed here
!! ǩeâlǥal+N+Sg+Nom **kilkura**
!!€ ǩeâlggal^Pen^XYY2XY
!!€ ǩeâlǥ0al00


!Not needed here
!!€ dura^1VOWk{XC}^C2CC>a
!!€ dura0kk0>a
!! kaađneǩ+N+Sg+Ill:
!!€ kaađne^1VOWǩ{XC}^PALNo^C2CC>a
!!€ kaađne0kk00>a

!Not needed here
!! čårrõs+N+Sg+Gen
!!€ čårrõ^1VOWs{XC}^V2VV^K2GG
!!€ čårrõõzz00


!Not needed here
!! lookkmõš+N+Sg+Gen
!!$ lookkmõ^1VOWš{XC}^V2VV^K2GG
!!$ lookkmõ0žž00

!Not needed here
!! čâustõk+N+Sg+Gen
!!€ čâustõ^1VOWk{XC}^V2VV^K2GG
!!€ čâustõõǥǥ00

!Not needed here
!! čâustõk+N+Sg+Gen
!!€ čâustõ^1VOW0k{XC}^V2VV^PAL^K2GG
!!€ čâustõõʹjj000

!! ## CONSONANT QUALITY CHANGE


!! ### Pedagogical X3 length mark after diphthongs in vertical line ˈ

!2015-05-20 What is this?(tbw)


!! ### Adding X3 length mark
"Consonant X3 lengthening after diphthong in vertical line ˈ character"
!! **@RULENAME@** - used in +N+Sg+Ill
!! **jeäll:jiâlˈlu,**
!! b c č ǯ d đ g ǧ k ǩ l m n p r s š t v also htˈt nˈnj
 0:ˈ <=>
 (ʹ:|%{ʹØ%}:)     b  _ b    [ NeutrVowLenghtHeightDiphPalAllegroPAL %^CC2CCC:| Vow: Cns:+ PenNeutrVowLenghtHeightDiphPalAllegroPAL %^Pen: %^CC2CCC:] ;
          (ʹ:|%{ʹØ%}:)     c  _ c    [ NeutrVowLenghtHeightDiphPalAllegroPAL %^CC2CCC:| Vow: Cns:+ PenNeutrVowLenghtHeightDiphPalAllegroPAL %^Pen: %^CC2CCC:] ;
          (ʹ:|%{ʹØ%}:)     č  _ č    [ NeutrVowLenghtHeightDiphPalAllegroPAL %^CC2CCC:| Vow: Cns:+ PenNeutrVowLenghtHeightDiphPalAllegroPAL %^Pen: %^CC2CCC:] ;
          (ʹ:|%{ʹØ%}:)     ʒ  _ ʒ    [ NeutrVowLenghtHeightDiphPalAllegroPAL %^CC2CCC:| Vow: Cns:+ PenNeutrVowLenghtHeightDiphPalAllegroPAL %^Pen: %^CC2CCC:] ;
          (ʹ:|%{ʹØ%}:)     ǯ  _ ǯ    [ NeutrVowLenghtHeightDiphPalAllegroPAL %^CC2CCC:| Vow: Cns:+ PenNeutrVowLenghtHeightDiphPalAllegroPAL %^Pen: %^CC2CCC:] ;
          (ʹ:|%{ʹØ%}:)     d  _ d    [ NeutrVowLenghtHeightDiphPalAllegroPAL %^CC2CCC:| Vow: Cns:+ PenNeutrVowLenghtHeightDiphPalAllegroPAL %^Pen: %^CC2CCC:] ;
          (ʹ:|%{ʹØ%}:)     đ  _ đ    [ NeutrVowLenghtHeightDiphPalAllegroPAL %^CC2CCC:| Vow: Cns:+ PenNeutrVowLenghtHeightDiphPalAllegroPAL %^Pen: %^CC2CCC:] ;
          (ʹ:|%{ʹØ%}:)     g: _ g:   [ NeutrVowLenghtHeightDiphPalAllegroPAL %^CC2CCC:| Vow: Cns:+ PenNeutrVowLenghtHeightDiphPalAllegroPAL %^Pen: %^CC2CCC:] ;
          (ʹ:|%{ʹØ%}:)     ǥ: _ ǥ:   [ NeutrVowLenghtHeightDiphPalAllegroPAL %^CC2CCC:| Vow: Cns:+ PenNeutrVowLenghtHeightDiphPalAllegroPAL %^Pen: %^CC2CCC:] ;
          (ʹ:|%{ʹØ%}:)     ǧ _ ǧ   [ NeutrVowLenghtHeightDiphPalAllegroPAL %^CC2CCC:| Vow: Cns:+ PenNeutrVowLenghtHeightDiphPalAllegroPAL %^Pen: %^CC2CCC:] ;
          (ʹ:|%{ʹØ%}:)     j _ j:   [ NeutrVowLenghtHeightDiphPalAllegroPAL %^CC2CCC:| Vow: Cns:+ PenNeutrVowLenghtHeightDiphPalAllegroPAL %^Pen: %^CC2CCC:] ;
          (ʹ:|%{ʹØ%}:)     k: _ k:   [ NeutrVowLenghtHeightDiphPalAllegroPAL %^CC2CCC:| Vow: Cns:+ PenNeutrVowLenghtHeightDiphPalAllegroPAL %^Pen: %^CC2CCC:] ;
          (ʹ:|%{ʹØ%}:)     ǩ: _ ǩ:   [ NeutrVowLenghtHeightDiphPalAllegroPAL %^CC2CCC:| Vow: Cns:+ PenNeutrVowLenghtHeightDiphPalAllegroPAL %^Pen: %^CC2CCC:] ;
          (ʹ:|%{ʹØ%}:)     l  _ :l (j)   [ NeutrVowLenghtHeightDiphPalAllegroPAL %^CC2CCC:| Vow: Cns:+ PenNeutrVowLenghtHeightDiphPalAllegroPAL %^Pen: %^CC2CCC:] ;
!!€ kuâl0l^CC2CCC>a
!!€ kuâlˈl0>a
          (ʹ:|%{ʹØ%}:)     m  _ :m    [ NeutrVowLenghtHeightDiphPalAllegroPAL %^CC2CCC:| Vow: Cns:+ PenNeutrVowLenghtHeightDiphPalAllegroPAL %^Pen: %^CC2CCC:] ;
          (ʹ:|%{ʹØ%}:)     n  _ :n (j)[ NeutrVowLenghtHeightDiphPalAllegroPAL %^CC2CCC:| Vow: Cns:+ PenNeutrVowLenghtHeightDiphPalAllegroPAL %^Pen: %^CC2CCC:] ;
          (ʹ:|%{ʹØ%}:)     ŋ  _ ŋ    [ NeutrVowLenghtHeightDiphPalAllegroPAL %^CC2CCC:| Vow: Cns:+ PenNeutrVowLenghtHeightDiphPalAllegroPAL %^Pen: %^CC2CCC:] ;
          (ʹ:|%{ʹØ%}:)     p  _ p    [ NeutrVowLenghtHeightDiphPalAllegroPAL %^CC2CCC:| Vow: Cns:+ PenNeutrVowLenghtHeightDiphPalAllegroPAL %^Pen: %^CC2CCC:] ;
          (ʹ:|%{ʹØ%}:)     r  _ :r    [ NeutrVowLenghtHeightDiphPalAllegroPAL %^CC2CCC:| Vow: Cns:+ PenNeutrVowLenghtHeightDiphPalAllegroPAL %^Pen: %^CC2CCC:] ;
          (ʹ:|%{ʹØ%}:)     s  _ s    [ NeutrVowLenghtHeightDiphPalAllegroPAL %^CC2CCC:| Vow: Cns:+ PenNeutrVowLenghtHeightDiphPalAllegroPAL %^Pen: %^CC2CCC:] ;
          (ʹ:|%{ʹØ%}:)     š  _ š    [ NeutrVowLenghtHeightDiphPalAllegroPAL %^CC2CCC:| Vow: Cns:+ PenNeutrVowLenghtHeightDiphPalAllegroPAL %^Pen: %^CC2CCC:] ;
          (ʹ:|%{ʹØ%}:) (h) t _ t     [ NeutrVowLenghtHeightDiphPalAllegroPAL %^CC2CCC:| Vow: Cns:+ PenNeutrVowLenghtHeightDiphPalAllegroPAL %^Pen: %^CC2CCC:] ;
          (ʹ:|%{ʹØ%}:)     v _ v     [ NeutrVowLenghtHeightDiphPalAllegroPAL %^CC2CCC:| Vow: Cns:+ PenNeutrVowLenghtHeightDiphPalAllegroPAL %^Pen: %^CC2CCC:] ;

!NO 2019-03-01 SurfaceDiphthong (ʹ:|%{ʹØ%}:)     Cx:Cns  _ Cx: (j) [ NeutrVowLenghtHeightDiphPalAllegroPAL %^CC2CCC:| Vow: Cns:+ PenNeutrVowLenghtHeightDiphPalAllegroPAL %^Pen: %^CC2CCC:] ;
!NO2019-03-01 SurfaceDiphthong _ (ʹ:|%{ʹØ%}:)     Cx:Cns Cx: (j) [ NeutrVowLenghtHeightDiphPalAllegroPAL (%^Allegro:) %^CC2CAllegro:| Vow: Cns:+ PenNeutrVowLenghtHeightDiphPalAllegroPAL %^Pen:  %^CC2CAllegro:] ;

!NO2019-03-01          where Cx in (b c č ʒ ǯ d đ g ǧ ǥ j k ǩ l m n ŋ p r s š t v) matched ; 

! SurfaceDiphthong _ (ʹ:|%{ʹØ%}:) [b (ˈ:) b:| c: (ˈ:) c:| č: (ˈ:) č:| ʒ: (ˈ:) ʒ:| ǯ: (ˈ:) ǯ:| d (ˈ:) d:| đ (ˈ:) đ:| g: (ˈ:) g:| ǧ: (ˈ:) ǧ:| ǥ: (ˈ:) ǥ:| j (ˈ:) j:| k: (ˈ:) k:| ǩ: (ˈ:) ǩ:| l (ˈ:) l:| m (ˈ:) m:| n (ˈ:) n:| ŋ (ˈ:) ŋ:| p: (ˈ:) p:| r (ˈ:) r:| s: (ˈ:) s:| š: (ˈ:) š:| t: (ˈ:) t:| v (ˈ:) v:] 
 SurfaceDiphthong _ (i) (ʹ:|%{ʹØ%}:) Cns:+ Vow: (0:) Cns:+ %^Pen: [%^Allegro:|%^CC2CAllegro:|%^CCC2CAllegro:|%^XYY2XY:|%^XYY2VY: |%^KK2GAllegro:] ; 
!Cns:+ (PenUltCns) (Vow: (0:) Cns:+ (PenUltCns) %^Pen:) %^VV2V:
!ǩeäsʼsted+V:ǩeässast TV_TEYPSTED
!!€ ǩeä0ssa0st^Pen^CC2CAllegro^RmVow^PALK>ed
!!€ ǩeäˈs00ʼst0000>ed
!!€ kue0ʹstelm^Pen^Allegro^RmVow>a
!!€ kueˈʹst0lm000>a

!%{ˈ1%}
"Diphthong extra short marker in vertical line ˈ character"
!! **@RULENAME@** - used in +N+Sg+Ill
!! **ciâlkâlm:ciâˈlklmest**
!!
 %{ˈ1%}:ˈ <=>
     SurfaceDiphthong _ (i|u) Cns:+ Vow: Cns:+ %^Pen: [%^Allegro:|%^CC2CAllegro:] ;
!! ciâlkâlm+N+Pl+Gen:
!!€ ciâ{ˈ1}lkâlm^Pen^Allegro^RmVow>i
!!€ ciâˈlk0lm000>i




!! ### Removing X3 length mark
"Removing Consonant X3 length mark after diphthong in vertical line ˈ LEFT ARROW"
!! **@RULENAME@** - deriving
!! b c č ǯ d đ g ǧ k ǩ l m n p r s š t v also htˈt nˈnj
! NeutrVowLenghtHeightDiphPalAllegroPAL [%^C2CC:]
!! ```
 ˈ:0 <=> (ʹ:|%{ʹØ%}:)     b _ b:     [NeutrVowLenghtHeightDiphPalAllegroPAL | Vow: Cns:*  PenNeutrVowLenghtHeightDiphPalAllegroPAL %^Pen: ][%^CCC2CC:|%^CCC2C:|%^CCC2CAllegro:] ;
         (ʹ:|%{ʹØ%}:)     c: _ c:     [NeutrVowLenghtHeightDiphPalAllegroPAL | Vow: Cns:*  PenNeutrVowLenghtHeightDiphPalAllegroPAL %^Pen: ][%^CCC2CC:|%^CCC2C:|%^KKK2G:|%^KKK2GG: ] ;
         (ʹ:|%{ʹØ%}:)     č: _ č:     [NeutrVowLenghtHeightDiphPalAllegroPAL | Vow: Cns:*  PenNeutrVowLenghtHeightDiphPalAllegroPAL %^Pen: ][%^CCC2CC:|%^CCC2C:|%^KKK2G:|%^KKK2GG:] ;
         (ʹ:|%{ʹØ%}:)     ʒ _ ʒ:     [NeutrVowLenghtHeightDiphPalAllegroPAL | Vow: Cns:*  PenNeutrVowLenghtHeightDiphPalAllegroPAL %^Pen: ][%^CCC2CC:|%^CCC2C:|%^CCC2CAllegro:] ;
         (ʹ:|%{ʹØ%}:)     ǯ _ ǯ:     [NeutrVowLenghtHeightDiphPalAllegroPAL | Vow: Cns:*  PenNeutrVowLenghtHeightDiphPalAllegroPAL %^Pen: ][%^CCC2CC:|%^CCC2C:|%^CCC2CAllegro:] ;
         (ʹ:|%{ʹØ%}:)     d _ d:     [NeutrVowLenghtHeightDiphPalAllegroPAL | Vow: Cns:*  PenNeutrVowLenghtHeightDiphPalAllegroPAL %^Pen: ][%^CCC2CC:|%^CCC2C:|%^CCC2CAllegro:] ;
         (ʹ:|%{ʹØ%}:)     đ _ đ:     [NeutrVowLenghtHeightDiphPalAllegroPAL | Vow: Cns:*  PenNeutrVowLenghtHeightDiphPalAllegroPAL %^Pen: ][%^CCC2C:|%^CCC2CC:|%^CCC2C:|%^CCC2CAllegro:] ;
         (ʹ:|%{ʹØ%}:)     g: _ g:     [NeutrVowLenghtHeightDiphPalAllegroPAL | Vow: Cns:*  PenNeutrVowLenghtHeightDiphPalAllegroPAL %^Pen: ][%^CCC2CC:|%^CCC2C:|%^CCC2CAllegro:] ;
!         (ʹ:|%{ʹØ%}:)     :ǧ _ [:ǧ|ǧ:0|g:0]     [NeutrVowLenghtHeightDiphPalAllegroPAL | Vow: Cns:*  PenNeutrVowLenghtHeightDiphPalAllegroPAL %^Pen: ][%^CCC2CC:|%^CCC2C:|%^CCC2CAllegro:] ;
         (ʹ:|%{ʹØ%}:)     j _ j:     [NeutrVowLenghtHeightDiphPalAllegroPAL | Vow: Cns:*  PenNeutrVowLenghtHeightDiphPalAllegroPAL %^Pen: ][%^CCC2CC:|%^CCC2C:|%^CCC2CAllegro:] ;
         (ʹ:|%{ʹØ%}:)     k: _ k:     [NeutrVowLenghtHeightDiphPalAllegroPAL | Vow: Cns:+ PenNeutrVowLenghtHeightDiphPalAllegroPAL] [%^CCC2CC:|%^CCC2C:|%^KKK2GG:|%^KKK2G:|%^Pen: %^KKK2ZERO: |%^Pen: %^KKK2GG:] ;
         (ʹ:|%{ʹØ%}:)     :ǩ _ [:ǩ|ǩ:0|k:0]     [NeutrVowLenghtHeightDiphPalAllegroPAL | Vow: Cns:*  PenNeutrVowLenghtHeightDiphPalAllegroPAL %^Pen: ][%^CCC2CC:|%^CCC2C:|%^CCC2CAllegro:] ;
         (ʹ:|%{ʹØ%}:)     l _ l:     [NeutrVowLenghtHeightDiphPalAllegroPAL | Vow: Cns:*  PenNeutrVowLenghtHeightDiphPalAllegroPAL %^Pen: ][%^CCC2CC:|%^CCC2C:|%^CCC2CAllegro:] ;
         (ʹ:|%{ʹØ%}:)     m _ m:     [NeutrVowLenghtHeightDiphPalAllegroPAL | Vow: Cns:*  PenNeutrVowLenghtHeightDiphPalAllegroPAL %^Pen: ][%^CCC2CC:|%^CCC2C:|%^CCC2CAllegro:] ;
         (ʹ:|%{ʹØ%}:)     n _ n: (j) [NeutrVowLenghtHeightDiphPalAllegroPAL | Vow: Cns:*  PenNeutrVowLenghtHeightDiphPalAllegroPAL %^Pen: ][%^CCC2CC:|%^CCC2C:|%^CCC2CAllegro:] ;
         (ʹ:|%{ʹØ%}:)     ŋ: _ ŋ:     [NeutrVowLenghtHeightDiphPalAllegroPAL | Vow: Cns:*  PenNeutrVowLenghtHeightDiphPalAllegroPAL %^Pen: ][%^CCC2CC:|%^CCC2C:|%^CCC2CAllegro:] ;
         (ʹ:|%{ʹØ%}:)     p: _ p:     [NeutrVowLenghtHeightDiphPalAllegroPAL | Vow: Cns:*  PenNeutrVowLenghtHeightDiphPalAllegroPAL %^Pen: ][%^CCC2CC:|%^CCC2C:|%^CCC2CAllegro:] ;
         (ʹ:|%{ʹØ%}:)     r _ r:     [NeutrVowLenghtHeightDiphPalAllegroPAL | Vow: Cns:*  PenNeutrVowLenghtHeightDiphPalAllegroPAL %^Pen: ][%^CCC2CC:|%^CCC2C:|%^CCC2CAllegro:] ;
         (ʹ:|%{ʹØ%}:)     s: _ s:     [NeutrVowLenghtHeightDiphPalAllegroPAL | Vow: Cns:*  PenNeutrVowLenghtHeightDiphPalAllegroPAL %^Pen: ][%^CCC2CC:|%^CCC2C:|%^KKK2G:|%^KKK2GG:] ;
         (ʹ:|%{ʹØ%}:)     š: _ š:     [NeutrVowLenghtHeightDiphPalAllegroPAL | Vow: Cns:*  PenNeutrVowLenghtHeightDiphPalAllegroPAL %^Pen: ][%^CCC2CC:|%^CCC2C:|%^KKK2G:|%^KKK2GG:] ;
         (ʹ:|%{ʹØ%}:) (h) t: _ t:     [NeutrVowLenghtHeightDiphPalAllegroPAL | Vow: Cns:*  PenNeutrVowLenghtHeightDiphPalAllegroPAL %^Pen: ][%^CCC2CC:|%^CCC2C:|%^CCC2CAllegro:] ;
         (ʹ:|%{ʹØ%}:)     v _ v:     [NeutrVowLenghtHeightDiphPalAllegroPAL | Vow: Cns:*  PenNeutrVowLenghtHeightDiphPalAllegroPAL %^Pen: ][%^CCC2CC:|%^CCC2C:|%^CCC2CAllegro:] ;
!! ```

!W!"Hyphen with Vx in b, h, etc "
!W!!! __@RULENAME@__ check sma
!W!#:%- <=> :Vx  _ :Vx    ;
!W!      where Vx in ( b  h  p  t ) matched ;
!W!
!W!"Hyphen with Vx in f, j, etc "
!W!!! __@RULENAME@__ check sma
!W!#:%- <=> :Vx  _ :Vx    ;
!W!      where Vx in ( f  j  m  r ) matched ;
!W!
!W!
!W!"Hyphen with Vx  in multiples k ǩ "
!W!!! __@RULENAME@__ check sma
!W! #:%- <=> :Vx (:0) _ :Vx  ;
!W!        where Vx in ( k ǩ ) ;
!W!
!W!"Hyphen with Vx and Vx in multiples g ǧ ǥ "
!W!!! __@RULENAME@__ check sma
!W! #:%- <=> :Vx (:0) _ :Vx  ;
!W!        where Vx in ( g ǧ ǥ ) ;
!W!
!W!"Hyphen with Vx and Vx in multiples c č "
!W!!! __@RULENAME@__ check sma
!W! #:%- <=> :Vx (:0) _ :Vx  ;
!W!        where Vx in ( c č ) ;
!W!
!W!"Hyphen with Vx and Vx in multiples s š "
!W!!! __@RULENAME@__ check sma
!W! #:%- <=> :Vx (:0) _ :Vx  ;
!W!        where Vx in ( s š ) ;
!W!
!W!"Hyphen with Vx and Vx in multiples z ž "
!W!!! __@RULENAME@__ check sma
!W! #:%- <=> :Vx (:0) _ :Vx  ;
!W!        where Vx in ( z ž ) ;
!W!
!W!"Hyphen with Vx and Vx in multiples d đ "
!W!!! __@RULENAME@__ check sma
!W! #:%- <=> :Vx (:0) _ :Vx  ;
!W!        where Vx in ( d đ ) ;
!W!
!W!"Hyphen with Vx and Vx in multiples v w "
!W!!! __@RULENAME@__ check sma
!W! #:%- <=> :Vx (:0) _ :Vx  ;
!W!        where Vx in ( v w ) ;
!W!
!W!"Hyphen with Vx and Vx in multiples a å ä â "
!W!!! __@RULENAME@__ check sma
!W! #:%- <=> :Vx  _ :Vx  ;
!W!        where Vx in ( a å ä â ) ;
!W!
!W!"Hyphen with Vx and Vx in multiples o õ "
!W!!! __@RULENAME@__ check sma
!W! #:%- <=> :Vx _ :Vx  ;
!W!        where Vx in ( o õ ) ;
!W!
!W!"Hyphen with Vx and Vx in multiples l n "
!W!!! __@RULENAME@__ check sma
!W! #:%- <=> :Vx (:0) (j) _ :Vx  ;
!W!        where Vx in ( l n ) ;

!"#:# retension to avoid hyphen"
!! **@RULENAME@** - $ Sakssa-jânnam
! #:# <=>
!     [k|:ǩ] (:0) Dummy:* _  [Ltrs - [k|ǩ]] ;
!        [g|:ǧ|:ǥ] (:0) Dummy:* _ [Ltrs - [g|ǧ|ǥ]] ;
!        [c|č] (:0) Dummy:* _ [Ltrs - [c|č]] ;
!        [s|š] (:0) Dummy:* _ [Ltrs - [s|š]] ;
!        [:z|:ž] (:0) Dummy:* _ [Ltrs - [z|ž]] ;
!        [d|:đ] (t:0|đ:0) Dummy:* _ [Ltrs - [d|đ]] ;
!    :i Dummy:* _ [Ltrs - i] ;
!        :u Dummy:* _ [Ltrs - u] ;
!        :y Dummy:* _ [Ltrs - y] ;
!        [:e|:ẹ] Dummy:* _ [Ltrs - [e|ẹ]] ;
!        [:o|:õ|:ö] Dummy:* _ [Ltrs - [o|õ|ö]] ;
!        [:a|:å|:ä|:â] Dummy:* _ [Ltrs - [a|å|ä|â]] ;
!        [:v|w] (v:0) Dummy:* _ [Ltrs - [v|w]] ;
!        b Dummy:* _ [Ltrs - b] ;
!        f (f:0) Dummy:* _ [Ltrs - f] ;
!        h Dummy:* _ [Ltrs - h] ;
!        [k:j|č:j|j j:|[Ltrs - [l:|n:]] j] Dummy:* _ [Ltrs - j] ;
!!FIXME work with [n (n:)|l (l:)] j
!    l (l:0)  Dummy:* _ [Ltrs - l] ;
!    m (m:0) Dummy:* _ [Ltrs - m] ;
!    [ŋ (ŋ:0)|n (n:0) ] Dummy:* _ [Ltrs - n] ;
!    p Dummy:* _ [Ltrs - p] ;
!    r (r:0) Dummy:* _ [Ltrs - r] ;
!    t Dummy:* _ [Ltrs - t] ;


"Hyphen for splitting between look-alikes"
!! **@RULENAME@** - used in Kääzzkõsraajõstuâjj-joouk
 %#:%- <=>
      [:k|:ǩ]          (:0) Dummy:* (∑) _ [k|ǩ] ;
   [:g|:ǧ|:ǥ]          (:0) Dummy:* (∑) _ [g|ǧ|ǥ] ;
      [:c|:č]          (:0) Dummy:* (∑) _ [c|č] ;
      [:s|:š]          (:0) Dummy:* (∑) _ [s|š] ;
      [:z|:ž]          (:0) Dummy:* (∑) _ [z|ž] ;
      [:d|:đ]     (t:0|đ:0) Dummy:* (∑) _ [d|đ] ;
      [:v|:w]         (v:0) Dummy:* (∑) _ [v|w] ;
           :b               Dummy:* (∑) _ b ;
           :f         (f:0) Dummy:* (∑) _ f ;
           :h               Dummy:* (∑) _ h ;
           :j         (j:0) Dummy:* (∑) _ j ;
           :l (l:0) (j)     Dummy:* (∑) _ l ;
           :m         (m:0) Dummy:* (∑) _ m ;
 [:ŋ (ŋ:0)|:n (n:0) (j)]    Dummy:* (∑) _ n ;
           :p               Dummy:* (∑) _ p ;
           :r         (r:0) Dummy:* (∑) _ r ;
           :t               Dummy:* (∑) _ t ;

           :i               Dummy:* (∑) _ i ;
           :u               Dummy:* (∑) _ u ;
           :y               Dummy:* (∑) _ y ;
      [:e|:ẹ]               Dummy:* (∑) _ [e|ẹ] ;
   [:o|:õ|:ö]               Dummy:* (∑) _ [o|õ|ö] ;
[:a|:å|:ä|:â]               Dummy:* (∑) _ [a|å|ä|â] ;

!! Sakssajânnam+N+Prop+Sg+Nom: (∑) **Germany/Saksa**
!!€ Sakssa#jânnam
!!€ Sakssa#jânnam
!!$ Sakssa#jânnam
!!$ Sakssa-jânnam

!! koummlo-õhtt+Num+Sg+Nom: (∑) **21**
!!€ koummlo#õhtt
!!€ koummlo-õhtt
!!$ koummlo#õhtt
!!$ koummlo#õhtt

!! tuâjj+N+Cmp/SgNom+Cmp#joukk+N+Sg+Nom:  **team/työryhmä**
!!€ tuâjj#joukk
!!€ tuâjj-joukk
!!$ tuâjj#joukk
!!$ tuâjj#joukk

!! sääʹmm+N+Cmp/SgGen+Cmp#musikk+N+Sg+Nom: **Skolt Sámi music/kolttamusiikki**
!! sää0mm^PAL^CC2C{-Ø}#musikk
!! sääʹm000-#musikk

!Q!!        Cx  _  Cx ;
!Q!!  where Cx in (b f h j l m n p q r t v w x ʒ ǯ ŋ) ;
!Q!!!€ kääzzkõsraajõstuâjj0#joukk
!Q!!!€ kääzzkõsraajõstuâjj-#joukk
!Q!

!!TESTS
!echo 'c e ä {ʹØ}:0 v 0:ˈ v ^CC2CCC:0 > a' | hfst-pair-test src/fst/phonology.compose.hfst
!echo 'cieʹvv+N+Sg+Ill' | hfst-lookup src/morphology/lexicon.hfst
