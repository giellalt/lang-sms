! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or feedback@divvun.no

!! !!!Skolt Saami Pronoun Morphology 

LEXICON PRON_
!! The lexicon ''@LEXNAME@'' assigns the tag __+Pron__
+Pron: # ;

LEXICON PRON-PERS_
+Pron+Pers: # ;
LEXICON PRON-PERS-SG1_
+Pron+Pers+Sg1: # ;
LEXICON PRON-PERS-SG2_
+Pron+Pers+Sg2: # ;
LEXICON PRON-PERS-SG3_
+Pron+Pers+Sg3: # ;
LEXICON PRON-PERS-PL1_
+Pron+Pers+Pl1: # ;
LEXICON PRON-PERS-PL2_
+Pron+Pers+Pl2: # ;
LEXICON PRON-PERS-PL3_
+Pron+Pers+Pl3: # ;


LEXICON PRON-REL_
+Pron+Rel: # ;

!! !!!Pronouns


!! Pointing to all the pronominal subgroups

LEXICON Pronoun !!= * @CODE@
Personal ;      !!= * @CODE@
Demonstrative ; !!= * @CODE@
Reflexive ;     !!= * @CODE@
Interrogative ; !!= * @CODE@
! Reciprocal ;
 Relative ;	!!= * @CODE@ kååʹtt, mii
! Indefinite ;


!! !!Personal pronouns


!! Splitting according to person

LEXICON Personal           !!= * @CODE@
mon+Pron+Pers:0m perspron ;  !!= * @CODE@ The Epsilon  inserted 2015-02-11
ton+Pron+Pers:t perspron ;  !!= * @CODE@ The Epsilon  inserted 2015-02-11
!ton+Pron+Pers:0t perspron ;  !!= * @CODE@ The Epsilon  inserted 2015-02-11
son+Pron+Pers:0s perspron ;  !!= * @CODE@ The Epsilon  inserted 2015-02-11

LEXICON perspron
:u	    perspronoblsg ;
:uän    persprondu ;
:ån	    persprondu ;
:	    perspronpl ;
+Sg+Nom:on	K ;
+Du+Nom:ååna	K ;
+Pl+Loc:eeʹst	K ;

LEXICON perspronoblsg 
+Sg+Gen:u	      K ;
+Sg+Acc:u	      K ;
+Sg+Ill:ʹnne	      K ;
+Sg+Loc:ʹst	      K ;
+Sg+Com:in	      K ;
+Sg+Abe:utää	      K ;
+Ess:uʹnen	      K ;
+Par:uʹđed	      K ;



LEXICON persprondu
+Du+Nom:a		K ;
+Du+Gen:nai	      K ;
+Du+Acc:naid	      K ;
+Du+Ill:naid	      K ;
+Du+Loc:nast	      K ;
+Du+Com:nain	      K ;
+Du+Abe:naitää	      K ;
+Du+Ess:nan	      K ;

LEXICON perspronpl
+Pl+Nom:ij		K ; !!= * @CODE@ mij, tij, sij
+Pl+Gen:ij	      K ; !!= * @CODE@ mij, tij, sij
+Pl+Acc:iʹjjid	      K ; !!= * @CODE@ miʹjjid, tiʹjjid, siʹjjid
+Pl+Ill:iʹjjid	      K ; !!= * @CODE@ miʹjjid, tiʹjjid, siʹjjid
+Pl+Loc:eeʹst	      K ; !!= * @CODE@ meeʹst, teeʹst, seeʹst
+Pl+Com:ijvuiʹm     K ; !!= * @CODE@ mijvuiʹm, tijvuiʹm, sijvuiʹm
+Pl+Abe:ijtää	      K ; !!= * @CODE@ mijtää, tijtää, sijtää




!! !!Demonstrative pronouns


LEXICON Demonstrative !!= * @CODE@
tät+Pron+Dem:tä		SHORTDEM ;
tut+Pron+Dem:tu 	SHORTDEM ;
tõt+Pron+Dem:tõ 	SHORTDEM ;
tät+Pron+Dem:tää 	LONGDEM ;
tut+Pron+Dem:tuu 	LONGDEM ;
tõt+Pron+Dem:t  	TOTDEM ; !!= * @CODE@ The Epsilon has been removed 2015-05-13
tiet-aa:tie 		TIET-AA ;

LEXICON SHORTDEM !!= * @CODE@
+Sg+Nom:t	K ;
+Sg+Acc:n	K ;
+Sg+Gen:n	K ;
+Sg+Loc:ʹst	K ;
+Sg+Com:in	K ;
+Sg+Abe:ntää	K ;
+Pl+Nom:k	K ;
+Pl+Acc:id	K ;
+Pl+Gen:i	K ;
+Pl+Ill:id	K ;
+Pl+Loc:in	K ;
+Pl+Com:ivuim	K ;
+Pl+Abe:itää	K ;

LEXICON LONGDEM !!= * @CODE@
+Sg+Ill:zz	K ;
+Ess:ʹđen	K ;
+Par:đ	K ;
+Par:ʹđed	K ;

LEXICON TOTDEM  !!= * @CODE@
+Sg+Ill:õõzz 	K ;
+Ess:âʹđen 	K ;
+Par:âʹđ 	K ;
+Par:âʹđed 	K ;
+Pl+Nom:õõk 	K ;
+Pl+Gen:õõi 	K ;
+Pl+Loc:in 	K ;

LEXICON TIET-AA !!= * @CODE@
+Sg+Nom:t	-AA ;
+Sg+Acc:n	-AA ;
+Sg+Gen:n	-AA ;
+Sg+Ill:zz	-AA ;
+Sg+Loc:ʹst	-AA ;
+Sg+Com:in	-AA ;
+Pl+Nom:k	-AA ;
+Pl+Acc:id	-AA ;
+Pl+Gen:i	-AA ;
+Pl+Ill:id	-AA ;
+Pl+Com:i-aavuiʹm    K ;

LEXICON -AA !!= * @CODE@
:-aa K ;



!! !!REFLEXIVE PRONOUNS

!work required
LEXICON PRON-REFL_
+Pron+Refl+WORK: # ;

LEXICON PRON-REFL-SG1_
+Pron+Refl+Sg1+WORK: K ;
LEXICON PRON-REFL-SG2_
+Pron+Refl+Sg2+WORK: K ;
LEXICON PRON-REFL-SG3_
+Pron+Refl+Sg3+WORK: K ;
LEXICON PRON-REFL-PL1_
+Pron+Refl+Pl1+WORK: K ;
LEXICON PRON-REFL-PL2_
+Pron+Refl+Pl2+WORK: K ;
LEXICON PRON-REFL-PL3_
+Pron+Refl+Pl3+WORK: K ;

LEXICON PRON-REFL-SG1-ILL_
+Pron+Refl+Sg1+Ill: # ;
LEXICON PRON-REFL-SG2-ILL_
+Pron+Refl+Sg2+Ill: # ;
LEXICON PRON-REFL-SG3-ILL_
+Pron+Refl+Sg3+Ill: # ;
LEXICON PRON-REFL-PL1-ILL_
+Pron+Refl+Pl1+Ill: # ;
LEXICON PRON-REFL-PL2-ILL_
+Pron+Refl+Pl2+Ill: # ;
LEXICON PRON-REFL-PL3-ILL_
+Pron+Refl+Pl3+Ill: # ;

LEXICON PRON-REFL-SG1-LOC_
+Pron+Refl+Sg1+Loc: # ;
LEXICON PRON-REFL-SG2-LOC_
+Pron+Refl+Sg2+Loc: # ;
LEXICON PRON-REFL-SG3-LOC_
+Pron+Refl+Sg3+Loc: # ;
LEXICON PRON-REFL-PL1-LOC_
+Pron+Refl+Pl1+Loc: # ;
LEXICON PRON-REFL-PL2-LOC_
+Pron+Refl+Pl2+Loc: # ;
LEXICON PRON-REFL-PL3-LOC_
+Pron+Refl+Pl3+Loc: # ;



LEXICON Reflexive !!= * @CODE@
jiõčč+Pron+Refl+Gen+Sg3:jiijjâs	K ;
jiõčč+Pron+Refl+Acc+Sg3:jiijjâs	K ;
jiõčč+Pron+Refl+Gen+Pl1:jieʹččen	K ;
jiõčč+Pron+Refl+Acc+Pl1:jieʹččen	K ;
jiõčč+Pron+Refl+Gen:jiõčč		GA-I ;
jiõčč+Pron+Refl+Acc:jiõčč		GA-I ;
jiõčč+Pron+Refl+Gen:jiičč		GA-I ;
jiõčč+Pron+Refl+Acc:jiičč		GA-I ;
jiõčč+Pron+Refl+Gen:jiijj		GA-II ;
jiõčč+Pron+Refl+Acc:jiijj		GA-II ;
jiõčč+Pron+Refl+Gen:jiiʹjj		GA-II ;
jiõčč+Pron+Refl+Acc:jiiʹjj		GA-II ;
jiõčč+Pron+Refl+Ill:jiõccs		ILEPX ;
jiõčč+Pron+Refl+Ill:jiccs		ILEPX ;
jiõčč+Pron+Refl+Loc:jijst		ILEPX ;
jiõčč+Pron+Refl+Loc:jiijst		LOCPX ;
jiõčč+Pron+Refl+Ess:jiõččn		ILEPX ;
jiõčč+Pron+Refl+Com:jijj		COMPX ;


LEXICON GA-I !!= * @CODE@
+Sg1:an K ;
+Pl1:ân K ;

LEXICON GA-II !!= * @CODE@
+Sg2:ad K ;
+Pl1:ân K ;
+Pl2:âd K ;
+Pl3:âz K ;

LEXICON ILEPX !!= * @CODE@
ROBLSGPX ;
ROBLPLPX ;

LEXICON LOCPX !!= * @CODE@
+Sg1:an	K ;
ROBLPLPX ;

LEXICON COMPX !!= * @CODE@
ROBLSGPX ;
RCOMPLPX ;

LEXICON ROBLSGPX !!= * @CODE@
+Sg1:an K ;
+Sg2:ad K ;
+Sg3:es K ;

LEXICON ROBLPLPX !!= * @CODE@
+Pl1:een K ;
+Pl2:eed K ;
+Pl3:eez K ;

LEXICON RCOMPLPX !!= * @CODE@
+Pl1:eenvuiʹm K ;
+Pl2:eedvuiʹm K ;
+Pl3:eezvuiʹm K ;




!! !!Interrogative pronouns


LEXICON Interrogative !!= * @CODE@
mii+Pron+Interr:m	MK ; !!= * @CODE@ The Epsilon  inserted 2015-02-11
ǩii+Pron+Interr:ǩ	MK ; !!= * @CODE@ The Epsilon  inserted 2015-02-11
mii+Pron+Interr:mõõ	MK-long ;
ǩii+Pron+Interr:ǩeä	MK-long ;
mii+Pron+Interr:mõ	MK-short ;
ǩii+Pron+Interr:ǩeä	MK-short ;
mii+Pron+Interr:mââ	MK-low ;
ǩii+Pron+Interr:ǩeä	MK-low ;
mii+Pron+Interr+Sg+Loc:mâʹst	K ;

LEXICON MK !!= * @CODE@
+Sg+Nom:ii	K ;

LEXICON MK-long !!= * @CODE@
+Sg+Acc:n	K ;
+Sg+Gen:n	K ;
+Sg+Ill:zz	K ;
+Pl+Nom:k	K ;

LEXICON MK-short !!= * @CODE@
+Sg+Com:in	K ;
+Sg+Abe:ntää	K ;

LEXICON MK-low !!= * @CODE@
+Ess:ʹđen	K ;
+Par:ʹđ	K ;
+Par:ʹđed	K ;
+Pl+Com:ivuiʹm	K ;
+Pl+Abe:itää	K ;


LEXICON Relative !!= * @CODE@
mii+Pron+Rel:m       MK ;
ǩii+Pron+Rel:ǩ       MK ;
mii+Pron+Rel:mõõ     MK-long ;
ǩii+Pron+Rel:ǩeä     MK-long ;
mii+Pron+Rel:mõ      MK-short ;
ǩii+Pron+Rel:ǩeä     MK-short ;
mii+Pron+Rel:mââ     MK-low ;
ǩii+Pron+Rel:ǩeä     MK-low ;
mii+Pron+Rel+Sg+Loc:mâʹst	K ;
kååʹtt+Pron+Rel+Sg+Nom:kååʹtt K ;
kååʹtt+Pron+Rel+Sg+Acc:koon K ;
kååʹtt+Pron+Rel+Sg+Gen:koon K ;
kååʹtt+Pron+Rel+Sg+Gen+Err/Orth:koonn K ;
kååʹtt+Pron+Rel+Sg+Ill:koozz K ;
kååʹtt+Pron+Rel+Sg+Loc:koʹst K ;
kååʹtt+Pron+Rel+Sg+Com:kooin K ;
kååʹtt+Pron+Rel+Pl+Nom:kook K ;
kååʹtt+Pron+Rel+Pl+Gen:kooi K ;
kååʹtt+Pron+Rel+Pl+Acc:koid K ;
kååʹtt+Pron+Rel+Pl+Ill:koid K ;
kååʹtt+Pron+Rel+Pl+Loc:koin K ;
kååʹtt+Pron+Rel+Pl+Loc:kooin K ;
kååʹtt+Pron+Rel+Sg+Prl:kooiǥ K ;
!kååʹtt+Pron+Rel:koot K ; 


!! !SPATIAL PRONOUNS
LEXICON PRON-SPAT-SG1_
+Pron+Spat+Sg1: K ;
LEXICON PRON-SPAT-SG2_
+Pron+Spat+Sg2: K ;
LEXICON PRON-SPAT-SG3_
+Pron+Spat+Sg3: K ;
LEXICON PRON-SPAT-PL1_
+Pron+Spat+Pl1: K ;
LEXICON PRON-SPAT-PL2_
+Pron+Spat+Pl2: K ;
LEXICON PRON-SPAT-PL3_
+Pron+Spat+Pl3: K ;

!! !MISC
LEXICON PRON_DÕÕT
+Pron+Det+Sg+Nom:t K ;
+Pron+Det+Sg+Ill:zze K ;
+Pron+Det+Sg+Ill:zz K ;
+Pron+Det+Ess:đen K ;
+Pron+Det+Par:đed K ;
+Pron+Det+Sg+Gen:n K ;
+Pron+Det+Sg+Loc:ʹst K ;
+Pron+Det+Pl+Nom:k K ;
