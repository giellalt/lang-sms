

!! !!! Skolt Saami noun morphology 


!! !!Unclassified

LEXICON n 
!! The lexicon ''@LEXNAME@'' is a dummy lexicon containing N Sg Nom only
!! We resend it to N-toll while waiting for a better analysis.

N-toll ;

!! !!â-stems

LEXICON N-toll !!= @CODE@ for Nouns with -âst Loc
+N+Sg+Nom: K ;
+N+Sg+Gen:%^VOWXL K ;
+N+Sg+Acc:%^VOWXL K ;
+N+Sg+Ill:%^VOWSH%^PAL%>e K ;
+N+Sg+Loc:%^VOWXL%>âst K ;
+N+Sg+Com:%^VOWXL%>in K ;
+N+Ess:%>ân K ; ! ess and prt without
+N+Par:%>âd K ;
+N+Sg+Abe:%^VOWXL%>tää K ;

+N+Pl+Nom:%^VOWXL K ;
+N+Pl+Gen:%^VOWXL%>i K ;
+N+Pl+Acc:%^VOWXL%>id K ;
+N+Pl+Ill:%^VOWXL%>id K ;
+N+Pl+Loc:%^VOWXL%>in K ;
+N+Pl+Com:%^VOWXL%>ivuiˊm K ;
+N+Pl+Abe:%^VOWXL%>itää K ;

LEXICON N-miarr !!= @CODE@ with -âst Loc but ense Ill
+N+Sg+Nom: K ;
+N+Sg+Gen:%^VOWXL K ;
+N+Sg+Acc:%^VOWXL K ;
+N+Sg+Ill:%^VOWTense%^PAL%>e K ;
+N+Sg+Loc:%^VOWXL%>âst K ;
+N+Sg+Com:%^VOWXL%>in K ;
+N+Ess:%>ân K ; ! ess and prt without
+N+Par:%>âd K ;
+N+Sg+Abe:%^VOWXL%>tää K ;

+N+Pl+Nom:%^VOWXL K ;
+N+Pl+Gen:%^VOWXL%>i K ;
+N+Pl+Acc:%^VOWXL%>id K ;
+N+Pl+Ill:%^VOWXL%>id K ;
+N+Pl+Loc:%^VOWXL%>in K ;
+N+Pl+Com:%^VOWXL%>ivuiˊm K ;
+N+Pl+Abe:%^VOWXL%>itää K ;


!! !!a-stems

LEXICON N-pess !!= @CODE@ Nouns with -ast Loc
+N+Sg+Nom: K ;
+N+Sg+Gen:%^VOWL%^CShort K ;
+N+Sg+Acc:%^VOWL%^CShort K ;
+N+Sg+Ill:%^VOWRaise%>u K ;
+N+Sg+Loc:%^VOWL%^CShort%>ast K ;
+N+Sg+Com:%^VOWL%^CShort%>ain K ;
+N+Ess:%>an K ; ! ess and prt without
+N+Par:%>ad K ;
+N+Sg+Abe:%^VOWL%^CShort%>tää K ;

+N+Pl+Nom:%^VOWL%^CShort K ;
+N+Pl+Gen:%^VOWL%^CShort%>ai K ;
+N+Pl+Acc:%^VOWL%^CShort%>aid K ;
+N+Pl+Ill:%^VOWL%^CShort%>aid K ;
+N+Pl+Loc:%^VOWL%^CShort%>ain K ;
+N+Pl+Com:%^VOWL%^CShort%>aivuiˊm K ;
+N+Pl+Abe:%^VOWL%^CShort%>aitää K ;



!! !!e-stems

LEXICON N-piess !!= @CODE@ Nouns with -est +Loc 
!! * There are several stem types
!! * pieˊss:pieˊss
+N+Sg+Ill:%^VOWLower%^PALNo%>a K ;
COMMON_E_A_STEM ;

LEXICON COMMON_E_A_STEM
+N+Sg+Nom:%^PAL K ;
+N+Sg+Gen:%^VOWL%^CShort K ;
+N+Sg+Acc:%^VOWL%^CShort K ;
+N+Sg+Loc:%^VOWL%^CShort%>est K ;
+N+Sg+Com:%^VOWL%^CShort%>in K ;
+N+Ess:%>en K ; ! ess and prt without
+N+Par:%>ed K ;
+N+Sg+Abe:%^VOWL%^CShort%>tää K ;

+N+Pl+Nom:%^VOWL%^CShort K ;
+N+Pl+Gen:%^VOWL%^CShort%>i K ;
+N+Pl+Acc:%^VOWL%^CShort%>id K ;
+N+Pl+Ill:%^VOWL%^CShort%>id K ;
+N+Pl+Loc:%^VOWL%^CShort%>in K ;
+N+Pl+Com:%^VOWL%^CShort%>ivuiˊm K ;
+N+Pl+Abe:%^VOWL%^CShort%>itää K ;


!! !!e-a-stems

LEXICON N-ress !!= @CODE@ Nouns with -est Loc and -a Ill without ˊ
+N+Sg+Ill:%^VOWLower%>a K ; !!= @CODE@ as piess but without ^PALNo
COMMON_E_A_STEM ;


!! !!m-stems
LEXICON N-laajj
+N+Sg+Nom:%^WG K ;
+N+Sg+Gen:âm K ;
+N+Sg+Acc:âm K ;
+N+Sg+Ill:m%>a K ;
+N+Sg+Loc:m%>est K ;
+N+Sg+Com:m%>in K ;
+N+Ess:m%>en K ; ! ess and prt without
+N+Par:m%>ed K ;
+N+Sg+Abe:âm%>tää K ;

+N+Pl+Nom:âm K ;
+N+Pl+Gen:m%>i K ;
+N+Pl+Acc:m%>id K ;
+N+Pl+Ill:m%>id K ;
+N+Pl+Loc:m%>in K ;
+N+Pl+Com:m%>ivuiˊm K ;
+N+Pl+Abe:m%>itää K ;


!! !!n-stems
LEXICON N-kuolban
+N+Sg+Nom: K ;
+N: N_OBL ;

LEXICON N-siom
+N+Sg+Nom: K ;
+N:ân N_OBL ;


LEXICON N_OBL
+N+Sg+Gen: K ;
+N+Sg+Acc: K ;
+N+Sg+Ill:%>a K ;
+N+Sg+Loc:%>est K ;
+N+Sg+Com:%>in K ;
+N+Ess:%>en K ; ! ess and prt without
+N+Par:%>ed K ;
+N+Sg+Abe:%>tää K ;

+N+Pl+Nom: K ;
+N+Pl+Gen:%>i K ;
+N+Pl+Acc:%>id K ;
+N+Pl+Ill:%>id K ;
+N+Pl+Loc:%>in K ;
+N+Pl+Com:%>ivuiˊm K ;
+N+Pl+Abe:%>itää K ;

