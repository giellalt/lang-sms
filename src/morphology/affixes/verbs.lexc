
!! !!!Skolt Saami verb morphology

!! First a lexicon V_ for still unclassified entries.

LEXICON V_
+V+WORK: # ;

LEXICON V-NEG_
+V+Neg+WORK: # ;

!! !!Irregular verbs

!! Then irregular verbs ij and the copula.

LEXICON IJ
!! The lexicon ''@LEXNAME@''

+Ind+Sg1:jiõm	   K ; !!= * @CODE@
+Ind+Sg1:jim	   K ; !!= * @CODE@
+Ind+Sg2:jiõk	   K ; !!= * @CODE@
+Ind+Sg2:jik	   K ;
+Ind+Sg3:ij		   K ;
+Ind+Sg4:jeäʹt	   K ;
+Ind+Pl1:jeäʹp	   K ;
+Ind+Pl1:jep	   K ;
+Ind+Pl2:jeäʹped   K ;
+Ind+Pl2:jeʹped	   K ;
+Ind+Pl3:jiâ	   K ;
+Ind+Pl3:jeä	   K ;
+Ind+Pl3:jie	   K ;

+Imprt+Sg2:jieʹl	   K ;
+Imprt+Sg2:jeʹl	   K ;
+Imprt+Pl2:jieʹlˈled  K ;	
+Imprt+Pl2:jeʹlˈled   K ;	

+Opt+Sg2:jieʹl	   K ;
+Opt+Sg2:jeʹl	   K ;
+Opt+Sg3:jeälas	   K ;
+Opt+Pl1:jeälˈlap   K ;
+Opt+Pl2:jieʹlˈled  K ;
+Opt+Pl2:jeʹlˈled   K ;			
+Opt+Pl3:jeälˈlaz   K ;


! leeʹd
LEXICON LEED
!! The lexicon ''@LEXNAME@'' for the copula
 V_LEEʹD ;
LEXICON V_LEEʹD !!= * @CODE@ leeʹd:l
!! The lexicon ''@LEXNAME@'' for the copula

+V+Ind+Prs:		LEED-PRS ;
+V+Ind+Prt:		LEED-PRT ;
+V+Imprt:			LEED-IMPRT ;
+V+Imprt+Sg2:eäkˈku	K ;
+V+Imprt+ConNeg:%>eäkˈku K ;
+V:%>eʹč		CONDITIONAL ; !
+V+Pot+Sg1:%>eʹžžem K	;
+V+Pot+Sg2:%>eʹžžiǩ K	;
+V+Pot+Sg3:%>eežž	K ; !WG
+V+Pot+Sg3:%>eež	K ; !
+V+Pot+Pl1:%>eʹžžep K	; !
+V+Pot+Pl1:%>eʹžžim K	; !
+V+Pot+Pl1:%>eʹžžin K	; !Does this exist
+V+Pot+Pl2:%>eʹžžveʹted K	;
+V+Pot+Pl3:%>eʹžže K	;
+V+Pot+Sg4:%>eʹžžet K ;
+V+Pot+ConNeg:%>eʹžže K ;
+V+Inf:%>eeʹd K ;
+V+PrsPrc:%>eʹddi K ;
+V+PrfPrc:%>eämˈmaž K ;
+V+PrfPrc:%>eämˈma K ;
+V+PrfPrc:%>eäm K ;

!! !!REGULAR VERBS
!! !CLASS 1 HIGH VOWEL, NO PALATALIZATION

LEXICON V_PÕÕLLÂD !!= * @CODE@ põõllâd:põõll
! pos: V
! 1. VERBS WITH SINGLE-SYLLABLE Ind.Prs.Sg3 (2009: 354)
! inf: âd
! foot: 2syll
! 1.1 Inf in âd (vowel shift)
! diphthong: no
! vowel_shift: yes
! consonant_type: geminate-single
! gradation: yes3
! weak_grade: single
! strong_grade: geminate

!   Raised: Inf, Ind.Prt.Pl3, Imprt.Sg2, Pot.Sg3
!   Lowered: Ind.Prs.Sg3, Ind.Prs.Pl3, Imprt.Sg3
! palatal_coloring: no (%^PALÄ, %^PALE, %^PALẸ)
! palatalization: yes (%^PAL, %^PALNo)
! velar_palatalization: FIX no (pååttaǩ)
! 1.1.1 Has Palatalization
! 1.1.1[1] (Palatalization pattern)
!   Palatalized: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Not Palatalized: Inf, Ind.Prs.Sg3, Imprt.Sg2, Imprt.Sg3, Pot.Sg3
! pedagogical_gradation: no
! orthographic_gradation: yes
! 1.1.1[1].1 Lacks Specifically Pedagogical Gradation
! 1.1.1[1].1.1 Has Orthographic Gradation
! 1.1.1[1].1.1[231] (Monophthong + Consonant and Consonant Geminate alternation)
!   Strong grade: Inf, Ind.Prs.Sg3  = põõllâd
!   Extra strong grade: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Weak grade: Imprt.Sg2, Pot.Sg3, Imprt.Sg3
! FORMS
! (1)  Inf: põõllâd
! (2)  Ind.Prs.Sg3: pââll
! (3)  Ind.Prs.Pl3: pâʹlle
! (4)  Ind.Prt.Pl3: põʹlle
! (5)  Ind.Imprt.Sg2: põõl
! (6)  Ind.Pot.Sg3: põõlâž
! (7)  Ind.Imprt.Sg3: pââlas
! (8)  Imprt.13.ConNeg: põll
! (9)  Ind.Imprt.Pl3: pâllaz
! (10) Allegro for incoative: põl
! * Yaml: __V-moonnad__
! põõllâd:põõll
! Strong grade in penultimate syllable
+V: VSUF-INF_ÂD ; !1 põõllâd
!+V:%^VOWLower VSUF-IND-PRS-SG3_ÂD ; !2
+V+Imprt+Pl3:%^VV2V%^VOWLower%>az K ; ! pâllaz
+V+Ind+Prs+Sg3:%^VOWLower K ; ! pââll
+V+Ind+Prs+Pl3:%^VV2V%^VOWLower%^PAL%>e K ; !3 pâʹlle
+V:%^VV2V%^PAL VSUF-IND-PRT-PL3_ÂD ; !4 põʹlle
+V:%^CC2C VSUF-I-POTKOND_ÂD ; !5 põõl-
+V+Pot+Sg3:%^CC2C%>âž   K ; !6 põõl-
+V+Imprt+Sg3:%^VOWLower%^CC2C%>as K ; !7 pââlas
+V:%^VV2V VSUF-IMPRT-13-CONNEG_ÂD ; !8 põll-
+V+Imprt+13+ConNeg:%^VV2V%>u K ; ! põllu
+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:%^VV2V%^CC2C»škuätt V_PUEʹTTED ;
+V+Der/ted:%^CC2C»ât  V_AALǤTED ;

LEXICON V_VIIKKÂD !!= * @CODE@ viikkâd:viikk
! pos: V
! gradation: yes
! weak geminate
! 1. VERBS WITH SINGLE-SYLLABLE Ind.Prs.Sg3 (2009: 354)
! foot: 2syll
! inf: âd
! 1.1 Inf in âd (vowel shift)
! vowel_shift: yes
! diphthong: no
! consonant_type: single::geminate
!   Raised: Inf, Ind.Prt.Pl3, Imprt.Sg2, Pot.Sg3
!   Lowered: Ind.Prs.Sg3, Ind.Prs.Pl3, Imprt.Sg3
! palatal_coloring: no (%^PALÄ, %^PALE, %^PALẸ)
! palatalization: yes (%^PAL, %^PALNo)
! velar_palatalization: FIX no (pååttaǩ)
! 1.1.1 Has Palatalization
! 1.1.1[1] (Palatalization pattern)
!   Palatalized: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Not Palatalized: Inf, Ind.Prs.Sg3, Imprt.Sg2, Imprt.Sg3, Pot.Sg3
! pedagogical_gradation: no
! orthographic_gradation: yes
! 1.1.1[1].1 Lacks Specifically Pedagogical Gradation
! 1.1.1[1].1.1 Has Orthographic Gradation
! 1.1.1[1].1.1[231] (Monophthong + Consonant and Consonant Geminate alternation)
!   Strong grade: Inf, Ind.Prs.Sg3  = viikkâd
!   Extra strong grade: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Weak grade: Imprt.Sg2, Pot.Sg3, Imprt.Sg3
! FORMS
! (1)  Inf: viikkâd
! (2)  Ind.Prs.Sg3: veekk
! (3)  Ind.Prs.Pl3: veʹǩǩe
! (4)  Ind.Prt.Pl3: viʹǩǩe
! (5)  Ind.Imprt.Sg2: viiǥǥ
! (6)  Ind.Pot.Sg3: viiǥǥâž
! (7)  Ind.Imprt.Sg3: veeǥǥas
! (8)  Imprt.13.ConNeg: vikk
! (9)  Ind.Imprt.Pl3: vekkaz
! (10) Allegro for incoative: viǥ»
! * Yaml: __V-viikkad__
! viikkâd:viikk
! Strong grade in penultimate syllable
+V: VSUF-INF_ÂD ; !1 viikkâd
!+V:%^VOWLower VSUF-IND-PRS-SG3_ÂD ; !2
+V+Imprt+Pl3:%^VV2V%^VOWLower%>az K ; ! vekkaz
+V+Ind+Prs+Sg3:%^VOWLower K ; ! veekk
+V+Ind+Prs+Pl3:%^VV2V%^VOWLower%^PAL%>e K ; !3 veʹǩǩe
+V:%^VV2V%^PAL VSUF-IND-PRT-PL3_ÂD ; !4 viʹǩǩe
+V:%^KK2GG VSUF-I-POTKOND_ÂD ; !5 viiǥǥ-
+V+Pot+Sg3:%^KK2GG%>âž   K ; !6 viiǥǥ-
+V+Imprt+Sg3:%^VOWLower%^KK2GG%>as K ; !7 veeǥǥas
+V:%^VV2V VSUF-IMPRT-13-CONNEG_ÂD ; !8 vikk-
+V+Imprt+13+ConNeg:%^VV2V%>u K ; ! vikku
+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:%^VV2V%^KK2G»škuätt V_PUEʹTTED ;
+V+Der/ted:%^KK2GG»ât  V_AALǤTED ;

LEXICON V_RIČČÂD !!= * @CODE@ riččâd:ričč
 V_VIǮǮÂD ;
LEXICON V_RÕVVÂD !!= * @CODE@ rõvvâd:rõvv
 V_VIǮǮÂD ;
LEXICON V_VIǮǮÂD !!= * @CODE@ viǯǯâd:viǯǯ
! 1. VERBS WITH SINGLE-SYLLABLE Ind.Prs.Sg3 (2009: 354)
! 1.1 Inf in âd (vowel shift)
!   Raised: Inf, Ind.Prt.Pl3, Imprt.Sg2, Pot.Sg3
!   Lowered: Ind.Prs.Sg3, Ind.Prs.Pl3, Imprt.Sg3
! 1.1.1 Has Palatalization
! 1.1.1[1] (Palatalization pattern)
!   Palatalized: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Not Palatalized: Inf, Ind.Prs.Sg3, Imprt.Sg2, Imprt.Sg3, Pot.Sg3
! 1.1.1[1].1 Lacks Specifically Pedagogical Gradation
! 1.1.1[1].1.1 Has Orthographic Gradation
! 1.1.1[1].1.1[332] (Monophthong + Consonant Geminate)  
!   Extra Strong grade: Inf, Ind.Prs.Sg3
!   Extra strong grade: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Strong grade: Imprt.Sg2, Pot.Sg3, Imprt.Sg3
! FORMS
! (1)  Inf: viǯǯâd
! (2)  Ind.Prs.Sg3: veǯǯ
! (3)  Ind.Prs.Pl3: veʹǯǯe
! (4)  Ind.Prt.Pl3: viʹǯǯe
! (5)  Ind.Imprt.Sg2: viiǯǯ
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6)  Pot.Sg3: viiǯǯâž
! (7)  Imprt.Sg3: veeǯǯ
! (8)  Imprt.13.ConNeg: viǯǯu
! (9)  Ind.Imprt.Pl3: veǯǯaz
! (10) Allegro for incoative: viǯ»
! * Yaml: __V-vizzad__
! viǯǯâd:viǯǯ
! Extra strong grade in penultimate syllable
+V: VSUF-INF_ÂD ; !1
+V:%^VOWLower VSUF-IND-PRS-SG3_ÂD ; !2
+V+Ind+Prs+Pl3:%^VOWLower%^PAL%>e K ; !3
+V:%^PAL VSUF-IND-PRT-PL3_ÂD ; !4
+V:%^V2VV VSUF-I-POTKOND_ÂD ; !5
+V+Pot+Sg3:%^V2VV%>âž   K ; !6
+V+Imprt+Sg3:%^V2VV%^VOWLower%>as K ; !7
+V: VSUF-IMPRT-13-CONNEG_ÂD ; !8

+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:%^CC2C»škuätt V_PUEʹTTED ;
+V+Der/ted:%^V2VV»ât  V_AALǤTED ;

LEXICON V_RIÂDˈDÂD !!= * @CODE@ riâddâd:riâdˈd
! gradation: extra::strong
! 1. VERBS WITH SINGLE-SYLLABLE Ind.Prs.Sg3 (2009: 354, 356)
! 1.1 Inf in âd (vowel shift)
!   Raised: Inf, Ind.Prt.Pl3, Imprt.Sg2, Pot.Sg3
!   Lowered: Ind.Prs.Sg3, Ind.Prs.Pl3, Imprt.Sg3
! 1.1.1 Has Palatalization
! 1.1.1[1] (Palatalization pattern)
!   Palatalized: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Not Palatalized: Inf, Ind.Prs.Sg3, Imprt.Sg2, Imprt.Sg3, Pot.Sg3
! 1.1.1[1].2 Has Specifically Pedagogical Gradation
! 1.1.1[1].2.2 Lacks Orthographic Gradation
! 1.1.1[1].2.2[332] (Diphthong + Consonant Geminate)  
!   Extra strong grade: Inf, Ind.Prs.Sg3
!   Extra strong grade: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Strong grade: Imprt.Sg2, Pot.Sg3, Imprt.Sg3
! FORMS
! (1)  Inf: riâdˈdâd
! (2)  Ind.Prs.Sg3: reädˈd
! (3)  Ind.Prs.Pl3: reäʹdˈde
! (4)  Ind.Prt.Pl3: rieʹdˈde
! (5)  Ind.Imprt.Sg2: riâdd
! (6)  Ind.Pot.Sg3: riâddâž
! (7)  Ind.Imprt.Sg3: reäddas
! (8)  Imprt.13.ConNeg: riâdˈdu
! (9)  Ind.Imprt.Pl3: reädˈdaz
! (10) Allegro for incoative: riâd»
! * Yaml: __V-riokkad__
! riâddâd:riâdˈd
! Extra strong grade in penultimate syllable
+V: VSUF-INF_ÂD ; !1 riâddâd
+V:%^VOWLower VSUF-IND-PRS-SG3_ÂD ; !2 reädˈd
+V+Ind+Prs+Pl3:%^VOWLower%^PAL%>e K ; !3 reäʹdˈde
+V:%^PALE%^PAL VSUF-IND-PRT-PL3_ÂD ; !4 rieʹdˈde
+V:%^CCC2CC VSUF-I-POTKOND_ÂD ; !5 riâdd
+V+Pot+Sg3:%^CCC2CC%>âž   K ; !6 riâddâž
+V+Imprt+Sg3:%^VOWLower%^CCC2CC%>as K ; !7 riâddas
+V: VSUF-IMPRT-13-CONNEG_ÂD ; !8 riâdˈdu

+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:%^CCC2C»škuätt V_PUEʹTTED ;
+V+Der/ted:%^CCC2CC»ât  V_AALǤTED ;


LEXICON V_RIÕKˈKÂD !!= * @CODE@ riõkkâd:riõkˈk
! gradation: extra::strong
! 1. VERBS WITH SINGLE-SYLLABLE Ind.Prs.Sg3 (2009: 354, 356)
! 1.1 Inf in âd (vowel shift)
!   Raised: Inf, Ind.Prt.Pl3, Imprt.Sg2, Pot.Sg3
!   Lowered: Ind.Prs.Sg3, Ind.Prs.Pl3, Imprt.Sg3
! 1.1.1 Has Palatalization
! 1.1.1[1] (Palatalization pattern)
!   Palatalized: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Not Palatalized: Inf, Ind.Prs.Sg3, Imprt.Sg2, Imprt.Sg3, Pot.Sg3
! 1.1.1[1].2 Has Specifically Pedagogical Gradation
! 1.1.1[1].2.2 Lacks Orthographic Gradation
! 1.1.1[1].2.2[332] (Diphthong + Consonant Geminate)  
!   Extra strong grade: Inf, Ind.Prs.Sg3
!   Extra strong grade: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Strong grade: Imprt.Sg2, Pot.Sg3, Imprt.Sg3
! FORMS
! (1)  Inf: riõkˈkâd
! (2)  Ind.Prs.Sg3: reâkˈk
! (3)  Ind.Prs.Pl3: reäʹǩˈǩe
! (4)  Ind.Prt.Pl3: riõʹǩˈǩe
! (5)  Ind.Imprt.Sg2: riõkk
! (6)  Ind.Pot.Sg3: riõkkâž
! (7)  Ind.Imprt.Sg3: reâkkas
! (8)  Imprt.13.ConNeg: riõkˈku
! (9)  Ind.Imprt.Pl3: reâkˈkaz
! (10) Allegro for incoative: riõk»
! * Yaml: __V-riokkad__
! riõkkâd:riõkˈk
! Extra strong grade in penultimate syllable
+V: VSUF-INF_ÂD ; !1 riõkkâd
+V:%^VOWLower VSUF-IND-PRS-SG3_ÂD ; !2 reâkˈk
+V+Ind+Prs+Pl3:%^VOWLower%^PALÄ%^PAL%>e K ; !3 reäʹǩˈǩe
+V:%^PAL VSUF-IND-PRT-PL3_ÂD ; !4 riõʹǩˈǩe
+V:%^CCC2CC VSUF-I-POTKOND_ÂD ; !5 riõkk
+V+Pot+Sg3:%^CCC2CC%>âž   K ; !6 riõkkâž
+V+Imprt+Sg3:%^VOWLower%^CCC2CC%>as K ; !7 riõkkas
+V: VSUF-IMPRT-13-CONNEG_ÂD ; !8 riõkˈku

+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:%^CCC2C»škuätt V_PUEʹTTED ;
+V+Der/ted:%^CCC2CC»ât  V_AALǤTED ;

LEXICON V_PIÕGˈGÂD !!= * @CODE@ piõggâd:piõgˈg
! gradation: extra::strong
! 1. VERBS WITH SINGLE-SYLLABLE Ind.Prs.Sg3 (2009: 354, 356)
! 1.1 Inf in âd (vowel shift)
!   Raised: Inf, Ind.Prt.Pl3, Imprt.Sg2, Pot.Sg3
!   Lowered: Ind.Prs.Sg3, Ind.Prs.Pl3, Imprt.Sg3
! 1.1.1 Has Palatalization
! 1.1.1[1] (Palatalization pattern)
!   Palatalized: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Not Palatalized: Inf, Ind.Prs.Sg3, Imprt.Sg2, Imprt.Sg3, Pot.Sg3
! 1.1.1[1].2 Has Specifically Pedagogical Gradation
! 1.1.1[1].2.2 Lacks Orthographic Gradation
! 1.1.1[1].2.2[332] (Diphthong + Consonant Geminate)  
!   Extra strong grade: Inf, Ind.Prs.Sg3
!   Extra strong grade: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Strong grade: Imprt.Sg2, Pot.Sg3, Imprt.Sg3
! FORMS
! (1)  Inf: piõgˈgâd
! (2)  Ind.Prs.Sg3: peâgˈg
! (3)  Ind.Prs.Pl3: peäʹǧˈǧe
! (4)  Ind.Prt.Pl3: piõʹǧˈǧe
! (5)  Ind.Imprt.Sg2: piõgg
! (6)  Ind.Pot.Sg3: piõggâž
! (7)  Ind.Imprt.Sg3: peâggas
! (8)  Imprt.13.ConNeg: piõgˈgu
! (9)  Ind.Imprt.Pl3: peâgˈgaz
! (10) Allegro for incoative: piõg»
! * Yaml: __V-pioggad__
! piõggâd:piõgˈg
! Extra strong grade in penultimate syllable
+V: VSUF-INF_ÂD ; !1 piõggâd
+V:%^VOWLower VSUF-IND-PRS-SG3_ÂD ; !2 peâgˈg
+V+Ind+Prs+Pl3:%^VOWLower%^PALÄ%^PAL%>e K ; !3 peäʹǧˈǧe
+V:%^PAL VSUF-IND-PRT-PL3_ÂD ; !4 piõʹǧˈǧe
+V:%^CCC2CC VSUF-I-POTKOND_ÂD ; !5 piõgg
+V+Pot+Sg3:%^CCC2CC%>âž   K ; !6 piõggâž
+V+Imprt+Sg3:%^VOWLower%^CCC2CC%>as K ; !7 piõggas
+V: VSUF-IMPRT-13-CONNEG_ÂD ; !8 piõgˈgu

+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:%^CCC2C»škuätt V_PUEʹTTED ;
+V+Der/ted:%^CCC2CC»ât  V_AALǤTED ;


LEXICON V_PIÂSSÂD !!= * @CODE@ piâssâd:piâss
! gradation: extra::strong::weak
! weak geminate
! 1. VERBS WITH SINGLE-SYLLABLE Ind.Prs.Sg3 (2009: 354)
! 1.1 Inf in âd (vowel shift)
!   Raised: Inf, Ind.Prt.Pl3, Imprt.Sg2, Pot.Sg3
!   Lowered: Ind.Prs.Sg3, Ind.Prs.Pl3, Imprt.Sg3
! 1.1.1 Has Palatalization
! 1.1.1[1] (Palatalization pattern)
!   Palatalized: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Not Palatalized: Inf, Ind.Prs.Sg3, Imprt.Sg2, Imprt.Sg3, Pot.Sg3
!    vowel_palatalization: â:e
! 1.1.1[1].2 Has Specifically Pedagogical Gradation
! 1.1.1[1].2.1 Has Orthographic Gradation
! 1.1.1[1].2.1[231].2 (Diphthong + Consonant Geminate alternation)
!   Strong grade: Inf, Ind.Prs.Sg3
!   Extra strong grade: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Weak grade: Imprt.Sg2, Pot.Sg3, Imprt.Sg3
! FORMS
! (1)  Inf: piâssâd
! (2)  Ind.Prs.Sg3: peäss
! (3)  Ind.Prs.Pl3: peäʹsˈse
! (4)  Ind.Prt.Pl3: pieʹsˈse (allophonic)
! (5)  Ind.Imprt.Sg2: piâzz
! (6)  Ind.Pot.Sg3: piâzzâž
! (7)  Ind.Imprt.Sg3: peäzzas
! (8)  Imprt.13.ConNeg: piâsˈsu
! (9)  Ind.Imprt.Pl3: peäsˈsaz
! (10) Allegro for incoative: piâz»
! Lexicon ''@LEXNAME@'' is for verbs like
!  piâssâd:piâss
! * Yaml: __V-piassad__
!  with lexc stem
+V: VSUF-INF_ÂD ; !1 piâssâd
+V:%^VOWLower VSUF-IND-PRS-SG3_ÂD ; !2 peäss
+V+Ind+Prs+Pl3:%^VOWLower%^PAL%^CC2CCC%>e K ; !3 peäʹsˈse
+V:%^PALE%^PAL%^CC2CCC VSUF-IND-PRT-PL3_ÂD ; !4 pieʹsˈse
+V:%^KK2GG VSUF-I-POTKOND_ÂD ; !5 piâzz
+V+Pot+Sg3:%^KK2GG%>âž   K ; !6 piâzzâž
+V+Imprt+Sg3:%^VOWLower%^KK2GG%>as K ; !7 piâzzas
+V:%^CC2CCC VSUF-IMPRT-13-CONNEG_ÂD ; !8 piâsˈsu
+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:%^KK2GG»škuätt V_PUEʹTTED ;
+V+Der/ted:%^KK2GG»ât  V_AALǤTED ;

LEXICON V_ČIÕKKÂD !!= * @CODE@ čiõkkâd:čiõkk
! 1. VERBS WITH SINGLE-SYLLABLE Ind.Prs.Sg3 (2009: 354)
! 1.1 Inf in âd (vowel shift)
!   Raised: Inf, Ind.Prt.Pl3, Imprt.Sg2, Pot.Sg3
!   Lowered: Ind.Prs.Sg3, Ind.Prs.Pl3, Imprt.Sg3
! 1.1.1 Has Palatalization
! 1.1.1[1] (Palatalization pattern)
!   Palatalized: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Not Palatalized: Inf, Ind.Prs.Sg3, Imprt.Sg2, Imprt.Sg3, Pot.Sg3
! 1.1.1[1].2 Has Specifically Pedagogical Gradation
! 1.1.1[1].2.1 Has Orthographic Gradation
! 1.1.1[1].2.1[231].2 (Diphthong + Consonant Geminate alternation)
!   Strong grade: Inf, Ind.Prs.Sg3
!   Extra strong grade: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Weak grade: Imprt.Sg2, Pot.Sg3, Imprt.Sg3
! FORMS
! (1)  Inf: čiõkkâd
! (2)  Ind.Prs.Sg3: čeâkk
! (3)  Ind.Prs.Pl3: čeäʹǩˈǩe (allophonic)
! (4)  Ind.Prt.Pl3: čiõʹǩˈǩe
! (5)  Ind.Imprt.Sg2: čiõǥǥ
! (6)  Ind.Pot.Sg3: čiõǥǥâž
! (7)  Ind.Imprt.Sg3: čeâǥǥ
! (8)  Imprt.13.ConNeg: čiõkˈku
! (9)  Ind.Imprt.Pl3: čeâkˈkaz
! (10) Allegro for incoative: čiõǥ»
! Lexicon ''@LEXNAME@'' is for verbs like
!  čiõkkâd:čiõkk
! * Yaml: __V-piassad__
!  with lexc stem
+V: VSUF-INF_ÂD ; !1 čiõkkâd
+V:%^VOWLower VSUF-IND-PRS-SG3_ÂD ; !2 čeâkk
+V+Ind+Prs+Pl3:%^VOWLower%^PALÄ%^PAL%^CC2CCC%>e K ; !3 čeäʹǩˈǩe
+V:%^PAL%^CC2CCC VSUF-IND-PRT-PL3_ÂD ; !4 čiõʹǩˈǩe
+V:%^KK2GG VSUF-I-POTKOND_ÂD ; !5 čiõǥǥ
+V+Pot+Sg3:%^KK2GG%>âž   K ; !6 čiõǥǥâž
+V+Imprt+Sg3:%^VOWLower%^KK2GG%>as K ; !7 čiõǥǥas
+V:%^CC2CCC VSUF-IMPRT-13-CONNEG_ÂD ; !8 čiõkˈku
+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:%^CC2C»škuätt V_PUEʹTTED ;
+V+Der/ted:%^KK2GG»ât  V_AALǤTED ;


LEXICON V_HUÕLLÂD !!= * @CODE@ huõllâd:huõll
! 1. VERBS WITH SINGLE-SYLLABLE Ind.Prs.Sg3 (2009: 354)
! 1.1 Inf in âd (vowel shift)
!   Raised: Inf, Ind.Prt.Pl3, Imprt.Sg2, Pot.Sg3
!   Lowered: Ind.Prs.Sg3, Ind.Prs.Pl3, Imprt.Sg3
! 1.1.1 Has Palatalization
! 1.1.1[1] (Palatalization pattern)
!   Palatalized: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Not Palatalized: Inf, Ind.Prs.Sg3, Imprt.Sg2, Imprt.Sg3, Pot.Sg3
! 1.1.1[1].2 Has Specifically Pedagogical Gradation
! 1.1.1[1].2.1 Has Orthographic Gradation
! 1.1.1[1].2.1[231].2 (Diphthong + Consonant Geminate alternation)
!   Strong grade: Inf, Ind.Prs.Sg3
!   Extra strong grade: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Weak grade: Imprt.Sg2, Pot.Sg3, Imprt.Sg3
! FORMS
! (1)  Inf: huõllâd
! (2)  Ind.Prs.Sg3: huâll
! (3)  Ind.Prs.Pl3: hueʹlˈle (allophonic)
! (4)  Ind.Prt.Pl3: huõʹlˈle
! (5)  Ind.Imprt.Sg2: huõl
! (6)  Ind.Pot.Sg3: huõlâž
! (7)  Ind.Imprt.Sg3: huâlas
! (8)  Imprt.13.ConNeg: huõlˈlu
! (9)  Ind.Imprt.Pl3: huâlˈlaz
! (10) Allegro for incoative: huõl»
! Lexicon ''@LEXNAME@'' is for verbs like
!  huõllâd:huõll
! * Yaml: __V-huollad__
!  with lexc stem
+V: VSUF-INF_ÂD ; !1 huõllâd
+V:%^VOWLower VSUF-IND-PRS-SG3_ÂD ; !2 huâll
+V+Ind+Prs+Pl3:%^VOWLower%^PALE%^PAL%^CC2CCC%>e K ; !3 hueʹlˈle
+V:%^PAL%^CC2CCC VSUF-IND-PRT-PL3_ÂD ; !4 huõʹlˈle
+V:%^CC2C VSUF-I-POTKOND_ÂD ; !5 huõl
+V+Pot+Sg3:%^CC2C%>âž   K ; !6 huõlâž
+V+Imprt+Sg3:%^VOWLower%^CC2C%>as K ; !7 huõlas
+V:%^CC2CCC VSUF-IMPRT-13-CONNEG_ÂD ; !8 huõlˈlu
+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:%^CC2C»škuätt V_PUEʹTTED ;
+V+Der/ted:%^CC2C»ât  V_AALǤTED ;


LEXICON V_AIBBÂD !!= * @CODE@ aibbâd:aibb
! 1. VERBS WITH SINGLE-SYLLABLE Ind.Prs.Sg3 (2009: 354)
! 1.1 Inf in âd (vowel shift)
!   Raised: Inf, Ind.Prt.Pl3, Imprt.Sg2, Pot.Sg3
!   Lowered: Ind.Prs.Sg3, Ind.Prs.Pl3, Imprt.Sg3
! 1.1.1 Has Palatalization
! 1.1.1[1] (Palatalization pattern)
!   Palatalized: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Not Palatalized: Inf, Ind.Prs.Sg3, Imprt.Sg2, Imprt.Sg3, Pot.Sg3
! 1.1.1[1].1 Lacks Specifically Pedagogical Gradation
! 1.1.1[1].1.1 Has Orthographic Gradation
! 1.1.1[1].1.1[221] (Monophthong + Consonant Cluster alternation)
!   Strong grade: Inf, Ind.Prs.Sg3
!   Strong grade: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Weak grade: Imprt.Sg2, Pot.Sg3, Imprt.Sg3
! FORMS
! (1)  Inf: aibbâd
! (2)  Ind.Prs.Sg3: äibb
! (3)  Ind.Prs.Pl3: äiʹbbe
! (4)  Ind.Prt.Pl3: aiʹbbe
! (5)  Imprt.Sg2: aaib
! (6)  Pot.Sg3: aaibâž
! (7)  Imprt.Sg3: ääibas
! (8)  Imprt.13.ConNeg: aibbu
! (9)  Ind.Imprt.Pl3: äibbaz
! (10) Allegro for incoative: aib»
! * Yaml: 
!  aibbâd:aibb
!  with lexc stem
+V: VSUF-INF_ÂD ; !1
+V:%^VOWLower VSUF-IND-PRS-SG3_ÂD ; !2
+V+Ind+Prs+Pl3:%^VOWLower%^PAL%>e K ; !3
+V:%^PAL VSUF-IND-PRT-PL3_ÂD ; !4
+V:%^V2VV%^XYY2XY VSUF-I-POTKOND_ÂD ; !5
+V+Pot+Sg3:%^V2VV%^XYY2XY%>âž   K ; !6
+V+Imprt+Sg3:%^V2VV%^VOWLower%^XYY2XY%>as K ; !7
+V: VSUF-IMPRT-13-CONNEG_ÂD ; !8
+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:%^XYY2XY»škuätt V_PUEʹTTED ;
+V+Der/ted:%^V2VV%^XYY2XY»ât  V_AALǤTED ;


LEXICON V_TOBDDÂD !!= * @CODE@ tobddâd:tobdd
! 1. VERBS WITH SINGLE-SYLLABLE Ind.Prs.Sg3 (2009: 354)
! 1.1 Inf in âd (vowel shift)
!   Raised: Inf, Ind.Prt.Pl3, Imprt.Sg2, Pot.Sg3
!   Lowered: Ind.Prs.Sg3, Ind.Prs.Pl3, Imprt.Sg3
! 1.1.1 Has Palatalization
! 1.1.1[1] (Palatalization pattern)
!   Palatalized: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Not Palatalized: Inf, Ind.Prs.Sg3, Imprt.Sg2, Imprt.Sg3, Pot.Sg3
! 1.1.1[1].1 Lacks Specifically Pedagogical Gradation
! 1.1.1[1].1.1 Has Orthographic Gradation
! 1.1.1[1].1.1[221] (Monophthong + Consonant Cluster alternation)
!   Strong grade: Inf, Ind.Prs.Sg3
!   Strong grade: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Weak grade: Imprt.Sg2, Pot.Sg3, Imprt.Sg3
! FORMS
! (1)  Inf: tobddâd
! (2)  Ind.Prs.Sg3: tåbdd
! (3)  Ind.Prs.Pl3: tåʹbdde
! (4)  Ind.Prt.Pl3: toʹbdde
! (5)  Imprt.Sg2: toobd
! (6)  Pot.Sg3: toobdâž
! (7)  Imprt.Sg3: tååbdas
! (8)  Imprt.13.ConNeg: tobddu
! (9)  Ind.Imprt.Pl3: tåbddaz
! (10) Allegro for incoative: tobd»
! * Yaml: 
!  tobddâd:tobdd
!  with lexc stem
+V: VSUF-INF_ÂD ; !1
+V:%^VOWLower VSUF-IND-PRS-SG3_ÂD ; !2
+V+Ind+Prs+Pl3:%^VOWLower%^PAL%>e K ; !3
+V:%^PAL VSUF-IND-PRT-PL3_ÂD ; !4
+V:%^V2VV%^XYY2XY VSUF-I-POTKOND_ÂD ; !5
+V+Pot+Sg3:%^V2VV%^XYY2XY%>âž   K ; !6
+V+Imprt+Sg3:%^V2VV%^VOWLower%^XYY2XY%>as K ; !7
+V: VSUF-IMPRT-13-CONNEG_ÂD ; !8

+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:%^XYY2XY»škuätt V_PUEʹTTED ;
+V+Der/ted:%^V2VV%^XYY2XY»ât  V_AALǤTED ;

LEXICON V_KUÕSKKÂD !!= * @CODE@ kuõskkâd:kuõskk
! 1. VERBS WITH SINGLE-SYLLABLE Ind.Prs.Sg3 (2009: 354)
! 1.1 Inf in âd (vowel shift)
!   Raised: Inf, Ind.Prt.Pl3, Imprt.Sg2, Pot.Sg3
!   Lowered: Ind.Prs.Sg3, Ind.Prs.Pl3, Imprt.Sg3
! 1.1.1 Has Palatalization
! 1.1.1[1] (Palatalization pattern)
!   Palatalized: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Not Palatalized: Inf, Ind.Prs.Sg3, Imprt.Sg2, Imprt.Sg3, Pot.Sg3
! 1.1.1[1].1 Lacks Specifically Pedagogical Gradation
! 1.1.1[1].1.1 Has Orthographic Gradation
! 1.1.1[1].1.1[332].2 (Diphthong + Consonant Cluster alternation)
!   Strong grade: Inf, Ind.Prs.Sg3
!   Strong grade: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Weak grade: Imprt.Sg2, Pot.Sg3, Imprt.Sg3
! FORMS
! (1)  Inf: kuõskkâd
! (2)  Ind.Prs.Sg3: kuâskk
! (3)  Ind.Prs.Pl3: kueʹsǩǩe (allophonic)
! (4)  Ind.Prt.Pl3: kuõʹsǩǩe
! (5)  Ind.Imprt.Sg2: kuõsk
! (6)  Ind.Pot.Sg3: kuõskâž
! (7)  Ind.Imprt.Sg3: kuâskas
! (8)  Imprt.13.ConNeg: kuõskku
! (9)  Ind.Imprt.Pl3: kuâskkaz
! (10) Allegro for incoative: kuõsk»
! Lexicon ''@LEXNAME@'' is for verbs like
!  kuõskkâd:kuõskk
!  with lexc stem
! Strong consonant cluster
! Diphthong
! THIS NEEDS MORE DEVELOPING
+V: VSUF-INF_ÂD ; !1
+V:%^VOWLower VSUF-IND-PRS-SG3_ÂD ; !2
+V+Ind+Prs+Pl3:%^VOWLower%^PAL%>e K ; !3
+V:%^PAL VSUF-IND-PRT-PL3_ÂD ; !4
+V:%^XYY2XY VSUF-I-POTKOND_ÂD ; !5
+V+Pot+Sg3:%^XYY2XY%>âž   K ; !6
+V+Imprt+Sg3:%^VOWLower%^XYY2XY%>as K ; !7
+V: VSUF-IMPRT-13-CONNEG_ÂD ; !8

+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:%^XYY2XY»škuätt V_PUEʹTTED ;
+V+Der/ted:%^XYY2XY»ât  V_AALǤTED ;

LEXICON V_ČUÕIGGÂD !!= * @CODE@ čuõiggâd:čuõigg
! 1. VERBS WITH SINGLE-SYLLABLE Ind.Prs.Sg3 (2009: 354)
! 1.1 Inf in âd (vowel shift)
!   Raised: Inf, Ind.Prt.Pl3, Imprt.Sg2, Pot.Sg3
!   Lowered: Ind.Prs.Sg3, Ind.Prs.Pl3, Imprt.Sg3
! 1.1.1 Has Palatalization
! 1.1.1[1] (Palatalization pattern)
!   Palatalized: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Not Palatalized: Inf, Ind.Prs.Sg3, Imprt.Sg2, Imprt.Sg3, Pot.Sg3
! 1.1.1[1].1 Lacks Specifically Pedagogical Gradation
! 1.1.1[1].1.1 Has Orthographic Gradation
! 1.1.1[1].1.1[332].2 (Diphthong + Consonant Cluster alternation)
!   Strong grade: Inf, Ind.Prs.Sg3
!   Strong grade: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Weak grade: Imprt.Sg2, Pot.Sg3, Imprt.Sg3
! FORMS
! (1)  Inf: čuõiggâd
! (2)  Ind.Prs.Sg3: čuâigg
! (3)  Ind.Prs.Pl3: čueiʹǧǧe (allophonic)
! (4)  Ind.Prt.Pl3: čuõiʹǧǧe
! (5)  Ind.Imprt.Sg2: čuõiǥ
! (6)  Ind.Pot.Sg3: čuõiǥâž
! (7)  Ind.Imprt.Sg3: čuâiǥas
! (8)  Imprt.13.ConNeg: čuõiggu
! (9)  Ind.Imprt.Pl3: čuâiggaz
! (10) Allegro for incoative: čuõiǥ»
! Lexicon ''@LEXNAME@'' is for verbs like
!  čuõiggâd:čuõigg
!  with lexc stem
! Strong consonant cluster
! Diphthong
! THIS NEEDS MORE DEVELOPING
+V: VSUF-INF_ÂD ; !1
+V:%^VOWLower VSUF-IND-PRS-SG3_ÂD ; !2
+V+Ind+Prs+Pl3:%^VOWLower%^PAL%>e K ; !3
+V:%^PAL VSUF-IND-PRT-PL3_ÂD ; !4
+V:%^XYY2XY VSUF-I-POTKOND_ÂD ; !5
+V+Pot+Sg3:%^XYY2XY%>âž   K ; !6
+V+Imprt+Sg3:%^VOWLower%^XYY2XY%>as K ; !7
+V: VSUF-IMPRT-13-CONNEG_ÂD ; !8

+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:%^XYY2XY»škuätt V_PUEʹTTED ;
+V+Der/ted:%^XYY2XY»ât  V_AALǤTED ;


! Even-syllable stems

LEXICON V_LAADDÂD !!= * @CODE@ laaddâd:laadd
! 1. VERBS WITH SINGLE-SYLLABLE Ind.Prs.Sg3 (2009: 354)
! 1.1 Inf in âd (vowel shift)
!   Raised: Inf, Ind.Prt.Pl3, Imprt.Sg2, Pot.Sg3
!   Lowered: Ind.Prs.Sg3, Ind.Prs.Pl3, Imprt.Sg3
! 1.1.1 Has Palatalization
! 1.1.1[1] (Palatalization pattern)
!   Palatalized: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Not Palatalized: Inf, Ind.Prs.Sg3, Imprt.Sg2, Imprt.Sg3, Pot.Sg3
! 1.1.1[1].1 Lacks Specifically Pedagogical Gradation
! 1.1.1[1].1.2 Lacks Orthographic Gradation
! 1.1.1[1].1.2[111] (Monophthong + Consonant Cluster)
!   Weak grade: Inf, Ind.Prs.Sg3
!   Weak grade: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Weak grade: Imprt.Sg2, Pot.Sg3, Imprt.Sg3
! FORMS
! (1)  Inf: laaddâd
! (2)  Ind.Prs.Sg3: läädd
! (3)  Ind.Prs.Pl3: lääʹdde
! (4)  Ind.Prt.Pl3: laaʹdde
! (5)  Ind.Imprt.Sg2: laadd
! (6)  Ind.Pot.Sg3: laaddâž
! (7)  Ind.Imprt.Sg3: lääddas
! (8)  Imprt.13.ConNeg: laaddu
! (9)  Ind.Imprt.Pl3: lääddaz
! (10) Allegro for incoative: laad»
! * Yaml: 
! laaddâd:laadd
+V: VSUF-INF_ÂD ; !1
+V:%^VOWLower VSUF-IND-PRS-SG3_ÂD ; !2
+V+Ind+Prs+Pl3:%^VOWLower%^PAL%>e K ; !3
+V:%^PAL VSUF-IND-PRT-PL3_ÂD ; !4
+V: VSUF-I-POTKOND_ÂD ; !5
+V+Pot+Sg3:%>âž   K ; !6
+V+Imprt+Sg3:%^VOWLower%>as K ; !7
+V: VSUF-IMPRT-13-CONNEG_ÂD ; !8

+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:%^CC2C»škuätt V_PUEʹTTED ;
+V+Der/ted:»ât  V_AALǤTED ;


LEXICON V_VÕÕIDÂD !!= * @CODE@ võõidâd:võõid
! 1. VERBS WITH SINGLE-SYLLABLE Ind.Prs.Sg3 (2009: 354)
! 1.1 Inf in âd (vowel shift)
!   Raised: Inf, Ind.Prt.Pl3, Imprt.Sg2, Pot.Sg3
!   Lowered: Ind.Prs.Sg3, Ind.Prs.Pl3, Imprt.Sg3
! 1.1.1 Has Palatalization
! 1.1.1[1] (Palatalization pattern)
!   Palatalized: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Not Palatalized: Inf, Ind.Prs.Sg3, Imprt.Sg2, Imprt.Sg3, Pot.Sg3
! 1.1.1[1].1 Lacks Specifically Pedagogical Gradation
! 1.1.1[1].1.2 Lacks Orthographic Gradation
! 1.1.1[1].1.2[111] (Monophthong + Consonant Cluster)
!   Weak grade: Inf, Ind.Prs.Sg3
!   Weak grade: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Weak grade: Imprt.Sg2, Pot.Sg3, Imprt.Sg3
! FORMS
! (1)  Inf: võõidâd
! (2)  Ind.Prs.Sg3: vââid
! (3)  Ind.Prs.Pl3: vââiʹde
! (4)  Ind.Prt.Pl3: võõiʹde
! (5)  Ind.Imprt.Sg2: võõid
! (6)  Ind.Pot.Sg3: võõidâž
! (7)  Ind.Imprt.Sg3: vââidas
! (8)  Imprt.13.ConNeg: võõidu
! (9)  Ind.Imprt.Pl3: vââidaz
! (10) Allegro for incoative: vââid»
! * Yaml: 
! võõidâd:võõid
+V: VSUF-INF_ÂD ; !1
+V:%^VOWLower VSUF-IND-PRS-SG3_ÂD ; !2
+V+Ind+Prs+Pl3:%^VOWLower%^PAL%>e K ; !3
+V:%^PAL VSUF-IND-PRT-PL3_ÂD ; !4
+V: VSUF-I-POTKOND_ÂD ; !5
+V+Pot+Sg3:%>âž   K ; !6
+V+Imprt+Sg3:%^VOWLower%>as K ; !7
+V: VSUF-IMPRT-13-CONNEG_ÂD ; !8

+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:»škuätt V_PUEʹTTED ;
+V+Der/ted:»ât  V_AALǤTED ;

LEXICON V_ČIISTÂD !!= * @CODE@ čiistâd:čiist
! 1. VERBS WITH SINGLE-SYLLABLE Ind.Prs.Sg3 (2009: 354)
! 1.1 Inf in âd (vowel shift)
!   Raised: Inf, Ind.Prt.Pl3, Imprt.Sg2, Pot.Sg3
!   Lowered: Ind.Prs.Sg3, Ind.Prs.Pl3, Imprt.Sg3
! 1.1.1 Has Palatalization
! 1.1.1[1] (Palatalization pattern)
!   Palatalized: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Not Palatalized: Inf, Ind.Prs.Sg3, Imprt.Sg2, Imprt.Sg3, Pot.Sg3
! 1.1.1[1].1 Lacks Specifically Pedagogical Gradation
! 1.1.1[1].1.2 Lacks Orthographic Gradation
! 1.1.1[1].1.2[111] (Monophthong + Consonant Cluster)
!   Weak grade: Inf, Ind.Prs.Sg3
!   Weak grade: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Weak grade: Imprt.Sg2, Pot.Sg3, Imprt.Sg3
! FORMS
! (1)  Inf: čiistâd
! (2)  Ind.Prs.Sg3: čeest
! (3)  Ind.Prs.Pl3: čeeʹste
! (4)  Ind.Prt.Pl3: čiiʹste
! (5)  Ind.Imprt.Sg2: čiist
! (6)  Ind.Pot.Sg3: čiistâž
! (7)  Ind.Imprt.Sg3: čeestas
! (8)  Imprt.13.ConNeg: čiistu
! (9)  Ind.Imprt.Pl3: čeestaz
! (10) Allegro for incoative: čiist»
! * Yaml: 
! čiistâd:čiist
+V: VSUF-INF_ÂD ; !1
+V:%^VOWLower VSUF-IND-PRS-SG3_ÂD ; !2
+V+Ind+Prs+Pl3:%^VOWLower%^PAL%>e K ; !3
+V:%^PAL VSUF-IND-PRT-PL3_ÂD ; !4
+V: VSUF-I-POTKOND_ÂD ; !5
+V+Pot+Sg3:%>âž   K ; !6
+V+Imprt+Sg3:%^VOWLower%>as K ; !7
+V: VSUF-IMPRT-13-CONNEG_ÂD ; !8

+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:»škuätt V_PUEʹTTED ;
+V+Der/ted:»ât  V_AALǤTED ;

LEXICON V_SLUUŽÂD !!= * @CODE@ sluužâd:sluuž
! 1. VERBS WITH SINGLE-SYLLABLE Ind.Prs.Sg3 (2009: 354)
! 1.1 Inf in âd (vowel shift)
!   Raised: Inf, Ind.Prt.Pl3, Imprt.Sg2, Pot.Sg3
!   Lowered: Ind.Prs.Sg3, Ind.Prs.Pl3, Imprt.Sg3
! 1.1.1 Has Palatalization
! 1.1.1[1] (Palatalization pattern)
!   Palatalized: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Not Palatalized: Inf, Ind.Prs.Sg3, Imprt.Sg2, Imprt.Sg3, Pot.Sg3
! 1.1.1[1].1 Lacks Specifically Pedagogical Gradation
! 1.1.1[1].1.2 Lacks Orthographic Gradation
! 1.1.1[1].1.2[111] (Monophthong + Single Consonant)
!   Weak grade: Inf, Ind.Prs.Sg3
!   Weak grade: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Weak grade: Imprt.Sg2, Pot.Sg3, Imprt.Sg3
! FORMS
! (1)  Inf: sluužâd
! (2)  Ind.Prs.Sg3: sloož
! (3)  Ind.Prs.Pl3: slooʹže
! (4)  Ind.Prt.Pl3: sluuʹže
! (5)  Ind.Imprt.Sg2: sluuž
! (6)  Ind.Pot.Sg3: sluužâž
! (7)  Ind.Imprt.Sg3: sloožas
! (8)  Imprt.13.ConNeg: sluužu
! (9)  Ind.Imprt.Pl3: sloožaz
! (10) Allegro for incoative: sluuž»
! * Yaml: 
! sluužâd:sluuž
+V: VSUF-INF_ÂD ; !1
+V:%^VOWLower VSUF-IND-PRS-SG3_ÂD ; !2
+V+Ind+Prs+Pl3:%^VOWLower%^PAL%>e K ; !3
+V:%^PAL VSUF-IND-PRT-PL3_ÂD ; !4
+V: VSUF-I-POTKOND_ÂD ; !5
+V+Pot+Sg3:%>âž   K ; !6
+V+Imprt+Sg3:%^VOWLower%>as K ; !7
+V: VSUF-IMPRT-13-CONNEG_ÂD ; !8

+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:»škuätt V_PUEʹTTED ;
+V+Der/ted:»ât  V_AALǤTED ;

LEXICON V_PLIÂSNÂD !!= * @CODE@ pliâsnâd:pliâsn
! 1. VERBS WITH SINGLE-SYLLABLE Ind.Prs.Sg3 (2009: 354)
! 1.1 Inf in âd (vowel shift)
!   Raised: Inf, Ind.Prt.Pl3, Imprt.Sg2, Pot.Sg3
!   Lowered: Ind.Prs.Sg3, Ind.Prs.Pl3, Imprt.Sg3
! 1.1.1 Has Palatalization
! 1.1.1[1] (Palatalization pattern)
!   Palatalized: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Not Palatalized: Inf, Ind.Prs.Sg3, Imprt.Sg2, Imprt.Sg3, Pot.Sg3
! 1.1.1[1].1 Lacks Specifically Pedagogical Gradation
! 1.1.1[1].1.2 Lacks Orthographic Gradation
! 1.1.1[1].1.2[111] (Monophthong + Consonant Cluster)
!   Weak grade: Inf, Ind.Prs.Sg3
!   Weak grade: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Weak grade: Imprt.Sg2, Pot.Sg3, Imprt.Sg3
! FORMS
! (1)  Inf: pliâsnâd
! (2)  Ind.Prs.Sg3: pleäsn
! (3)  Ind.Prs.Pl3: pleäʹsne
! (4)  Ind.Prt.Pl3: plieʹsne
! (5)  Ind.Imprt.Sg2: pliâsn
! (6)  Ind.Pot.Sg3: pliâsnâž
! (7)  Ind.Imprt.Sg3: pleäsnas
! (8)  Imprt.13.ConNeg: pliâsnu
! (9)  Ind.Imprt.Pl3: pleäsnaz
! (10) Allegro for incoative: pliâsn»
! * Yaml: 
! pliâsnâd:pliâsn
+V: VSUF-INF_ÂD ; !1
+V:%^VOWLower VSUF-IND-PRS-SG3_ÂD ; !2
+V+Ind+Prs+Pl3:%^VOWLower%^PAL%>e K ; !3
+V:%^PAL VSUF-IND-PRT-PL3_ÂD ; !4
+V: VSUF-I-POTKOND_ÂD ; !5
+V+Pot+Sg3:%>âž   K ; !6
+V+Imprt+Sg3:%^VOWLower%>as K ; !7
+V: VSUF-IMPRT-13-CONNEG_ÂD ; !8

+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:»škuätt V_PUEʹTTED ;
+V+Der/ted:»ât  V_AALǤTED ;

LEXICON V_SUUDÂD !!= * @CODE@ suudâd:suud
!
! 1. VERBS WITH SINGLE-SYLLABLE Ind.Prs.Sg3 (2009: 354)
! 1.1 Inf in âd (vowel shift)
!   Raised: Inf, Ind.Prt.Pl3, Imprt.Sg2, Pot.Sg3
!   Lowered: Ind.Prs.Sg3, Ind.Prs.Pl3, Imprt.Sg3
! 1.1.1 Has Palatalization
! 1.1.1[1] (Palatalization pattern)
!   Palatalized: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Not Palatalized: Inf, Ind.Prs.Sg3, Imprt.Sg2, Imprt.Sg3, Pot.Sg3
! 1.1.1[1].1 Lacks Specifically Pedagogical Gradation
! 1.1.1[1].1.2 Lacks Orthographic Gradation
! 1.1.1[1].1.2[111] (Diphthong + Consonant Cluster)
!   Weak grade: Inf, Ind.Prs.Sg3
!   Weak grade: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Weak grade: Imprt.Sg2, Pot.Sg3, Imprt.Sg3
! FORMS
! (1)  Inf: suudâd
! (2)  Ind.Prs.Sg3: sooud
! (3)  Ind.Prs.Pl3: soouʹde
! (4)  Ind.Prt.Pl3: suuʹde
! (5)  Ind.Imprt.Sg2: suud
! (6)  Ind.Pot.Sg3: suudâž
! (7)  Ind.Imprt.Sg3: sooudas
! (8)  Imprt.13.ConNeg: suudu
! (9)  Ind.Imprt.Pl3: sooudaz
! (10) Allegro for incoative: suud»
! * Yaml: 
! suudâd:suud
+V: VSUF-INF_ÂD ; !1
+V:%^VOWLower VSUF-IND-PRS-SG3_ÂD ; !2
+V+Ind+Prs+Pl3:%^VOWLower%^PAL%>e K ; !3
+V:%^PAL VSUF-IND-PRT-PL3_ÂD ; !4
+V: VSUF-I-POTKOND_ÂD ; !5
+V+Pot+Sg3:%>âž   K ; !6
+V+Imprt+Sg3:%^VOWLower%>as K ; !7
+V: VSUF-IMPRT-13-CONNEG_ÂD ; !8

+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:»škuätt V_PUEʹTTED ;
+V+Der/ted:»ât  V_AALǤTED ;

!! !CLASS 1 LOW VOWEL, NO PALATALIZATION
!! Even-syllable stems in -AD

LEXICON V_TEEVVAD !!= * @CODE@ teevvad:teevv
! ** Yaml: __V-teevvad__
! In the lexicon ''@LEXNAME@''
! teevvad:teevv
! 1. The Ind.Prs.Sg3 form is a single syllable
! 1.2 The infinitive ends in ad
! 1.2.1 Has gradation
! 1.2.1.1 Monophthong 
! Stem is strong
! one vowel
! FORMS
! (1)  Inf: teevvad
! (2)  Ind.Prs.Sg3: teevv
! (3)  Ind.Prs.Pl3: tevva
! (4)  Ind.Prt.Pl3: tivvu
! (5)  Ind.Imprt.Sg2: teev
! (6)  Ind.Pot.Sg3: teevež
! (7)  Ind.Imprt.Sg3: teevas
! (8)  Imprt.13.ConNeg: tivvu
! (9) Ind.Imprt.Pl3: tevvaz
! (10) Allegro for incoative: tev»

+V: VSUF-INF_AD ; !1 teevvad
+V: VSUF-IND-PRS-SG3_AD ; !2 teevv
+V+Ind+Prs+Pl3:%^VV2V%>a K ; !3 tevva
+V:%^VV2V%^VOWRaise%> VSUF-IND-PRT-PL3_AD ; !4 tivvu
+V:%^CC2C%> VSUF-I-POTKOND_AD ; !5 teev-
+V+Pot+Sg3:%^CC2C%>ež	K ; !6 teevež
+V+Imprt+Sg3:%^CC2C%>as K ; !7 teevas
+V:%^VV2V%^VOWRaise VSUF-IMPRT-13-CONNEG_AD ; !8 tivvu
+V:%^VV2V VSUF-IMPRT-PL3_AD ; ! tevv-
+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:%^VV2V%^CC2C»škuätt V_PUEʹTTED ;


LEXICON V_PÅÅSSAD !!= * @CODE@ pååssad:pååss
! ** Yaml: __V-paassad__
! In the lexicon ''@LEXNAME@''
! pååssad:pååss
! 1. The Ind.Prs.Sg3 form is a single syllable
! 1.2 The infinitive ends in ad
! 1.2.1 Has gradation
! 1.2.1.1 Monophthong 
! Stem is strong
! one vowel
! FORMS
! (1)  Inf: pååssad
! (2)  Ind.Prs.Sg3: pååss
! (3)  Ind.Prs.Pl3: påssa
! (4)  Ind.Prt.Pl3: possu
! (5)  Ind.Imprt.Sg2: pååzz
! (6)  Ind.Pot.Sg3: pååzzež
! (7)  Ind.Imprt.Sg3: pååzzas
! (8)  Imprt.13.ConNeg: possu
! (9) Ind.Imprt.Pl3: påssaz
! (10) Allegro for incoative: påz»
+V: VSUF-INF_AD ; !1 pååssad
+V: VSUF-IND-PRS-SG3_AD ; !2 pååss
+V+Ind+Prs+Pl3:%^VV2V%>a K ; !3 påssa
+V:%^VV2V%^VOWRaise%> VSUF-IND-PRT-PL3_AD ; !4 possu
+V:%^CC2C%> VSUF-I-POTKOND_AD ; !5 pååzz-
+V+Pot+Sg3:%^CC2C%>ež	K ; !6 pååzzež
+V+Imprt+Sg3:%^CC2C%>as K ; !7 pååzzas
+V:%^VV2V%^VOWRaise VSUF-IMPRT-13-CONNEG_AD ; !8 possu
+V:%^VV2V VSUF-IMPRT-PL3_AD ; ! påss-
+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:%^VV2V%^KK2G»škuätt V_PUEʹTTED ;

LEXICON V_CÅKKAD
 V_TÄTTAD ;
LEXICON V_ÄBBAD
 V_TÄTTAD ;
LEXICON V_TÄTTAD
! tättad:tätt
! ** Yaml: __V-taettad__
! In the lexicon ''@LEXNAME@'' 
! 1. The Ind.Prs.Sg3 form is a single syllable
! 1.2 The infinitive ends in ad
! 1.2.1 Has gradation
! 1.2.1.1 Monophthong 
! The stem is extra strong
! Relative grade indicates
! W => St
! St => XSt
! XSt == XSt
! Monophthong
! Raising 
! FORMS
! (1) +V+Inf: tättad
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: tätt
! (3) +V+Ind+Prs+Pl3: tätta
! (4) +V+Ind+Prt+Pl3: tattu
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: täätt
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: täättež
! (7) +V+Imprt+Sg3: täättas
! (8) +V+Imprt+13+ConNeg: tattu
!    Pass.PrfPrc
! (9) Ind.Imprt.Pl3: tättaz
! (10) Allegro for incoative: tät»
+V: VSUF-INF_AD ; !1
+V: VSUF-IND-PRS-SG3_AD ; !2
+V+Ind+Prs+Pl3:%>a K ; !3
+V:%^VOWRaise%> VSUF-IND-PRT-PL3_AD ; !4
+V:%^V2VV%> VSUF-I-POTKOND_AD ; !5
+V+Pot+Sg3:%^V2VV%>ež   K ; !6
+V+Imprt+Sg3:%^V2VV%>as K ; !7
+V:%^VOWRaise VSUF-IMPRT-13-CONNEG_AD ; !8
+V: VSUF-IMPRT-PL3_AD ;
+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:%^CC2C»škuätt V_PUEʹTTED ;


LEXICON V_REÄKKAD
! gradation: yes
! weak geminate
! ** Yaml: __V-reakkad__
! In the lexicon ''@LEXNAME@''
! reäkkad:reäkk
!!
! 1. The Ind.Prs.Sg3 form is a single syllable
! 1.2 The infinitive ends in ad
! 1.2.1 Has gradation
! 1.2.1.1 Diphthong 
! The stem is strong
! Relative grade indicates
! W => St
! St => XSt
! XSt == XSt
! Monophthong
! Raising 
! FORMS
! (1) +V+Inf: reäkkad
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: reäkk
! (3) +V+Ind+Prs+Pl3: reäkˈka
! (4) +V+Ind+Prt+Pl3: riâkˈku
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: reäǥǥ
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: reäǥǥaž
! (7) +V+Imprt+Sg3: reäǥǥas
! (8) +V+Imprt+13+ConNeg: riâkˈku
!    Pass.PrfPrc
! (9) Ind.Imprt.Pl3: reäkˈkaz
! (10) Allegro for incoative: reäǥ»
!
+V: VSUF-INF_AD ; !1
+V: VSUF-IND-PRS-SG3_AD ; !2
+V+Ind+Prs+Pl3:%^CC2CCC%>a K ; !3
+V:%^VOWRaise%^CC2CCC%> VSUF-IND-PRT-PL3_AD ; !4
+V:%^KK2GG%> VSUF-I-POTKOND_AD ; !5
+V+Pot+Sg3:%^KK2GG%>až   K ; !6
+V+Imprt+Sg3:%^KK2GG%>as K ; !7
+V:%^VOWRaise%^CC2CCC VSUF-IMPRT-13-CONNEG_AD ; !8
+V:%^CC2CCC VSUF-IMPRT-PL3_AD ;
+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:%^KK2G»škuätt V_PUEʹTTED ;

LEXICON V_HEÂRRAD
! gradation: yes
! weak: single
! ** Yaml: __V-hearrad__
! In the lexicon ''@LEXNAME@''
! heârrad:heârr
!!
! 1. The Ind.Prs.Sg3 form is a single syllable
! 1.2 The infinitive ends in ad
! 1.2.1 Has gradation
! 1.2.1.1 Diphthong 
! The stem is strong
! Relative grade indicates
! W => St
! St => XSt
! XSt == XSt
! Monophthong
! Raising 
! FORMS
! (1) +V+Inf: heârrad
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: heârr
! (3) +V+Ind+Prs+Pl3: heârˈra
! (4) +V+Ind+Prt+Pl3: hiõrˈru
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: heâr
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: heâraž
! (7) +V+Imprt+Sg3: heâras
! (8) +V+Imprt+13+ConNeg: heârˈru
!    Pass.PrfPrc
! (9) Ind.Imprt.Pl3: heârˈraz
! (10) Allegro for incoative: heâr»
!
+V: VSUF-INF_AD ; !1
+V: VSUF-IND-PRS-SG3_AD ; !2
+V+Ind+Prs+Pl3:%^CC2CCC%>a K ; !3
+V:%^VOWRaise%^CC2CCC%> VSUF-IND-PRT-PL3_AD ; !4
+V:%^CC2C%> VSUF-I-POTKOND_AD ; !5
+V+Pot+Sg3:%^CC2C%>až   K ; !6
+V+Imprt+Sg3:%^CC2C%>as K ; !7
+V:%^VOWRaise%^CC2CCC VSUF-IMPRT-13-CONNEG_AD ; !8
+V:%^CC2CCC VSUF-IMPRT-PL3_AD ;
+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:%^CC2C»škuätt V_PUEʹTTED ;

LEXICON V_LUÂDˈDAD
 V_CEÄKˈKAD ;
LEXICON V_CEÄKˈKAD
! gradation: extra::strong
! ** Yaml: __V-hearrad__
! In the lexicon ''@LEXNAME@''
! ceäkkad:ceäkˈk
!!
! 1. The Ind.Prs.Sg3 form is a single syllable
! 1.2 The infinitive ends in ad
! 1.2.1 Has gradation
! 1.2.1.1 Diphthong 
! The stem is strong
! Relative grade indicates
! W => St
! St => XSt
! XSt == XSt
! Diphthong
! Raising 
! FORMS
! (1) +V+Inf: ceäkˈkad
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: ceäkˈk
! (3) +V+Ind+Prs+Pl3: ceäkˈka
! (4) +V+Ind+Prt+Pl3: ciâkˈku
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: ceäkk
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: ceäkkaž
! (7) +V+Imprt+Sg3: ceäkkas
! (8) +V+Imprt+13+ConNeg: riâkˈku
!    Pass.PrfPrc
! (9) Ind.Imprt.Pl3: ceäkˈkaz
! (10) Allegro for incoative: ceäk»
!
+V: VSUF-INF_AD ; !1
+V: VSUF-IND-PRS-SG3_AD ; !2
+V+Ind+Prs+Pl3:%>a K ; !3
+V:%^VOWRaise%> VSUF-IND-PRT-PL3_AD ; !4
+V:%^CC2C%> VSUF-I-POTKOND_AD ; !5
+V+Pot+Sg3:%^CCC2CC%>až   K ; !6
+V+Imprt+Sg3:%^CCC2CC%>as K ; !7
+V:%^VOWRaise VSUF-IMPRT-13-CONNEG_AD ; !8
+V: VSUF-IMPRT-PL3_AD ;
+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:%^CCC2C»škuätt V_PUEʹTTED ;

LEXICON V_VUÄGˈGAD !!= * @CODE@ vuägˈgad:vuägˈg
! ** Yaml: __V-vuaeggad__
! In the lexicon ''@LEXNAME@''
! vuägˈgad:vuägˈg
! Stem is extra strong
! diphthong
! FORMS
! (1) +V+Inf: vuägˈgad
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: vuägˈg
! (3) +V+Ind+Prs+Pl3: vuägˈga
! (4) +V+Ind+Prt+Pl3: ciâgˈgu
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: vuägˈg
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: vuägˈgaž
! (7) +V+Imprt+Sg3: vuägˈgas
! (8) +V+Imprt+13+ConNeg: vuågˈgu
!    Pass.PrfPrc
! (9) Ind.Imprt.Pl3: vuägˈgaz
! (10) Allegro for incoative: ceäg»
!
+V: VSUF-INF_AD ; !1
+V: VSUF-IND-PRS-SG3_AD ; !2
+V+Ind+Prs+Pl3:%>a K ; !3
+V:%^VOWRaise%> VSUF-IND-PRT-PL3_AD ; !4
+V:%> VSUF-I-POTKOND_AD ; !5
+V+Pot+Sg3:%^CCC2CC%>až   K ; !6
+V+Imprt+Sg3:%^CCC2CC%>as K ; !7
+V:%^VOWRaise VSUF-IMPRT-13-CONNEG_AD ; !8
+V: VSUF-IMPRT-PL3_AD ;
+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:%^CCC2C»škuätt V_PUEʹTTED ;


LEXICON V_LÄULLAD !!= * @CODE@ läullad:läull
! ** Yaml: __V-laullad__
! In the lexicon ''@LEXNAME@''
! läullad:läull
! Gradation
! monophthong followed by consonant cluster
! Stem is strong
! FORMS
! (1) +V+Inf: läullad
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: läull
! (3) +V+Ind+Prs+Pl3: läulla
! (4) +V+Ind+Prt+Pl3: laullu
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: lääul
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: lääulež
! (7) +V+Imprt+Sg3: lääulas
! (8) +V+Imprt+13+ConNeg: laullu
!    Pass.PrfPrc
! (9) Ind.Imprt.Pl3: läullaz
! (10) Allegro for incoative: läul»
+V: VSUF-INF_AD ; !1
+V: VSUF-IND-PRS-SG3_AD ; !2
+V+Ind+Prs+Pl3:%>a K ; !3
+V:%^VOWRaise%> VSUF-IND-PRT-PL3_AD ; !4
+V:%^V2VV%^CC2C%> VSUF-I-POTKOND_AD ; !5
+V+Pot+Sg3:%^V2VV%^CC2C%>ež   K ; !6
+V+Imprt+Sg3:%^V2VV%^CC2C%>as K ; !7
+V:%^VOWRaise VSUF-IMPRT-13-CONNEG_AD ; !8
+V: VSUF-IMPRT-PL3_AD ;
+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:%^XYY2XY»škuätt V_PUEʹTTED ;

LEXICON V_SÄRNNAD !!= * @CODE@ särnnad:särnn
! ** Yaml: __V-särnnad__
! In the lexicon ''@LEXNAME@''
! särnnad:särnn
! 1. The Ind.Prs.Sg3 form is a single syllable
! 1.2 The infinitive ends in ad
! 1.2.1 Has gradation
! 1.2.1.1 Monophthong 
! Stem is strong
! one vowel
! FORMS
! (1)  Inf: särnnad
! (2)  Ind.Prs.Sg3: särnn
! (3)  Ind.Prs.Pl3: särnna
! (4)  Ind.Prt.Pl3: sarnnu
! (5)  Ind.Imprt.Sg2: säärn
! (6)  Ind.Pot.Sg3: säärnež
! (7)  Ind.Imprt.Sg3: säärnas
! (8)  Imprt.13.ConNeg: sarnnu
! (9) Ind.Imprt.Pl3: särnnaz
! (10) Allegro for incoative: särnn»

+V: VSUF-INF_AD ; !1 särnnad
+V: VSUF-IND-PRS-SG3_AD ; !2 särnn
+V+Ind+Prs+Pl3:%>a K ; !3 särnna
+V:%^VOWRaise%> VSUF-IND-PRT-PL3_AD ; !4 sarnnu
+V:%^V2VV%^XYY2XY%> VSUF-I-POTKOND_AD ; !5 säärn-
+V+Pot+Sg3:%^V2VV%^XYY2XY%>ež	K ; !6 säärnež
+V+Imprt+Sg3:%^V2VV%^XYY2XY%>as K ; !7 säärnas
+V:%^VOWRaise VSUF-IMPRT-13-CONNEG_AD ; !8 sarnnu
+V: VSUF-IMPRT-PL3_AD ; ! särnn-
+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:%^XYY2XY»škuätt V_PUEʹTTED ;

LEXICON V_KÄIDDAD !!= * @CODE@ käiddad:käidd
! ** Yaml: __V-käiddad__
! In the lexicon ''@LEXNAME@''
! käiddad:käidd
! 1. The Ind.Prs.Sg3 form is a single syllable
! 1.2 The infinitive ends in ad
! 1.2.1 Has gradation
! 1.2.1.1 Monophthong 
! Stem is strong
! one vowel
! FORMS
! (1)  Inf: käiddad
! (2)  Ind.Prs.Sg3: käidd
! (3)  Ind.Prs.Pl3: käidda
! (4)  Ind.Prt.Pl3: kaiddu
! (5)  Ind.Imprt.Sg2: kääid
! (6)  Ind.Pot.Sg3: kääidež
! (7)  Ind.Imprt.Sg3: kääidas
! (8)  Imprt.13.ConNeg: kaiddu
! (9) Ind.Imprt.Pl3: käiddaz
! (10) Allegro for incoative: käidd»

+V: VSUF-INF_AD ; !1 käiddad
+V: VSUF-IND-PRS-SG3_AD ; !2 käidd
+V+Ind+Prs+Pl3:%>a K ; !3 käidda
+V:%^VOWRaise%> VSUF-IND-PRT-PL3_AD ; !4 kaiddu
+V:%^V2VV%^XYY2XY%> VSUF-I-POTKOND_AD ; !5 kääid-
+V+Pot+Sg3:%^V2VV%^XYY2XY%>ež	K ; !6 kääidež
+V+Imprt+Sg3:%^V2VV%^XYY2XY%>as K ; !7 kääidas
+V:%^VOWRaise VSUF-IMPRT-13-CONNEG_AD ; !8 kaiddu
+V: VSUF-IMPRT-PL3_AD ; ! käidd-
+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:%^XYY2XY»škuätt V_PUEʹTTED ;

LEXICON V_KUÄIVVAD !!= * @CODE@ kuäivvad:kuäivv
! ** Yaml: __V-kuaeivvad__
! In the lexicon ''@LEXNAME@''
! kuäivvad:kuäivv
! 1. The Ind.Prs.Sg3 form is a single syllable
! 1.2 The infinitive ends in ad
! 1.2.1 Has gradation
! 1.2.1.1 Monophthong 
! Stem is strong
! one vowel
! FORMS
! (1)  Inf: kuäivvad
! (2)  Ind.Prs.Sg3: kuäivv
! (3)  Ind.Prs.Pl3: kuäivva
! (4)  Ind.Prt.Pl3: kuaivvu
! (5)  Ind.Imprt.Sg2: kuäiv
! (6)  Ind.Pot.Sg3: kuäivež
! (7)  Ind.Imprt.Sg3: kuäivas
! (8)  Imprt.13.ConNeg: kuaivvu
! (9) Ind.Imprt.Pl3: kuäivvaz
! (10) Allegro for incoative: kuäivv»

+V: VSUF-INF_AD ; !1 kuäivvad
+V: VSUF-IND-PRS-SG3_AD ; !2 kuäivv
+V+Ind+Prs+Pl3:%>a K ; !3 kuäivva
+V:%^VOWRaise%> VSUF-IND-PRT-PL3_AD ; !4 kuaivvu
+V:%^XYY2XY%> VSUF-I-POTKOND_AD ; !5 kuäiv-
+V+Pot+Sg3:%^XYY2XY%>ež	K ; !6 kuäivež
+V+Imprt+Sg3:%^XYY2XY%>as K ; !7 kuäivas
+V:%^VOWRaise VSUF-IMPRT-13-CONNEG_AD ; !8 kuaivvu
+V: VSUF-IMPRT-PL3_AD ; ! kuäivv-
+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:%^XYY2XY»škuätt V_PUEʹTTED ;

LEXICON V_KUÄSTTAD !!= * @CODE@ kuästtad:kuästt
! ** Yaml: __V-kuaeivvad__
! In the lexicon ''@LEXNAME@''
! kuästtad:kuästt
! 1. The Ind.Prs.Sg3 form is a single syllable
! 1.2 The infinitive ends in ad
! 1.2.1 Has gradation
! 1.2.1.1 Monophthong 
! Stem is strong
! one vowel
! FORMS
! (1)  Inf: kuästtad
! (2)  Ind.Prs.Sg3: kuästt
! (3)  Ind.Prs.Pl3: kuästta
! (4)  Ind.Prt.Pl3: kuasttu
! (5)  Ind.Imprt.Sg2: kuäst
! (6)  Ind.Pot.Sg3: kuästež
! (7)  Ind.Imprt.Sg3: kuästas
! (8)  Imprt.13.ConNeg: kuasttu
! (9) Ind.Imprt.Pl3: kuästtaz
! (10) Allegro for incoative: kuästt»

+V: VSUF-INF_AD ; !1 kuästtad
+V: VSUF-IND-PRS-SG3_AD ; !2 kuästt
+V+Ind+Prs+Pl3:%>a K ; !3 kuästta
+V:%^VOWRaise%> VSUF-IND-PRT-PL3_AD ; !4 kuasttu
+V:%^XYY2XY%> VSUF-I-POTKOND_AD ; !5 kuäst-
+V+Pot+Sg3:%^XYY2XY%>ež	K ; !6 kuästež
+V+Imprt+Sg3:%^XYY2XY%>as K ; !7 kuästas
+V:%^VOWRaise VSUF-IMPRT-13-CONNEG_AD ; !8 kuasttu
+V: VSUF-IMPRT-PL3_AD ; ! kuästt-
+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:%^XYY2XY»škuätt V_PUEʹTTED ;

LEXICON V_MEINNAD !!= * @CODE@ meinnad:meinn
! ** Yaml: __V-meinnad__
! In the lexicon ''@LEXNAME@''
! meinnad:meinn
! 1. The Ind.Prs.Sg3 form is a single syllable
! 1.2 The infinitive ends in ad
! 1.2.1 Has gradation
! 1.2.1.1 Monophthong 
! Stem is strong
! one vowel
! FORMS
! (1)  Inf: meinnad
! (2)  Ind.Prs.Sg3: meinn
! (3)  Ind.Prs.Pl3: meinna
! (4)  Ind.Prt.Pl3: meinnu
! (5)  Ind.Imprt.Sg2: meein
! (6)  Ind.Pot.Sg3: meeinež
! (7)  Ind.Imprt.Sg3: meeinas
! (8)  Imprt.13.ConNeg: meinnu
! (9) Ind.Imprt.Pl3: meinnaz
! (10) Allegro for incoative: meinn»

+V: VSUF-INF_AD ; !1 meinnad
+V: VSUF-IND-PRS-SG3_AD ; !2 meinn
+V+Ind+Prs+Pl3:%>a K ; !3 meinna
+V:%> VSUF-IND-PRT-PL3_AD ; !4 meinnu
+V:%^V2VV%^XYY2XY%> VSUF-I-POTKOND_AD ; !5 meein-
+V+Pot+Sg3:%^V2VV%^XYY2XY%>ež	K ; !6 meeinež
+V+Imprt+Sg3:%^V2VV%^XYY2XY%>as K ; !7 meeinas
+V: VSUF-IMPRT-13-CONNEG_AD ; !8 meinnu
+V: VSUF-IMPRT-PL3_AD ; ! meinn-
+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:%^XYY2XY»škuätt V_PUEʹTTED ;

!! !CLASS 1 HIGH VOWEL, PALATALIZATION
LEXICON V_MUʹTTED !!= * @CODE@ muʹtted:mutt
! seek parallel in RÕVVÂD
! FORMS
! (1) +V+Inf: muʹtted
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc

! muʹtted:mutt
+V:%^PAL VSUF-INF_ED ; !1
! (2) +V+Ind+Prs+Sg3: mott
+V:%^VOWLower VSUF-IND-PRS-SG3_ED ; !2
! (3) +V+Ind+Prs+Pl3: moʹtte
+V+Ind+Prs+Pl3:%^VOWLower%^PAL%>e K ; !3
! (4) +V+Ind+Prt+Pl3: muʹtte
!   Ind.Prt.Sg1-Sg2-4
+V:%^PAL%> VSUF-IND-PRT-PL3_ED ; !4
! (5) +V+Imprt+Sg2: mooʹtt
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
+V:%^V2VV%^PAL%> VSUF-I-POTKOND_AD ; !5
! (6) +V+Pot+Sg3: muuʹttež
+V+Pot+Sg3:%^V2VV%^PAL%>ež   K ; !6
! (7) +V+Imprt+Sg3: moottas
+V+Imprt+Sg3:%^V2VV%^VOWLower%>as K ; !7
+V:%^V2VV%^PAL VSUF-IND-PRS-CONNEG_ED ;
! (8) +V+Imprt+13+ConNeg: muttu
!    Pass.PrfPrc
+V: VSUF-IMPRT-13-CONNEG_ED ; !8
! (9) Ind.Imprt.Pl3: mottaz

! (10) Allegro for incoative: muʹt»
+V+Der/škueʹtted:%^PAL%^CC2C»škuätt V_PUEʹTTED ; 


LEXICON V_LAʹDDED !!= * @CODE@ laʹdded:kädd
! seek parallel in VIǮǮÂD
! FORMS
! (1) +V+Inf: laʹdded
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: lädd
! (3) +V+Ind+Prs+Pl3: läʹdde
! (4) +V+Ind+Prt+Pl3: laʹdde
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: laaʹdd
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: laaʹddež
! (7) +V+Imprt+Sg3: lääʹddas
! (8) +V+Imprt+13+ConNeg: laddu
!    Pass.PrfPrc
! (9) Ind.Imprt.Pl3: läddaz
! laʹdded:kädd
+V:%^PAL VSUF-INF_ED ; !1 laʹdded
+V:%^VOWLower VSUF-IND-PRS-SG3_ED ; !2 lädd
+V+Ind+Prs+Pl3:%^VOWLower%^PAL%>e K ; !3 läʹdde
+V:%^PAL%> VSUF-IND-PRT-PL3_ED ; !4 laʹdde
+V:%^V2VV%^PAL%> VSUF-POTENTIAL_ED ; !5 laaʹdd-
+V:%^V2VV%> VSUF-CONDITIONAL_ED ; !5 ?2 ?7 laaʹdd-
+V+Pot+Sg3:%^V2VV%^PAL%>ež   K ; !6 laaʹddež
+V+Imprt+Sg3:%^V2VV%^VOWLower%>as K ; !7 lääddas
+V:%^V2VV%^VOWLower VSUF-IND-PRS-CONNEG_ED ;
+V:%^VOWLower VSUF-IMPRT-13-CONNEG_ED ; !8 lädd-

! (10) Allegro for incoative: laʹd»
+V+Der/škueʹtted:%^PAL%^CC2C»škuätt V_PUEʹTTED ;

LEXICON V_PIEʹǨǨED !!= * @CODE@ pieʹǩǩed:peäkk
! Like JURDDED
! 1. VERBS WITH SINGLE-SYLLABLE Ind.Prs.Sg3 (2009: ???)
! 1.3 Inf in palatalization + ed (vowel shift)
!   Raised: Inf, Ind.Prt.Pl3, Imprt.Sg2, Pot.Sg3
!   Lowered: Ind.Prs.Sg3, Ind.Prs.Pl3, Imprt.Sg3
! 1.3.1 Has Palatalization
! 1.3.1[1] (Palatalization pattern)
!   Palatalized: Inf, Ind.Prs.Pl3, Ind.Prt.Pl3, Pot.Sg3
!   Not Palatalized: Ind.Prs.Sg3, Imprt.Sg2, Imprt.Sg3
! 1.3.1[1].1 Lacks Specifically Pedagogical Gradation
! 1.3.1[1].1.1 Has Orthographic Gradation
! 1.3.1[1].1.1[231] (Diphthong + Consonant and Consonant Geminate alternation)
!   Strong grade: 
!   Extra strong grade: 
!   Weak grade: 
! FORMS
! (1)  Inf: pieʹǩǩed
! (2)  Ind.Prs.Sg3: peäkk
! (3)  Ind.Prs.Pl3: peäʹǩˈǩe
! (4)  Ind.Prt.Pl3: pieʹǩˈǩe
! (5)  Ind.Imprt.Sg2: pieʹjj
! (6)  Ind.Pot.Sg3: pieʹjjež
! (7)  Imprt.Sg3: peäǥǥas
! (8)  Imprt.13.ConNeg: piâkˈku 
! (9)  Imprt.Pl3: peäkˈkaz
! (9) Ind.Imprt.Pl3: peäkˈkaz
! (10) Allegro for incoative: pieʹj»
! (11)  Ger.Temp.PxSg3: peäʹjjeeʹstes
! * Yaml: __V-piekked__
! pieʹǩǩed:peäkk
! Strong grade in penultimate syllable
+V:%^VOWRaise%^PALE%^PAL VSUF-INF_ED ; !1
+V: VSUF-IND-PRS-SG3_ED ; !2
+V+Imprt+Pl3:%^CC2CCC%>az K ;
+V+Ind+Prs+Pl3:%^PAL%>e K ; !3
+V:%^VOWRaise%^PALE%^PAL VSUF-IND-PRT-PL3_ED ; !4
+V:%^VOWRaise%^PALE%^PAL%^KK2GG VSUF-POTENTIAL_ED ; !5
+V+Pot+Sg3:%^VOWRaise%^PALE%^PAL%^KK2GG%>âž   K ; !6
+V+Imprt+Sg3:%^CC2C%>as K ; !7
+V:%^PALE%^PAL%^KK2GG VSUF-IND-PRS-CONNEG_ED ;
+V:%^VOWRaise VSUF-IMPRT-13-CONNEG_ED ; !8
 !9V

+V+Der/škueʹtted:%^PAL%^CC2C»škuätt V_PUEʹTTED ;

LEXICON V_JUʹRDDED !!= * @CODE@ juʹrdded:jurdd
! seek parallel in TOBDDÂD
! juʹrdded:jurdd
! FORMS
! (1) +V+Inf: juʹrdded
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: jordd
! (3) +V+Ind+Prs+Pl3: joʹrdde
! (4) +V+Ind+Prt+Pl3: juʹrdde
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: juuʹrd
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: juuʹrdež
! (7) +V+Imprt+Sg3: joordas
! (8) +V+Imprt+13+ConNeg: jurddu
!    Pass.PrfPrc
! juʹrdded:jurdd
+V:%^PAL VSUF-INF_ED ; !1
+V:%^VOWLower VSUF-IND-PRS-SG3_ED ; !2 jordd
+V+Ind+Prs+Pl3:%^VOWLower%^PAL%>e K ; !3 joʹrdde
+V:%^PAL VSUF-IND-PRT-PL3_ED ; !4 juʹrdde
+V:%^V2VV%^PAL%^XYY2XY VSUF-POTENTIAL_ED ; !5 juuʹrd-
+V:%^V2VV%^VOWLower%^XYY2XY%> VSUF-CONDITIONAL_ED ; !5 ?2 ?7
+V+Pot+Sg3:%^V2VV%^PAL%^XYY2XY%>ež   K ; !6
+V+Imprt+Sg3:%^V2VV%^XYY2XY%>as K ; !7 joordas
+V:%^V2VV%^VOWLower%^XYY2XY VSUF-IND-PRS-CONNEG_ED ;
+V: VSUF-IMPRT-13-CONNEG_ED ; !8

+V+Der/škueʹtted:%^PAL%^XYY2XY»škuätt V_PUEʹTTED ;

LEXICON V_PUʹHTTED !!= * @CODE@ puʹhtted:puhtt
! seek parallel in TOBDDÂD
! puʹhtted:jurdd
! FORMS
! (1) +V+Inf: puʹhtted
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: jordd
! (3) +V+Ind+Prs+Pl3: joʹrdde
! (4) +V+Ind+Prt+Pl3: puʹhtte
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: juuʹrd
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: juuʹrdež
! (7) +V+Imprt+Sg3: joordas
! (8) +V+Imprt+13+ConNeg: jurddu
!    Pass.PrfPrc
! puʹhtted:puhtt
+V:%^PAL VSUF-INF_ED ; !1
+V:%^VOWLower VSUF-IND-PRS-SG3_ED ; !2 pohtt
+V+Ind+Prs+Pl3:%^VOWLower%^PAL%>e K ; !3 poʹhtte
+V:%^PAL VSUF-IND-PRT-PL3_ED ; !4 puʹhtte
+V:%^V2VV%^PAL%^XYY2VY VSUF-POTENTIAL_ED ; !5 puuʹt-
+V:%^V2VV%^VOWLower%^XYY2VY%> VSUF-CONDITIONAL_ED ; !5 ?2 ?7
+V:%^V2VV%^VOWLower%^XYY2VY VSUF-IND-PRS-CONNEG_ED ;
+V+Pot+Sg3:%^V2VV%^PAL%^XYY2VY%>ež   K ; !6
+V+Imprt+Sg3:%^V2VV%^XYY2VY%>as K ; !7 pooutas
+V: VSUF-IMPRT-13-CONNEG_ED ; !8

+V+Der/škueʹtted:%^PAL%^XYY2VY»škuätt V_PUEʹTTED ;

LEXICON V_UʹVDDED
! inf: ed
! ** Yaml: __vuvdded__
! uʹvdded:uvdd
! In the lexicon ''@LEXNAME@'' the verb __uʹvdded__ has the lexc stem
! __uʹvdded__. It raises in +Ind+Prt+Pl3, Pot, and has lengthening.
! FORMS
! (1) +V+Inf: uʹvdded
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: oudd
! (3) +V+Ind+Prs+Pl3: ouʹdde
! (4) +V+Ind+Prt+Pl3: uʹvdde
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: uuʹd
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: uuʹdež
! (7) +V+Imprt+Sg3: ooudas
! (8) +V+Imprt+13+ConNeg: uvddu
!    Pass.PrfPrc
! uʹvdded:uvdd
+V:%^PAL VSUF-INF_ED ; !1
+V:%^VOWLower%^XYY2VYY VSUF-IND-PRS-SG3_ED ; !2
+V+Ind+Prs+Pl3:%^VOWLower%^PAL%^XYY2VYY%>e K ; !3
+V:%^PAL%> VSUF-IND-PRT-PL3_ED ; !4
+V:%^PAL%^XYY2VY%> VSUF-POTENTIAL_ED ; !5
+V:%^V2VV%^VOWLower%^XYY2VY%> VSUF-CONDITIONAL_ED ; !5 ?2 ?7
+V:%^V2VV%^VOWLower%^XYY2VY VSUF-IND-PRS-CONNEG_ED ;
+V+Pot+Sg3:%^VOWRaise%^PAL%^XYY2VY%>ež   K ; !6
+V+Imprt+Sg3:%^V2VV%^VOWLower%^XYY2VY%>as K ; !7
+V: VSUF-IMPRT-13-CONNEG_ED ; !8

+V+Der/škueʹtted:%^PAL%^XYY2XY»škuätt V_PUEʹTTED ;

LEXICON V_TIEʹTTED !!= * @CODE@ tieʹtted:teâtt
! In the lexicon ''@LEXNAME@'' the verb __tieʹtted__ has the form __tiõtt__
+V+Ind+Prs+Sg1:%^CC2C%>am		FINAL1	; !WG _teâđam_ 
+V+Ind+Prs+Sg2:%^PALNo%^CC2C%>ak		FINAL1	; !WG ! _teâđak_
+V+Ind+Prs+Sg3: FINAL1	; ! _teâtt_
+V+Ind+Prs+Pl1:%^VOWRaise%^PALE%^PAL%>ep		FINAL1	; ! _äʹlǧǧep_ _tieʹttep_
+V+Ind+Prs+Pl2:%^VOWRaise%^PALE%^PAL%>veʹted		FINAL1	; ! _tieʹttveʹted_
+V+Ind+Prs+Pl3:%^PALÄ%^PAL%>e		FINAL1	; ! _teäʹtte_ 
+V+Ind+Prs+Sg4:%^VOWRaise%^PALE%^PAL%^CC2C%>et FINAL1 ; ! tieʹđet
+V+Ind+Prs+ConNeg:%^VOWRaise%^PALE%^PAL%^CC2C FINAL1 ;
+V:%^VOWRaise%^PALE%^PAL%^CC2C VSUF-IND-PRS-CONNEG_ED ;

!+V+Ind+Prt:		TIETTED-PRT ;
+V+Ind+Prt+Sg1:%^VOWRaise%^PAL%>em		K	; ! _tiõʹttem_
+V+Ind+Prt+Sg2:%^VOWRaise%^PAL%>iǩ		K	; ! _tiõʹttiǩ_
+V+Ind+Prt+Sg3:%^VOWRaise%^PAL%^CC2C%>i		K	; !WG ! _tiõʹđi_
+V+Ind+Prt+Pl1:%^VOWRaise%^PAL%^CC2C%>im		K  ; !WG ! _tiõʹđim_
+V+Ind+Prt+Pl1:%^VOWRaise%^PAL%^CC2C%>in		K  ; !WG ! _tiõʹđin_
+V+Ind+Prt+Pl2:%^VOWRaise%^PAL%^CC2C%>id		K	; !WG ! _tiõʹđid_
+V+Ind+Prt+Pl2:%^VOWRaise%^PALE%^PAL%^CC2C%>id		K	; ! _tieʹđid_
+V+Ind+Prt+Pl3:%^VOWRaise%^PAL%^CC2CCC%>e		K	; ! _tiõʹtte_ 
+V+Ind+Prt+Pl3:%^VOWRaise%^PALE%^PAL%^CC2CCC%>e		K	; ! _tieʹtte_
+V+Ind+Prt+Sg4:%^VOWRaise%^PAL%>eš FINAL1 ; ! tiõʹtteš
+V+Ind+Prt+ConNeg:%>am FINAL1 ; ! teâttam

!+V+Imprt:			TIETTED-IMPRT ;
+V+Imprt+Sg2:%^VOWRaise%^PALE%^PAL%^CC2C K	; ! tieʹđ
+V+Imprt+Sg3:%^CC2C%>as		K	; ! teâđas
+V+Imprt+Pl1:%^VOWRaise%^PALE%^PAL%>ep 		K	;
+V+Imprt+Pl2:%^VOWRaise%^PALE%^PAL%>ed 		K  ;
+V+Imprt+Pl3:%^CC2CCC%>az		K	; ! teâtˈtaz
+V+Imprt+2+ConNeg:%^PAL%^CC2C K	; !WG

+V+Pot+Sg3:%^VOWRaise%^PAL%^CC2C%>ež	K ; !WG tiõʹđež
+V:%^VOWRaise%^PAL%^CC2C%>	POTENTIAL ; ! tiõʹđežem
+V:%^CC2C%>	CONDITIONAL ; !WG vowel lowering to cope with
+V+Inf:%^VOWRaise%^PALE%^PAL%>ed K ;
+V+ActEss:%^PAL%>men K ;
+V:%^PAL%^CC2C GER_ ;
+V+Actio:%^PAL%>em K ;
+V+Actio:%^PAL%>em N_MINIʹSTTER ;
+V+PrsPrc:%^VOWRaise%^PAL%>i K ; !vuõʹlǧǧi
+V+PrsPrc:%^PAL%>i K ; !vueʹlǧǧi
+V+PrfPrc:%^VOWLower%^VOWBack%^PALNo%>am K ;
+V+Pass+PrfPrc:%^VOWRaise%>um K ;
+V+Imprt+13+ConNeg:%^VOWRaise%>u K ;
+V:%^PAL%^CC2C  VABESS ;		!WG



LEXICON V_SUIʹTTED !!= * @CODE@ suiʹtted:suitt
! seek parallell in AIBBÂD
! FORMS
! (1) +V+Inf: suiʹtted
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: soitt
! (3) +V+Ind+Prs+Pl3: soiʹtte
! (4) +V+Ind+Prt+Pl3: suiʹtte
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: vueiʹn
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: suuiʹtež
! (7) +V+Imprt+Sg3: ???sooitas
! (8) +V+Imprt+13+ConNeg: ???suittu
!    Pass.PrfPrc
! suiʹtted:suitt
! Stem is Strong
! Diphthong
! Vowel raising
! NEEDS WORK
!(son soitt) ??
+V:%^VOWRaise%^PALE%^PAL VSUF-INF_ED ; !1
+V: VSUF-IND-PRS-SG3_ED ; !2
+V+Ind+Prs+Pl3:%^PAL%>e K ; !3
+V:%^VOWRaise%^PAL%> VSUF-IND-PRT-PL3_ED ; !4
+V:%^VOWRaise%^PAL%^XYY2XY%> VSUF-POTENTIAL_ED ; !5 vueiʹnet
+V:%^VOWBack%^PAL%^XYY2XY%> VSUF-POTENTIAL_ED ; !5 vuõiʹnet
+V:%^XYY2XY%> VSUF-CONDITIONAL_ED ; !5 ?2 ?7
+V:%^XYY2XY VSUF-IND-PRS-CONNEG_ED ;
+V+Pot+Sg3:%^VOWBack%^PAL%^XYY2XY%>ež   K ; !6
+V+Pot+Sg3:%^VOWRaise%^PALE%^PAL%^XYY2XY%>ež   K ; !6
+V+Imprt+Sg3:%^XYY2XY%>as K ; !7
+V:%^VOWBack VSUF-IMPRT-13-CONNEG_ED ; !8



LEXICON V_VUEʹLǦǦED !!= * @CODE@ vueʹlǧǧed:vuâlgg
! FORMS
! (1) +V+Inf: vueʹlǧǧed
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: vuâlgg
! (3) +V+Ind+Prs+Pl3: vueʹlǧǧe
! (4) +V+Ind+Prt+Pl3: vuõʹlǧǧe
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: vueʹlj
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: vuõʹljež
! (7) +V+Imprt+Sg3: vuâlǥas
! (8) +V+Imprt+13+ConNeg: vuõlggu
!    Pass.PrfPrc
! vueʹlǧǧed:vuâlgg
+V:%^PALE%^PAL VSUF-INF_ED ; !1
+V: VSUF-IND-PRS-SG3_ED ; !2
+V+Ind+Prs+Pl3:%^PALE%^PAL%>e K ; !3
+V+Ind+Prs+Pl3:%^PALÄ%^PAL%>e K ; !3
+V:%^VOWRaise%^PAL%> VSUF-IND-PRT-PL3_ED ; !4
+V:%^VOWRaise%^PAL%^XYY2XY%> VSUF-POTENTIAL_ED ; !5
+V:%^PALE%^PAL%^XYY2XY%> VSUF-POTENTIAL_ED ; !5
+V:%^XYY2XY%> VSUF-CONDITIONAL_ED ; !5 ?2 ?7
+V:%^PALE%^PAL%^XYY2XY VSUF-IND-PRS-CONNEG_ED ;
+V+Pot+Sg3:%^VOWRaise%^PAL%^XYY2XY%>ež   K ; !6
+V+Pot+Sg3:%^PALE%^PAL%^XYY2XY%>ež   K ; !6
+V+Imprt+Sg3:%^XYY2XY%>as K ; !7
+V:%^VOWRaise VSUF-IMPRT-13-CONNEG_ED ; !8

+V+Der/škueʹtted:%^PALE%^PAL%^XYY2XY»škuätt V_PUEʹTTED ;

!! !CLASS 1 LOW VOWEL, PALATALIZATION
!! EVEN-SYLLABLE STEMS IN -ED

LEXICON V_SOLLÂʹTTED !!= * @CODE@ sollâʹtted:sollâtt
! seek parallel in TÄTTAD
! FORMS
! (1) +V+Inf: sollâʹtted
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: sollâtt
! (3) +V+Ind+Prs+Pl3: sollâʹtte
! (4) +V+Ind+Prt+Pl3: sollõʹtte
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: sollââʹtt
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: sollõõʹttež
! (7) +V+Imprt+Sg3: sollââttas
! (8) +V+Imprt+13+ConNeg: sollõttu
!    Pass.PrfPrc
! (9) Ind.Imprt.Pl3: sollâttaz
! (10) Allegro for incoative: sollâʹđ»

! sollâʹtted:sollâtt
+V:%^PAL VSUF-INF_ED ; !1
+V: VSUF-IND-PRS-SG3_ED ; !2
+V+Ind+Prs+Pl3:%^PAL%>e K ; !3
+V:%^VOWRaise%^PAL%> VSUF-IND-PRT-PL3_ED ; !4
+V:%^V2VV%^VOWRaise%^PAL%> VSUF-I-POTKOND_AD ; !5
+V+Pot+Sg3:%^V2VV%^VOWRaise%^PAL%>ež   K ; !6
+V+Imprt+Sg3:%^V2VV%>as K ; !7
+V:%^V2VV%^PAL VSUF-IND-PRS-CONNEG_ED ;
+V:%^VOWRaise VSUF-IMPRT-13-CONNEG_ED ; !8
+V+Der/škueʹtted:%^PAL%^CC2C%>e»škuätt V_PUEʹTTED ; 



LEXICON V_JÅÅʹTTED !!= * @CODE@ jååʹtted:jåått
! FORMS
! (1) +V+Inf: jååʹtted
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: jåått
! (3) +V+Ind+Prs+Pl3: jååʹtte
! (4) +V+Ind+Prt+Pl3: jooʹtte
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: jååʹđ
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: jååttež
! (7) +V+Imprt+Sg3: jååttas
! (8) +V+Imprt+13+ConNeg: jottu
!    Pass.PrfPrc
! (9) Ind.Imprt.Pl3: jåttaz

! jååʹtted:jåått
+V:%^PAL VSUF-INF_ED ; !1 jååʹtted
+V: VSUF-IND-PRS-SG3_ED ; !2 jåått
+V+Ind+Prs+Pl3:%>a K ; !3 jååʹtte
+V:%^VOWRaise%> VSUF-IND-PRT-PL3_ED ; !4 jooʹtte
+V:%^V2VV%^PAL%> VSUF-I-POTKOND_AD ; !5
+V+Pot+Sg3:%^V2VV%>ež   K ; !6
+V+Imprt+Sg3:%^V2VV%>as K ; !7
+V:%^V2VV%^PAL VSUF-IND-PRS-CONNEG_ED ;

+V:%^VOWRaise VSUF-IMPRT-13-CONNEG_ED ; !8

! (10) Allegro for incoative: jåʹđ»
+V+Der/škueʹtted:%^VV2V%^PAL%^CC2C»škuätt V_PUEʹTTED ;

LEXICON V_CEʹPCCED !!= * @CODE@ ceʹpcced:cepcc
! Associate with särnnad parallel
! FORMS
! (1) +V+Inf: ceʹpcced
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: cepcc
! (3) +V+Ind+Prs+Pl3: ceʹpcce
! (4) +V+Ind+Prt+Pl3: ciʹpcce
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: ceeʹpc
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: ciiʹpcež
! (7) +V+Imprt+Sg3: ceeʹpcas
! (8) +V+Imprt+13+ConNeg: cipccu
!    Pass.PrfPrc
! (9) Ind.Imprt.Pl3: cepccaz
! (10) Allegro for incoative: ceʹpc»
! ceʹpcced:cepcc
+V:%^PAL VSUF-INF_ED ; !1 ceʹpcced
+V: VSUF-IND-PRS-SG3_ED ; !2 cepcc
+V+Ind+Prs+Pl3:%^PAL%>e K ; !3 ceʹpcce
+V:%^VOWRaise%^PAL%> VSUF-IND-PRT-PL3_ED ; !4 ciʹpcce
+V:%^V2VV%^VOWRaise%^PAL%^XYY2XY%> VSUF-POTENTIAL_ED ; !5 ciiʹpc-
+V:%^V2VV%^PAL%^XYY2XY%> VSUF-CONDITIONAL_ED ; !5 ?2 ?7 ceeʹpc-
+V+Pot+Sg3:%^V2VV%^VOWRaise%^PAL%^XYY2XY%>ež   K ; !6 ciiʹpcež
+V+Imprt+Sg3:%^V2VV%^PAL%^XYY2XY%>as K ; !7 ceeʹpcas
+V:%^V2VV%^PAL%^XYY2XY VSUF-IND-PRS-CONNEG_ED ;
+V: VSUF-IMPRT-13-CONNEG_ED ; !8 cepcc-

+V+Der/škueʹtted:%^PAL%^XYY2XY»škuätt V_PUEʹTTED ;


LEXICON V_KÄʹDDED !!= * @CODE@ käʹdded:kädd
! seek parallel in ÄBBAD
! FORMS
! (1) +V+Inf: käʹdded
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: kädd
! (3) +V+Ind+Prs+Pl3: käʹdde
! (4) +V+Ind+Prt+Pl3: kaʹdde
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: kääʹdd
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: kaaʹddež
! (7) +V+Imprt+Sg3: kääʹddas
! (8) +V+Imprt+13+ConNeg: kaddu
!    Pass.PrfPrc
! (9) Ind.Imprt.Pl3: käddaz
! käʹdded:kädd
+V:%^PAL VSUF-INF_ED ; !1 käʹdded
+V: VSUF-IND-PRS-SG3_ED ; !2 kädd
+V+Ind+Prs+Pl3:%^PAL%>e K ; !3 käʹdde
+V:%^VOWRaise%^PAL%> VSUF-IND-PRT-PL3_ED ; !4 kaʹdde
+V:%^V2VV%^VOWRaise%^PAL%> VSUF-POTENTIAL_ED ; !5 kaaʹdd-
+V:%^V2VV%> VSUF-CONDITIONAL_ED ; !5 ?2 ?7 kääʹdd-
+V+Pot+Sg3:%^V2VV%^VOWRaise%^PAL%>ež   K ; !6 kaaʹddež
+V+Imprt+Sg3:%^V2VV%^PAL%>as K ; !7 kääʹddas
+V:%^V2VV%^PAL VSUF-IND-PRS-CONNEG_ED ;
+V: VSUF-IMPRT-13-CONNEG_ED ; !8 kädd-

! (10) Allegro for incoative: käʹd»
+V+Der/škueʹtted:%^PAL%^CC2C»škuätt V_PUEʹTTED ;

LEXICON V_VEÄʹDˈDED !!= * @CODE@ veäʹdˈded:veädd
! seek parallel in LUÂDˈDAD
! FORMS
! (1) +V+Inf: veäʹdˈded
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: veädd
! (3) +V+Ind+Prs+Pl3: veäʹdˈde
! (4) +V+Ind+Prt+Pl3: vieʹdde
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: veäʹdˈd
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: vieʹddež
! (7) +V+Imprt+Sg3: veäʹdˈdas
! (8) +V+Imprt+13+ConNeg: viâddu
!    Pass.PrfPrc
! (9) Ind.Imprt.Pl3: veäddaz
! veäʹdˈded:veädd
+V:%^PAL VSUF-INF_ED ; !1 veäʹdˈded
+V: VSUF-IND-PRS-SG3_ED ; !2 veädd
+V+Ind+Prs+Pl3:%^PAL%>e K ; !3 veäʹdˈde
+V:%^VOWRaise%^PALẸ%^PAL%> VSUF-IND-PRT-PL3_ED ; !4 vieʹdˈde
+V:%^VOWRaise%^PALẸ%^PAL%^CC2CCC%> VSUF-POTENTIAL_ED ; !5 vieʹdd-
+V:%^CC2CCC%> VSUF-CONDITIONAL_ED ; !5 ?2 ?7 veäʹdˈd-
+V+Pot+Sg3:%^VOWRaise%^PALẸ%^PAL%^CC2CCC%>ež   K ; !6 vieʹddež
+V+Imprt+Sg3:%^PAL%>as K ; !7 veäʹddas
+V:%^PAL VSUF-IND-PRS-CONNEG_ED ;
+V: VSUF-IMPRT-13-CONNEG_ED ; !8 veädd-

! (10) Allegro for incoative: veäʹd»
+V+Der/škueʹtted:%^PAL%^CC2C»škuätt V_PUEʹTTED ;

LEXICON V_KÂÂʹǨǨED !!= * @CODE@ kââʹǩǩed:kââkk
! 1. VERBS WITH SINGLE-SYLLABLE Ind.Prs.Sg3 (2009: ???)
! 1.3 Inf in palatalization + ed (vowel shift)
!   Raised: Inf, Ind.Prt.Pl3, Imprt.Sg2, Pot.Sg3
!   Lowered: Ind.Prs.Sg3, Ind.Prs.Pl3, Imprt.Sg3
! 1.3.1 Has Palatalization
! 1.3.1[1] (Palatalization pattern)
!   Palatalized: Inf, Ind.Prs.Pl3, Ind.Prt.Pl3, Pot.Sg3
!   Not Palatalized: Ind.Prs.Sg3, Imprt.Sg2, Imprt.Sg3
! 1.3.1[1].1 Lacks Specifically Pedagogical Gradation
! 1.3.1[1].1.1 Has Orthographic Gradation
! 1.3.1[1].1.1[231] (Diphthong + Consonant and Consonant Geminate alternation)
!   Strong grade: 
!   Extra strong grade: 
!   Weak grade: 
! FORMS
! (1)  Inf: kââʹǩǩed
! (2)  Ind.Prs.Sg3: kââkk
! (3)  Ind.Prs.Pl3: kââʹǩˈǩe
! (4)  Ind.Prt.Pl3: kõõʹǩˈǩe
! (5)  Ind.Imprt.Sg2: kââʹjj
! (6)  Ind.Pot.Sg3: kõõʹjjež
! (7)  Imprt.Sg3: kââǥǥas
! (8)  Imprt.13.ConNeg: kõkku 
! (9)  Imprt.Pl3: kâkkaz
! (9) Ind.Imprt.Pl3: kâkˈkaz
! (10) Allegro for incoative: kâʹj»
! (11)  Ger.Temp.PxSg3: kââʹjjeeʹstes
! * Yaml: __V-kaakked__
! kââʹǩǩed:kââkk
! Strong grade in penultimate syllable
+V:%^PAL VSUF-INF_ED ; !1
+V: VSUF-IND-PRS-SG3_ED ; !2
+V+Imprt+Pl3:%^VV2V%>az K ;
+V:%^KK2GG VSUF-IND-PRS-CONNEG_ED ;
+V+Ind+Prs+Pl3:%^PAL%>e K ; !3
+V:%^VOWRaise%^PAL VSUF-IND-PRT-PL3_ED ; !4
+V:%^VOWRaise%^PAL%^KK2GG VSUF-POTENTIAL_ED ; !5
+V+Pot+Sg3:%^VOWRaise%^PAL%^KK2GG%>âž   K ; !6
+V+Imprt+Sg3:%^CC2C%>as K ; !7
+V:%^VOWRaise VSUF-IMPRT-13-CONNEG_ED ; !8
 !9V

+V+Der/škueʹtted:%^PAL%^CC2C»škuätt V_PUEʹTTED ;



LEXICON V_PUEʹTTED !!= * @CODE@ pueʹtted:puätt
! Stem is Strong
! Diphthong
! Vowel shift
! FORMS
! (1) +V+Inf: pueʹtted
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: puätt
! (3) +V+Ind+Prs+Pl3: puäʹtte
! (4) +V+Ind+Prt+Pl3: pueʹtte, puõʹtte
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: puäʹđ
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: puäđaž, pueʹđež
! (7) +V+Imprt+Sg3: puäʹđas
! (8) +V+Imprt+13+ConNeg: puõttu
!    Pass.PrfPrc
! 
! NEEDS WORK
+V:%^VOWRaise%^PALE%^PAL VSUF-INF_ED ;		!1 pueʹtted 
+V: VSUF-IND-PRS-SG3_ED ; 	     		!2 puätt
+V+Ind+Prs+Pl3:%^PAL%>e K ; 			!3 puäʹtte
+V:%^VOWRaise%^PALE%^PAL%> VSUF-IND-PRT-PL3_ED ; !4 pueʹtte
+V:%^VOWRaise%^PALÕ%^PAL%> VSUF-IND-PRT-PL3_ED ; !4 puõʹtte
+V:%^VOWRaise%^PALE%^PAL%^CC2C%> VSUF-POTENTIAL_ED ; !5 pueʹđež
!+V:%^VOWRaise%^PALÕ%^PAL%^CC2C%> VSUF-POTENTIAL_ED ; !5 puõʹđež
+V:%^VOWRaise%^PALÕ%^PAL%^CC2C VSUF-IND-PRT-SG3-PL1-PL2_ ; ! puõʹđi

+V:%^CC2C%> VSUF-CONDITIONAL_ED ; !5 ?2 ?7
+V:%^PAL%^CC2C%> VSUF-CONDITIONAL_ED ; !5 ?2 ?7
+V:%^PAL%^CC2C VSUF-IND-PRS-CONNEG_ED ;
+V+Pot+Sg3:%^PAL%^CC2C%>ež   K ; !6   puäʹđež
+V+Pot+Sg3:%^VOWRaise%^PALE%^PAL%^CC2C%>ež   K ; !6  pueʹđež
+V+Imprt+Sg3:%^CC2C%>as K ; !7 puäʹđas
+V:%^VOWRaise%^PALÕ VSUF-IMPRT-13-CONNEG_ED ; !8 puõttu
+V:%^VOWRaise VSUF-IMPRT-13-CONNEG_ED ; !8 puåttu

+V+Der/škueʹtted:%^VOWLower%^PAL%^CC2C%>e»škuätt V_PUEʹTTED ;

LEXICON V_VUEIʹNNED !!= * @CODE@ vueiʹnned:vuäinn
! FORMS
! (1) +V+Inf: vueiʹnned
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: vuäinn
! (3) +V+Ind+Prs+Pl3: vuäiʹnne
! (4) +V+Ind+Prt+Pl3: vuõiʹnne
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: vueiʹn
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: vuõiʹnež
! (7) +V+Imprt+Sg3: ???vuâlǥas
! (8) +V+Imprt+13+ConNeg: ???vuõlggu
!    Pass.PrfPrc
! vueiʹnned:vuäinn
! Stem is Strong
! Diphthong
! Vowel raising
! NEEDS WORK
!(son vuäinn)
+V:%^VOWRaise%^PALE%^PAL VSUF-INF_ED ; !1
+V: VSUF-IND-PRS-SG3_ED ; !2
+V+Ind+Prs+Pl3:%^PAL%>e K ; !3
+V:%^VOWRaise%^PALE%^PAL%> VSUF-IND-PRT-PL3_ED ; !4
+V:%^VOWRaise%^PALE%^PAL%^XYY2XY%> VSUF-POTENTIAL_ED ; !5 vueiʹnet
+V:%^VOWBack%^PAL%^XYY2XY%> VSUF-POTENTIAL_ED ; !5 vuõiʹnet
+V:%^XYY2XY%> VSUF-CONDITIONAL_ED ; !5 ?2 ?7
+V:%^VOWRaise%^PALE%^PAL%^XYY2XY VSUF-IND-PRS-CONNEG_ED ;
+V+Pot+Sg3:%^VOWBack%^PAL%^XYY2XY%>ež   K ; !6
+V+Pot+Sg3:%^VOWRaise%^PALE%^PAL%^XYY2XY%>ež   K ; !6
+V+Imprt+Sg3:%^XYY2XY%>as K ; !7
+V:%^VOWBack VSUF-IMPRT-13-CONNEG_ED ; !8


LEXICON V_VUẸʹĐĐED !!= * @CODE@ vuẹʹđđed:vuäđđ
! FORMS
! (1) +V+Inf: vuẹʹđđed
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: vuäđđ
! (3) +V+Ind+Prs+Pl3: vuäʹđˈđe
! (4) +V+Ind+Prt+Pl3: vueʹđˈđe
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: vuäʹđ
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: vueʹđež
! (7) +V+Imprt+Sg3: ???vuäđas
! (8) +V+Imprt+13+ConNeg: ???vueđđu
!    Pass.PrfPrc
! Stem is Strong
! Diphthong
! Vowel raising
! NEEDS WORK
!(son vuäđđ)
+V:%^VOWRaise%^PALẸ%^PAL VSUF-INF_ED ; !1	vuẹʹđđed
+V: VSUF-IND-PRS-SG3_ED ; !2	     vuäđđ
+V+Ind+Prs+Pl3:%^PAL%>e K ; !3	     vuäʹđđe
+V:%^VOWRaise%^PALẸ%^PAL%> VSUF-IND-PRT-PL3_ED ; !4 vueʹđˈđe
+V:%^VOWRaise%^PALẸ%^PAL%^CC2C%> VSUF-POTENTIAL_ED ; !5 vuẹʹđet
+V:%^CC2C%> VSUF-CONDITIONAL_ED ; !5 ?2 ?7
+V:%^VOWRaise%^PALE%^PAL%^CC2C VSUF-IND-PRS-CONNEG_ED ;
+V+Pot+Sg3:%^VOWBack%^PAL%^CC2C%>ež   K ; !6 vueʹđež
+V+Pot+Sg3:%^VOWRaise%^PALE%^PAL%^CC2C%>ež   K ; !6
+V+Imprt+Sg3:%^CC2C%>as K ; !7
+V:%^VOWBack VSUF-IMPRT-13-CONNEG_ED ; !8


LEXICON V_LEUʹǨǨED !!= * @CODE@ leuʹǩǩed:leukk
! seek parallell in MEINNAD
! FORMS
! (1) +V+Inf: leuʹǩǩed
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: vuäinn
! (3) +V+Ind+Prs+Pl3: vuäiʹnne
! (4) +V+Ind+Prt+Pl3: vuõiʹnne
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: vueiʹn
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: vuõiʹnež
! (7) +V+Imprt+Sg3: ???vuâlǥas
! (8) +V+Imprt+13+ConNeg: ???vuõlggu
!    Pass.PrfPrc
! leuʹǩǩed:leukk
! Stem is Strong
! Diphthong
! Vowel raising
! NEEDS WORK
!(son leukk) ??
+V:%^VOWRaise%^PALE%^PAL VSUF-INF_ED ; !1
+V: VSUF-IND-PRS-SG3_ED ; !2
+V+Ind+Prs+Pl3:%^PAL%>e K ; !3
+V:%^VOWRaise%^PALE%^PAL%> VSUF-IND-PRT-PL3_ED ; !4
+V:%^VOWRaise%^PALE%^PAL%^XYY2XY%> VSUF-POTENTIAL_ED ; !5 vueiʹnet
+V:%^VOWBack%^PAL%^XYY2XY%> VSUF-POTENTIAL_ED ; !5 vuõiʹnet
+V:%^XYY2XY%> VSUF-CONDITIONAL_ED ; !5 ?2 ?7
+V:%^XYY2XY VSUF-IND-PRS-CONNEG_ED ;
+V+Pot+Sg3:%^VOWBack%^PAL%^XYY2XY%>ež   K ; !6
+V+Pot+Sg3:%^VOWRaise%^PALE%^PAL%^XYY2XY%>ež   K ; !6
+V+Imprt+Sg3:%^XYY2XY%>as K ; !7
+V:%^VOWBack VSUF-IMPRT-13-CONNEG_ED ; !8



LEXICON V_TÄIʹDDED !!= * @CODE@ täiʹdded:täidd
! seek parallell in MEINNAD
! FORMS
! (1) +V+Inf: täiʹdded
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: täidd
! (3) +V+Ind+Prs+Pl3: täiʹdde
! (4) +V+Ind+Prt+Pl3: taiʹdde
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: vueiʹn
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: taaiʹdež
! (7) +V+Imprt+Sg3: ???tääidas
! (8) +V+Imprt+13+ConNeg: ???taiddu
!    Pass.PrfPrc
! täiʹdded:täidd
! Stem is Strong
! Diphthong
! Vowel raising
! NEEDS WORK
!(son täidd) ??
+V:%^PAL VSUF-INF_ED ; !1
+V: VSUF-IND-PRS-SG3_ED ; !2
+V+Ind+Prs+Pl3:%^PAL%>e K ; !3
+V:%^VOWRaise%^PAL%> VSUF-IND-PRT-PL3_ED ; !4
+V:%^VOWRaise%^PAL%^XYY2XY%> VSUF-POTENTIAL_ED ; !5 vueiʹnet
+V:%^XYY2XY%> VSUF-CONDITIONAL_ED ; !5 ?2 ?7
+V:%^XYY2XY VSUF-IND-PRS-CONNEG_ED ;
+V+Pot+Sg3:%^VOWRaise%^PAL%^XYY2XY%>ež   K ; !6
+V+Imprt+Sg3:%^XYY2XY%>as K ; !7
+V:%^VOWRaise VSUF-IMPRT-13-CONNEG_ED ; !8

LEXICON V_CEÄʹLǨǨED !!= * @CODE@ ceäʹlǩǩed:ceälkk
! FORMS
! (1) +V+Inf: ceäʹlǩǩed
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: ceälkk
! (3) +V+Ind+Prs+Pl3: ceäʹlǩǩe
! (4) +V+Ind+Prt+Pl3: cieʹlǩǩe
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: cieʹlj
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: cieʹljež
! (7) +V+Imprt+Sg3: ceälkas
! (8) +V+Imprt+13+ConNeg: ciâlkku
!    Pass.PrfPrc
! ceäʹlǩǩed:ceälkk
+V:%^PAL VSUF-INF_ED ; !1
+V: VSUF-IND-PRS-SG3_ED ; !2 ceälkk
+V+Ind+Prs+Pl3:%^PAL%>e K ; !3
+V:%^VOWRaise%^PALE%^PAL%> VSUF-IND-PRT-PL3_ED ; !4
+V:%^VOWRaise%^PALE%^PAL%^XYY2XY%> VSUF-POTENTIAL_ED ; !5
+V:%^XYY2XY%> VSUF-CONDITIONAL_ED ; !5 ?2 ?7
+V:%^XYY2XY VSUF-IND-PRS-CONNEG_ED ;
+V+Pot+Sg3:%^VOWRaise%^PALE%^PAL%^XYY2XY%>ež   K ; !6
+V+Imprt+Sg3:%^XYY2XY%>as K ; !7
+V: VSUF-IMPRT-13-CONNEG_ED ; !8

+V+Der/škueʹtted:%^PAL%^XYY2XY»škuätt V_PUEʹTTED ;

LEXICON V_MEÄʹTTED !!= * @CODE@ meäʹtted:meätt
! FORMS
! (1) +V+Inf: meäʹtted
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: meätt
! (3) +V+Ind+Prs+Pl3: meäʹtˈte
! (4) +V+Ind+Prt+Pl3: mieʹtˈte
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: mieʹđ
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: mieʹđež
! (7) +V+Imprt+Sg3: meättas
! (8) +V+Imprt+13+ConNeg: miâtˈtu
!    Pass.PrfPrc
! meäʹtted:meätt
+V:%^PAL VSUF-INF_ED ; !1
+V: VSUF-IND-PRS-SG3_ED ; !2 meätt
+V+Ind+Prs+Pl3:%^PAL%>e K ; !3
+V:%^VOWRaise%^PALE%^PAL%> VSUF-IND-PRT-PL3_ED ; !4
+V:%^VOWRaise%^PALE%^PAL%^XYY2XY%> VSUF-POTENTIAL_ED ; !5
+V:%^XYY2XY%> VSUF-CONDITIONAL_ED ; !5 ?2 ?7
+V:%^XYY2XY VSUF-IND-PRS-CONNEG_ED ;
+V+Pot+Sg3:%^VOWRaise%^PALE%^PAL%^XYY2XY%>ež   K ; !6
+V+Imprt+Sg3:%^XYY2XY%>as K ; !7
+V: VSUF-IMPRT-13-CONNEG_ED ; !8

+V+Der/škueʹtted:%^PAL%^XYY2XY»škuätt V_PUEʹTTED ;

LEXICON V_MÄʹHSSED !!= * @CODE@ mäʹhssed:mähss
! FORMS
! (1) +V+Inf: mäʹhssed
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: mähss
! (3) +V+Ind+Prs+Pl3: mäʹhsse
! (4) +V+Ind+Prt+Pl3: maʹhsse
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: maauʹs
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: maauʹsež
! (7) +V+Imprt+Sg3: määusas
! (8) +V+Imprt+13+ConNeg: mahssu
!    Pass.PrfPrc
! mäʹhssed:mähss
+V:%^PAL VSUF-INF_ED ; !1
+V: VSUF-IND-PRS-SG3_ED ; !2 mähss
+V+Ind+Prs+Pl3:%^PAL%>e K ; !3
+V:%^VOWRaise%^PAL%> VSUF-IND-PRT-PL3_ED ; !4
+V:%^VOWRaise%^PAL%^XYY2VY%> VSUF-POTENTIAL_ED ; !5
+V:%^XYY2VY%> VSUF-CONDITIONAL_ED ; !5 ?2 ?7
+V:%^XYY2VY VSUF-IND-PRS-CONNEG_ED ;
+V+Pot+Sg3:%^VOWRaise%^PAL%^XYY2VY%>ež   K ; !6
+V+Imprt+Sg3:%^XYY2VY%>as K ; !7
+V: VSUF-IMPRT-13-CONNEG_ED ; !8

+V+Der/škueʹtted:%^PAL%^XYY2VY»škuätt V_PUEʹTTED ;

!! DERIVED VERBS WITH PENULTIMATE VOWEL LOSS AND CHANGE

!! !CLASS 2 HIGH VOWEL, NO PALATALIZATION
LEXICON V_AALǤTED !!= * @CODE@  aalǥted:aalǥât
! lexicon ''@LEXNAME@''
+V:%^PenVow2a VSUF-VUEJTED-IND-PRS-SG3 ; ! aalǥat
+V: VSUF-VUEJTED-IND-PRS-CONNEG ;        ! aalǥât
+V:%^RmVow VSUF-VUEJTED-ELSE ;		 ! aalǥted


LEXICON V_MAINSTED !!= * @CODE@ mainsted:maainâst
! inf: sted
! Ind.Prs.Sg3: -â-
! foot: 2syll
! 1.1 Inf in âd (vowel shift)
! diphthong: no
! vowel_shift: yes
! consonant_type: geminate-single
! gradation: yes3
! weak_grade: single
! strong_grade: geminate
! lexicon ''@LEXNAME@'' 
! mainsted:mainâst
! * Yaml: __V-mainsted__
+V:%^PenVow2a VSUF-VUEJTED-IND-PRS-SG3 ; ! maainast
+V: VSUF-VUEJTED-IND-PRS-CONNEG ;        ! maainâst
+V:%^PenVV2V%^RmVow VSUF-VUEJTED-ELSE ;           ! mainsted
+V+Use/NG:%^RmVow VSUF-VUEJTED-ELSE ;           ! maainsted

+V+Ind+Prt+Pl1+Use/NG:%^PenV2VV%^RmVow%>in             K  ; !WG ! maainstin
+V+Ind+Prt+Pl1+Use/NG:%^PenV2VV%^RmVow%>iim             K  ; !WG ! maainstiim
+V+Ind+Prt+Pl1+Use/NG:%^PenV2VV%^RmVow%>iin             K  ; !WG ! maainstiin
+V+Ind+Prt+Pl1+Use/NG:%^RmVow%>in             K  ; ! ! mainstin
+V+Ind+Prt+Pl1+Use/NG:%^RmVow%>iim             K  ; !WG ! maainstiim
+V+Ind+Prt+Pl1+Use/NG:%^RmVow%>iin             K  ; !WG ! maainstiin
+V+Ind+Prt+Pl2+Use/NG:%^PenV2VV%^RmVow%>iid             K  ; !WG ! mainstiid
+V+Ind+Prt+Pl2+Use/NG:%^RmVow%>iid             K  ; !WG ! mainstiid

+V+Actio:%^RmVow%>am K ; ! 2015-05-07 Is this necessary


LEXICON V_TAARBŠED !!= * @CODE@ taarbšed:taarbâš
! No Gradation
! ž:š
+V:%^PenVow2a VSUF-VUEJTED-IND-PRS-SG3 ;	! taarbâš
+V:%^VC VSUF-VUEJTED-IND-PRS-CONNEG ;        	! taarbâž
+V:%^RmVow VSUF-VUEJTED-ELSE ;                  ! taarbšed

LEXICON V_ǨIÕRGGNED
! Gradation: none
! ǩiõrggned:ǩiõrggân
! (ij ǩiõrggân) %^RmVow
! ǩiõrggned:ǩiõrggân
! FORMS
! (1) +V+Inf: ǩiõrggned
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: ǩiõrggan
! (3) +V+Ind+Prs+Pl3: ǩiõrggne
! (4) +V+Ind+Prt+Pl3: ǩiõrggne
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: ǩiõrggân
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: ǩiõrggnež
! (7) +V+Imprt+Sg3: ǩiõrggnââǥǥas
! (8) +V+Imprt+13+ConNeg: ǩiõrggnuku
!    Pass.PrfPrc
+V:%^PenVow2a VSUF-VUEJTED-IND-PRS-SG3 ; ! ǩiõrggan
+V: VSUF-VUEJTED-IND-PRS-CONNEG ;        ! ǩiõrggân
+V:%^RmVow VSUF-VUEJTED-ELSE ;           ! ǩiõrggned



!! !CLASS 2 LOW VOWEL, NO PALATALIZATION
LEXICON V_VUEJTED !!= * @CODE@ vuejted:vuejat
! lexicon ''@LEXNAME@'' 
! FORMS
! (2) +V+Ind+Prs+Sg3: vuejat
+V:%^PenVow2a VSUF-VUEJTED-IND-PRS-SG3  ;  !2 vuejat

! (5) +V+Imprt+Sg2:  vuejat
!   V.Imprt.2.ConNeg, V.Ind.Prs.ConNeg
+V: VSUF-VUEJTED-IND-PRS-CONNEG ;

! (1) +V+Inf: vuejted
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (3) +V+Ind+Prs+Pl3: vuejte
! (4) +V+Ind+Prt+Pl3: vuejte
!   Ind.Prt.Sg1-Sg2-4
! (6) +V+Pot+Sg3: vuejtež
! (7) +V+Imprt+Sg3: vuejtââǥǥas
! (8) +V+Imprt+13+ConNeg: vuejtuku
!    Pass.PrfPrc

!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
+V:%^RmVow VSUF-VUEJTED-ELSE ;

LEXICON VSUF-VUEJTED-IND-PRS-SG3
+Ind+Prs+Sg3: K ;

LEXICON VSUF-VUEJTED-IND-PRS-CONNEG
+Ind+Prs+ConNeg:	K ;
+Imprt+Sg2:		K ;
+Imprt+2+ConNeg: 	K ;

LEXICON VSUF-VUEJTED-ELSE
+Inf:%>ed K ;
+Ind+Prs+Pl1:%>ep  K  ; 
+Ind+Prs+Pl2:%>eʹped K  ;
+Ind+Prs+Pl3:%>e     K  ;
+Ind+Prt+Pl3:%>e              K  ; ! vuejte
+Ind+Prs+Sg1:%>am  K  ;  ! vuejtam
+Ind+Prs+Sg2:%>ak  K  ; !
+Ind+Prs+Sg4:%>et K ; !

+Ind+Prt+Sg1:%>em             K  ; ! vuejtem
+Ind+Prt+Sg2:%>iǩ             K  ; ! vuejtiǩ
+Ind+Prt+Sg3:%>i              K  ; ! ! vuejti
+Ind+Prt+Pl1:%>im             K  ; ! ! vuejtim
+Ind+Prt+Pl2:%>id             K  ; ! ! vuejtid
+Ind+Prt+Sg4:%>eš K ; ! vuejteš
+Ind+Prt+ConNeg:%>am K ; ! vuejtam

+Imprt+Sg3:%>ââǥǥas          K  ; !WG
+Imprt+Pl1:%>âkap                    K  ;
+Imprt+Pl2:%>eʹǩed           K  ;
+Imprt+Pl3:%>âkaz            K  ;

+Imprt+13+ConNeg:%>uku K ;
+Imprt+13+ConNeg+Use/NG:%>uku K ;
:%>eʹ		VSUF-I-POTKOND ; !WG
+Pot+Sg3:%>ež	K ; !WG
+ActEss:%>eʹmen K ;
+Actio:%>em K ;
+Actio:%>em N_PEʹRRJEM ;
+PrsPrc:%>eei K ;
+PrsPrc+Der/N:%>ee N_TUEJJEEI ;
+PrfPrc:%>am K ;
+Pass+PrfPrc:%>ummu K ;
+Pass+PrfPrc:%>uum K ;
:e VABESS ;
 GER_ ;
+Der/mmuš:%>ummuš N_MAINSTUMMUŠ ;
+Err/Orth+Der/muš:%>umuš N_MAINSTUMMUŠ ;
+Der/škueʹtted:%>e»škuätt V_PUEʹTTED ;


LEXICON V_JÄLSTED !!= * @CODE@ jälsted:jäälast
! lexicon ''@LEXNAME@''
!
! * Yaml: __V-jaelsted__

+V:%^PenVow2a VSUF-VUEJTED-IND-PRS-SG3 ; ! jäälast
+V: VSUF-VUEJTED-IND-PRS-CONNEG ;      	 ! jäälast
+V:%^PenVV2V%^RmVow VSUF-VUEJTED-ELSE ; 		 ! jälsted


LEXICON V_PÅǤSTED !!= * @CODE@ påǥsted:pååǥǥast
! lexicon ''@LEXNAME@''
!
! * Yaml: __V-poagsted__

+V:%^PenVow2a VSUF-VUEJTED-IND-PRS-SG3 ; ! pååǥǥast
+V: VSUF-VUEJTED-IND-PRS-CONNEG ;      	 ! pååǥǥast
+V:%^PenVV2V%^PenCC2C%^RmVow VSUF-VUEJTED-ELSE ; 		 ! påǥsted

LEXICON V_ŠORRNED !!= * @CODE@ šorrned:šorran
! (ij šorran) %^RmVow
! šorrned:šorran
! FORMS
! (1) +V+Inf: šorrned
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: šorran
! (3) +V+Ind+Prs+Pl3: šorrne
! (4) +V+Ind+Prt+Pl3: šorrne
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: šorrân
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: šorrnež
! (7) +V+Imprt+Sg3: šorrnââǥǥas
! (8) +V+Imprt+13+ConNeg: šorrnuku
!    Pass.PrfPrc
+V:%^PenVow2a VSUF-VUEJTED-IND-PRS-SG3 ; ! šorran
+V: VSUF-VUEJTED-IND-PRS-CONNEG ;        ! šorran
+V:%^RmVow VSUF-VUEJTED-ELSE ;  	 ! šorrned


!! !CLASS 2 HIGH VOWEL, PALATALIZATION


LEXICON V_ČIEʹǨRDED !!= * @CODE@ čieʹǩrded:čieʹǩǩerd
! FORMS
! (1) +V+Inf: čieʹǩrded
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: čieʹǩǩard
! (3) +V+Ind+Prs+Pl3: čieʹǩrdje
! (4) +V+Ind+Prt+Pl3: čieʹǩrdje
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: čieʹǩǩerd
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: kåʹmrdež
! (7) +V+Imprt+Sg3: čieʹǩrdââǥǥas
! (8) +V+Imprt+13+ConNeg: čieʹǩrduku
!    Pass.PrfPrc
+V:%^PenVow2a VSUF-VUEJTED-IND-PRS-SG3 ; ! čieʹǩǩard
+V: VSUF-VUEJTED-IND-PRS-CONNEG ;        ! čieʹǩǩerd
+V:%^PenCC2C%^RmVow VSUF-VUEJTED-ELSE ;  ! čieʹǩrded


!! !CLASS 2 LOW VOWEL, PALATALIZATION
LEXICON V_KÅʹMRDED !!= * @CODE@ kåʹmrded:kååʹmmerd
! FORMS
! (1) +V+Inf: kåʹmrded
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: kååʹmmard
! (3) +V+Ind+Prs+Pl3: kåʹmrde
! (4) +V+Ind+Prt+Pl3: kåʹmrde
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: kååʹmmerd
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: kåʹmrdež
! (7) +V+Imprt+Sg3: kåʹmrdââǥǥas
! (8) +V+Imprt+13+ConNeg: kåʹmrduku
!    Pass.PrfPrc
+V:%^PenVow2a VSUF-VUEJTED-IND-PRS-SG3 ; ! kååʹmmard
+V: VSUF-VUEJTED-IND-PRS-CONNEG ;        ! kååʹmmerd
+V:%^PenVV2V%^PenCC2C%^RmVow VSUF-VUEJTED-ELSE ;  ! kåʹmrded

LEXICON V_ǨEʹRJJTED !!= * @CODE@ ǩeʹrjjted:ǩeʹrjjet
! lexicon ''@LEXNAME@'' 
! ǩeʹrjjted:ǩeʹrjjet
! * Yaml: __V-kjerjjted__
+V:%^PenVow2a VSUF-VUEJTED-IND-PRS-SG3 ; ! ǩeʹrjjat
+V: VSUF-VUEJTED-IND-PRS-CONNEG ;        ! ǩeʹrjjet
+V:%^RmVow VSUF-VUEJTED-ELSE ;  ! ǩeʹrjjted


!! !CLASS 3 HIGH VOWEL, NO PALATALIZATION



!! !CLASS 3 LOW VOWEL, NO PALATALIZATION
LEXICON V_KUÂĐĐJED !!= * @CODE@ kuâđđjed:kuâđđ
! +Ind+Prs+ConNeg: kuõđđu
! FORMS
! (1) +V+Inf: kuâđđjed
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: kuâđđai
! (3) +V+Ind+Prs+Pl3: kuâđđje
! (4) +V+Ind+Prt+Pl3: kuâđđje
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: kuõđđu
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: kuâđđjež
! (7) +V+Imprt+Sg3: kuâđđjââǥǥas
! (8) +V+Imprt+13+ConNeg: kuâđđjuku
!    Pass.PrfPrc
! kuâđđjed:kuâđđ
+V+Ind+Prs+Sg3:ai K        ; !kuâđđai
+V+Ind+Prs+ConNeg:%^VOWRaise%>u K ; ! kuõđđu
+V+Ind+Prs+Sg1:%>jam              K  ; !kuâđđjam
+V+Ind+Prs+Sg2:%>jak              K  ; !
+V+Ind+Prs+Pl1:%>jep      K  ;
+V+Ind+Prs+Pl2:%>jeʹped           K  ;
+V+Ind+Prs+Pl3:%>je               K  ;
+V+Ind+Prs+Sg4:%>jet K ; !

+V+Ind+Prt+Sg1:%>jem             K  ; ! kuâđđjem
+V+Ind+Prt+Sg2:%>jiǩ             K  ; ! kuâđđjiǩ
+V+Ind+Prt+Sg3:%>ji              K  ; ! ! kuâđđji
+V+Ind+Prt+Pl1:%>jim             K  ; ! ! kuâđđjim
+V+Ind+Prt+Pl2:%>jid             K  ; ! ! kuâđđjid
+V+Ind+Prt+Pl3:%>je              K  ; ! kuâđđje
+V+Ind+Prt+Sg4:%>ješ K ; ! kuâđđješ
+V+Ind+Prt+ConNeg:%>jam K ; ! kuâđđjam
+V+Ind+Prt+ConNeg:%>jâmma K ; ! kuâđđjâmma

+V+Imprt+Sg3:%>jââǥǥas          K  ; !WG
+V+Imprt+Pl1:%>jâkap                    K  ;
+V+Imprt+Pl2:%>jeʹǩed           K  ;
+V+Imprt+Pl3:%>jâkaz            K  ;

+V+Imprt+Sg2:%^VOWRaise%>u  K ;
+V+Imprt+2+ConNeg:%^VOWRaise%>u     K ;
+V+Imprt+13+ConNeg:%>juku K ;

+V:%>jeʹ         VSUF-I-POTKOND ; !WG
+V+Pot+Sg3:%>jež K ; !WG
+V+Inf:%>jed K ;
+V+ActEss:%>jeʹmen K ;
+V+Actio:%>jam K ;
+V+Actio:%>jam N_SLOOHMAN ;
+V+PrsPrc:%>jeei K ;
+V+PrsPrc+Der/N:%>jee N_TUEJJEEI ;
+V+PrfPrc:%>jam K ;
+V+PrfPrc:%>jâmma K ;
+V+Pass+PrfPrc:%>jummu K ;
+V+Pass+PrfPrc:%>jum K ;
+V+VAbess:%>jeǩâni K ;
+V:%>j GER_ ;

+V+Der/mmuš:%>jummuš N_MAINSTUMMUŠ ;
+V+Der/škueʹtted:je»škuätt V_PUEʹTTED ;

LEXICON SEDGGJED !!= * @CODE@ sedggjed:sedgg
! see KUÂĐĐJED
:%{A1%}i KAMRDED-01 ;
+V+Imprt+Sg2:%^VOWRaise%>u	K ;
+V+Imprt+2+ConNeg:%^VOWRaise%>u K ;
+V+Der/škueʹtted:je»škuätt V_PUEʹTTED ;


!! !CLASS 3 HIGH VOWEL, PALATALIZATION
LEXICON V_VÕʹLLʼJED !!= * @CODE@ võʹll’jed:võʹll
! E.Koponen pg. 31
! võʹllʼjed:võʹll
! +Ind+Prs+ConNeg: võʹlle
! võʹll’jed:võʹll
+V+Imprt+Sg2:%^IMPSG2	K ;
+V+Imprt+2+ConNeg:%>juku K ; ! võʹllʼjuku

!+V+Ind+Prs:%{A1%}i		KAMRDED-PRS ;
+V+Ind+Prs+Sg1:%>jam              FINAL1  ; ! võʹllʼjam
+V+Ind+Prs+Sg2:%>jak              FINAL1  ; ! võʹllʼjak
+V+Ind+Prs+Sg3:ai FINAL1        ; ! võʹllai
+V+Ind+Prs+Pl1:%>jep      FINAL1  ;         ! võʹllʼjep
+V+Ind+Prs+Pl2:%>jeʹped           FINAL1  ;  ! võʹllʼjeʹped
+V+Ind+Prs+Pl3:%>je               FINAL1  ;  ! võʹllʼje
+V+Ind+Prs+Sg4:%>jet FINAL1 ; !              !   võʹllʼjet
+V+Ind+Prs+ConNeg:e K ; ! võʹlle


!+V+Ind+Prt:%{A1%}i		K1AMRDED-PRT ;

+V+Ind+Prt+Sg1:%>jem             FINAL1  ; ! võʹllʼjem
+V+Ind+Prt+Sg2:%>jiǩ             FINAL1  ; ! võʹllʼjiǩ
+V+Ind+Prt+Sg3:%>ji              FINAL1  ; !WG ! võʹllʼji
+V+Ind+Prt+Pl1:%>jim             FINAL1  ; !WG ! võʹllʼjim
+V+Ind+Prt+Pl1:%>jin             FINAL1  ; !WG ! võʹllʼjin
+V+Ind+Prt+Pl1:%>jiim             FINAL1  ; !WG ! võʹllʼjiim
+V+Ind+Prt+Pl1:%>jiin             FINAL1  ; !WG ! võʹllʼjiin
+V+Ind+Prt+Pl2:%>jid             FINAL1  ; !WG ! võʹllʼjid
+V+Ind+Prt+Pl2:%>jiid             FINAL1  ; !WG ! võʹllʼjiid
+V+Ind+Prt+Pl3:%>je              FINAL1  ; ! võʹllʼje
+V+Ind+Prt+Sg4:%>ješ FINAL1 ; ! võʹllʼješ


!+V+Imprt:%{A1%}i			KAMRDED-IMPRT ;
+V+Imprt+Sg3:%>jââǥǥas          FINAL1  ;    ! võʹllʼjââǥǥas
+V+Imprt+Pl1:%>jâkap            FINAL1  ;    ! võʹllʼjâkap
+V+Imprt+Pl2:%>jeʹǩed           FINAL1  ;    ! võʹllʼjeʹǩed
+V+Imprt+Pl3:%>jâkaz            FINAL1  ;    ! võʹllʼjâkaz

+V+Imprt+ConNeg:%>juku K ;    ! võʹllʼjuku
+V:%>jeʹ		VSUF-I-POTKOND ; !WG
+V+Pot+Sg3:%>jež	K ; !WG
+V+Inf:%>jed K ;
+V+ActEss:%>jeʹmen K ;
+V+Actio:%>jam K ;
+V+Actio:%>jam N_SLOOHMAN ;
+V+PrsPrc:%>jeei K ;
+V+PrsPrc+Der/N:%>jee N_TUEJJEEI ;
+V+PrfPrc:%>jam K ;
+V+PrfPrc:%>jâmma K ;
+V+Pass+PrfPrc:%>jummu K ;
+V+Pass+PrfPrc:%>jum K ; !
+V+Pass+PrfPrc:%>juum K ; !
+V+VAbess:%>jeǩâni K ;
+V+VAbess:%>jeǩeânai K ;
+V:%>j GER_ ;
+V+Der/mmuš:%>jummuš N_MAINSTUMMUŠ ;

+V+Imprt+Sg2:%>e        K ; ! võʹlle
+V+Imprt+2+ConNeg:%>e K ;   ! võʹlle
+V+Der/škueʹtted:%>je»škuätt V_PUEʹTTED ;

!! !CLASS 3 LOW VOWEL, PALATALIZATION


!! !CLASS 3 HIGH VOWEL, NO PALATALIZATION, GH
LEXICON V_KUÕCCJED !!= * @CODE@ kuõccjed:kuõcc
! +Ind+Prs+ConNeg: kuõccâǥ
! cf. kuâđđjed, võʹllʼjed
! 
!!
! FORMS
! (1) +V+Inf: kuõccjed
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: kuõccai
! (3) +V+Ind+Prs+Pl3: kuõccje
! (4) +V+Ind+Prt+Pl3: kuõccje
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: kuõccâǥ
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: kuõccjež
! (7) +V+Imprt+Sg3: kuõccjââǥǥas
! (8) +V+Imprt+13+ConNeg: kuõccjuku
!    Pass.PrfPrc
! kuõccjed:kuõcc
+V+Ind+Prs+Sg3:ai K        ; ! kuõccai
+V+Ind+Prs+ConNeg:âǥ K ; ! kuõccâǥ
+V+Ind+Prs+Sg1:%>jam              K  ; !
+V+Ind+Prs+Sg2:%>jak              K  ; !
+V+Ind+Prs+Pl1:%>jep      K  ;
+V+Ind+Prs+Pl2:%>jeʹped           K  ;
+V+Ind+Prs+Pl3:%>je               K  ;
+V+Ind+Prs+Sg4:%>jet K ; !

+V+Ind+Prt+Sg1:%>jem             K  ; ! kuõccjem
+V+Ind+Prt+Sg2:%>jiǩ             K  ; ! kuõccjiǩ
+V+Ind+Prt+Sg3:%>ji              K  ; ! ! kuõccji
+V+Ind+Prt+Pl1:%>jim             K  ; ! ! kuõccjim
+V+Ind+Prt+Pl2:%>jid             K  ; ! ! kuõccjid
+V+Ind+Prt+Pl3:%>je              K  ; ! kuõccje
+V+Ind+Prt+Sg4:%>ješ K ; ! kuõccješ
+V+Ind+Prt+ConNeg:%>jam K ; ! kuõccjam
+V+Ind+Prt+ConNeg:%>jâmma K ; ! kuõccjâmma

+V+Imprt+Sg3:%>jââǥǥas          K  ; !WG
+V+Imprt+Pl1:%>jâkap                    K  ;
+V+Imprt+Pl2:%>jeʹǩed           K  ;
+V+Imprt+Pl3:%>jâkaz            K  ;

+V+Imprt+Sg2:âǥ  K ; ! kuõccâǥ
+V+Imprt+2+ConNeg:âǥ     K ;
+V+Imprt+13+ConNeg:%>juku K ;

+V:%>jeʹ         VSUF-I-POTKOND ; !WG
+V+Pot+Sg3:%>jež K ; !WG
+V+Inf:%>jed K ;
+V+ActEss:%>jeʹmen K ;
+V+Actio:%>jam K ;
+V+Actio:%>jam N_SLOOHMAN ;
+V+PrsPrc:%>jeei K ;
+V+PrsPrc+Der/N:%>jee N_TUEJJEEI ;
+V+PrfPrc:%>jam K ;
+V+Pass+PrfPrc:%>jummu K ;
+V+Pass+PrfPrc:%>juum K ;
+V+VAbess:%>jeǩâni K ;
+V:%>j GER_ ;

+V+Der/mmuš:%>jummuš N_MAINSTUMMUŠ ;
+V+Der/škueʹtted:je»škuätt V_PUEʹTTED ;


!! !CLASS 3 LOW VOWEL, NO PALATALIZATION, GH

LEXICON V_STÅÅRJED
! page 388
! ståårjed:ståår
!  (ij stååraǥ)
! Work to be done 2015-01-10
+V+Imprt+Sg2:%^IMPSG2     K ;
+V+Imprt+2+ConNeg:aǥ K ;
! FORMS
! (1) +V+Inf: ståårjed
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: ståårai
! (3) +V+Ind+Prs+Pl3: ståårje
! (4) +V+Ind+Prt+Pl3: ståårje
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: stååraǥ
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: ståårjež
! (7) +V+Imprt+Sg3: ståårjââǥǥas
! (8) +V+Imprt+13+ConNeg: ståårjuku
!    Pass.PrfPrc
! ståårjed:ståår
+V+Ind+Prs+Sg3:ai K        ;		! ståårai
+V+Ind+Prs+ConNeg:aǥ K ;   		! ståårâǥ
+V+Ind+Prs+Sg1:%>jam              K  ; 	! ståårjam
+V+Ind+Prs+Sg2:%>jak              K  ; !
+V+Ind+Prs+Pl1:%>jep      K  ;
+V+Ind+Prs+Pl2:%>jeʹped           K  ;
+V+Ind+Prs+Pl3:%>je               K  ;
+V+Ind+Prs+Sg4:%>jet K ; !

+V+Ind+Prt+Sg1:%>jem             K  ; ! ståårjem
+V+Ind+Prt+Sg2:%>jiǩ             K  ; ! ståårjiǩ
+V+Ind+Prt+Sg3:%>ji              K  ; ! ! ståårji
+V+Ind+Prt+Pl1:%>jim             K  ; ! ! ståårjim
+V+Ind+Prt+Pl2:%>jid             K  ; ! ! ståårjid
+V+Ind+Prt+Pl3:%>je              K  ; ! ståårje
+V+Ind+Prt+Sg4:%>ješ K ; ! ståårješ
+V+Ind+Prt+ConNeg:%>jam K ; ! ståårjam
+V+Ind+Prt+ConNeg:%>jâmma K ; ! ståårjâmma

+V+Imprt+Sg3:%>jââǥǥas          K  ; ! ståårjââǥǥas
+V+Imprt+Pl1:%>jâkap            K  ;
+V+Imprt+Pl2:%>jeʹǩed           K  ;
+V+Imprt+Pl3:%>jâkaz            K  ;

+V+Imprt+Sg2:aǥ  K ; ! stååraǥ
+V+Imprt+2+ConNeg:aǥ     K ;
+V+Imprt+13+ConNeg:%>juku K ;

+V:%>jeʹ         VSUF-I-POTKOND ; !WG
+V+Pot+Sg3:%>jež K ; !WG
+V+Inf:%>jed K ;
+V+ActEss:%>jeʹmen K ;
+V+Actio:%>jam K ;
+V+Actio:%>jam N_SLOOHMAN ;
+V+PrsPrc:%>jeei K ;
+V+PrsPrc+Der/N:%>jee N_TUEJJEEI ;
+V+PrfPrc:%>jam K ;
+V+Pass+PrfPrc:%>jummu K ;
+V+Pass+PrfPrc:%>juum K ;
+V+VAbess:%>jeǩâni K ;
+V:%>j GER_ ;

+V+Der/mmuš:%>jummuš N_MAINSTUMMUŠ ;
+V+Der/škueʹtted:je»škuätt V_PUEʹTTED ;


!  -jed -ted

!!

!! !CLASS 4 HIGH VOWEL, NO PALATALIZATION

LEXICON V_SILTTEED !!= * @CODE@ siltteed:siltt
! Ind+Prs+ConNeg: -âd
! lexicon ''@LEXNAME@'' 
+V+Ind+Prs:		TEEMEED-PRS ;
+V+Ind+Prt:		TEEMEED-PRT ;
+V+Imprt:			TEEMEED-IMPRT ;
+V+Imprt+Sg2:%>âd		K ;
+V:%>eʹ		VSUF-I-POTKOND ; !WG
+V+Pot+Sg3:%>eež	K ; !WG
+V+Inf:%>eed K ;
+V+ActEss:%>eʹmen K ;
+V: GER_ ;
+V+Actio:%>eem K ;
+V+Actio:%>eem ATTESTED-NOUN-STEM ;
+V+PrsPrc:%>eei K ;
+V+PrsPrc+Der/N:%>ee N_TUEJJEEI ;
+V+PrfPrc:%>ääm K ;
+V+Pass+PrfPrc:%>ummu K ;
+V+Pass+PrfPrc:%>uum K ;
+V+VAbess:%>eǩâni K ;		!WG
+V+Imprt+ConNeg:%>ukku K ;

!! !CLASS 4 LOW VOWEL, NO PALATALIZATION
LEXICON V_SOLLEED
! Ind+Prs+ConNeg: -ad
! solleed:soll
! * Yaml: __V-solleed__
! +Ind+Prs+ConNeg: sollad
! lexicon ''@LEXNAME@'' 
+V+Ind+Prs:		TEEMEED-PRS ;
+V+Ind+Prt:		TEEMEED-PRT ;
+V+Imprt:			TEEMEED-IMPRT ;
+V+Imprt+Sg2:%>ad		K ;
+V:%>eʹ		VSUF-I-POTKOND ; !WG
+V+Pot+Sg3:%>eež	K ; !WG
+V+Inf:%>eed K ;
+V+ActEss:%>eʹmen K ;
+V: GER_ ;
+V+Actio:%>eem K ;
+V+Actio:%>eem ATTESTED-NOUN-STEM ;
+V+PrsPrc:%>eei K ;
+V+PrsPrc+Der/N:%>ee N_TUEJJEEI ;
+V+PrfPrc:%>ääm K ;
+V+Pass+PrfPrc:%>ummu K ;
+V+Pass+PrfPrc:%>uum K ;
+V+VAbess:%>eǩâni K ;		!WG
+V+Imprt+ConNeg:%>ukku K ;


!! !CLASS 4 HIGH VOWEL,  PALATALIZATION

!! !CLASS 4 LOW VOWEL, PALATALIZATION

LEXICON V_TEEʹMEED
! Ind+Prs+ConNeg: Palatalization AND -ed
! teeʹmeed:teeʹm
! lexicon ''@LEXNAME@'' 
+V+Ind+Prs:		TEEMEED-PRS ;
+V+Ind+Prt:		TEEMEED-PRT ;
+V+Imprt:			TEEMEED-IMPRT ;
+V+Imprt+Sg2:%>ed		K ;
+V:%>eʹ		VSUF-I-POTKOND ; !WG
+V+Pot+Sg3:%>eež	K ; !WG
+V+Inf:%>eed K ;
+V+ActEss:%>eʹmen K ;
+V: GER_ ;
+V+Actio:%>eem K ;
+V+Actio:%>eem ATTESTED-NOUN-STEM ;
+V+PrsPrc:%>eei K ;
+V+PrsPrc+Der/N:%>ee N_TUEJJEEI ;
+V+PrfPrc:%>ääm K ;
+V+Pass+PrfPrc:%>ummu K ;
+V+Pass+PrfPrc:%>uum K ;
+V+VAbess:%>eǩâni K ;		!WG
+V+Imprt+ConNeg:%>ukku K ;

!! Not yet written








LEXICON KAMRDED-01

+V+Ind+Prs:		KAMRDED-PRS ;
+V+Ind+Prt:		KAMRDED-PRT ;
+V+Imprt:			KAMRDED-IMPRT ;
+V+Imprt+ConNeg:%>uku K ;
+V:%>eʹ		VSUF-I-POTKOND ; !WG
+V+Pot+Sg3:%>ež	K ; !WG
+V+Inf:%>ed K ;
+V+ActEss:%>eʹmen K ;
+V+Actio:%>am K ;
+V+PrsPrc:%>eei K ;
+V+PrsPrc+Der/N:%>ee N_TUEJJEEI ;
+V+PrfPrc:%>am K ;
+V+PrfPrc:%>âmma K ;
+V+Pass+PrfPrc:%>ummu K ;
+V+Pass+PrfPrc:%>um K ; !
+V+Pass+PrfPrc:%>uum K ; !
+V+VAbess:%>eǩâni K ;
+V+VAbess:%>eǩeânai K ;
+V:  GER_ ;
+V+Der/mmuš:%^PenV2VV%>ummuš N_MAINSTUMMUŠ ;



! Intermediate suffix lexica
! --------------------------


! Final suffix lexica
! -------------------

! Suffix series I
! - - - - - - - -

! assuming stem __viǯǯ__


! Present tense
! - - - - - - - -

! assuming stem _teevv_

! assuming stem _kâlmm_

! assuming stem _juʹrdd_

! assuming stem _tieʹtt_

! assuming stem _ääʹv_ _teeʹm_
LEXICON TEEMEED-PRS
 +Sg1:%>ääm		FINAL1	; !WG _ääʹvääm_ 
 +Sg2:%>ääk		FINAL1	; !WG ! _ääʹvääk_
 +Sg3:%>ad FINAL1	; ! _ääʹvad_
 +Pl1:%>eep		FINAL1	; ! _ääʹveep_ _tieʹttep_
 +Pl2:%>veʹted		FINAL1	; ! _ääʹvveʹted_
 +Pl3:%>ee		FINAL1	; ! _ääʹvee_
 +Sg4:%>eet FINAL1 ; ! ääʹveet
 +ConNeg:%>ed FINAL1 ; ! ääʹved


! assuming stem kååʹmmerded
LEXICON KAMRDED-PRS
 +Sg1:%>am		FINAL1	; !WG 
 +Sg2:%>ak		FINAL1	; !WG 
 +Sg3:%^INDPRSSG3 FINAL1	; 
 +Pl1:%>ep	FINAL1	; 
 +Pl2:%>eʹped		FINAL1	; 
 +Pl3:%>e		FINAL1	; 
 +Sg4:%>et FINAL1 ; ! 
 +ConNeg:%^IMPSG2 FINAL1 ; ! kååʹmmerd

LEXICON LEED-PRS
 +Sg1:eäm		K	; !WG
 +Sg2:eäk		K	; !WG
 +Sg3:ij		K	; 
 +Pl1:eäˈʹp	K	; ! __
 +Pl2:%>eäˈʹped		K	; ! __
 +Pl2:%>eʹped		K	; ! __
 +Pl3:%>ie		K	; ! __
 +Pl3:%>iâ		K	; ! __
 +Pl3:%>ee		K	; ! __
 +Sg4:%>eäˈʹt K ; ! 
 +ConNeg:eäkˈku FINAL1 ; ! 

! Past tense
! - - - - - - - -


LEXICON TEEMEED-PRT
 +Sg1:%>eem		FINAL1	; ! _ääʹveem_
 +Sg2:%>iiǩ		FINAL1	; ! _ääʹviiǩ_
 +Sg3:%>ii		FINAL1	; !WG ! _ääʹvii_
 +Pl1:%>iim		FINAL1	; !WG ! _ääʹviim_
 +Pl1:%>iin		FINAL1	; !WG ! _ääʹviin_ ! sub??
 +Pl2:%>iid		FINAL1	; !WG ! _ääʹviid_
 +Pl3:%>ee		FINAL1	; ! _ääʹvee_
 +Sg4:%>eeš FINAL1 ; ! ääʹveeš
 +ConNeg:%>ääm FINAL1 ;



! assuming stem _kååʹmmerd_
LEXICON KAMRDED-PRT
 +Sg1:%>em             FINAL1  ; ! kåʹmrdem
 +Sg2:%>iǩ             FINAL1  ; ! kåʹmrdiǩ
 +Sg3:%>i              FINAL1  ; !WG ! kåʹmrdi
 +Pl1:%>im             FINAL1  ; !WG ! kåʹmrdim
 +Pl1:%>in             FINAL1  ; !WG ! kåʹmrdin
 +Pl1:%>iim             FINAL1  ; !WG ! kåʹmrdiim
 +Pl1:%>iin             FINAL1  ; !WG ! kåʹmrdiin
 +Pl2:%>id             FINAL1  ; !WG ! kåʹmrdid
 +Pl2:%>iid             FINAL1  ; !WG ! kåʹmrdiid
 +Pl3:%>e              FINAL1  ; ! kåʹmrde
 +Sg4:%>eš FINAL1 ; ! kåʹmrdeš

LEXICON LEED-PRT
 +Sg1:%>eʹjjem             FINAL1  ; !
 +Sg2:%>eʹjjiǩ             FINAL1  ; !
 +Sg3:%>eäi              FINAL1  ; !
 +Sg3:%>eei              FINAL1  ; !
 +Sg3:%>äi              FINAL1  ; !
 +Pl1:%>eeiʹm             FINAL1  ; !
 +Pl2:%>eeiʹd             FINAL1  ; !
 +Pl3:%>eʹjje              FINAL1  ; !
 +Sg4:%>eʹjješ FINAL1 ; ! 
 +ConNeg:eämˈmaž FINAL1 ; !
 +ConNeg:eämˈma FINAL1 ; !
 +ConNeg:eäm FINAL1 ; !


! Imperative
! - - - - - - -
! teevvad
! 
!

! tieʹtted

! teeʹmeed
LEXICON TEEMEED-IMPRT
 +Sg3:%>ââǥǥas		FINAL1	; !WG
 +Pl1:%>âkap			FINAL1	;
 +Pl2:%>eʹǩed		FINAL1	;
 +Pl3:%>âkaz		FINAL1	;

LEXICON KAMRDED-IMPRT
 +Sg3:%>ââǥǥas          FINAL1  ; !WG
 +Pl1:%>âkap                    FINAL1  ;
 +Pl2:%>eʹǩed           FINAL1  ;
 +Pl3:%>âkaz            FINAL1  ;

LEXICON LEED-IMPRT
 +Sg3:%>eäǥǥas          FINAL1  ; !WG
 +Pl1:%>eäkˈkap                    FINAL1  ;
 +Pl2:%>eäkˈku           FINAL1  ;
 +Pl2:%>eäkˈkveʹted           FINAL1  ;
 +Pl3:%>eäkˈkaz            FINAL1  ;




! Potentialis & Konditionalis
! not including +Pot+Sg3
! - - - - - - - - - - - - - -
LEXICON VSUF-INF_ÂD
+Inf:%>âd FINAL1 ;
+Ind+Prs+Pl1:%>âp FINAL1 ;
+Ind+Prs+Pl2:%>veʹted FINAL1 ;
+Ind+Prt+ConNeg:%>âm FINAL1 ;
+Imprt+Pl1:%>âp FINAL1 ;
+Imprt+Pl2:%>âd FINAL1 ;
+ActEss:%>men FINAL1 ;
+Actio:%>âm FINAL1 ;
+Actio:%>âm N_ǨIÕRÂČ ; ! mõõnnâm
+PrsPrc:%>i FINAL1 ;
+PrfPrc:%>âm FINAL1 ;
LEXICON VSUF-IND-PRS-SG3_ÂD
+Ind+Prs+Sg3: FINAL1 ;
+Imprt+Pl3:%>az FINAL1 ;


LEXICON VSUF-INF_AD
+Inf:%>ad FINAL1 ;
+Ind+Prs+Pl1:%>ap FINAL1 ;
+Ind+Prs+Pl2:%>veʹted FINAL1 ;
+Ind+Prt+ConNeg:%>am FINAL1 ;
+Imprt+Pl1:%>ap FINAL1 ;
+Imprt+Pl2:%>ad FINAL1 ;
+ActEss:%>men FINAL1 ;
+Actio:%>am FINAL1 ;
+Actio:%>am N_SLOOHMAN ;
 VSUF-PRSPRC_AD ;
+PrfPrc:%>am FINAL1 ;
LEXICON VSUF-IND-PRS-SG3_AD
+Ind+Prs+Sg3: FINAL1 ;

!+Imprt+Pl3:%>az FINAL1 ; VSUF-IMPRT-PL3_AD


LEXICON VSUF-INF_ED
+Inf:%>ed FINAL1 ;
+Ind+Prs+Pl1:%>ep FINAL1 ;
+Ind+Prs+Pl2:%>veʹted FINAL1 ;
+Ind+Prt+ConNeg:%>am FINAL1 ;
+Imprt+Pl1:%>ep FINAL1 ;
+Imprt+Pl2:%>ed FINAL1 ;
+ActEss:%>men FINAL1 ;
+Actio:%>em FINAL1 ;
+Actio:%>em  N_MINIʹSTTER ; ! Should there be a derivation here 2015-05-06
 VSUF-PRSPRC_ED ;

LEXICON VSUF-IND-PRS-SG3_ED
+PrfPrc:%>am FINAL1 ; !HERE or ONE LEXICON UP
+Ind+Prs+Sg3: FINAL1 ;
+Imprt+Pl3:%>az FINAL1 ;
+Der/mõš:»mõš N_LOOKKMÕŠ ;

!
LEXICON VSUF-IND-PRT-PL3_ÂD
+Ind+Prt+Sg1:%>em FINAL1 ;
+Ind+Prt+Sg2:%>iǩ FINAL1 ;
+Ind+Prt+Pl3:%>e FINAL1 ;
+Ind+Prt+Sg4:%>eš FINAL1 ;
LEXICON VSUF-IND-PRT-PL3_AD
+Ind+Prt+Sg1:%>um FINAL1 ;
+Ind+Prt+Sg2:%>uk FINAL1 ;
+Ind+Prt+Pl3:%>u FINAL1 ;
+Ind+Prt+Sg4:%>uš FINAL1 ;
LEXICON VSUF-IND-PRT-PL3_ED
+Ind+Prt+Sg1:%>em FINAL1 ;
+Ind+Prt+Sg2:%>iǩ FINAL1 ;
+Ind+Prt+Pl3:%>e FINAL1 ;
+Ind+Prt+Sg4:%>eš FINAL1 ;
!
LEXICON VSUF-IND-PRT-SG3-PL1-PL2_
+Ind+Prt+Sg3:%>i FINAL1 ;
+Ind+Prt+Pl1:%>im FINAL1 ;
+Ind+Prt+Pl1:%>in FINAL1 ;
+Ind+Prt+Pl2:%>id FINAL1 ;

!
LEXICON VSUF-I-POTKOND_ÂD
 VSUF-I-POTKOND ;
 VSUF-IND-PRT-SG3-PL1-PL2_ ;
+Ind+Prs+Sg1:%>am FINAL1 ;
+Ind+Prs+Sg2:%>ak FINAL1 ;
+Ind+Prs+Sg4:%>ât FINAL1 ;
+Ind+Prs+ConNeg: FINAL1 ;

+Imprt+Sg2: FINAL1 ;
+Imprt+2+ConNeg: FINAL1 ;
 VABESS ;
 GER_ ;

LEXICON VSUF-I-POTKOND_AD
 VSUF-I-POTKOND ;
+Ind+Prs+Sg1:%>am FINAL1 ;
+Ind+Prs+Sg2:%>ak FINAL1 ;
+Ind+Prs+Sg4:%>at FINAL1 ;
+Ind+Prs+ConNeg: FINAL1 ;
:a VSUF-IND-PRT-SG3-PL1-PL2_ ;

+Imprt+Sg2: FINAL1 ;
+Imprt+2+ConNeg: FINAL1 ;
 VABESS ;
 GER_ ;

LEXICON VSUF-POTENTIAL_ED
 POTENTIAL ;
 VSUF-IND-PRT-SG3-PL1-PL2_ ;
 VABESS ;

LEXICON VABESS
+VAbess:%>kani K ;
+VAbess:%>kâni K ;
+VAbess:%>ǩeânai K ;
+VAbess:%>ǩâni K ;
+VAbess:%>ǩânai K ;
+VAbess:%>ǩani K ;

LEXICON GER_
+GerInstr:%>eeʹl K ;
+GerTemp:%>een K ;
+GerTemp:%>eeʹst K ;

LEXICON VSUF-CONDITIONAL_ED
+Ind+Prs+Sg1:%>am K ;
+Ind+Prs+Sg2:%>ak K ;
 CONDITIONAL ;
 VABESS ;
 GER_ ;

LEXICON VSUF-IND-PRS-CONNEG_ED
+Ind+Prs+Sg4:%>et FINAL1 ;
+Ind+Prs+ConNeg: FINAL1 ;
+Imprt+2+ConNeg: FINAL1 ;


LEXICON VSUF-I-POTKOND
 POTENTIAL ;
 CONDITIONAL ;

LEXICON POTENTIAL
 +Pot+Sg1:žem K	;
 +Pot+Sg2:žiǩ K	;
 +Pot+Pl1:žep K	; ! How are these related?
 +Pot+Pl1:žim K	; ! Are there sub standards?
 +Pot+Pl1:žin K	; !
 +Pot+Pl2:žid K	;
 +Pot+Pl3:že K	;
 +Pot+Sg4:žet K ;
 +Pot+ConNeg:že K ;

LEXICON CONDITIONAL
 +Cond+Sg1:čem K	;
 +Cond+Sg2:čiǩ K	;
 +Cond+Sg3:či K	;
 +Cond+Pl1:čim K	; ! How are these related?
 +Cond+Pl1+Use/NG:čep K	; ! Are there substandards? 
 +Cond+Pl1+Use/NG:čin K	; !
 +Cond+Pl2:čid K	;
 +Cond+Pl3:če K	;
 +Cond+Sg4:češ K ;
 +Cond+ConNeg:če K ;


LEXICON VSUF-IMPRT-13-CONNEG_ÂD
+Imprt+13+ConNeg:%>u K ;
+Pass+PrfPrc:%>um K ;
LEXICON VSUF-IMPRT-13-CONNEG_AD
+Imprt+13+ConNeg:%>u K ;
+Pass+PrfPrc:%>um K ;
LEXICON VSUF-IMPRT-PL3_AD
+Imprt+Pl3:%>az K ;

LEXICON VSUF-IMPRT-13-CONNEG_ED
+Imprt+13+ConNeg:%>u K ;
+Pass+PrfPrc:%>um K ;

! Suffix series II

! Not written

! Other series
! - - - - - -

LEXICON VSUF-PRSPRC_AD
+PrsPrc:ai K ;
+PrsPrc+Sg+Nom:ai K ;
+PrsPrc+Pl+Nom:%^CC2C%>ai K ;
+PrsPrc+Pl+Nom:ai K ;
+PrsPrc+Der/MWN: N_PÅÅSSAI ;

LEXICON VSUF-PRSPRC_ED
+PrsPrc:i K ;
+PrsPrc+Sg+Nom:i K ;
+PrsPrc+Pl+Nom:%^CC2C%>i K ;
+PrsPrc+Pl+Nom:i K ;
+PrsPrc+Der/MWN: N_JOOʹTTI ;
