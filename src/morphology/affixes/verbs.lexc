
!! !!!Skolt Saami verb morphology



!! !!Irregular verbs


LEXICON IJ
!! The lexicon ''@LEXNAME@''

+Ind+Sg1:jiõm	   K ; !!= * @CODE@
+Ind+Sg1:jim	   K ; !!= * @CODE@
+Ind+Sg2:jiõk	   K ; !!= * @CODE@
+Ind+Sg2:jik	   K ;
+Ind+Sg3:ij		   K ;
+Ind+4:jeäˊt	   K ;
+Ind+Pl1:jeäˊp	   K ;
+Ind+Pl1:jep	   K ;
+Ind+Pl2:jeäˊped   K ;
+Ind+Pl2:jeˊped	   K ;
+Ind+Pl3:jiâ	   K ;
+Ind+Pl3:jeä	   K ;
+Ind+Pl3:jie	   K ;

+Imp+Sg2:jieˊl	   K ;
+Imp+Sg2:jeˊl	   K ;
+Imp+Pl2:jieˊlled  K ;	
+Imp+Pl2:jeˊlled   K ;	

+Opt+Sg1:jeällap   K ;
+Opt+Sg2:jieˊl	   K ;
+Opt+Sg2:jeˊl	   K ;
+Opt+Sg3:jeälas	   K ;
+Opt+Pl2:jieˊlled  K ;
+Opt+Pl2:jeˊlled   K ;			
+Opt+Pl3:jeällaz   K ;

! leeˊd
LEXICON LEED
!! The lexicon ''@LEXNAME@'' for the copula

+V+Ind+Prs:		LEED-PRS ;
+V+Ind+Prt:		LEED-PRT ;
+V+Imp:			LEED-IMP ;
+V+Imp+Sg2:eäk'ku	FINAL1 ;
+V+Imp+ConNeg:%>eäk'ku FINAL1 ;
+V:%>eˊč		CONDITIONAL ; !
+V+Pot+Sg1:%>eˊžžem FINAL1	;
+V+Pot+Sg2:%>eˊžžiǩ FINAL1	;
+V+Pot+Sg3:%>eežž	FINAL1 ; !WG
+V+Pot+Sg3:%>eež	FINAL1 ; !
+V+Pot+Pl1:%>eˊžžep FINAL1	; !
+V+Pot+Pl1:%>eˊžžim FINAL1	; !
+V+Pot+Pl1:%>eˊžžin FINAL1	; !Does this exist
+V+Pot+Pl2:%>eˊžžveˊted FINAL1	;
+V+Pot+Pl3:%>eˊžže FINAL1	;
+V+Pot+4:%>eˊžžet FINAL1 ;
+V+Pot+ConNeg:%>eˊžže FINAL1 ;
+V+Inf:%>eeˊd FINAL1 ;
+V+PrsPrc:%>eˊddi FINAL1 ;
+V+PrfPrc:%>eäm'maž FINAL1 ;
+V+PrfPrc:%>eäm'ma FINAL1 ;
+V+PrfPrc:%>eäm FINAL1 ;



!! !!Even-syllable stems


LEXICON LAADDAD ! error? or unwritten ?Å
+V: # ;

LEXICON VOOIDAD !error? or unwritten ?Å
+V: # ;


LEXICON VIQQAD
!! Lexicon ''@LEXNAME@'' is for verbs like
!!  __viǯǯâd__ , with lexc stem __viǯǯ__
+V+Inf:%>âd FINAL1 ;
+V+Ind+Prs: VIQQAD-Prs ;
+V+Ind+Prt: VIQQAD-Prt ;
+V+Imp: VIQQAD-Imp ;
+V:%^VOWL%^CShort%> VSUF-I-POTKOND ; !WG
+V+Pot+Sg3:%^VOWL%^CShort%>âž	FINAL1 ; !WG
+V+ActEss:%>men FINAL1 ;
+V+GerTemp:%^VOWL%^CShort%>een FINAL1 ;
+V+GerInstr:%^VOWL%^CShort%>eeˊl FINAL1 ;
+V+Action:%>âm FINAL1 ;
+V+PrsPrc:%>i FINAL1 ;
+V+PrfPrc:%>âm FINAL1 ;
+V+Pass+PrfPrc:%^VOWSH%^CLong%>um FINAL1 ;
+V+VAbess:%^VOWL%^CShort%>kâni FINAL1 ;		!WG
+V+VAbess:%^VOWL%^CShort%>kani FINAL1 ;		!WG
+V+VAbess:%^VOWL%^CShort%>ǩeânai FINAL1 ;		!WG




LEXICON TEEVVAD
!! In the lexicon ''@LEXNAME@'' the verb __teevvad__ has the lexc stem __teevv__
+V+Ind+Prs:		TEEVVAD-PRS ;
+V+Ind+Prt:		TEEVVAD-PRT ;
+V+Imp:			TEEVVAD-IMP ;
+V:%^VOWL%^CShort%> VSUF-I-POTKOND ; !WG
+V+Pot+Sg3:%^VOWL%^CShort%>ež	FINAL1 ; !WG
+V+Inf:%>ad FINAL1 ;
+V+PrsPrc:%>ai FINAL1 ;
+V+PrfPrc:%>am FINAL1 ;
+V+ActEss:%>men FINAL1 ;
+V+Pass+PrfPrc:%^VOWSH%^CLong%^VOWRaise%>um FINAL1 ;
+V+GerTemp:%^VOWL%^CShort%>een FINAL1 ;
+V+GerInstr:%^VOWL%^CShort%>eeˊl FINAL1 ;
+V+Action:%>am FINAL1 ;
+V+VAbess:%^VOWL%^CShort%>kâni FINAL1 ;		!WG
+V+VAbess:%^VOWL%^CShort%>kani FINAL1 ;		!WG
+V+VAbess:%^VOWL%^CShort%>ǩeânai FINAL1 ;		!WG

 
 
 
LEXICON KALMMED
!! In the lexicon ''@LEXNAME@'' the verb __kâˊlmmed__ has the lexc stem
!! __kâlmm__. It raises in +Ind+Prt+Pl3, Pot and has lengthening in 
!! hmm, yes, where? 
!! It seems KALMMED has the same stem (^VOWRaise, ^PAL) in Pot and Cond, 
!! whereas JURDDED has ^PAL in Pot and ^VOWLower in Cond.
+V+Ind+Prs:		KALMMED-PRS ;
+V+Ind+Prt:		KALMMED-PRT ;
+V+Imp:			KALMMED-IMP ;
+V:%^VOWL%^CShort%>		VSUF-I-POTKOND ; !WG
+V+Pot+Sg3:%^VOWL%^CShort%^VOWRaise%^PAL%>ež	FINAL1 ; !WG
+V+Inf:%^VOWFront%^PAL%>ed FINAL1 ;
+V+ActEss:%>men FINAL1 ;
+V+GerTemp:%^VOWL%^CShort%>een FINAL1 ;
+V+GerInstr:%^VOWL%^CShort%>eeˊl FINAL1 ;
+V+Action:%>em FINAL1 ;
+V+PrsPrc:%^PAL%>i FINAL1 ;
!+V+PrfPrc:%>ääm FINAL1 ;
!+V+Pass+PrfPrc:%>ummu FINAL1 ;
!+V+Imp+ConNeg:%>ukku FINAL1 ;
+V+VAbess:%^VOWL%^CShort%>kâni FINAL1 ;			!WG
+V+VAbess:%^VOWL%^CShort%>kani FINAL1 ;		!WG
+V+VAbess:%^VOWL%^CShort%>ǩeânai FINAL1 ;		!WG

LEXICON JURDDED
!! juˊrdded:ju%{%ˊ%}rdd
!! In the lexicon ''@LEXNAME@'' the verb __kâˊlmmed__ has the lexc stem
!! __kâlmm__. It raises in +Ind+Prt+Pl3, Pot, and has lengthening.

+V+Ind+Prs:             JURDDED-PRS ;
+V+Ind+Prt:             JURDDED-PRT ;
+V+Imp:                 JURDDED-IMP ;
+V:%^VOWL%^CShort%^PAL%>       POTENTIAL ; !
+V:%^VOWL%^CShort%^VOWLower%>    CONDITIONAL ; !
+V+Pot+Sg3:%^VOWL%^CShort%^VOWRaise%^PAL%>ež    FINAL1 ; !WG
+V+Inf:%^VOWFront%^PAL%>ed FINAL1 ;
+V+Action:%>em FINAL1 ;
+V+ActEss:%^PAL%>men FINAL1 ;
+V+GerTemp:%^VOWL%^CShort%^PAL%>een FINAL1 ;
+V+GerInstr:%^VOWL%^CShort%^PAL%>eeˊl FINAL1 ;
+V+Action:%^PAL%>em FINAL1 ;
+V+PrsPrc:%^PAL%>i FINAL1 ;
+V+PrfPrc:%^VOWLower%>am FINAL1 ;
+V+Pass+PrfPrc:%>um FINAL1 ;
+V+Imp+ConNeg:%>u FINAL1 ;
+V+VAbess:%^VOWL%^CShort%^PAL%>kâni FINAL1 ;                 !WG
+V+VAbess:%^VOWL%^CShort%^PAL%>kani FINAL1 ;         !WG
+V+VAbess:%^VOWL%^CShort%^PAL%>ǩeânai FINAL1 ;               !WG


LEXICON TIETTED
!! In the lexicon ''@LEXNAME@'' the verb __tieˊtted__ has the form __tiõtt__
+V+Ind+Prs:		TIETTED-PRS ;
+V+Ind+Prt:		TIETTED-PRT ;
+V+Imp:			TIETTED-IMP ;
+V:%^VOWL%^CShort%^PAL%>	POTENTIAL ; !WG PAL for vuõˊljžem
+V:%^VOWL%^CShort%^VOWFront%^PAL%>	POTENTIAL ; !WG PAL for vueˊljžem
+V:%^VOWL%^CShort%^VOWLower%>	CONDITIONAL ; !WG vowel lowering to cope with
! vuâlǥas this would mean there is a form teâđas 2012-11-27
+V+Pot+Sg3:%^VOWL%^CShort%^VOWFront%^PAL%>ež	FINAL1 ; !WG vueˊljež
+V+Pot+Sg3:%^VOWL%^CShort%^PAL%>ež	FINAL1 ; !WG vuõˊljež
+V+Inf:%^VOWFront%^PAL%>ed FINAL1 ;
+V+ActEss:%^VOWFront%^PAL%>men FINAL1 ;
+V+GerTemp:%^VOWL%^CShort%^VOWFront%^PAL%>een FINAL1 ;
+V+GerInstr:%^VOWL%^CShort%^VOWFront%^PAL%>eeˊl FINAL1 ;
+V+Action:%^VOWFront%^PAL%>em FINAL1 ;
+V+PrsPrc:%^PAL%>i FINAL1 ; !vuõˊlǧǧi
+V+PrsPrc:%^VOWFront%^PAL%>i FINAL1 ; !vueˊlǧǧi
+V+PrfPrc:%^VOWLower%>am FINAL1 ;
+V+Pass+PrfPrc:%>um FINAL1 ;
+V+Imp+ConNeg:%>u FINAL1 ;
!+V+VAbess:%^VOWL%^CShort%>kâni FINAL1 ;			!WG
+V+VAbess:%^VOWL%^CShort%^VOWFront%^PAL%>kani FINAL1 ;		!WG
+V+VAbess:%^VOWL%^CShort%^VOWFront%^PAL%>ǩeännai FINAL1 ;		!WG

!! !!Odd-syllable stems

!! Not yet written

!! !!Contracted stems


LEXICON SILTTEED
!! lexicon ''@LEXNAME@'' 
+V+Ind+Prs:		TEEMEED-PRS ;
+V+Ind+Prt:		TEEMEED-PRT ;
+V+Imp:			TEEMEED-IMP ;
+V+Imp+Sg2:%>âd		FINAL1 ;
+V:%>eˊ		VSUF-I-POTKOND ; !WG
+V+Pot+Sg3:%>eež	FINAL1 ; !WG
+V+Inf:%>eed FINAL1 ;
+V+ActEss:%>eˊmen FINAL1 ;
+V+GerTemp:%>een FINAL1 ;
+V+GerInstr:%>eeˊl FINAL1 ;
+V+Action:%>eem FINAL1 ;
+V+PrsPrc:%>eei FINAL1 ;
+V+PrfPrc:%>ääm FINAL1 ;
+V+Pass+PrfPrc:%>ummu FINAL1 ;
+V+Pass+PrfPrc:%>uum FINAL1 ;
+V+VAbess:%>eǩâni FINAL1 ;		!WG
+V+Imp+ConNeg:%>ukku FINAL1 ;

LEXICON SOLLEED
!! lexicon ''@LEXNAME@'' 
+V+Ind+Prs:		TEEMEED-PRS ;
+V+Ind+Prt:		TEEMEED-PRT ;
+V+Imp:			TEEMEED-IMP ;
+V+Imp+Sg2:%>ad		FINAL1 ;
+V:%>eˊ		VSUF-I-POTKOND ; !WG
+V+Pot+Sg3:%>eež	FINAL1 ; !WG
+V+Inf:%>eed FINAL1 ;
+V+ActEss:%>eˊmen FINAL1 ;
+V+GerTemp:%>een FINAL1 ;
+V+GerInstr:%>eeˊl FINAL1 ;
+V+Action:%>eem FINAL1 ;
+V+PrsPrc:%>eei FINAL1 ;
+V+PrfPrc:%>ääm FINAL1 ;
+V+Pass+PrfPrc:%>ummu FINAL1 ;
+V+Pass+PrfPrc:%>uum FINAL1 ;
+V+VAbess:%>eǩâni FINAL1 ;		!WG
+V+Imp+ConNeg:%>ukku FINAL1 ;

LEXICON TEEMEED
!! lexicon ''@LEXNAME@'' 
+V+Ind+Prs:		TEEMEED-PRS ;
+V+Ind+Prt:		TEEMEED-PRT ;
+V+Imp:			TEEMEED-IMP ;
+V+Imp+Sg2:%>ed		FINAL1 ;
+V:%>eˊ		VSUF-I-POTKOND ; !WG
+V+Pot+Sg3:%>eež	FINAL1 ; !WG
+V+Inf:%>eed FINAL1 ;
+V+ActEss:%>eˊmen FINAL1 ;
+V+GerTemp:%>een FINAL1 ;
+V+GerInstr:%>eeˊl FINAL1 ;
+V+Action:%>eem FINAL1 ;
+V+PrsPrc:%>eei FINAL1 ;
+V+PrfPrc:%>ääm FINAL1 ;
+V+Pass+PrfPrc:%>ummu FINAL1 ;
+V+Pass+PrfPrc:%>uum FINAL1 ;
+V+VAbess:%>eǩâni FINAL1 ;		!WG
+V+Imp+ConNeg:%>ukku FINAL1 ;



LEXICON VUEJTED
!! lexicon ''@LEXNAME@'' 
 KAMRDED ; ! The distinction is taken care of in twol and stem formulation


LEXICON MAINSTED
!! lexicon ''@LEXNAME@'' 
+V+Ind+Prs:		MAINSTED-PRS ;
+V+Ind+Prt:		MAINSTED-PRT ;
+V+Imp:			MAINSTED-IMP ;
+V+Ind+Prs:%^VOWL	MAINSTED-PRS ;
+V+Ind+Prt:%^VOWL	MAINSTED-PRT ;
+V+Imp:%^VOWL		MAINSTED-IMP ;
+V+Imp+Sg2:%^IMPSG2	FINAL1 ;
+V+Imp+ConNeg:%>uku FINAL1 ;
+V:%>eˊ		VSUF-I-POTKOND ; !WG
+V+Pot+Sg3:%>ež	FINAL1 ; !WG
+V+Inf:%>ed FINAL1 ;
+V+ActEss:%>eˊmen FINAL1 ;
+V+Action:%>am FINAL1 ;
+V+PrsPrc:%>eei FINAL1 ;
+V+PrfPrc:%>am FINAL1 ;
+V+Pass+PrfPrc:%>ummu FINAL1 ;
+V+Pass+PrfPrc:%>uum FINAL1 ;
+V+VAbess:%>eǩâni FINAL1 ;
+V+GerTemp:%>een FINAL1 ;
+V+GerInstr:%>eeˊl FINAL1 ;
+V+Imp+ConNeg:%^VOWL%>uku FINAL1 ;
+V:%^VOWL%>eˊ	VSUF-I-POTKOND ; !WG
+V+Pot+Sg3:%^VOWL%>eˊž	FINAL1 ; !WG
+V+Inf:%^VOWL%>ed FINAL1 ;
+V+ActEss:%^VOWL%>eˊmen FINAL1 ;
+V+Action:%^VOWL%>am FINAL1 ;
+V+PrsPrc:%^VOWL%>eei FINAL1 ;
+V+PrfPrc:%^VOWL%>am FINAL1 ;
+V+Pass+PrfPrc:%^VOWL%>ummu FINAL1 ;
+V+Pass+PrfPrc:%^VOWL%>uum FINAL1 ;
+V+VAbess:%^VOWL%>eǩâni FINAL1 ;
+V+GerTemp:%^VOWL%>een FINAL1 ;
+V+GerInstr:%^VOWL%>eeˊl FINAL1 ;


LEXICON KAMRDED
!! The lexicon ''@LEXNAME@'' is for verbs like  __kåˊmrded__

 KAMRDED-01 ;
+V+Imp+Sg2:%^IMPSG2	FINAL1 ;
+V+Imp+ConNeg+Sg2:%>uku FINAL1 ;

LEXICON KAMRDED-01

+V+Ind+Prs:		KAMRDED-PRS ;
+V+Ind+Prt:		KAMRDED-PRT ;
+V+Imp:			KAMRDED-IMP ;
+V+Imp+ConNeg:%>uku FINAL1 ;
+V:%>eˊ		VSUF-I-POTKOND ; !WG
+V+Pot+Sg3:%>ež	FINAL1 ; !WG
+V+Inf:%>ed FINAL1 ;
+V+ActEss:%>eˊmen FINAL1 ;
+V+Action:%>am FINAL1 ;
+V+PrsPrc:%>eei FINAL1 ;
+V+PrfPrc:%>am FINAL1 ;
+V+PrfPrc:%>âmma FINAL1 ;
+V+Pass+PrfPrc:%>ummu FINAL1 ;
+V+Pass+PrfPrc:%>um FINAL1 ; !
+V+Pass+PrfPrc:%>uum FINAL1 ; !
+V+VAbess:%>eǩâni FINAL1 ;
+V+VAbess:%>eǩeânai FINAL1 ;
+V+GerTemp:%>een FINAL1 ;
+V+GerInstr:%>eeˊl FINAL1 ;

LEXICON SEDGGJED
!! sedggjed:
:QA1i KAMRDED-01 ;
+V+Imp+Sg2:%^VOWRaise%>u	FINAL1 ;
+V+Imp+ConNeg+Sg2:%^VOWRaise%>u FINAL1 ;

LEXICON VOLLJED
!! võˊll’jed:võˊll
:QA1i KAMRDED-01 ;
+V+Imp+Sg2:%>e        FINAL1 ;
+V+Imp+ConNeg+Sg2:%>e FINAL1 ;




! Intermediate suffix lexica
! --------------------------


! Final suffix lexica
! -------------------

! Suffix series I
! - - - - - - - -

! assuming stem __viǯǯ__
LEXICON VIQQAD-Prs
+Sg1:%^VOWL%^CShort%>am FINAL1 ; ! _viǯǯam_
+Sg2:%^VOWL%^CShort%>ak FINAL1 ; ! _viǯǯak_
!+Sg3:^P3 FINAL1 ; ! _veǯǯ_
+Sg3:%^VOWLower FINAL1 ; ! _veǯǯ_
+Pl1:%>âp FINAL1 ; ! _viǯǯâp_
+Pl2:%>veˊted FINAL1 ; ! _viǯǯveˊted_
+Pl3:%^VOWSH%^CLong%^VOWLower%^VOWFront%^PAL%>e FINAL1 ; ! _veˊǯǯe_
+4:%^VOWL%^CShort%>ât FINAL1 ;
+ConNeg:%^VOWL%^CShort FINAL1 ;

LEXICON VIQQAD-Prt
+Sg1:%^VOWSH%^CLong%^PAL%>em FINAL1 ; ! _kaˊǧǧem_
+Sg2:%^VOWSH%^CLong%^PAL%>iǩ FINAL1 ; ! _kaˊǧǧiǩ_
+Sg3:%^VOWL%^CShort%>i FINAL1 ; ! _kaaggi_
+Pl1:%^VOWL%^CShort%>im FINAL1 ; ! _kaaggim_
+Pl1:%^VOWL%^CShort%>in FINAL1 ; ! _kaaggin_ is this sub?
+Pl2:%^VOWL%^CShort%>id FINAL1 ; ! _kaaggid_
+Pl3:%^VOWSH%^CLong%^PAL%>e FINAL1 ; ! _viˊǯǯe_ _kuõˊsǩǩe_ _ka'ǧǧe_
+4:%^VOWSH%^CLong%^PAL%>eš FINAL1 ;

LEXICON VIQQAD-Imp
+Sg2:%^VOWL%^CShort FINAL1 ; ! _kaagg_
+Sg3:%^VOWL%^CShort%^VOWLower%>as FINAL1 ;
+Pl1:%>âp FINAL1 ;
+Pl2:%>âd FINAL1 ;
+Pl3:%^VOWSH%^CLong%^VOWLower%>az FINAL1 ;
+ConNeg:%^VOWSH%^CLong%>u FINAL1 ;


! Present tense
! - - - - - - - -

! assuming stem _teevv_
LEXICON TEEVVAD-PRS
 +Sg1:%^VOWL%^CShort%>am		FINAL1	; !WG !_teevam_ _lääulam_
 +Sg2:%^VOWL%^CShort%>ak			FINAL1	; !WG _teevak_ _lääulak_
 +Sg3:			FINAL1	; !_teevv_ _läull_
 +Pl1:%>ap			FINAL1	; !_teevvap_ _läullap_
 +Pl2:%>veˊted		FINAL1	; !_teevvve'ted_ _läullve'ted_
 +Pl3:%^VOWSH%^CLong%>a		FINAL1	; !_tevva_ _läulla_
 +4:%^VOWL%^CShort%>at FINAL1  ; !WG

! assuming stem _kâlmm_
LEXICON KALMMED-PRS
 +Sg1:%^VOWL%^CShort%^VOWLower%>am		FINAL1	; !WG _kâlmam_ 
 +Sg2:%^VOWL%^CShort%^VOWLower%>ak		FINAL1	; !WG ! _kâlmak_
 +Sg3: FINAL1	; ! _kâlmm_
 +Pl1:%^VOWFront%^PAL%>ep		FINAL1	; ! _äˊlǧǧep_ _tieˊttep_
 +Pl2:%^VOWFront%^PAL%>veˊted		FINAL1	; ! _tieˊttveˊted_
 +Pl3:%^VOWSH%^CLong%^VOWLower%^PAL%>e		FINAL1	; ! _teäˊtte_
 +Pl3:%^VOWSH%^CLong%^VOWLower%^VOWFront%^PAL%>e		FINAL1	; ! _teäˊtte_
 +4:%^VOWL%^CShort%^VOWFront%^PAL%>et FINAL1 ; ! vueˊljet
 +ConNeg:%^VOWL%^CShort%^VOWFront%^PAL FINAL1 ;

! assuming stem _jurdd_
LEXICON JURDDED-PRS
 +Sg1:%^VOWL%^CShort%^VOWLower%>am		FINAL1	; !WG _joordam_ 
 +Sg2:%^VOWL%^CShort%^VOWLower%>ak		FINAL1	; !WG ! _joordak_
 +Sg3:%^VOWLower FINAL1	; ! _jordd_
 +Pl1:%^PAL%>ep		FINAL1	; ! _äˊlǧǧep_ _tieˊttep_
 +Pl2:%^PAL%>veˊted		FINAL1	; ! _tieˊttveˊted_
 +Pl3:%^VOWSH%^CLong%^VOWLower%^PAL%>e		FINAL1	; ! _joˊrdde_
 +Pl3:%^VOWSH%^CLong%^VOWLower%^VOWFront%^PAL%>e		FINAL1	; ! _teäˊtte_
 +4:%^VOWL%^CShort%^PAL%>et FINAL1 ; ! juˊrddet
 +ConNeg:%^VOWL%^CShort%^VOWFront%^PAL FINAL1 ;

! assuming stem _tieˊtt_
LEXICON TIETTED-PRS
 +Sg1:%^VOWL%^CShort%^VOWLower%>am		FINAL1	; !WG _teâđam_ 
 +Sg2:%^VOWL%^CShort%^VOWLower%>ak		FINAL1	; !WG ! _teâđak_
 +Sg3:%^VOWLower FINAL1	; ! _teâtt_
 +Pl1:%^VOWFront%^PAL%>ep		FINAL1	; ! _äˊlǧǧep_ _tieˊttep_
 +Pl2:%^VOWFront%^PAL%>veˊted		FINAL1	; ! _tieˊttveˊted_
 +Pl3:%^VOWLower%^PAL%>e		FINAL1	; ! _teâˊtte_ vuâˊlǧǧe
 +Pl3:%^VOWSH%^CLong%^VOWLower%^VOWFront%^PAL%>e		FINAL1	; ! _teäˊtte_ vuäˊlǧǧe
 +Pl3:%^VOWSH%^CLong%^VOWLower%^VOWFront%^PAL%>a		FINAL1	; !  vuäˊlǧǧa
 +Pl3:%^VOWFront%^PAL%>e                FINAL1  ; !vueˊlǧǧe
 +4:%^VOWL%^CShort%^VOWFront%^PAL%>et FINAL1 ; ! vueˊljet
 +ConNeg:%^VOWL%^CShort%^VOWFront%^PAL FINAL1 ;

! assuming stem _ääˊv_ _teeˊm_
LEXICON TEEMEED-PRS
 +Sg1:%>ääm		FINAL1	; !WG _ääˊvääm_ 
 +Sg2:%>ääk		FINAL1	; !WG ! _ääˊvääk_
 +Sg3:%>ad FINAL1	; ! _ääˊvad_
 +Pl1:%>eep		FINAL1	; ! _ääˊveep_ _tieˊttep_
 +Pl2:%>veˊted		FINAL1	; ! _ääˊvveˊted_
 +Pl3:%>ee		FINAL1	; ! _ääˊvee_
 +4:%>eet FINAL1 ; ! ääˊveet
 +ConNeg:%>ed FINAL1 ; ! ääˊved

LEXICON MAINSTED-PRS
 +Sg1:%>am              FINAL1  ; !WG __
 +Sg2:%>ak              FINAL1  ; !WG ! __
 +Sg3:%^INDPRSSG3 FINAL1        ; ! __
 +Pl1:%>ep      FINAL1  ; ! __
 +Pl2:%>eˊped           FINAL1  ; ! __
 +Pl3:%>e               FINAL1  ; ! __
 +4:%>et FINAL1 ; !
 +ConNeg:%^IMPSG2 FINAL1 ; ! kååˊmmerd

! assuming stem _kåˊmQE1rded_
LEXICON KAMRDED-PRS
 +Sg1:%>am		FINAL1	; !WG __ 
 +Sg2:%>ak		FINAL1	; !WG ! __
 +Sg3:%^INDPRSSG3 FINAL1	; ! __
 +Pl1:%>ep	FINAL1	; ! __
 +Pl2:%>eˊped		FINAL1	; ! __
 +Pl3:%>e		FINAL1	; ! __
 +4:%>et FINAL1 ; ! 
 +ConNeg:%^IMPSG2 FINAL1 ; ! kååˊmmerd

LEXICON LEED-PRS
 +Sg1:eäm		FINAL1	; !WG __ 
 +Sg2:eäk		FINAL1	; !WG ! __
 +Sg3:ij		FINAL1	; ! __
 +Pl1:eäˊp	FINAL1	; ! __
 +Pl2:%>eäˊped		FINAL1	; ! __
 +Pl2:%>eˊped		FINAL1	; ! __
 +Pl3:%>ie		FINAL1	; ! __
 +Pl3:%>iâ		FINAL1	; ! __
 +Pl3:%>ee		FINAL1	; ! __
 +4:%>eät FINAL1 ; ! 
 +ConNeg:eäk'ku FINAL1 ; ! 

! Past tense
! - - - - - - - -

LEXICON TEEVVAD-PRT
 +Sg1:%^VOWRaise%>um		FINAL1	; !đđ>dd ! _laullum_
 +Sg2:%^VOWRaise%>uk		FINAL1	; !đđ>dd ! _laulluk_
 +Sg3:%^VOWL%^CShort%>ai			FINAL1	; !WG ! _lääulai_
 +Pl1:%^VOWL%^CShort%>aim			FINAL1	; !WG ! _lääulaim_
 +Pl1:%^VOWL%^CShort%>ain			FINAL1	; !WG ! _lääulain_
 +Pl2:%^VOWL%^CShort%>aid			FINAL1	; !WG ! _lääulaid_
 +Pl3:%^VOWSH%^CLong%^VOWRaise%>u		FINAL1	; ! _laullu_
 +4:%^VOWRaise%>uš FINAL1 ;

LEXICON KALMMED-PRT
 +Sg1:%^PAL%>em		FINAL1	; ! _kâˊlmmem_
 +Sg2:%^PAL%>iǩ		FINAL1	; ! _kâˊlmmiǩ_
 +Sg3:%^VOWL%^CShort%^PAL%>i FINAL1	; !WG ! _tiõˊđi_
 +Pl1:%^VOWL%^CShort%^PAL%>im FINAL1	; !WG ! _tiõˊđim_
 +Pl2:%^VOWL%^CShort%^PAL%>id FINAL1	; !WG ! _tiõˊđid_
 +Pl3:%^VOWSH%^CLong%^VOWRaise%^PAL%>e FINAL1	; ! _kõˊlmme_
 +4:%^VOWL%^CShort%^VOWFront%^PAL%>eš FINAL1 ; ! vueˊlješ

LEXICON JURDDED-PRT
 +Sg1:%^PAL%>em         FINAL1  ; ! _juˊrddem_
 +Sg2:%^PAL%>iǩ         FINAL1  ; ! _juˊrddiǩ_
 +Sg3:%^VOWL%^CShort%^PAL%>i FINAL1     ; !WG ! _juuˊrdi_
 +Pl1:%^VOWL%^CShort%^PAL%>im FINAL1    ; !WG ! _juuˊrdim_
 +Pl1:%^VOWL%^CShort%^PAL%>in FINAL1    ; !WG ! _juuˊrdin_
 +Pl2:%^VOWL%^CShort%^PAL%>id FINAL1    ; !WG ! _tiõˊđid_
 +Pl3:%^VOWSH%^CLong%^VOWRaise%^PAL%>e FINAL1   ; ! _kõˊlmme_
 +4:%^VOWSH%^CLong%^VOWFront%^PAL%>eš FINAL1 ; ! juˊrddeš

LEXICON TIETTED-PRT
 +Sg1:%^PAL%>em		FINAL1	; ! _tiõˊttem_
 +Sg2:%^PAL%>iǩ		FINAL1	; ! _tiõˊttiǩ_
 +Sg3:%^VOWL%^CShort%^PAL%>i		FINAL1	; !WG ! _tiõˊđi_ vuõˊlji
 +Sg3:%^VOWL%^CShort%^VOWFront%^PAL%>i		FINAL1	; !WG ! vueˊlji
 +Pl1:%^VOWL%^CShort%^PAL%>im		FINAL1	; !WG ! _tiõˊđim_
 +Pl1:%^VOWL%^CShort%^PAL%>in		FINAL1	; !WG ! _tiõˊđin_
 +Pl1:%^VOWL%^CShort%^VOWFront%^PAL%>im		FINAL1	; !WG ! _vueˊljim_
 +Pl1:%^VOWL%^CShort%^VOWFront%^PAL%>in		FINAL1	; !WG ! _vueˊljin_
 +Pl2:%^VOWL%^CShort%^PAL%>id		FINAL1	; !WG ! _tiõˊđid_
 +Pl2:%^VOWL%^CShort%^VOWFront%^PAL%>id		FINAL1	; !WG ! _tieˊđid_
 +Pl3:%^VOWSH%^CLong%^PAL%>e		FINAL1	; ! _tiõˊtte_ 
 +Pl3:%^VOWSH%^CLong%^VOWFront%^PAL%>e		FINAL1	; ! _tieˊtte_
 +4:%^PAL%>eš FINAL1 ; ! vuõˊlǧǧeš
 +ConNeg:%^VOWLower%>am FINAL1 ; !

LEXICON TEEMEED-PRT
 +Sg1:%>eem		FINAL1	; ! _ääˊveem_
 +Sg2:%>iiǩ		FINAL1	; ! _ääˊviiǩ_
 +Sg3:%>ii		FINAL1	; !WG ! _ääˊvii_
 +Pl1:%>iim		FINAL1	; !WG ! _ääˊviim_
 +Pl1:%>iin		FINAL1	; !WG ! _ääˊviin_ ! sub??
 +Pl2:%>iid		FINAL1	; !WG ! _ääˊviid_
 +Pl3:%>ee		FINAL1	; ! _ääˊvee_
 +4:%>eeš FINAL1 ; ! ääˊveeš

LEXICON MAINSTED-PRT
 +Sg1:%>em             FINAL1  ; ! kåˊmrdem
 +Sg2:%>iǩ             FINAL1  ; ! kåˊmrdiǩ
 +Sg3:%>i              FINAL1  ; !WG ! kåˊmrdi
 +Pl1:%>im             FINAL1  ; !WG ! kåˊmrdim
 +Pl1:%>in             FINAL1  ; !WG ! kåˊmrdin
 +Pl1:%>iim             FINAL1  ; !WG ! kåˊmrdiim
 +Pl1:%>iin             FINAL1  ; !WG ! kåˊmrdiin
 +Pl2:%>id             FINAL1  ; !WG ! kåˊmrdid
 +Pl2:%>iid             FINAL1  ; !WG ! kåˊmrdiid
 +Pl3:%>e              FINAL1  ; ! kåˊmrde
 +4:%>eš FINAL1 ; ! kåˊmrdeš


! assuming stem _kåˊmQE1rd_
LEXICON KAMRDED-PRT
 +Sg1:%>em             FINAL1  ; ! kåˊmrdem
 +Sg2:%>iǩ             FINAL1  ; ! kåˊmrdiǩ
 +Sg3:%>i              FINAL1  ; !WG ! kåˊmrdi
 +Pl1:%>im             FINAL1  ; !WG ! kåˊmrdim
 +Pl1:%>in             FINAL1  ; !WG ! kåˊmrdin
 +Pl1:%>iim             FINAL1  ; !WG ! kåˊmrdiim
 +Pl1:%>iin             FINAL1  ; !WG ! kåˊmrdiin
 +Pl2:%>id             FINAL1  ; !WG ! kåˊmrdid
 +Pl2:%>iid             FINAL1  ; !WG ! kåˊmrdiid
 +Pl3:%>e              FINAL1  ; ! kåˊmrde
 +4:%>eš FINAL1 ; ! kåˊmrdeš

LEXICON LEED-PRT
 +Sg1:%>eˊjjem             FINAL1  ; !
 +Sg2:%>eˊjjiǩ             FINAL1  ; !
 +Sg3:%>eäi              FINAL1  ; !
 +Sg3:%>eei              FINAL1  ; !
 +Sg3:%>äi              FINAL1  ; !
 +Pl1:%>eeim             FINAL1  ; !
 +Pl2:%>eeid             FINAL1  ; !
 +Pl3:%>eˊjje              FINAL1  ; !
 +4:%>eˊjješ FINAL1 ; ! 
 +ConNeg:eäm'maž FINAL1 ; !
 +ConNeg:eäm'ma FINAL1 ; !
 +ConNeg:eäm FINAL1 ; !


! Imperative
! - - - - - - -
! teevvad
LEXICON TEEVVAD-IMP
 +Sg2:%^VOWL%^CShort FINAL1	; !WG
 +Sg3:%^VOWL%^CShort%>as			FINAL1	; !WG
 +Pl1:%>ap			FINAL1	;
 +Pl2:%>ad			FINAL1	;
 +Pl3:%^VOWSH%^CLong%>az		FINAL1	;
 +ConNeg:%^VOWRaise%>u FINAL1  ;
! 
LEXICON KALMMED-IMP
 +Sg2:%^VOWL%^CShort%^PAL FINAL1	; !WG
 +Sg3:%^VOWL%^CShort%>as		FINAL1	; !WG
 +Pl1:%>ep			FINAL1	;
 +Pl2:%>ed			FINAL1	;
 +Pl3:%^1VOWaz		FINAL1	;

!
LEXICON JURDDED-IMP
 +Sg2:%^VOWL%^CShort%^PAL FINAL1        ; !WG
 +Sg3:%^VOWL%^CShort%^VOWLower%>as                FINAL1  ; !WG
 +Pl1:%^PAL%>ep                      FINAL1  ;
 +Pl2:%^PAL%>ed                      FINAL1  ;
 +Pl3:%^VOWLower%>az          FINAL1  ;


! tieˊtted
LEXICON TIETTED-IMP
 +Sg2:%^VOWL%^CShort%^VOWFront%^PAL FINAL1	; !WG
 +Sg3:%^VOWL%^CShort%^VOWLower%>as		FINAL1	; !WG vuâlǥas
 +Pl1:%^VOWFront%^PAL%>ep 		FINAL1	;
 +Pl2:%^VOWFront%^PAL%>ed 		FINAL1	;
 +Pl3:%^VOWL%^FRICG%^CShort%^VOWLower%>az		FINAL1	; !vuâlǥǥaz

! teeˊmeed
LEXICON TEEMEED-IMP
 +Sg3:%>ââǥǥas		FINAL1	; !WG
 +Pl1:%>âkap			FINAL1	;
 +Pl2:%>eˊǩed		FINAL1	;
 +Pl3:%>âkaz		FINAL1	;

LEXICON MAINSTED-IMP
 +Sg3:%>ââǥǥas          FINAL1  ; !WG
 +Pl1:%>âkap                    FINAL1  ;
 +Pl2:%>eˊǩed           FINAL1  ;
 +Pl3:%>âkaz            FINAL1  ;


LEXICON KAMRDED-IMP
 +Sg3:%>ââǥǥas          FINAL1  ; !WG
 +Pl1:%>âkap                    FINAL1  ;
 +Pl2:%>eˊǩed           FINAL1  ;
 +Pl3:%>âkaz            FINAL1  ;

LEXICON LEED-IMP
 +Sg3:%>eäǥǥas          FINAL1  ; !WG
 +Pl1:%>eäk'kap                    FINAL1  ;
 +Pl2:%>eäk'ku           FINAL1  ;
 +Pl2:%>eäk'kveˊted           FINAL1  ;
 +Pl3:%>eäk'kaz            FINAL1  ;



! Potentialis & Konditionalis
! not including +Pot+Sg3
! - - - - - - - - - - - - - -

LEXICON VSUF-I-POTKOND
 POTENTIAL ;
 CONDITIONAL ;

LEXICON POTENTIAL
 +Pot+Sg1:žem FINAL1	;
 +Pot+Sg2:žiǩ FINAL1	;
 +Pot+Pl1:žep FINAL1	; ! How are these related?
 +Pot+Pl1:žim FINAL1	; ! Are there sub standards?
 +Pot+Pl1:žin FINAL1	; !
 +Pot+Pl2:žid FINAL1	;
 +Pot+Pl3:že FINAL1	;
 +Pot+4:žet FINAL1 ;
 +Pot+ConNeg:že FINAL1 ;

LEXICON CONDITIONAL
 +Cond+Sg1:čem FINAL1	;
 +Cond+Sg2:čiǩ FINAL1	;
 +Cond+Sg3:či FINAL1	;
 +Cond+Pl1:čim FINAL1	; ! How are these related?
 +Cond+Pl1:čep FINAL1	; ! Are there substandards? 
 +Cond+Pl1:čin FINAL1	; !
 +Cond+Pl2:čid FINAL1	;
 +Cond+Pl3:če FINAL1	;
 +Cond+4:češ FINAL1 ;
 +Cond+ConNeg:če FINAL1 ;


!! !!Suffix series II

!! Not written

! Other series
! - - - - - -


