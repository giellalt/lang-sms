
!! !!!Skolt Saami verb morphology

!! First a lexicon V_ for still unclassified entries.

LEXICON V_
+V+WORK: # ;

LEXICON V-NEG_
+V+Neg+WORK: # ;

!! !!Irregular verbs

!! Then irregular verbs ij and the copula.

LEXICON V-NEG_IJ  !!= * @CODE@ ij:
!! The lexicon ''@LEXNAME@''

+V+Neg+Ind+Sg1:jiõˈm	   K ; !!= * @CODE@
+Use/NG+V+Neg+Ind+Sg1:jim	   K ; !!= * @CODE@
+V+Neg+Ind+Sg2:jiõˈk	   K ; !!= * @CODE@
+Use/NG+V+Neg+Ind+Sg2:jik	   K ;
+V+Neg+Ind+Sg3:ij		   K ;
+V+Neg+Ind+Sg4:jeäˈʹt	   K ;
+V+Neg+Ind+Pl1:jeäˈʹp	   K ;
+Use/NG+V+Neg+Ind+Pl1:jeʹp	   K ;
+V+Neg+Ind+Pl2:jeäˈʹped   K ;
+Use/NG+V+Neg+Ind+Pl2:jeʹped	   K ;
+V+Neg+Ind+Pl3:jiâ	   K ;
+Use/NG+V+Neg+Ind+Pl3:jeä	   K ;
+V+Neg+Ind+Pl3:jie	   K ;
+Use/NG+V+Neg+Ind+Pl3:jee	   K ;

+V+Neg+Imprt+Sg2:jieˈʹl	   K ;
+Use/NG+V+Neg+Imprt+Sg2:jeäˈʹl	   K ;
+Use/NG+V+Neg+Imprt+Sg2:jeˈʹl	   K ;
+V+Neg+Imprt+Pl2:jieʹlˈled  K ;	
+Use/NG+V+Neg+Imprt+Pl2:jeʹlˈled   K ;	

+V+Neg+Opt+Sg2:jieˈʹl	   K ;
+Use/NG+V+Neg+Opt+Sg2:jeˈʹl	   K ;
+V+Neg+Opt+Sg3:jeälas	   K ;
+V+Neg+Opt+Pl1:jeälˈlap   K ;
+V+Neg+Opt+Pl2:jieʹlˈled  K ;
+Use/NG+V+Neg+Opt+Pl2:jeʹlˈled   K ;			
+V+Neg+Opt+Pl3:jeälˈlaz   K ;


! leeʹd
LEXICON LEED
!! The lexicon ''@LEXNAME@'' for the copula
 V_LEEʹD ;
LEXICON V_LEEʹD !!= * @CODE@ leeʹd:l
!! The lexicon ''@LEXNAME@'' for the copula

+V+Ind+Prs:		LEED-PRS ;
+V+Ind+Prt:		LEED-PRT ;
+V+Imprt:			LEED-IMPRT ;
+V+Imprt+Sg2:eäkˈku	K ;
+V+Imprt+ConNeg:%>eäkˈku K ;
+V:%>eʹč		CONDITIONAL ; !
+V+Pot+Sg1:%>eʹžžem K	;
+V+Pot+Sg2:%>eʹžžiǩ K	;
+V+Pot+Sg3:%>eežž	K ; !WG
+V+Pot+Sg3:%>eež	K ; !
+V+Pot+Pl1:%>eʹžžep K	; !
+V+Pot+Pl1:%>eʹžžim K	; !
+V+Pot+Pl1:%>eʹžžin K	; !Does this exist
+V+Pot+Pl2:%>eʹžžveʹted K	;
+V+Pot+Pl3:%>eʹžže K	;
+V+Pot+Sg4:%>eʹžžet K ;
+V+Pot+ConNeg:%>eʹžže K ;
+V+Inf:%>eeʹd K ;
+V+PrsPrc:%>eʹddi K ;
+V+PrfPrc:%>eämˈmaž K ;
+V+PrfPrc:%>eämˈma K ;
+V+PrfPrc:%>eäm K ;

!! !!REGULAR VERBS
!! !CLASS 1 HIGH VOWEL, NO PALATALIZATION

LEXICON V_PÕÕLLÂD !!= * @CODE@ põõllâd:põõll
! pos: V
! 1. VERBS WITH SINGLE-SYLLABLE Ind.Prs.Sg3 (2009: 354)
! inf: âd
! foot: 2syll
! 1.1 Inf in âd (vowel shift)
! diphthong: no
! vowel_shift: yes
! consonant_type: geminate-single
! gradation: yes3
! weak_grade: single
! strong_grade: geminate

!   Raised: Inf, Ind.Prt.Pl3, Imprt.Sg2, Pot.Sg3
!   Lowered: Ind.Prs.Sg3, Ind.Prs.Pl3, Imprt.Sg3
! palatal_coloring: no (%^PALÄ, %^PALE, %^PALẸ)
! palatalization: yes (%^PAL, %^PALNo)
! velar_palatalization: FIX no (pååttaǩ)
! 1.1.1 Has Palatalization
! 1.1.1[1] (Palatalization pattern)
!   Palatalized: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Not Palatalized: Inf, Ind.Prs.Sg3, Imprt.Sg2, Imprt.Sg3, Pot.Sg3
! pedagogical_gradation: no
! orthographic_gradation: yes
! 1.1.1[1].1 Lacks Specifically Pedagogical Gradation
! 1.1.1[1].1.1 Has Orthographic Gradation
! 1.1.1[1].1.1[231] (Monophthong + Consonant and Consonant Geminate alternation)
!   Strong grade: Inf, Ind.Prs.Sg3  = põõllâd
!   Extra strong grade: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Weak grade: Imprt.Sg2, Pot.Sg3, Imprt.Sg3
! FORMS
! (1)  Inf: põõllâd
! (2)  Ind.Prs.Sg3: pââll
! (3)  Ind.Prs.Pl3: pâʹlle
! (4)  Ind.Prt.Pl3: põʹlle
! (5)  Ind.Imprt.Sg2: põõl
! (6)  Ind.Pot.Sg3: põõlâž
! (7)  Ind.Imprt.Sg3: pââlas
! (8)  Imprt.13.ConNeg: põll
! (9)  Ind.Imprt.Pl3: pâllaz
! (10) Allegro for incoative: põl
! * Yaml: __V-moonnad__
! põõllâd:põõll
! Strong grade in penultimate syllable
+V: VSUF-INF_ÂD ; !1 põõllâd
!+V:%^VOWLower VSUF-IND-PRS-SG3_ÂD ; !2
+V+Imprt+Pl3:%^VV2V%^VOWLower%>az K ; ! pâllaz
+V+Ind+Prs+Sg3:%^VOWLower K ; ! pââll
+V+Ind+Prs+Pl3:%^VV2V%^VOWLower%^PAL%>e K ; !3 pâʹlle
+V:%^VV2V%^PAL VSUF-IND-PRT-PL3_ÂD ; !4 põʹlle
+V:%^CC2C VSUF-I-POTKOND_ÂD ; !5 põõl-
+V+Pot+Sg3:%^CC2C%>âž   K ; !6 põõl-
+V+Imprt+Sg3:%^VOWLower%^CC2C%>as K ; !7 pââlas
+V:%^VV2V VSUF-IMPRT-CONNEGII_ÂD ; !8 põll-
+V+Imprt+ConNegII:%^VV2V%>u K ; ! põllu
+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:%^VV2V%^CC2C»škuätt V_PUEʹTTED ;
+V+Der/ted:%^CC2C»ât  V_AALǤTED ;

LEXICON V_LAUKKÕÕLLÂD !!= * @CODE@ laukkõõllâd:laukkõõll
! pos: V
! 1. VERBS WITH SINGLE-SYLLABLE Ind.Prs.Sg3 (2009: 354)
! inf: âd
! foot: 2syll
! 1.1 Inf in âd (vowel shift)
! diphthong: no
! vowel_shift: yes
! consonant_type: geminate-single
! gradation: yes3
! weak_grade: single
! strong_grade: geminate

!   Raised: Inf, Ind.Prt.Pl3, Imprt.Sg2, Pot.Sg3
!   Lowered: Ind.Prs.Sg3, Ind.Prs.Pl3, Imprt.Sg3
! palatal_coloring: no (%^PALÄ, %^PALE, %^PALẸ)
! palatalization: yes (%^PAL, %^PALNo)
! velar_palatalization: FIX no (pååttaǩ)
! 1.1.1 Has Palatalization
! 1.1.1[1] (Palatalization pattern)
!   Palatalized: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Not Palatalized: Inf, Ind.Prs.Sg3, Imprt.Sg2, Imprt.Sg3, Pot.Sg3
! pedagogical_gradation: no
! orthographic_gradation: yes
! 1.1.1[1].1 Lacks Specifically Pedagogical Gradation
! 1.1.1[1].1.1 Has Orthographic Gradation
! 1.1.1[1].1.1[231] (Monophthong + Consonant and Consonant Geminate alternation)
!   Strong grade: Inf, Ind.Prs.Sg3  = laukkõõllâd
!   Extra strong grade: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Weak grade: Imprt.Sg2, Pot.Sg3, Imprt.Sg3
! FORMS
! (1)  Inf: laukkõõllâd
! (2)  Ind.Prs.Sg3: pââll
! (3)  Ind.Prs.Pl3: pâʹlle
! (4)  Ind.Prt.Pl3: põʹlle
! (5)  Ind.Imprt.Sg2: põõl
! (6)  Ind.Pot.Sg3: põõlâž
! (7)  Ind.Imprt.Sg3: pââlas
! (8)  Imprt.ConNegII: põll
! (9)  Ind.Imprt.Pl3: pâllaz
! (10) Allegro for incoative: põl
! * Yaml: __V-moonnad__
! laukkõõllâd:laukkõõll
! Strong grade in penultimate syllable
+V: VSUF-INF_ÂD ; !!= * @CODE@1 laukkõõllâd
!+V:%^VOWLower VSUF-IND-PRS-SG3_ÂD ; !2
+V+Imprt+Pl3:%^VV2V%^VOWLower%>az K ; !!= * @CODE@ pâllaz
+V+Ind+Prs+Sg3:%^VOWLower K ; !!= * @CODE@ pââll
+V+Ind+Prs+Pl3:%^VV2V%^VOWLower%^PAL%>e K ; !!= * @CODE@3 pâʹlle
+V:%^VV2V%^PAL VSUF-IND-PRT-PL3_ÂD ; !!= * @CODE@4 põʹlle
+V:%^CC2C VSUF-I-POTKOND_ÂD ; !!= * @CODE@5 põõl-
+V+Pot+Sg3:%^CC2C%>âž   K ; !!= * @CODE@6 põõl-
+V+Imprt+Sg3:%^CC2C%>as K ; !!= * @CODE@7 põõlas
+V:%^VV2V VSUF-IMPRT-CONNEGII_ÂD ; !!= * @CODE@8 põll-
+V+Imprt+ConNegII:%^VV2V%>u K ; !!= * @CODE@ põllu
+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:%^VV2V%^CC2C»škuätt V_PUEʹTTED ;
+V+Der/ted:%^CC2C»ât  V_AALǤTED ;

LEXICON V_VIIKKÂD !!= * @CODE@ viikkâd:viikk
! pos: V
! gradation: yes
! weak geminate
! 1. VERBS WITH SINGLE-SYLLABLE Ind.Prs.Sg3 (2009: 354)
! foot: 2syll
! inf: âd
! 1.1 Inf in âd (vowel shift)
! vowel_shift: yes
! diphthong: no
! consonant_type: single::geminate
!   Raised: Inf, Ind.Prt.Pl3, Imprt.Sg2, Pot.Sg3
!   Lowered: Ind.Prs.Sg3, Ind.Prs.Pl3, Imprt.Sg3
! palatal_coloring: no (%^PALÄ, %^PALE, %^PALẸ)
! palatalization: yes (%^PAL, %^PALNo)
! velar_palatalization: FIX no (pååttaǩ)
! 1.1.1 Has Palatalization
! 1.1.1[1] (Palatalization pattern)
!   Palatalized: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Not Palatalized: Inf, Ind.Prs.Sg3, Imprt.Sg2, Imprt.Sg3, Pot.Sg3
! pedagogical_gradation: no
! orthographic_gradation: yes
! 1.1.1[1].1 Lacks Specifically Pedagogical Gradation
! 1.1.1[1].1.1 Has Orthographic Gradation
! 1.1.1[1].1.1[231] (Monophthong + Consonant and Consonant Geminate alternation)
!   Strong grade: Inf, Ind.Prs.Sg3  = viikkâd
!   Extra strong grade: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Weak grade: Imprt.Sg2, Pot.Sg3, Imprt.Sg3
! FORMS
! (1)  Inf: viikkâd
! (2)  Ind.Prs.Sg3: veekk
! (3)  Ind.Prs.Pl3: veʹǩǩe
! (4)  Ind.Prt.Pl3: viʹǩǩe
! (5)  Ind.Imprt.Sg2: viiǥǥ
! (6)  Ind.Pot.Sg3: viiǥǥâž
! (7)  Ind.Imprt.Sg3: veeǥǥas
! (8)  Imprt.13.ConNeg: vikk
! (9)  Ind.Imprt.Pl3: vekkaz
! (10) Allegro for incoative: viǥ»
! * Yaml: __V-viikkad__
! viikkâd:viikk
! Strong grade in penultimate syllable
+V: VSUF-INF_ÂD ; !1 viikkâd
!+V:%^VOWLower VSUF-IND-PRS-SG3_ÂD ; !2
+V+Imprt+Pl3:%^VV2V%^VOWLower%>az K ; ! vekkaz
+V+Ind+Prs+Sg3:%^VOWLower K ; ! veekk
+V+Ind+Prs+Pl3:%^VV2V%^VOWLower%^PAL%>e K ; !3 veʹǩǩe
+V:%^VV2V%^PAL VSUF-IND-PRT-PL3_ÂD ; !4 viʹǩǩe
+V:%^KK2GG VSUF-I-POTKOND_ÂD ; !5 viiǥǥ-
+V+Pot+Sg3:%^KK2GG%>âž   K ; !6 viiǥǥ-
+V+Imprt+Sg3:%^VOWLower%^KK2GG%>as K ; !7 veeǥǥas
+V:%^VV2V VSUF-IMPRT-CONNEGII_ÂD ; !8 vikk-
+V+Imprt+ConNegII:%^VV2V%>u K ; ! vikku
+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:%^VV2V%^KK2G»škuätt V_PUEʹTTED ;
+V+Der/ted:%^KK2GG»ât  V_AALǤTED ;

LEXICON V_RIČČÂD !!= * @CODE@ riččâd:ričč
 V_VIǮǮÂD ;
LEXICON V_RÕVVÂD !!= * @CODE@ rõvvâd:rõvv
 V_VIǮǮÂD ;
LEXICON V_VIǮǮÂD !!= * @CODE@ viǯǯâd:viǯǯ
! 1. VERBS WITH SINGLE-SYLLABLE Ind.Prs.Sg3 (2009: 354)
! 1.1 Inf in âd (vowel shift)
!   Raised: Inf, Ind.Prt.Pl3, Imprt.Sg2, Pot.Sg3
!   Lowered: Ind.Prs.Sg3, Ind.Prs.Pl3, Imprt.Sg3
! 1.1.1 Has Palatalization
! 1.1.1[1] (Palatalization pattern)
!   Palatalized: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Not Palatalized: Inf, Ind.Prs.Sg3, Imprt.Sg2, Imprt.Sg3, Pot.Sg3
! 1.1.1[1].1 Lacks Specifically Pedagogical Gradation
! 1.1.1[1].1.1 Has Orthographic Gradation
! 1.1.1[1].1.1[332] (Monophthong + Consonant Geminate)  
!   Extra Strong grade: Inf, Ind.Prs.Sg3
!   Extra strong grade: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Strong grade: Imprt.Sg2, Pot.Sg3, Imprt.Sg3
! FORMS
! (1)  Inf: viǯǯâd
! (2)  Ind.Prs.Sg3: veǯǯ
! (3)  Ind.Prs.Pl3: veʹǯǯe
! (4)  Ind.Prt.Pl3: viʹǯǯe
! (5)  Ind.Imprt.Sg2: viiǯǯ
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6)  Pot.Sg3: viiǯǯâž
! (7)  Imprt.Sg3: veeǯǯ
! (8)  Imprt.13.ConNeg: viǯǯu
! (9)  Ind.Imprt.Pl3: veǯǯaz
! (10) Allegro for incoative: viǯ»
! * Yaml: __V-vizzad__
! viǯǯâd:viǯǯ
! Extra strong grade in penultimate syllable
+V: VSUF-INF_ÂD ; !1
+V:%^VOWLower VSUF-IND-PRS-SG3_ÂD ; !2
+V+Ind+Prs+Pl3:%^VOWLower%^PAL%>e K ; !3
+V:%^PAL VSUF-IND-PRT-PL3_ÂD ; !4
+V:%^V2VV VSUF-I-POTKOND_ÂD ; !5
+V+Pot+Sg3:%^V2VV%>âž   K ; !6
+V+Imprt+Sg3:%^V2VV%^VOWLower%>as K ; !7
+V: VSUF-IMPRT-CONNEGII_ÂD ; !8

+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:%^CC2C»škuätt V_PUEʹTTED ;
+V+Der/ted:%^V2VV»ât  V_AALǤTED ;

LEXICON V_RIÂDˈDÂD !!= * @CODE@ riâddâd:riâdˈd
! gradation: extra::strong
! 1. VERBS WITH SINGLE-SYLLABLE Ind.Prs.Sg3 (2009: 354, 356)
! 1.1 Inf in âd (vowel shift)
!   Raised: Inf, Ind.Prt.Pl3, Imprt.Sg2, Pot.Sg3
!   Lowered: Ind.Prs.Sg3, Ind.Prs.Pl3, Imprt.Sg3
! 1.1.1 Has Palatalization
! 1.1.1[1] (Palatalization pattern)
!   Palatalized: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Not Palatalized: Inf, Ind.Prs.Sg3, Imprt.Sg2, Imprt.Sg3, Pot.Sg3
! 1.1.1[1].2 Has Specifically Pedagogical Gradation
! 1.1.1[1].2.2 Lacks Orthographic Gradation
! 1.1.1[1].2.2[332] (Diphthong + Consonant Geminate)  
!   Extra strong grade: Inf, Ind.Prs.Sg3
!   Extra strong grade: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Strong grade: Imprt.Sg2, Pot.Sg3, Imprt.Sg3
! FORMS
! (1)  Inf: riâdˈdâd
! (2)  Ind.Prs.Sg3: reädˈd
! (3)  Ind.Prs.Pl3: reäʹdˈde
! (4)  Ind.Prt.Pl3: rieʹdˈde
! (5)  Ind.Imprt.Sg2: riâdd
! (6)  Ind.Pot.Sg3: riâddâž
! (7)  Ind.Imprt.Sg3: reäddas
! (8)  Imprt.13.ConNeg: riâdˈdu
! (9)  Ind.Imprt.Pl3: reädˈdaz
! (10) Allegro for incoative: riâd»
! * Yaml: __V-riokkad__
! riâddâd:riâdˈd
! Extra strong grade in penultimate syllable
+V: VSUF-INF_ÂD ; !1 riâddâd
+V:%^VOWLower VSUF-IND-PRS-SG3_ÂD ; !2 reädˈd
+V+Ind+Prs+Pl3:%^VOWLower%^PAL%>e K ; !3 reäʹdˈde
+V:%^PALE%^PAL VSUF-IND-PRT-PL3_ÂD ; !4 rieʹdˈde
+V:%^CCC2CC VSUF-I-POTKOND_ÂD ; !5 riâdd
+V+Pot+Sg3:%^CCC2CC%>âž   K ; !6 riâddâž
+V+Imprt+Sg3:%^VOWLower%^CCC2CC%>as K ; !7 riâddas
+V: VSUF-IMPRT-CONNEGII_ÂD ; !8 riâdˈdu

+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:%^CCC2C»škuätt V_PUEʹTTED ;
+V+Der/ted:%^CCC2CC»ât  V_AALǤTED ;


LEXICON V_RIÕKˈKÂD !!= * @CODE@ riõkkâd:riõkˈk
! gradation: extra::strong
! 1. VERBS WITH SINGLE-SYLLABLE Ind.Prs.Sg3 (2009: 354, 356)
! 1.1 Inf in âd (vowel shift)
!   Raised: Inf, Ind.Prt.Pl3, Imprt.Sg2, Pot.Sg3
!   Lowered: Ind.Prs.Sg3, Ind.Prs.Pl3, Imprt.Sg3
! 1.1.1 Has Palatalization
! 1.1.1[1] (Palatalization pattern)
!   Palatalized: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Not Palatalized: Inf, Ind.Prs.Sg3, Imprt.Sg2, Imprt.Sg3, Pot.Sg3
! 1.1.1[1].2 Has Specifically Pedagogical Gradation
! 1.1.1[1].2.2 Lacks Orthographic Gradation
! 1.1.1[1].2.2[332] (Diphthong + Consonant Geminate)  
!   Extra strong grade: Inf, Ind.Prs.Sg3
!   Extra strong grade: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Strong grade: Imprt.Sg2, Pot.Sg3, Imprt.Sg3
! FORMS
! (1)  Inf: riõkˈkâd
! (2)  Ind.Prs.Sg3: reâkˈk
! (3)  Ind.Prs.Pl3: reäʹǩˈǩe
! (4)  Ind.Prt.Pl3: riõʹǩˈǩe
! (5)  Ind.Imprt.Sg2: riõkk
! (6)  Ind.Pot.Sg3: riõkkâž
! (7)  Ind.Imprt.Sg3: reâkkas
! (8)  Imprt.13.ConNeg: riõkˈku
! (9)  Ind.Imprt.Pl3: reâkˈkaz
! (10) Allegro for incoative: riõk»
! * Yaml: __V-riokkad__
! riõkkâd:riõkˈk
! Extra strong grade in penultimate syllable
+V: VSUF-INF_ÂD ; !1 riõkkâd
+V:%^VOWLower VSUF-IND-PRS-SG3_ÂD ; !2 reâkˈk
+V+Ind+Prs+Pl3:%^VOWLower%^PALÄ%^PAL%>e K ; !3 reäʹǩˈǩe
+V:%^PAL VSUF-IND-PRT-PL3_ÂD ; !4 riõʹǩˈǩe
+V:%^CCC2CC VSUF-I-POTKOND_ÂD ; !5 riõkk
+V+Pot+Sg3:%^CCC2CC%>âž   K ; !6 riõkkâž
+V+Imprt+Sg3:%^VOWLower%^CCC2CC%>as K ; !7 riõkkas
+V: VSUF-IMPRT-CONNEGII_ÂD ; !8 riõkˈku

+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:%^CCC2C»škuätt V_PUEʹTTED ;
+V+Der/ted:%^CCC2CC»ât  V_AALǤTED ;

LEXICON V_PIÕGˈGÂD !!= * @CODE@ piõggâd:piõgˈg
! gradation: extra::strong
! 1. VERBS WITH SINGLE-SYLLABLE Ind.Prs.Sg3 (2009: 354, 356)
! 1.1 Inf in âd (vowel shift)
!   Raised: Inf, Ind.Prt.Pl3, Imprt.Sg2, Pot.Sg3
!   Lowered: Ind.Prs.Sg3, Ind.Prs.Pl3, Imprt.Sg3
! 1.1.1 Has Palatalization
! 1.1.1[1] (Palatalization pattern)
!   Palatalized: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Not Palatalized: Inf, Ind.Prs.Sg3, Imprt.Sg2, Imprt.Sg3, Pot.Sg3
! 1.1.1[1].2 Has Specifically Pedagogical Gradation
! 1.1.1[1].2.2 Lacks Orthographic Gradation
! 1.1.1[1].2.2[332] (Diphthong + Consonant Geminate)  
!   Extra strong grade: Inf, Ind.Prs.Sg3
!   Extra strong grade: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Strong grade: Imprt.Sg2, Pot.Sg3, Imprt.Sg3
! FORMS
! (1)  Inf: piõgˈgâd
! (2)  Ind.Prs.Sg3: peâgˈg
! (3)  Ind.Prs.Pl3: peäʹǧˈǧe
! (4)  Ind.Prt.Pl3: piõʹǧˈǧe
! (5)  Ind.Imprt.Sg2: piõgg
! (6)  Ind.Pot.Sg3: piõggâž
! (7)  Ind.Imprt.Sg3: peâggas
! (8)  Imprt.13.ConNeg: piõgˈgu
! (9)  Ind.Imprt.Pl3: peâgˈgaz
! (10) Allegro for incoative: piõg»
! * Yaml: __V-pioggad__
! piõggâd:piõgˈg
! Extra strong grade in penultimate syllable
+V: VSUF-INF_ÂD ; !1 piõggâd
+V:%^VOWLower VSUF-IND-PRS-SG3_ÂD ; !2 peâgˈg
+V+Ind+Prs+Pl3:%^VOWLower%^PALÄ%^PAL%>e K ; !3 peäʹǧˈǧe
+V:%^PAL VSUF-IND-PRT-PL3_ÂD ; !4 piõʹǧˈǧe
+V:%^CCC2CC VSUF-I-POTKOND_ÂD ; !5 piõgg
+V+Pot+Sg3:%^CCC2CC%>âž   K ; !6 piõggâž
+V+Imprt+Sg3:%^VOWLower%^CCC2CC%>as K ; !7 piõggas
+V: VSUF-IMPRT-CONNEGII_ÂD ; !8 piõgˈgu

+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:%^CCC2C»škuätt V_PUEʹTTED ;
+V+Der/ted:%^CCC2CC»ât  V_AALǤTED ;


LEXICON V_PIÂSSÂD !!= * @CODE@ piâssâd:piâss
! gradation: extra::strong::weak
! weak geminate
! 1. VERBS WITH SINGLE-SYLLABLE Ind.Prs.Sg3 (2009: 354)
! 1.1 Inf in âd (vowel shift)
!   Raised: Inf, Ind.Prt.Pl3, Imprt.Sg2, Pot.Sg3
!   Lowered: Ind.Prs.Sg3, Ind.Prs.Pl3, Imprt.Sg3
! 1.1.1 Has Palatalization
! 1.1.1[1] (Palatalization pattern)
!   Palatalized: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Not Palatalized: Inf, Ind.Prs.Sg3, Imprt.Sg2, Imprt.Sg3, Pot.Sg3
!    vowel_palatalization: â:e
! 1.1.1[1].2 Has Specifically Pedagogical Gradation
! 1.1.1[1].2.1 Has Orthographic Gradation
! 1.1.1[1].2.1[231].2 (Diphthong + Consonant Geminate alternation)
!   Strong grade: Inf, Ind.Prs.Sg3
!   Extra strong grade: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Weak grade: Imprt.Sg2, Pot.Sg3, Imprt.Sg3
! FORMS
! (1)  Inf: piâssâd
! (2)  Ind.Prs.Sg3: peäss
! (3)  Ind.Prs.Pl3: peäʹsˈse
! (4)  Ind.Prt.Pl3: pieʹsˈse (allophonic)
! (5)  Ind.Imprt.Sg2: piâzz
! (6)  Ind.Pot.Sg3: piâzzâž
! (7)  Ind.Imprt.Sg3: peäzzas
! (8)  Imprt.13.ConNeg: piâsˈsu
! (9)  Ind.Imprt.Pl3: peäsˈsaz
! (10) Allegro for incoative: piâz»
! Lexicon ''@LEXNAME@'' is for verbs like
!  piâssâd:piâss
! * Yaml: __V-piassad__
!  with lexc stem
+V: VSUF-INF_ÂD ; !1 piâssâd
+V:%^VOWLower VSUF-IND-PRS-SG3_ÂD ; !2 peäss
+V+Ind+Prs+Pl3:%^VOWLower%^PAL%^CC2CCC%>e K ; !3 peäʹsˈse
+V:%^PALE%^PAL%^CC2CCC VSUF-IND-PRT-PL3_ÂD ; !4 pieʹsˈse
+V:%^KK2GG VSUF-I-POTKOND_ÂD ; !5 piâzz
+V+Pot+Sg3:%^KK2GG%>âž   K ; !6 piâzzâž
+V+Imprt+Sg3:%^VOWLower%^KK2GG%>as K ; !7 piâzzas
+V:%^CC2CCC VSUF-IMPRT-CONNEGII_ÂD ; !8 piâsˈsu
+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:%^KK2GG»škuätt V_PUEʹTTED ;
+V+Der/ted:%^KK2GG»ât  V_AALǤTED ;

LEXICON V_ČIÕKKÂD !!= * @CODE@ čiõkkâd:čiõkk
! 1. VERBS WITH SINGLE-SYLLABLE Ind.Prs.Sg3 (2009: 354)
! 1.1 Inf in âd (vowel shift)
!   Raised: Inf, Ind.Prt.Pl3, Imprt.Sg2, Pot.Sg3
!   Lowered: Ind.Prs.Sg3, Ind.Prs.Pl3, Imprt.Sg3
! 1.1.1 Has Palatalization
! 1.1.1[1] (Palatalization pattern)
!   Palatalized: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Not Palatalized: Inf, Ind.Prs.Sg3, Imprt.Sg2, Imprt.Sg3, Pot.Sg3
! 1.1.1[1].2 Has Specifically Pedagogical Gradation
! 1.1.1[1].2.1 Has Orthographic Gradation
! 1.1.1[1].2.1[231].2 (Diphthong + Consonant Geminate alternation)
!   Strong grade: Inf, Ind.Prs.Sg3
!   Extra strong grade: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Weak grade: Imprt.Sg2, Pot.Sg3, Imprt.Sg3
! FORMS
! (1)  Inf: čiõkkâd
! (2)  Ind.Prs.Sg3: čeâkk
! (3)  Ind.Prs.Pl3: čeäʹǩˈǩe (allophonic)
! (4)  Ind.Prt.Pl3: čiõʹǩˈǩe
! (5)  Ind.Imprt.Sg2: čiõǥǥ
! (6)  Ind.Pot.Sg3: čiõǥǥâž
! (7)  Ind.Imprt.Sg3: čeâǥǥ
! (8)  Imprt.13.ConNeg: čiõkˈku
! (9)  Ind.Imprt.Pl3: čeâkˈkaz
! (10) Allegro for incoative: čiõǥ»
! Lexicon ''@LEXNAME@'' is for verbs like
!  čiõkkâd:čiõkk
! * Yaml: __V-piassad__
!  with lexc stem
+V: VSUF-INF_ÂD ; !1 čiõkkâd
+V:%^VOWLower VSUF-IND-PRS-SG3_ÂD ; !2 čeâkk
+V+Ind+Prs+Pl3:%^VOWLower%^PALÄ%^PAL%^CC2CCC%>e K ; !3 čeäʹǩˈǩe
+V:%^PAL%^CC2CCC VSUF-IND-PRT-PL3_ÂD ; !4 čiõʹǩˈǩe
+V:%^KK2GG VSUF-I-POTKOND_ÂD ; !5 čiõǥǥ
+V+Pot+Sg3:%^KK2GG%>âž   K ; !6 čiõǥǥâž
+V+Imprt+Sg3:%^VOWLower%^KK2GG%>as K ; !7 čiõǥǥas
+V:%^CC2CCC VSUF-IMPRT-CONNEGII_ÂD ; !8 čiõkˈku
+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:%^CC2C»škuätt V_PUEʹTTED ;
+V+Der/ted:%^KK2GG»ât  V_AALǤTED ;


LEXICON V_HUÕLLÂD !!= * @CODE@ huõllâd:huõll
! 1. VERBS WITH SINGLE-SYLLABLE Ind.Prs.Sg3 (2009: 354)
! 1.1 Inf in âd (vowel shift)
!   Raised: Inf, Ind.Prt.Pl3, Imprt.Sg2, Pot.Sg3
!   Lowered: Ind.Prs.Sg3, Ind.Prs.Pl3, Imprt.Sg3
! 1.1.1 Has Palatalization
! 1.1.1[1] (Palatalization pattern)
!   Palatalized: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Not Palatalized: Inf, Ind.Prs.Sg3, Imprt.Sg2, Imprt.Sg3, Pot.Sg3
! 1.1.1[1].2 Has Specifically Pedagogical Gradation
! 1.1.1[1].2.1 Has Orthographic Gradation
! 1.1.1[1].2.1[231].2 (Diphthong + Consonant Geminate alternation)
!   Strong grade: Inf, Ind.Prs.Sg3
!   Extra strong grade: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Weak grade: Imprt.Sg2, Pot.Sg3, Imprt.Sg3
! FORMS
! (1)  Inf: huõllâd
! (2)  Ind.Prs.Sg3: huâll
! (3)  Ind.Prs.Pl3: hueʹlˈle (allophonic)
! (4)  Ind.Prt.Pl3: huõʹlˈle
! (5)  Ind.Imprt.Sg2: huõl
! (6)  Ind.Pot.Sg3: huõlâž
! (7)  Ind.Imprt.Sg3: huâlas
! (8)  Imprt.13.ConNeg: huõlˈlu
! (9)  Ind.Imprt.Pl3: huâlˈlaz
! (10) Allegro for incoative: huõl»
! Lexicon ''@LEXNAME@'' is for verbs like
!  huõllâd:huõll
! * Yaml: __V-huollad__
!  with lexc stem
+V: VSUF-INF_ÂD ; !1 huõllâd
+V:%^VOWLower VSUF-IND-PRS-SG3_ÂD ; !2 huâll
+V+Ind+Prs+Pl3:%^VOWLower%^PALE%^PAL%^CC2CCC%>e K ; !3 hueʹlˈle
+V:%^PAL%^CC2CCC VSUF-IND-PRT-PL3_ÂD ; !4 huõʹlˈle
+V:%^CC2C VSUF-I-POTKOND_ÂD ; !5 huõl
+V+Pot+Sg3:%^CC2C%>âž   K ; !6 huõlâž
+V+Imprt+Sg3:%^VOWLower%^CC2C%>as K ; !7 huõlas
+V:%^CC2CCC VSUF-IMPRT-CONNEGII_ÂD ; !8 huõlˈlu
+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:%^CC2C»škuätt V_PUEʹTTED ;
+V+Der/ted:%^CC2C»ât  V_AALǤTED ;


LEXICON V_AIBBÂD !!= * @CODE@ aibbâd:aibb
! 1. VERBS WITH SINGLE-SYLLABLE Ind.Prs.Sg3 (2009: 354)
! 1.1 Inf in âd (vowel shift)
!   Raised: Inf, Ind.Prt.Pl3, Imprt.Sg2, Pot.Sg3
!   Lowered: Ind.Prs.Sg3, Ind.Prs.Pl3, Imprt.Sg3
! 1.1.1 Has Palatalization
! 1.1.1[1] (Palatalization pattern)
!   Palatalized: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Not Palatalized: Inf, Ind.Prs.Sg3, Imprt.Sg2, Imprt.Sg3, Pot.Sg3
! 1.1.1[1].1 Lacks Specifically Pedagogical Gradation
! 1.1.1[1].1.1 Has Orthographic Gradation
! 1.1.1[1].1.1[221] (Monophthong + Consonant Cluster alternation)
!   Strong grade: Inf, Ind.Prs.Sg3
!   Strong grade: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Weak grade: Imprt.Sg2, Pot.Sg3, Imprt.Sg3
! FORMS
! (1)  Inf: aibbâd
! (2)  Ind.Prs.Sg3: äibb
! (3)  Ind.Prs.Pl3: äiʹbbe
! (4)  Ind.Prt.Pl3: aiʹbbe
! (5)  Imprt.Sg2: aaib
! (6)  Pot.Sg3: aaibâž
! (7)  Imprt.Sg3: ääibas
! (8)  Imprt.13.ConNeg: aibbu
! (9)  Ind.Imprt.Pl3: äibbaz
! (10) Allegro for incoative: aib»
! * Yaml: 
!  aibbâd:aibb
!  with lexc stem
+V: VSUF-INF_ÂD ; !1
+V:%^VOWLower VSUF-IND-PRS-SG3_ÂD ; !2
+V+Ind+Prs+Pl3:%^VOWLower%^PAL%>e K ; !3
+V:%^PAL VSUF-IND-PRT-PL3_ÂD ; !4
+V:%^V2VV%^XYY2XY VSUF-I-POTKOND_ÂD ; !5
+V+Pot+Sg3:%^V2VV%^XYY2XY%>âž   K ; !6
+V+Imprt+Sg3:%^V2VV%^VOWLower%^XYY2XY%>as K ; !7
+V: VSUF-IMPRT-CONNEGII_ÂD ; !8
+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:%^XYY2XY»škuätt V_PUEʹTTED ;
+V+Der/ted:%^V2VV%^XYY2XY»ât  V_AALǤTED ;


LEXICON V_TOBDDÂD !!= * @CODE@ tobddâd:tobdd
! 1. VERBS WITH SINGLE-SYLLABLE Ind.Prs.Sg3 (2009: 354)
! 1.1 Inf in âd (vowel shift)
!   Raised: Inf, Ind.Prt.Pl3, Imprt.Sg2, Pot.Sg3
!   Lowered: Ind.Prs.Sg3, Ind.Prs.Pl3, Imprt.Sg3
! 1.1.1 Has Palatalization
! 1.1.1[1] (Palatalization pattern)
!   Palatalized: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Not Palatalized: Inf, Ind.Prs.Sg3, Imprt.Sg2, Imprt.Sg3, Pot.Sg3
! 1.1.1[1].1 Lacks Specifically Pedagogical Gradation
! 1.1.1[1].1.1 Has Orthographic Gradation
! 1.1.1[1].1.1[221] (Monophthong + Consonant Cluster alternation)
!   Strong grade: Inf, Ind.Prs.Sg3
!   Strong grade: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Weak grade: Imprt.Sg2, Pot.Sg3, Imprt.Sg3
! FORMS
! (1)  Inf: tobddâd
! (2)  Ind.Prs.Sg3: tåbdd
! (3)  Ind.Prs.Pl3: tåʹbdde
! (4)  Ind.Prt.Pl3: toʹbdde
! (5)  Imprt.Sg2: toobd
! (6)  Pot.Sg3: toobdâž
! (7)  Imprt.Sg3: tååbdas
! (8)  Imprt.13.ConNeg: tobddu
! (9)  Ind.Imprt.Pl3: tåbddaz
! (10) Allegro for incoative: tobd»
! * Yaml: 
!  tobddâd:tobdd
!  with lexc stem
+V: VSUF-INF_ÂD ; !1
+V:%^VOWLower VSUF-IND-PRS-SG3_ÂD ; !2
+V+Ind+Prs+Pl3:%^VOWLower%^PAL%>e K ; !3
+V:%^PAL VSUF-IND-PRT-PL3_ÂD ; !4
+V:%^V2VV%^XYY2XY VSUF-I-POTKOND_ÂD ; !5
+V+Pot+Sg3:%^V2VV%^XYY2XY%>âž   K ; !6
+V+Imprt+Sg3:%^V2VV%^VOWLower%^XYY2XY%>as K ; !7
+V: VSUF-IMPRT-CONNEGII_ÂD ; !8

+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:%^XYY2XY»škuätt V_PUEʹTTED ;
+V+Der/ted:%^V2VV%^XYY2XY»ât  V_AALǤTED ;

LEXICON V_KUÕSKKÂD !!= * @CODE@ kuõskkâd:kuõskk
! 1. VERBS WITH SINGLE-SYLLABLE Ind.Prs.Sg3 (2009: 354)
! 1.1 Inf in âd (vowel shift)
!   Raised: Inf, Ind.Prt.Pl3, Imprt.Sg2, Pot.Sg3
!   Lowered: Ind.Prs.Sg3, Ind.Prs.Pl3, Imprt.Sg3
! 1.1.1 Has Palatalization
! 1.1.1[1] (Palatalization pattern)
!   Palatalized: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Not Palatalized: Inf, Ind.Prs.Sg3, Imprt.Sg2, Imprt.Sg3, Pot.Sg3
! 1.1.1[1].1 Lacks Specifically Pedagogical Gradation
! 1.1.1[1].1.1 Has Orthographic Gradation
! 1.1.1[1].1.1[332].2 (Diphthong + Consonant Cluster alternation)
!   Strong grade: Inf, Ind.Prs.Sg3
!   Strong grade: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Weak grade: Imprt.Sg2, Pot.Sg3, Imprt.Sg3
! FORMS
! (1)  Inf: kuõskkâd
! (2)  Ind.Prs.Sg3: kuâskk
! (3)  Ind.Prs.Pl3: kueʹsǩǩe (allophonic)
! (4)  Ind.Prt.Pl3: kuõʹsǩǩe
! (5)  Ind.Imprt.Sg2: kuõsk
! (6)  Ind.Pot.Sg3: kuõskâž
! (7)  Ind.Imprt.Sg3: kuâskas
! (8)  Imprt.13.ConNeg: kuõskku
! (9)  Ind.Imprt.Pl3: kuâskkaz
! (10) Allegro for incoative: kuõsk»
! Lexicon ''@LEXNAME@'' is for verbs like
!  kuõskkâd:kuõskk
!  with lexc stem
! Strong consonant cluster
! Diphthong
! THIS NEEDS MORE DEVELOPING
+V: VSUF-INF_ÂD ; !1
+V:%^VOWLower VSUF-IND-PRS-SG3_ÂD ; !2
+V+Ind+Prs+Pl3:%^VOWLower%^PAL%>e K ; !3
+V:%^PAL VSUF-IND-PRT-PL3_ÂD ; !4
+V:%^XYY2XY VSUF-I-POTKOND_ÂD ; !5
+V+Pot+Sg3:%^XYY2XY%>âž   K ; !6
+V+Imprt+Sg3:%^VOWLower%^XYY2XY%>as K ; !7
+V: VSUF-IMPRT-CONNEGII_ÂD ; !8

+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:%^XYY2XY»škuätt V_PUEʹTTED ;
+V+Der/ted:%^XYY2XY»ât  V_AALǤTED ;

LEXICON V_ČUÕIGGÂD !!= * @CODE@ čuõiggâd:čuõigg
! 1. VERBS WITH SINGLE-SYLLABLE Ind.Prs.Sg3 (2009: 354)
! 1.1 Inf in âd (vowel shift)
!   Raised: Inf, Ind.Prt.Pl3, Imprt.Sg2, Pot.Sg3
!   Lowered: Ind.Prs.Sg3, Ind.Prs.Pl3, Imprt.Sg3
! 1.1.1 Has Palatalization
! 1.1.1[1] (Palatalization pattern)
!   Palatalized: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Not Palatalized: Inf, Ind.Prs.Sg3, Imprt.Sg2, Imprt.Sg3, Pot.Sg3
! 1.1.1[1].1 Lacks Specifically Pedagogical Gradation
! 1.1.1[1].1.1 Has Orthographic Gradation
! 1.1.1[1].1.1[332].2 (Diphthong + Consonant Cluster alternation)
!   Strong grade: Inf, Ind.Prs.Sg3
!   Strong grade: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Weak grade: Imprt.Sg2, Pot.Sg3, Imprt.Sg3
! FORMS
! (1)  Inf: čuõiggâd
! (2)  Ind.Prs.Sg3: čuâigg
! (3)  Ind.Prs.Pl3: čueiʹǧǧe (allophonic)
! (4)  Ind.Prt.Pl3: čuõiʹǧǧe
! (5)  Ind.Imprt.Sg2: čuõiǥ
! (6)  Ind.Pot.Sg3: čuõiǥâž
! (7)  Ind.Imprt.Sg3: čuâiǥas
! (8)  Imprt.13.ConNeg: čuõiggu
! (9)  Ind.Imprt.Pl3: čuâiggaz
! (10) Allegro for incoative: čuõiǥ»
! Lexicon ''@LEXNAME@'' is for verbs like
!  čuõiggâd:čuõigg
!  with lexc stem
! Strong consonant cluster
! Diphthong
! THIS NEEDS MORE DEVELOPING
+V: VSUF-INF_ÂD ; !1
+V:%^VOWLower VSUF-IND-PRS-SG3_ÂD ; !2
+V+Ind+Prs+Pl3:%^VOWLower%^PAL%>e K ; !3
+V:%^PAL VSUF-IND-PRT-PL3_ÂD ; !4
+V:%^XYY2XY VSUF-I-POTKOND_ÂD ; !5
+V+Pot+Sg3:%^XYY2XY%>âž   K ; !6
+V+Imprt+Sg3:%^VOWLower%^XYY2XY%>as K ; !7
+V: VSUF-IMPRT-CONNEGII_ÂD ; !8

+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:%^XYY2XY»škuätt V_PUEʹTTED ;
+V+Der/ted:%^XYY2XY»ât  V_AALǤTED ;


! Even-syllable stems

LEXICON V_LAADDÂD !!= * @CODE@ laaddâd:laadd
! 1. VERBS WITH SINGLE-SYLLABLE Ind.Prs.Sg3 (2009: 354)
! 1.1 Inf in âd (vowel shift)
!   Raised: Inf, Ind.Prt.Pl3, Imprt.Sg2, Pot.Sg3
!   Lowered: Ind.Prs.Sg3, Ind.Prs.Pl3, Imprt.Sg3
! 1.1.1 Has Palatalization
! 1.1.1[1] (Palatalization pattern)
!   Palatalized: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Not Palatalized: Inf, Ind.Prs.Sg3, Imprt.Sg2, Imprt.Sg3, Pot.Sg3
! 1.1.1[1].1 Lacks Specifically Pedagogical Gradation
! 1.1.1[1].1.2 Lacks Orthographic Gradation
! 1.1.1[1].1.2[111] (Monophthong + Consonant Cluster)
!   Weak grade: Inf, Ind.Prs.Sg3
!   Weak grade: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Weak grade: Imprt.Sg2, Pot.Sg3, Imprt.Sg3
! FORMS
! (1)  Inf: laaddâd
! (2)  Ind.Prs.Sg3: läädd
! (3)  Ind.Prs.Pl3: lääʹdde
! (4)  Ind.Prt.Pl3: laaʹdde
! (5)  Ind.Imprt.Sg2: laadd
! (6)  Ind.Pot.Sg3: laaddâž
! (7)  Ind.Imprt.Sg3: lääddas
! (8)  Imprt.13.ConNeg: laaddu
! (9)  Ind.Imprt.Pl3: lääddaz
! (10) Allegro for incoative: laad»
! * Yaml: 
! laaddâd:laadd
+V: VSUF-INF_ÂD ; !1
+V:%^VOWLower VSUF-IND-PRS-SG3_ÂD ; !2
+V+Ind+Prs+Pl3:%^VOWLower%^PAL%>e K ; !3
+V:%^PAL VSUF-IND-PRT-PL3_ÂD ; !4
+V: VSUF-I-POTKOND_ÂD ; !5
+V+Pot+Sg3:%>âž   K ; !6
+V+Imprt+Sg3:%^VOWLower%>as K ; !7
+V: VSUF-IMPRT-CONNEGII_ÂD ; !8

+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:%^CC2C»škuätt V_PUEʹTTED ;
+V+Der/ted:»ât  V_AALǤTED ;


LEXICON V_VÕÕIDÂD !!= * @CODE@ võõidâd:võõid
! 1. VERBS WITH SINGLE-SYLLABLE Ind.Prs.Sg3 (2009: 354)
! 1.1 Inf in âd (vowel shift)
!   Raised: Inf, Ind.Prt.Pl3, Imprt.Sg2, Pot.Sg3
!   Lowered: Ind.Prs.Sg3, Ind.Prs.Pl3, Imprt.Sg3
! 1.1.1 Has Palatalization
! 1.1.1[1] (Palatalization pattern)
!   Palatalized: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Not Palatalized: Inf, Ind.Prs.Sg3, Imprt.Sg2, Imprt.Sg3, Pot.Sg3
! 1.1.1[1].1 Lacks Specifically Pedagogical Gradation
! 1.1.1[1].1.2 Lacks Orthographic Gradation
! 1.1.1[1].1.2[111] (Monophthong + Consonant Cluster)
!   Weak grade: Inf, Ind.Prs.Sg3
!   Weak grade: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Weak grade: Imprt.Sg2, Pot.Sg3, Imprt.Sg3
! FORMS
! (1)  Inf: võõidâd
! (2)  Ind.Prs.Sg3: vââid
! (3)  Ind.Prs.Pl3: vââiʹde
! (4)  Ind.Prt.Pl3: võõiʹde
! (5)  Ind.Imprt.Sg2: võõid
! (6)  Ind.Pot.Sg3: võõidâž
! (7)  Ind.Imprt.Sg3: vââidas
! (8)  Imprt.13.ConNeg: võõidu
! (9)  Ind.Imprt.Pl3: vââidaz
! (10) Allegro for incoative: vââid»
! * Yaml: 
! võõidâd:võõid
+V: VSUF-INF_ÂD ; !1
+V:%^VOWLower VSUF-IND-PRS-SG3_ÂD ; !2
+V+Ind+Prs+Pl3:%^VOWLower%^PAL%>e K ; !3
+V:%^PAL VSUF-IND-PRT-PL3_ÂD ; !4
+V: VSUF-I-POTKOND_ÂD ; !5
+V+Pot+Sg3:%>âž   K ; !6
+V+Imprt+Sg3:%^VOWLower%>as K ; !7
+V: VSUF-IMPRT-CONNEGII_ÂD ; !8

+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:»škuätt V_PUEʹTTED ;
+V+Der/ted:»ât  V_AALǤTED ;

LEXICON V_ČIISTÂD !!= * @CODE@ čiistâd:čiist
! 1. VERBS WITH SINGLE-SYLLABLE Ind.Prs.Sg3 (2009: 354)
! 1.1 Inf in âd (vowel shift)
!   Raised: Inf, Ind.Prt.Pl3, Imprt.Sg2, Pot.Sg3
!   Lowered: Ind.Prs.Sg3, Ind.Prs.Pl3, Imprt.Sg3
! 1.1.1 Has Palatalization
! 1.1.1[1] (Palatalization pattern)
!   Palatalized: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Not Palatalized: Inf, Ind.Prs.Sg3, Imprt.Sg2, Imprt.Sg3, Pot.Sg3
! 1.1.1[1].1 Lacks Specifically Pedagogical Gradation
! 1.1.1[1].1.2 Lacks Orthographic Gradation
! 1.1.1[1].1.2[111] (Monophthong + Consonant Cluster)
!   Weak grade: Inf, Ind.Prs.Sg3
!   Weak grade: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Weak grade: Imprt.Sg2, Pot.Sg3, Imprt.Sg3
! FORMS
! (1)  Inf: čiistâd
! (2)  Ind.Prs.Sg3: čeest
! (3)  Ind.Prs.Pl3: čeeʹste
! (4)  Ind.Prt.Pl3: čiiʹste
! (5)  Ind.Imprt.Sg2: čiist
! (6)  Ind.Pot.Sg3: čiistâž
! (7)  Ind.Imprt.Sg3: čeestas
! (8)  Imprt.13.ConNeg: čiistu
! (9)  Ind.Imprt.Pl3: čeestaz
! (10) Allegro for incoative: čiist»
! * Yaml: 
! čiistâd:čiist
+V: VSUF-INF_ÂD ; !1
+V:%^VOWLower VSUF-IND-PRS-SG3_ÂD ; !2
+V+Ind+Prs+Pl3:%^VOWLower%^PAL%>e K ; !3
+V:%^PAL VSUF-IND-PRT-PL3_ÂD ; !4
+V: VSUF-I-POTKOND_ÂD ; !5
+V+Pot+Sg3:%>âž   K ; !6
+V+Imprt+Sg3:%^VOWLower%>as K ; !7
+V: VSUF-IMPRT-CONNEGII_ÂD ; !8

+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:»škuätt V_PUEʹTTED ;
+V+Der/ted:»ât  V_AALǤTED ;

LEXICON V_SLUUŽÂD !!= * @CODE@ sluužâd:sluuž
! 1. VERBS WITH SINGLE-SYLLABLE Ind.Prs.Sg3 (2009: 354)
! 1.1 Inf in âd (vowel shift)
!   Raised: Inf, Ind.Prt.Pl3, Imprt.Sg2, Pot.Sg3
!   Lowered: Ind.Prs.Sg3, Ind.Prs.Pl3, Imprt.Sg3
! 1.1.1 Has Palatalization
! 1.1.1[1] (Palatalization pattern)
!   Palatalized: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Not Palatalized: Inf, Ind.Prs.Sg3, Imprt.Sg2, Imprt.Sg3, Pot.Sg3
! 1.1.1[1].1 Lacks Specifically Pedagogical Gradation
! 1.1.1[1].1.2 Lacks Orthographic Gradation
! 1.1.1[1].1.2[111] (Monophthong + Single Consonant)
!   Weak grade: Inf, Ind.Prs.Sg3
!   Weak grade: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Weak grade: Imprt.Sg2, Pot.Sg3, Imprt.Sg3
! FORMS
! (1)  Inf: sluužâd
! (2)  Ind.Prs.Sg3: sloož
! (3)  Ind.Prs.Pl3: slooʹže
! (4)  Ind.Prt.Pl3: sluuʹže
! (5)  Ind.Imprt.Sg2: sluuž
! (6)  Ind.Pot.Sg3: sluužâž
! (7)  Ind.Imprt.Sg3: sloožas
! (8)  Imprt.13.ConNeg: sluužu
! (9)  Ind.Imprt.Pl3: sloožaz
! (10) Allegro for incoative: sluuž»
! * Yaml: 
! sluužâd:sluuž
+V: VSUF-INF_ÂD ; !1
+V:%^VOWLower VSUF-IND-PRS-SG3_ÂD ; !2
+V+Ind+Prs+Pl3:%^VOWLower%^PAL%>e K ; !3
+V:%^PAL VSUF-IND-PRT-PL3_ÂD ; !4
+V: VSUF-I-POTKOND_ÂD ; !5
+V+Pot+Sg3:%>âž   K ; !6
+V+Imprt+Sg3:%^VOWLower%>as K ; !7
+V: VSUF-IMPRT-CONNEGII_ÂD ; !8

+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:»škuätt V_PUEʹTTED ;
+V+Der/ted:»ât  V_AALǤTED ;

LEXICON V_PLIÂSNÂD !!= * @CODE@ pliâsnâd:pliâsn
! 1. VERBS WITH SINGLE-SYLLABLE Ind.Prs.Sg3 (2009: 354)
! 1.1 Inf in âd (vowel shift)
!   Raised: Inf, Ind.Prt.Pl3, Imprt.Sg2, Pot.Sg3
!   Lowered: Ind.Prs.Sg3, Ind.Prs.Pl3, Imprt.Sg3
! 1.1.1 Has Palatalization
! 1.1.1[1] (Palatalization pattern)
!   Palatalized: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Not Palatalized: Inf, Ind.Prs.Sg3, Imprt.Sg2, Imprt.Sg3, Pot.Sg3
! 1.1.1[1].1 Lacks Specifically Pedagogical Gradation
! 1.1.1[1].1.2 Lacks Orthographic Gradation
! 1.1.1[1].1.2[111] (Monophthong + Consonant Cluster)
!   Weak grade: Inf, Ind.Prs.Sg3
!   Weak grade: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Weak grade: Imprt.Sg2, Pot.Sg3, Imprt.Sg3
! FORMS
! (1)  Inf: pliâsnâd
! (2)  Ind.Prs.Sg3: pleäsn
! (3)  Ind.Prs.Pl3: pleäʹsne
! (4)  Ind.Prt.Pl3: plieʹsne
! (5)  Ind.Imprt.Sg2: pliâsn
! (6)  Ind.Pot.Sg3: pliâsnâž
! (7)  Ind.Imprt.Sg3: pleäsnas
! (8)  Imprt.13.ConNeg: pliâsnu
! (9)  Ind.Imprt.Pl3: pleäsnaz
! (10) Allegro for incoative: pliâsn»
! * Yaml: 
! pliâsnâd:pliâsn
+V: VSUF-INF_ÂD ; !1
+V:%^VOWLower VSUF-IND-PRS-SG3_ÂD ; !2
+V+Ind+Prs+Pl3:%^VOWLower%^PAL%>e K ; !3
+V:%^PAL VSUF-IND-PRT-PL3_ÂD ; !4
+V: VSUF-I-POTKOND_ÂD ; !5
+V+Pot+Sg3:%>âž   K ; !6
+V+Imprt+Sg3:%^VOWLower%>as K ; !7
+V: VSUF-IMPRT-CONNEGII_ÂD ; !8

+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:»škuätt V_PUEʹTTED ;
+V+Der/ted:»ât  V_AALǤTED ;

LEXICON V_SUUDÂD !!= * @CODE@ suudâd:suud
!
! 1. VERBS WITH SINGLE-SYLLABLE Ind.Prs.Sg3 (2009: 354)
! 1.1 Inf in âd (vowel shift)
!   Raised: Inf, Ind.Prt.Pl3, Imprt.Sg2, Pot.Sg3
!   Lowered: Ind.Prs.Sg3, Ind.Prs.Pl3, Imprt.Sg3
! 1.1.1 Has Palatalization
! 1.1.1[1] (Palatalization pattern)
!   Palatalized: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Not Palatalized: Inf, Ind.Prs.Sg3, Imprt.Sg2, Imprt.Sg3, Pot.Sg3
! 1.1.1[1].1 Lacks Specifically Pedagogical Gradation
! 1.1.1[1].1.2 Lacks Orthographic Gradation
! 1.1.1[1].1.2[111] (Diphthong + Consonant Cluster)
!   Weak grade: Inf, Ind.Prs.Sg3
!   Weak grade: Ind.Prs.Pl3, Ind.Prt.Pl3
!   Weak grade: Imprt.Sg2, Pot.Sg3, Imprt.Sg3
! FORMS
! (1)  Inf: suudâd
! (2)  Ind.Prs.Sg3: sooud
! (3)  Ind.Prs.Pl3: soouʹde
! (4)  Ind.Prt.Pl3: suuʹde
! (5)  Ind.Imprt.Sg2: suud
! (6)  Ind.Pot.Sg3: suudâž
! (7)  Ind.Imprt.Sg3: sooudas
! (8)  Imprt.13.ConNeg: suudu
! (9)  Ind.Imprt.Pl3: sooudaz
! (10) Allegro for incoative: suud»
! * Yaml: 
! suudâd:suud
+V: VSUF-INF_ÂD ; !1
+V:%^VOWLower VSUF-IND-PRS-SG3_ÂD ; !2
+V+Ind+Prs+Pl3:%^VOWLower%^PAL%>e K ; !3
+V:%^PAL VSUF-IND-PRT-PL3_ÂD ; !4
+V: VSUF-I-POTKOND_ÂD ; !5
+V+Pot+Sg3:%>âž   K ; !6
+V+Imprt+Sg3:%^VOWLower%>as K ; !7
+V: VSUF-IMPRT-CONNEGII_ÂD ; !8

+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:»škuätt V_PUEʹTTED ;
+V+Der/ted:»ât  V_AALǤTED ;

!! !CLASS 1 LOW VOWEL, NO PALATALIZATION
!! Even-syllable stems in -AD

LEXICON V_TEEVVAD !!= * @CODE@ teevvad:teevv
! ** Yaml: __V-teevvad__
! In the lexicon ''@LEXNAME@''
! teevvad:teevv
! 1. The Ind.Prs.Sg3 form is a single syllable
! 1.2 The infinitive ends in ad
! 1.2.1 Has gradation
! 1.2.1.1 Monophthong 
! Stem is strong
! one vowel
! FORMS
! (1)  Inf: teevvad
! (2)  Ind.Prs.Sg3: teevv
! (3)  Ind.Prs.Pl3: tevva
! (4)  Ind.Prt.Pl3: tivvu
! (5)  Ind.Imprt.Sg2: teev
! (6)  Ind.Pot.Sg3: teevež
! (7)  Ind.Imprt.Sg3: teevas
! (8)  Imprt.13.ConNeg: tivvu
! (9) Ind.Imprt.Pl3: tevvaz
! (10) Allegro for incoative: tev»

+V: VSUF-INF_AD ; !1 teevvad
+V: VSUF-IND-PRS-SG3_AD ; !2 teevv
+V+Ind+Prs+Pl3:%^VV2V%>a K ; !3 tevva
+V:%^VV2V%^VOWRaise%> VSUF-IND-PRT-PL3_AD ; !4 tivvu
+V:%^CC2C%> VSUF-I-POTKOND_AD ; !5 teev-
+V+Pot+Sg3:%^CC2C%>ež	K ; !6 teevež
+V+Imprt+Sg3:%^CC2C%>as K ; !7 teevas
+V:%^VV2V%^VOWRaise VSUF-IMPRT-CONNEGII_AD ; !8 tivvu
+V:%^VV2V VSUF-IMPRT-PL3_AD ; ! tevv-
+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:%^VV2V%^CC2C»škuätt V_PUEʹTTED ;


LEXICON V_PÅÅSSAD !!= * @CODE@ pååssad:pååss
! ** Yaml: __V-paassad__
! In the lexicon ''@LEXNAME@''
! pååssad:pååss
! 1. The Ind.Prs.Sg3 form is a single syllable
! 1.2 The infinitive ends in ad
! 1.2.1 Has gradation
! 1.2.1.1 Monophthong 
! Stem is strong
! one vowel
! FORMS
! (1)  Inf: pååssad
! (2)  Ind.Prs.Sg3: pååss
! (3)  Ind.Prs.Pl3: påssa
! (4)  Ind.Prt.Pl3: possu
! (5)  Ind.Imprt.Sg2: pååzz
! (6)  Ind.Pot.Sg3: pååzzež
! (7)  Ind.Imprt.Sg3: pååzzas
! (8)  Imprt.13.ConNeg: possu
! (9) Ind.Imprt.Pl3: påssaz
! (10) Allegro for incoative: påz»
+V: VSUF-INF_AD ; !1 pååssad
+V: VSUF-IND-PRS-SG3_AD ; !2 pååss
+V+Ind+Prs+Pl3:%^VV2V%>a K ; !3 påssa
+V:%^VV2V%^VOWRaise%> VSUF-IND-PRT-PL3_AD ; !4 possu
+V:%^CC2C%> VSUF-I-POTKOND_AD ; !5 pååzz-
+V+Pot+Sg3:%^CC2C%>ež	K ; !6 pååzzež
+V+Imprt+Sg3:%^CC2C%>as K ; !7 pååzzas
+V:%^VV2V%^VOWRaise VSUF-IMPRT-CONNEGII_AD ; !8 possu
+V:%^VV2V VSUF-IMPRT-PL3_AD ; ! påss-
+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:%^VV2V%^KK2G»škuätt V_PUEʹTTED ;

LEXICON V_CÅKKAD
 V_TÄTTAD ;
LEXICON V_ÄBBAD
 V_TÄTTAD ;
LEXICON V_TÄTTAD
! tättad:tätt
! ** Yaml: __V-taettad__
! In the lexicon ''@LEXNAME@'' 
! 1. The Ind.Prs.Sg3 form is a single syllable
! 1.2 The infinitive ends in ad
! 1.2.1 Has gradation
! 1.2.1.1 Monophthong 
! The stem is extra strong
! Relative grade indicates
! W => St
! St => XSt
! XSt == XSt
! Monophthong
! Raising 
! FORMS
! (1) +V+Inf: tättad
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: tätt
! (3) +V+Ind+Prs+Pl3: tätta
! (4) +V+Ind+Prt+Pl3: tattu
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: täätt
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: täättež
! (7) +V+Imprt+Sg3: täättas
! (8) +V+Imprt+ConNegII: tattu
!    Pass.PrfPrc
! (9) Ind.Imprt.Pl3: tättaz
! (10) Allegro for incoative: tät»
+V: VSUF-INF_AD ; !1
+V: VSUF-IND-PRS-SG3_AD ; !2
+V+Ind+Prs+Pl3:%>a K ; !3
+V:%^VOWRaise%> VSUF-IND-PRT-PL3_AD ; !4
+V:%^V2VV%> VSUF-I-POTKOND_AD ; !5
+V+Pot+Sg3:%^V2VV%>ež   K ; !6
+V+Imprt+Sg3:%^V2VV%>as K ; !7
+V:%^VOWRaise VSUF-IMPRT-CONNEGII_AD ; !8
+V: VSUF-IMPRT-PL3_AD ;
+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:%^CC2C»škuätt V_PUEʹTTED ;


LEXICON V_REÄKKAD
! gradation: yes
! weak geminate
! ** Yaml: __V-reakkad__
! In the lexicon ''@LEXNAME@''
! reäkkad:reäkk
!!
! 1. The Ind.Prs.Sg3 form is a single syllable
! 1.2 The infinitive ends in ad
! 1.2.1 Has gradation
! 1.2.1.1 Diphthong 
! The stem is strong
! Relative grade indicates
! W => St
! St => XSt
! XSt == XSt
! Monophthong
! Raising 
! FORMS
! (1) +V+Inf: reäkkad
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: reäkk
! (3) +V+Ind+Prs+Pl3: reäkˈka
! (4) +V+Ind+Prt+Pl3: riâkˈku
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: reäǥǥ
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: reäǥǥaž
! (7) +V+Imprt+Sg3: reäǥǥas
! (8) +V+Imprt+ConNegII: riâkˈku
!    Pass.PrfPrc
! (9) Ind.Imprt.Pl3: reäkˈkaz
! (10) Allegro for incoative: reäǥ»
!
+V: VSUF-INF_AD ; !1
+V: VSUF-IND-PRS-SG3_AD ; !2
+V+Ind+Prs+Pl3:%^CC2CCC%>a K ; !3
+V:%^VOWRaise%^CC2CCC%> VSUF-IND-PRT-PL3_AD ; !4
+V:%^KK2GG%> VSUF-I-POTKOND_AD ; !5
+V+Pot+Sg3:%^KK2GG%>až   K ; !6
+V+Imprt+Sg3:%^KK2GG%>as K ; !7
+V:%^VOWRaise%^CC2CCC VSUF-IMPRT-CONNEGII_AD ; !8
+V:%^CC2CCC VSUF-IMPRT-PL3_AD ;
+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:%^KK2G»škuätt V_PUEʹTTED ;

LEXICON V_HEÂRRAD
! gradation: yes
! weak: single
! ** Yaml: __V-hearrad__
! In the lexicon ''@LEXNAME@''
! heârrad:heârr
!!
! 1. The Ind.Prs.Sg3 form is a single syllable
! 1.2 The infinitive ends in ad
! 1.2.1 Has gradation
! 1.2.1.1 Diphthong 
! The stem is strong
! Relative grade indicates
! W => St
! St => XSt
! XSt == XSt
! Monophthong
! Raising 
! FORMS
! (1) +V+Inf: heârrad
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: heârr
! (3) +V+Ind+Prs+Pl3: heârˈra
! (4) +V+Ind+Prt+Pl3: hiõrˈru
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: heâr
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: heâraž
! (7) +V+Imprt+Sg3: heâras
! (8) +V+Imprt+ConNegII: heârˈru
!    Pass.PrfPrc
! (9) Ind.Imprt.Pl3: heârˈraz
! (10) Allegro for incoative: heâr»
!
+V: VSUF-INF_AD ; !1
+V: VSUF-IND-PRS-SG3_AD ; !2
+V+Ind+Prs+Pl3:%^CC2CCC%>a K ; !3
+V:%^VOWRaise%^CC2CCC%> VSUF-IND-PRT-PL3_AD ; !4
+V:%^CC2C%> VSUF-I-POTKOND_AD ; !5
+V+Pot+Sg3:%^CC2C%>až   K ; !6
+V+Imprt+Sg3:%^CC2C%>as K ; !7
+V:%^VOWRaise%^CC2CCC VSUF-IMPRT-CONNEGII_AD ; !8
+V:%^CC2CCC VSUF-IMPRT-PL3_AD ;
+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:%^CC2C»škuätt V_PUEʹTTED ;

LEXICON V_LUÂDˈDAD
 V_CEÄKˈKAD ;
LEXICON V_CEÄKˈKAD
! gradation: extra::strong
! ** Yaml: __V-hearrad__
! In the lexicon ''@LEXNAME@''
! ceäkkad:ceäkˈk
!!
! 1. The Ind.Prs.Sg3 form is a single syllable
! 1.2 The infinitive ends in ad
! 1.2.1 Has gradation
! 1.2.1.1 Diphthong 
! The stem is strong
! Relative grade indicates
! W => St
! St => XSt
! XSt == XSt
! Diphthong
! Raising 
! FORMS
! (1) +V+Inf: ceäkˈkad
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: ceäkˈk
! (3) +V+Ind+Prs+Pl3: ceäkˈka
! (4) +V+Ind+Prt+Pl3: ciâkˈku
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: ceäkk
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: ceäkkaž
! (7) +V+Imprt+Sg3: ceäkkas
! (8) +V+Imprt+ConNegII: riâkˈku
!    Pass.PrfPrc
! (9) Ind.Imprt.Pl3: ceäkˈkaz
! (10) Allegro for incoative: ceäk»
!
+V: VSUF-INF_AD ; !1
+V: VSUF-IND-PRS-SG3_AD ; !2
+V+Ind+Prs+Pl3:%>a K ; !3
+V:%^VOWRaise%> VSUF-IND-PRT-PL3_AD ; !4
+V:%^CC2C%> VSUF-I-POTKOND_AD ; !5
+V+Pot+Sg3:%^CCC2CC%>až   K ; !6
+V+Imprt+Sg3:%^CCC2CC%>as K ; !7
+V:%^VOWRaise VSUF-IMPRT-CONNEGII_AD ; !8
+V: VSUF-IMPRT-PL3_AD ;
+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:%^CCC2C»škuätt V_PUEʹTTED ;

LEXICON V_VUÄGˈGAD !!= * @CODE@ vuägˈgad:vuägˈg
! ** Yaml: __V-vuaeggad__
! In the lexicon ''@LEXNAME@''
! vuägˈgad:vuägˈg
! Stem is extra strong
! diphthong
! FORMS
! (1) +V+Inf: vuägˈgad
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: vuägˈg
! (3) +V+Ind+Prs+Pl3: vuägˈga
! (4) +V+Ind+Prt+Pl3: ciâgˈgu
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: vuägˈg
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: vuägˈgaž
! (7) +V+Imprt+Sg3: vuägˈgas
! (8) +V+Imprt+ConNegII: vuågˈgu
!    Pass.PrfPrc
! (9) Ind.Imprt.Pl3: vuägˈgaz
! (10) Allegro for incoative: ceäg»
!
+V: VSUF-INF_AD ; !1
+V: VSUF-IND-PRS-SG3_AD ; !2
+V+Ind+Prs+Pl3:%>a K ; !3
+V:%^VOWRaise%> VSUF-IND-PRT-PL3_AD ; !4
+V:%> VSUF-I-POTKOND_AD ; !5
+V+Pot+Sg3:%^CCC2CC%>až   K ; !6
+V+Imprt+Sg3:%^CCC2CC%>as K ; !7
+V:%^VOWRaise VSUF-IMPRT-CONNEGII_AD ; !8
+V: VSUF-IMPRT-PL3_AD ;
+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:%^CCC2C»škuätt V_PUEʹTTED ;


LEXICON V_LÄULLAD !!= * @CODE@ läullad:läull
! ** Yaml: __V-laullad__
! In the lexicon ''@LEXNAME@''
! läullad:läull
! Gradation
! monophthong followed by consonant cluster
! Stem is strong
! FORMS
! (1) +V+Inf: läullad
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: läull
! (3) +V+Ind+Prs+Pl3: läulla
! (4) +V+Ind+Prt+Pl3: laullu
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: lääul
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: lääulež
! (7) +V+Imprt+Sg3: lääulas
! (8) +V+Imprt+ConNegII: laullu
!    Pass.PrfPrc
! (9) Ind.Imprt.Pl3: läullaz
! (10) Allegro for incoative: läul»

!! ExtraStrong-LowVowel-Palatalization
+V: VSUF-INF_AD ; !!= * @CODE@ (1) +V+Inf: läullad
!! V+Inf, Ind+Prs+Pl1, Ind+Prs+Pl2,
!! Imprt+Pl1, Imprt+Pl2
!! Actio, ActEss, PrsPrc
!!  Ind.Prt.ConNeg, Imprt+Pl3, PrfPrc 
!! +Der/mõš

!! ExtraStrong-LowVowel-No-palatalization
+V: VSUF-IND-PRS-SG3_AD ; !!= * @CODE@ (2) +V+Ind+Prs+Sg3: läull
!! +V+Ind+Prs+Sg3, Ind.Prt.ConNeg, PrfPrc

!! ExtraStrong-LowVowel-Palatalization
+V+Ind+Prs+Pl3:%>a K ; !!= * @CODE@ (3) +V+Ind+Prs+Pl3: läulla
!! +V+Ind+Prs+Pl3

!! ExtraStrong-RaisedVowel-Palatalization
+V:%^VOWRaise%> VSUF-IND-PRT-PL3_AD ; !!= * @CODE@ (4) +V+Ind+Prt+Pl3: laullu
!! +V+Ind+Prt+Pl3,  Ind+Prt+Sg1, Ind+Prt+Sg2, Ind+Prt+Sg4

!! Weak-StableV-No-Palatalization
+V:%^V2VV%^PAL%^XYY2XY VSUF-IND-PRS-CONNEG_ED ; !!= * @CODE@ (5) lääul
!! Imprt+Sg2, Ind+Prs+ConNeg, Ind+Prs+Sg4, VAbess, GerTemp, GerInstr

!! Weak-StableVowel-No-Palatalization
+V:%^V2VV%^XYY2XY%> VSUF-I-POTKOND_AD ; !!= * @CODE@  (6) +V+Pot+Sg3: lääulež
!+V:%^V2VV%^VOWRaise%^PAL%^XYY2XY%> VSUF-POTENTIAL_ED ; = * @CODE@  (6) +V+Pot+Sg3: lääulež
!! Ind+Prt+Sg3, Ind+Prt+Pl1, Ind+Prt+Pl2, Pot,

!! Weak-LowVowel-No-palatalization
!+V:%^V2VV%^XYY2XY VSUF-CONDITIONAL_ED ; = * @CODE@ (7) +V+Imprt+Sg3: lääulas
!! Ind+Prs+Sg1, Ind+Prs+Sg2, Cond,  Imprt+Sg3

!! ExtraStrong-RaisedVowel-No-palatalization
+V:%^VOWRaise VSUF-IMPRT-CONNEGII_AD ; !!= * @CODE@ (8) +V+Imprt+ConNegII: laullu
!! Imprt+ConNeg, Pass+PrfPrc

!! ExtraStrong-LowVowel-No-palatalization
+V: VSUF-IMPRT-PL3_AD ; !!= * @CODE@ (9) Ind.Imprt.Pl3: läullaz
!! Imprt+Pl3

!! (10) Allegro for incoative: läul»
+V+Der/škueʹtted:%^XYY2XY»škuätt V_PUEʹTTED ;

LEXICON V_SÄRNNAD !!= * @CODE@ särnnad:särnn
! ** Yaml: __V-särnnad__
! In the lexicon ''@LEXNAME@''
! särnnad:särnn
! 1. The Ind.Prs.Sg3 form is a single syllable
! 1.2 The infinitive ends in ad
! 1.2.1 Has gradation
! 1.2.1.1 Monophthong 
! Stem is strong
! one vowel
! FORMS
! (1)  Inf: särnnad
! (2)  Ind.Prs.Sg3: särnn
! (3)  Ind.Prs.Pl3: särnna
! (4)  Ind.Prt.Pl3: sarnnu
! (5)  Ind.Imprt.Sg2: säärn
! (6)  Ind.Pot.Sg3: säärnež
! (7)  Ind.Imprt.Sg3: säärnas
! (8)  Imprt.13.ConNeg: sarnnu
! (9) Ind.Imprt.Pl3: särnnaz
! (10) Allegro for incoative: särnn»

+V: VSUF-INF_AD ; !1 särnnad
+V: VSUF-IND-PRS-SG3_AD ; !2 särnn
+V+Ind+Prs+Pl3:%>a K ; !3 särnna
+V:%^VOWRaise%> VSUF-IND-PRT-PL3_AD ; !4 sarnnu
+V:%^V2VV%^XYY2XY%> VSUF-I-POTKOND_AD ; !5 säärn-
+V+Pot+Sg3:%^V2VV%^XYY2XY%>ež	K ; !6 säärnež
+V+Imprt+Sg3:%^V2VV%^XYY2XY%>as K ; !7 säärnas
+V:%^VOWRaise VSUF-IMPRT-CONNEGII_AD ; !8 sarnnu
+V: VSUF-IMPRT-PL3_AD ; ! särnn-
+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:%^XYY2XY»škuätt V_PUEʹTTED ;

LEXICON V_KÄIDDAD !!= * @CODE@ käiddad:käidd
! ** Yaml: __V-käiddad__
! In the lexicon ''@LEXNAME@''
! käiddad:käidd
! 1. The Ind.Prs.Sg3 form is a single syllable
! 1.2 The infinitive ends in ad
! 1.2.1 Has gradation
! 1.2.1.1 Monophthong 
! Stem is strong
! one vowel
! FORMS
! (1)  Inf: käiddad
! (2)  Ind.Prs.Sg3: käidd
! (3)  Ind.Prs.Pl3: käidda
! (4)  Ind.Prt.Pl3: kaiddu
! (5)  Ind.Imprt.Sg2: kääid
! (6)  Ind.Pot.Sg3: kääidež
! (7)  Ind.Imprt.Sg3: kääidas
! (8)  Imprt.13.ConNeg: kaiddu
! (9) Ind.Imprt.Pl3: käiddaz
! (10) Allegro for incoative: käidd»

+V: VSUF-INF_AD ; !1 käiddad
+V: VSUF-IND-PRS-SG3_AD ; !2 käidd
+V+Ind+Prs+Pl3:%>a K ; !3 käidda
+V:%^VOWRaise%> VSUF-IND-PRT-PL3_AD ; !4 kaiddu
+V:%^V2VV%^XYY2XY%> VSUF-I-POTKOND_AD ; !5 kääid-
+V+Pot+Sg3:%^V2VV%^XYY2XY%>ež	K ; !6 kääidež
+V+Imprt+Sg3:%^V2VV%^XYY2XY%>as K ; !7 kääidas
+V:%^VOWRaise VSUF-IMPRT-CONNEGII_AD ; !8 kaiddu
+V: VSUF-IMPRT-PL3_AD ; ! käidd-
+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:%^XYY2XY»škuätt V_PUEʹTTED ;

LEXICON V_KUÄIVVAD !!= * @CODE@ kuäivvad:kuäivv
! ** Yaml: __V-kuaeivvad__
! In the lexicon ''@LEXNAME@''
! kuäivvad:kuäivv
! 1. The Ind.Prs.Sg3 form is a single syllable
! 1.2 The infinitive ends in ad
! 1.2.1 Has gradation
! 1.2.1.1 Monophthong 
! Stem is strong
! one vowel
! FORMS
! (1)  Inf: kuäivvad
! (2)  Ind.Prs.Sg3: kuäivv
! (3)  Ind.Prs.Pl3: kuäivva
! (4)  Ind.Prt.Pl3: kuaivvu
! (5)  Ind.Imprt.Sg2: kuäiv
! (6)  Ind.Pot.Sg3: kuäivež
! (7)  Ind.Imprt.Sg3: kuäivas
! (8)  Imprt.13.ConNeg: kuaivvu
! (9) Ind.Imprt.Pl3: kuäivvaz
! (10) Allegro for incoative: kuäivv»

+V: VSUF-INF_AD ; !1 kuäivvad
+V: VSUF-IND-PRS-SG3_AD ; !2 kuäivv
+V+Ind+Prs+Pl3:%>a K ; !3 kuäivva
+V:%^VOWRaise%> VSUF-IND-PRT-PL3_AD ; !4 kuaivvu
+V:%^XYY2XY%> VSUF-I-POTKOND_AD ; !5 kuäiv-
+V+Pot+Sg3:%^XYY2XY%>ež	K ; !6 kuäivež
+V+Imprt+Sg3:%^XYY2XY%>as K ; !7 kuäivas
+V:%^VOWRaise VSUF-IMPRT-CONNEGII_AD ; !8 kuaivvu
+V: VSUF-IMPRT-PL3_AD ; ! kuäivv-
+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:%^XYY2XY»škuätt V_PUEʹTTED ;

LEXICON V_KUÄSTTAD !!= * @CODE@ kuästtad:kuästt
! ** Yaml: __V-kuaeivvad__
! In the lexicon ''@LEXNAME@''
! kuästtad:kuästt
! 1. The Ind.Prs.Sg3 form is a single syllable
! 1.2 The infinitive ends in ad
! 1.2.1 Has gradation
! 1.2.1.1 Monophthong 
! Stem is strong
! one vowel
! FORMS
! (1)  Inf: kuästtad
! (2)  Ind.Prs.Sg3: kuästt
! (3)  Ind.Prs.Pl3: kuästta
! (4)  Ind.Prt.Pl3: kuasttu
! (5)  Ind.Imprt.Sg2: kuäst
! (6)  Ind.Pot.Sg3: kuästež
! (7)  Ind.Imprt.Sg3: kuästas
! (8)  Imprt.13.ConNeg: kuasttu
! (9) Ind.Imprt.Pl3: kuästtaz
! (10) Allegro for incoative: kuästt»

+V: VSUF-INF_AD ; !1 kuästtad
+V: VSUF-IND-PRS-SG3_AD ; !2 kuästt
+V+Ind+Prs+Pl3:%>a K ; !3 kuästta
+V:%^VOWRaise%> VSUF-IND-PRT-PL3_AD ; !4 kuasttu
+V:%^XYY2XY%> VSUF-I-POTKOND_AD ; !5 kuäst-
+V+Pot+Sg3:%^XYY2XY%>ež	K ; !6 kuästež
+V+Imprt+Sg3:%^XYY2XY%>as K ; !7 kuästas
+V:%^VOWRaise VSUF-IMPRT-CONNEGII_AD ; !8 kuasttu
+V: VSUF-IMPRT-PL3_AD ; ! kuästt-
+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:%^XYY2XY»škuätt V_PUEʹTTED ;

LEXICON V_MEINNAD !!= * @CODE@ meinnad:meinn
! ** Yaml: __V-meinnad__
! In the lexicon ''@LEXNAME@''
! meinnad:meinn
! 1. The Ind.Prs.Sg3 form is a single syllable
! 1.2 The infinitive ends in ad
! 1.2.1 Has gradation
! 1.2.1.1 Monophthong 
! Stem is strong
! one vowel
! FORMS
! (1)  Inf: meinnad
! (2)  Ind.Prs.Sg3: meinn
! (3)  Ind.Prs.Pl3: meinna
! (4)  Ind.Prt.Pl3: meinnu
! (5)  Ind.Imprt.Sg2: meein
! (6)  Ind.Pot.Sg3: meeinež
! (7)  Ind.Imprt.Sg3: meeinas
! (8)  Imprt.13.ConNeg: meinnu
! (9) Ind.Imprt.Pl3: meinnaz
! (10) Allegro for incoative: meinn»

+V: VSUF-INF_AD ; !1 meinnad
+V: VSUF-IND-PRS-SG3_AD ; !2 meinn
+V+Ind+Prs+Pl3:%>a K ; !3 meinna
+V:%> VSUF-IND-PRT-PL3_AD ; !4 meinnu
+V:%^V2VV%^XYY2XY%> VSUF-I-POTKOND_AD ; !5 meein-
+V+Pot+Sg3:%^V2VV%^XYY2XY%>ež	K ; !6 meeinež
+V+Imprt+Sg3:%^V2VV%^XYY2XY%>as K ; !7 meeinas
+V: VSUF-IMPRT-CONNEGII_AD ; !8 meinnu
+V: VSUF-IMPRT-PL3_AD ; ! meinn-
+V+Der/mõš:»mõš N_LOOKKMÕŠ ;
+V+Der/škueʹtted:%^XYY2XY»škuätt V_PUEʹTTED ;

!! !CLASS 1 HIGH VOWEL, PALATALIZATION
LEXICON V_MUʹTTED !!= * @CODE@ muʹtted:mutt
! seek parallel in RÕVVÂD
! FORMS

!! Strong-HighVowel-Palatalization
+V:%^PAL VSUF-INF_ED ; !!= * @CODE@ (1) +V+Inf: muʹtted
!! V+Inf, Ind+Prs+Pl1, Ind+Prs+Pl2, Imprt+Pl1, Imprt+Pl2
!! Actio, ActEss, PrsPrc

!! Strong-LoweredVowel-No-palatalization
+V:%^VOWLower VSUF-IND-PRS-SG3_ED ; !!= * @CODE@ (2) +V+Ind+Prs+Sg3: mott
!! +V+Ind+Prs+Sg3, Ind.Prt.ConNeg, PrfPrc

!! ExtraStrong-LoweredVowel-Palatalization
+V+Ind+Prs+Pl3:%^VOWLower%^PAL%>e K ; !!= * @CODE@ (3) +V+Ind+Prs+Pl3: moʹtte
!! +V+Ind+Prs+Pl3


!! ExtraStrong-RaisedVowel-Palatalization
+V:%^PAL%> VSUF-IND-PRT-PL3_ED ; !!= * @CODE@ (4) +V+Ind+Prt+Pl3: muʹtte
!! +V+Ind+Prt+Pl3,  Ind+Prt+Sg1, Ind+Prt+Sg2, Ind+Prt+Sg4

!! Weak-StableV-Palatalization 
+V:%^V2VV%^PAL VSUF-IND-PRS-CONNEG_ED ; !!= * @CODE@ (5) muuʹtt-
!! Imprt+Sg2, Ind+Prs+ConNeg, Ind+Prs+Sg4, VAbess, GerTemp, GerInstr

!! Weak-RaisedVowel-Palatalization
+V:%^V2VV%^PAL%> VSUF-POTENTIAL_ED ; !!= * @CODE@  (6) +V+Imprt+Sg2: muuʹtt
!! Ind+Prs+ConNeg, Ind+Prt+Sg3, Ind+Prt+Pl1, Ind+Prt+Pl2, Pot,


!! Weak-LoweredVowel-No-palatalization
+V:%^V2VV%^VOWLower VSUF-CONDITIONAL_ED ; !!= * @CODE@ (7) +V+Imprt+Sg3: moottas
!! Ind+Prs+Sg1, Ind+Prs+Sg2, Cond,  Imprt+Sg3

!! ExtraStrong-RaisedVowel-No-palatalization
+V: VSUF-IMPRT-CONNEGII_ED ; !!= * @CODE@ (8) +V+Imprt+ConNegII: muttu
!! Imprt+ConNeg, Pass+PrfPrc

!! ExtraStrong-LoweredVowel-No-palatalization
+V:%^VOWLower VSUF-IMPRT-PL3_ED ; !!= * @CODE@ (9) Ind.Imprt.Pl3: mottaz
!! Imprt+Pl3

! (10) Allegro for incoative: muʹt»
+V+Der/škueʹtted:%^PAL%^CC2C»škuätt V_PUEʹTTED ; 


LEXICON V_LAʹDDED !!= * @CODE@ laʹdded:ladd
! seek parallel in VIǮǮÂD
! FORMS
!! Strong-HighVowel-Palatalization
+V:%^PAL VSUF-INF_ED ; !!= * @CODE@ (1) +V+Inf: laʹdded
!! V+Inf, Ind+Prs+Pl1, Ind+Prs+Pl2, Imprt+Pl1, Imprt+Pl2
!! Actio, ActEss, PrsPrc

!! Strong-LoweredVowel-No-palatalization
+V:%^VOWLower VSUF-IND-PRS-SG3_ED ; !!= * @CODE@ (2) +V+Ind+Prs+Sg3: lädd
!! +V+Ind+Prs+Sg3, Ind.Prt.ConNeg, PrfPrc

!! ExtraStrong-LoweredVowel-Palatalization
+V+Ind+Prs+Pl3:%^VOWLower%^PAL%>e K ; !!= * @CODE@ (3) +V+Ind+Prs+Pl3: läʹdde
!! +V+Ind+Prs+Pl3

!! ExtraStrong-RaisedVowel-Palatalization
+V:%^PAL%> VSUF-IND-PRT-PL3_ED ; !!= * @CODE@ (4) +V+Ind+Prt+Pl3: laʹdde
!! +V+Ind+Prt+Pl3,  Ind+Prt+Sg1, Ind+Prt+Sg2, Ind+Prt+Sg4

!! Weak-StableV-Palatalization 
+V:%^V2VV%^PAL VSUF-IND-PRS-CONNEG_ED ; !!= * @CODE@ (5) laaʹdd-
!! Imprt+Sg2, Ind+Prs+ConNeg, Ind+Prs+Sg4, VAbess, GerTemp, GerInstr

!! Weak-RaisedVowel-Palatalization
+V:%^V2VV%^PAL%> VSUF-POTENTIAL_ED ; !!= * @CODE@  (6) +V+Imprt+Sg2: laaʹdd
!! Ind+Prs+ConNeg, Ind+Prt+Sg3, Ind+Prt+Pl1, Ind+Prt+Pl2, Pot,

!! Weak-LoweredVowel-No-palatalization
+V:%^V2VV%^VOWLower VSUF-CONDITIONAL_ED ; !!= * @CODE@ (7) +V+Imprt+Sg3: lääddas
!! Ind+Prs+Sg1, Ind+Prs+Sg2, Cond,  Imprt+Sg3

!! ExtraStrong-RaisedVowel-No-palatalization
+V: VSUF-IMPRT-CONNEGII_ED ; !!= * @CODE@ (8) +V+Imprt+ConNegII: laddu
!! Imprt+ConNeg, Pass+PrfPrc

!! ExtraStrong-LoweredVowel-No-palatalization
+V:%^VOWLower VSUF-IMPRT-PL3_ED ; !!= * @CODE@ (9) Ind.Imprt.Pl3: läddaz
!! Imprt+Pl3

! (10) Allegro for incoative: laʹd»
+V+Der/škueʹtted:%^PAL%^CC2C»škuätt V_PUEʹTTED ;

LEXICON V_PIEʹǨǨED !!= * @CODE@ pieʹǩǩed:peäkk
! Like JURDDED
! 1. VERBS WITH SINGLE-SYLLABLE Ind.Prs.Sg3 (2009: ???)
! 1.3 Inf in palatalization + ed (vowel shift)
!   Raised: Inf, Ind.Prt.Pl3, Imprt.Sg2, Pot.Sg3
!   Lowered: Ind.Prs.Sg3, Ind.Prs.Pl3, Imprt.Sg3
! 1.3.1 Has Palatalization
! 1.3.1[1] (Palatalization pattern)
!   Palatalized: Inf, Ind.Prs.Pl3, Ind.Prt.Pl3, Pot.Sg3
!   Not Palatalized: Ind.Prs.Sg3, Imprt.Sg2, Imprt.Sg3
! 1.3.1[1].1 Lacks Specifically Pedagogical Gradation
! 1.3.1[1].1.1 Has Orthographic Gradation
! 1.3.1[1].1.1[231] (Diphthong + Consonant and Consonant Geminate alternation)
!   Strong grade: 
!   Extra strong grade: 
!   Weak grade: 
!! * Yaml: __V-piekked__
! FORMS
!! Strong-HighVowel-Palatalization
+V:%^VOWRaise%^PALẸ%^PAL VSUF-INF_ED ; !!= * @CODE@ (1) +V+Inf: piẹʹǩǩed
!! V+Inf, Ind+Prs+Pl1, Ind+Prs+Pl2, Imprt+Pl1, Imprt+Pl2
!! Actio, ActEss, PrsPrc

!! Strong-LoweredVowel-No-palatalization
+V: VSUF-IND-PRS-SG3_ED ; !!= * @CODE@ (2) +V+Ind+Prs+Sg3: peäkk
!! +V+Ind+Prs+Sg3, Ind.Prt.ConNeg, PrfPrc

!! ExtraStrong-LoweredVowel-Palatalization
+V+Ind+Prs+Pl3:%^PAL%>e K ; !!= * @CODE@ (3) +V+Ind+Prs+Pl3: peäʹǩˈǩe
!! +V+Ind+Prs+Pl3

!! ExtraStrong-RaisedVowel-Palatalization
+V:%^VOWRaise%^PALE%^PAL%^CC2CCC%> VSUF-IND-PRT-PL3_ED ; !!= * @CODE@ (4) +V+Ind+Prt+Pl3: pieʹǩˈǩe
!! +V+Ind+Prt+Pl3,  Ind+Prt+Sg1, Ind+Prt+Sg2, Ind+Prt+Sg4

!! Weak-StableV-Palatalization 
+V:%^VOWRaise%^PALẸ%^PAL%^KK2GG VSUF-IND-PRS-CONNEG_ED ; !!= * @CODE@ (5) piẹʹjj-
!! Imprt+Sg2, Ind+Prs+ConNeg, Ind+Prs+Sg4, VAbess, GerTemp, GerInstr

!! Weak-RaisedVowel-Palatalization
+V:%^VOWRaise%^PALE%^PAL%> VSUF-POTENTIAL_ED ; !!= * @CODE@  (6) +V+Pot+Sg3: !! pieʹjjež
!! Ind+Prt+Sg3, Ind+Prt+Pl1, Ind+Prt+Pl2, Pot,

!! Weak-LoweredVowel-No-palatalization
+V:%^KK2GG VSUF-CONDITIONAL_ED ; !!= * @CODE@ (7) +V+Imprt+Sg3: peäǥǥas
!! Ind+Prs+Sg1, Ind+Prs+Sg2, Cond,  Imprt+Sg3

!! ExtraStrong-RaisedVowel-No-palatalization
+V:%^VOWRaise%^CC2CCC VSUF-IMPRT-CONNEGII_ED ; !!= * @CODE@ (8) +V+Imprt+ConNegII: piâkˈku
!! Imprt+ConNeg, Pass+PrfPrc

!! ExtraStrong-LoweredVowel-No-palatalization
+V:%^CC2CCC VSUF-IMPRT-PL3_ED ; !!= * @CODE@ (9) Ind.Imprt.Pl3: peäkˈkaz
!! Imprt+Pl3

! (10) Allegro for incoative: pieʹj»
+V+Der/škueʹtted:%^VOWRaise%^PALE%^Allegro%^PAL%^KK2G»škuätt V_PUEʹTTED ;

! (11)  Ger.Temp.PxSg3: peäʹjjeeʹstes

LEXICON V_JUEʹǨǨED !!= * @CODE@ jueʹǩǩed:juâkk
! Like JURDDED
! 1. VERBS WITH SINGLE-SYLLABLE Ind.Prs.Sg3 (2009: ???)
! 1.3 Inf in palatalization + ed (vowel shift)
!   Raised: Inf, Ind.Prt.Pl3, Imprt.Sg2, Pot.Sg3
!   Lowered: Ind.Prs.Sg3, Ind.Prs.Pl3, Imprt.Sg3
! 1.3.1 Has Palatalization
! 1.3.1[1] (Palatalization pattern)
!   Palatalized: Inf, Ind.Prs.Pl3, Ind.Prt.Pl3, Pot.Sg3
!   Not Palatalized: Ind.Prs.Sg3, Imprt.Sg2, Imprt.Sg3
! 1.3.1[1].1 Lacks Specifically Pedagogical Gradation
! 1.3.1[1].1.1 Has Orthographic Gradation
! 1.3.1[1].1.1[231] (Diphthong + Consonant and Consonant Geminate alternation)
!   Strong grade: 
!   Extra strong grade: 
!   Weak grade: 
!! * Yaml: __V-juekked__
! FORMS
!! Strong-HighVowel-Palatalization
+V:%^VOWRaise%^PALE%^PAL VSUF-INF_ED ; !!= * @CODE@ (1) +V+Inf: jueʹǩǩed
!! V+Inf, Ind+Prs+Pl1, Ind+Prs+Pl2, Imprt+Pl1, Imprt+Pl2
!! Actio, ActEss, PrsPrc

!! Strong-LoweredVowel-No-palatalization
+V: VSUF-IND-PRS-SG3_ED ; !!= * @CODE@ (2) +V+Ind+Prs+Sg3: juâkk
!! +V+Ind+Prs+Sg3, Ind.Prt.ConNeg, PrfPrc

!! ExtraStrong-LoweredVowel-Palatalization
+V+Ind+Prs+Pl3:%^PALẸ%^PAL%>e K ; !!= * @CODE@ (3) +V+Ind+Prs+Pl3: juẹʹǩˈǩe
!! +V+Ind+Prs+Pl3

!! ExtraStrong-RaisedVowel-Palatalization
+V:%^VOWRaise%^PALE%^PAL%^CC2CCC%> VSUF-IND-PRT-PL3_ED ; !!= * @CODE@ (4) +V+Ind+Prt+Pl3: jueʹǩˈǩe
!! +V+Ind+Prt+Pl3,  Ind+Prt+Sg1, Ind+Prt+Sg2, Ind+Prt+Sg4

!! Weak-StableV-Palatalization 
+V:%^VOWRaise%^PALE%^PAL%^KK2GG VSUF-IND-PRS-CONNEG_ED ; !!= * @CODE@ (5) jueʹjj-
!! Imprt+Sg2, Ind+Prs+ConNeg, Ind+Prs+Sg4, VAbess, GerTemp, GerInstr

!! Weak-RaisedVowel-Palatalization
+V:%^VOWRaise%^PALE%^PAL%> VSUF-POTENTIAL_ED ; !!= * @CODE@  (6) +V+Pot+Sg3: !! jueʹjjež
!! Ind+Prt+Sg3, Ind+Prt+Pl1, Ind+Prt+Pl2, Pot, 

!! Weak-LoweredVowel-No-palatalization
+V:%^KK2GG VSUF-CONDITIONAL_ED ; !!= * @CODE@ (7) +V+Imprt+Sg3: juâǥǥas
!! Ind+Prs+Sg1, Ind+Prs+Sg2, Cond,  Imprt+Sg3

!! ExtraStrong-RaisedVowel-No-palatalization
+V:%^VOWRaise%^CC2CCC VSUF-IMPRT-CONNEGII_ED ; !!= * @CODE@ (8) +V+Imprt+ConNegII: juõkˈku
!! Imprt+ConNeg, Pass+PrfPrc

!! ExtraStrong-LoweredVowel-No-palatalization
+V:%^CC2CCC VSUF-IMPRT-PL3_ED ; !!= * @CODE@ (9) Ind.Imprt.Pl3: juâkˈkaz
!! Imprt+Pl3

!! (10) Allegro for incoative: jueʹj»
+V+Der/škueʹtted:%^VOWRaise%^PALE%^Allegro%^PAL%^KK2G»škuätt V_PUEʹTTED ;
!! Secondary allegro for incoative: juâǥ»
+Use/NG+V+Der/škueʹtted:%^Allegro%^KK2G»škuätt V_PUEʹTTED ;


LEXICON V_JUʹRDDED !!= * @CODE@ juʹrdded:jurdd
! seek parallel in TOBDDÂD
! juʹrdded:jurdd
! FORMS
! (1) +V+Inf: juʹrdded
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: jordd
! (3) +V+Ind+Prs+Pl3: joʹrdde
! (4) +V+Ind+Prt+Pl3: juʹrdde
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: juuʹrd
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: juuʹrdež
! (7) +V+Imprt+Sg3: joordas
! (8) +V+Imprt+ConNegII: jurddu
!    Pass.PrfPrc
! juʹrdded:jurdd
+V:%^PAL VSUF-INF_ED ; !1
+V:%^VOWLower VSUF-IND-PRS-SG3_ED ; !2 jordd
!! Ind+Prs+Sg3, 
+V+Ind+Prs+Pl3:%^VOWLower%^PAL%>e K ; !3 joʹrdde
!! Ind+Prs+Pl3
+V:%^PAL VSUF-IND-PRT-PL3_ED ; !4 juʹrdde
!! Ind+Prt+Pl3, 
+V:%^V2VV%^VOWLower%^XYY2XY%> VSUF-CONDITIONAL_ED ; !5 ?2 ?7 joord
!! Ind+Prs+Sg1, Ind+Prs+Sg2, Cond
+V:%^V2VV%^PAL%^XYY2XY VSUF-POTENTIAL_ED ; !6 juuʹrd-
!! Ind+Prs+Sg4, Ind+Prt+Sg3, Ind+Prt+Pl1, Ind+Prt+Pl2
!! Pot, Imprt+Sg2, Ind+Prs+ConNeg
+V+Imprt+Sg3:%^V2VV%^VOWLower%^XYY2XY%>as K ; !7 joordas
!! Imprt+Sg3
+V:%^V2VV%^PAL%^XYY2XY VSUF-IND-PRS-CONNEG_ED ; !!= * @CODE@ juuʹrd
+V: VSUF-IMPRT-CONNEGII_ED ; !8

+V+Der/škueʹtted:%^PAL%^XYY2XY»škuätt V_PUEʹTTED ;

LEXICON V_PUʹHTTED !!= * @CODE@ puʹhtted:puhtt
! seek parallel in TOBDDÂD
! puʹhtted:jurdd
! FORMS
! (1) +V+Inf: puʹhtted
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: jordd
! (3) +V+Ind+Prs+Pl3: joʹrdde
! (4) +V+Ind+Prt+Pl3: puʹhtte
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: juuʹrd
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: juuʹrdež
! (7) +V+Imprt+Sg3: joordas
! (8) +V+Imprt+ConNegII: jurddu
!    Pass.PrfPrc
! puʹhtted:puhtt
!! Strong-HighVowel-Palatalization
+V:%^PAL VSUF-INF_ED ; !!= * @CODE@ (1) +V+Inf: puʹhtted
!! V+Inf, Ind+Prs+Pl1, Ind+Prs+Pl2, Imprt+Pl1, Imprt+Pl2
!! Actio, ActEss, PrsPrc

!! Strong-LoweredVowel-No-palatalization
+V:%^VOWLower VSUF-IND-PRS-SG3_ED ; !!= * @CODE@ (2) +V+Ind+Prs+Sg3: pohtt
!! +V+Ind+Prs+Sg3, Ind.Prt.ConNeg, PrfPrc

!! ExtraStrong-LoweredVowel-Palatalization
+V+Ind+Prs+Pl3:%^VOWLower%^PAL%>e K ; !!= * @CODE@ (3) +V+Ind+Prs+Pl3: poʹhtte
!! +V+Ind+Prs+Pl3

!! ExtraStrong-RaisedVowel-Palatalization
+V:%^PAL VSUF-IND-PRT-PL3_ED ; !!= * @CODE@ (4) +V+Ind+Prt+Pl3: puʹhtte
!! +V+Ind+Prt+Pl3,  Ind+Prt+Sg1, Ind+Prt+Sg2, Ind+Prt+Sg4

!! Weak-StableV-Palatalization
+V:%^V2VV%^VOWLower%^XYY2VY VSUF-IND-PRS-CONNEG_ED ; !!= * @CODE@ (5) 
!! Imprt+Sg2, Ind+Prs+ConNeg, Ind+Prs+Sg4, VAbess, GerTemp, GerInstr

!! Weak-RaisedVowel-Palatalization
+V:%^V2VV%^PAL%^XYY2VY VSUF-POTENTIAL_ED ; !!= * @CODE@ (6) +V+Pot+Sg3: puuʹtež
!! Ind+Prt+Sg3, Ind+Prt+Pl1, Ind+Prt+Pl2, Pot,

!! Weak-LoweredVowel-No-palatalization
+V:%^V2VV%^VOWLower%^XYY2VY%> VSUF-CONDITIONAL_ED ; !!= * @CODE@ (7) +V+Imprt+Sg3: pooutas
!! Ind+Prs+Sg1, Ind+Prs+Sg2, Cond,  Imprt+Sg3

!! ExtraStrong-RaisedVowel-No-palatalization
+V: VSUF-IMPRT-CONNEGII_ED ; !!= * @CODE@ (8) +V+Imprt+ConNegII: puhttu
!! Imprt+ConNeg, Pass+PrfPrc

!! ExtraStrong-LoweredVowel-No-palatalization
+V:%^VOWLower VSUF-IMPRT-PL3_ED ; !!= * @CODE@ (9) Ind.Imprt.Pl3: pohttaz ??
!! Imprt+Pl3

!! (10) Allegro for incoative: puʹht»
+V+Der/škueʹtted:%^PAL%^XYY2XY»škuätt V_PUEʹTTED ;

LEXICON V_UʹVDDED !!= * @CODE@ uʹvdded:uvdd
! inf: ed
! ** Yaml: __V-uvdded__
! FORMS
!! Strong-HighVowel-Palatalization
+V:%^PAL VSUF-INF_ED ; !!= * @CODE@ (1) +V+Inf: uʹvdded
!! V+Inf, Ind+Prs+Pl1, Ind+Prs+Pl2, Imprt+Pl1, Imprt+Pl2
!! Actio, ActEss, PrsPrc

!! Strong-LoweredVowel-No-palatalization xyy2Vyy
+V:%^VOWLower%^XYY2VYY VSUF-IND-PRS-SG3_ED ; !!= * @CODE@ (2) +V+Ind+Prs+Sg3: oudd
!! +V+Ind+Prs+Sg3, Ind.Prt.ConNeg, PrfPrc

!! ExtraStrong-LoweredVowel-Palatalization xyy2Vyy
+V+Ind+Prs+Pl3:%^VOWLower%^PAL%^XYY2VYY%>e K ; !!= * @CODE@ (3) +V+Ind+Prs+Pl3: ouʹdde
!! +V+Ind+Prs+Pl3

!! ExtraStrong-RaisedVowel-Palatalization
+V:%^PAL%> VSUF-IND-PRT-PL3_ED ; !!= * @CODE@ (4) +V+Ind+Prt+Pl3: uʹvdde
!! +V+Ind+Prt+Pl3,  Ind+Prt+Sg1, Ind+Prt+Sg2, Ind+Prt+Sg4

!! Weak-StableV-Palatalization
+V:%^PAL%^XYY2VY VSUF-IND-PRS-CONNEG_ED ;  !!= * @CODE@ (5) uuʹd
!! Imprt+Sg2, Ind+Prs+ConNeg, Ind+Prs+Sg4, VAbess, GerTemp, GerInstr

!! Weak-RaisedVowel-Palatalization
+V:%^PAL%^XYY2VY%> VSUF-POTENTIAL_ED ; !!= * @CODE@ (6) +V+Pot+Sg3: !! uuʹdež
!! Ind+Prt+Sg3, Ind+Prt+Pl1, Ind+Prt+Pl2, Pot,

!! Weak-LoweredVowel-No-palatalization
+V:%^V2VV%^VOWLower%^XYY2VY%> VSUF-CONDITIONAL_ED ; !!= * @CODE@ (7) +V+Imprt+Sg3: ooudas
!! Ind+Prs+Sg1, Ind+Prs+Sg2, Cond,  Imprt+Sg3

!! ExtraStrong-RaisedVowel-No-palatalization
+V: VSUF-IMPRT-CONNEGII_ED ; !!= * @CODE@ (8) +V+Imprt+ConNegII: uvddu
!! Imprt+ConNeg, Pass+PrfPrc

!! ExtraStrong-LoweredVowel-No-palatalization xyy2Vyy
+V:%^VOWLower%^XYY2VYY VSUF-IMPRT-PL3_ED ; !!= * @CODE@ (9) Ind.Imprt.Pl3: ouddaz
!! Imprt+Pl3

!! (10) Allegro for incoative: uʹvd»
+V+Der/škueʹtted:%^PAL%^XYY2XY»škuätt V_PUEʹTTED ;

LEXICON V_TIEʹTTED !!= * @CODE@ tieʹtted:teâtt
! In the lexicon ''@LEXNAME@'' the verb __tieʹtted__ has the form __tiõtt__
!! * Yaml: __V-tietted__
!! Strong-HighVowel-Palatalization
+V:%^VOWRaise%^PALE%^PAL VSUF-INF_ED ; !!= * @CODE@ (1) +V+Inf: tieʹtted
!! V+Inf, Ind+Prs+Pl1, Ind+Prs+Pl2, Imprt+Pl1, Imprt+Pl2
!! Actio, ActEss, PrsPrc

!! Strong-LoweredVowel-No-palatalization
+V: VSUF-IND-PRS-SG3_ED ; !!= * @CODE@ (2) +V+Ind+Prs+Sg3: teâtt
!! +V+Ind+Prs+Sg3, Ind.Prt.ConNeg, PrfPrc

!! ExtraStrong-LoweredVowel-Palatalization
+V+Ind+Prs+Pl3:%^PALÄ%^PAL%^CC2CCC%>e K ; !!= * @CODE@ (3) +V+Ind+Prs+Pl3: teäʹtˈte
!! +V+Ind+Prs+Pl3

!! ExtraStrong-RaisedVowel-Palatalization
+V:%^VOWRaise%^PAL%^CC2CCC%> VSUF-IND-PRT-PL3_ED ; !!= * @CODE@ (4) +V+Ind+Prt+Pl3: tiõʹtˈte
!! +V+Ind+Prt+Pl3,  Ind+Prt+Sg1, Ind+Prt+Sg2, Ind+Prt+Sg4

!! Weak-StableV-Palatalization
+V:%^VOWRaise%^PALE%^PAL%^CC2C VSUF-IND-PRS-CONNEG_ED ; !!= * @CODE@ (5) tieʹđ-
!! Imprt+Sg2, Ind+Prs+ConNeg, Ind+Prs+Sg4, VAbess, GerTemp, GerInstr

!! Weak-RaisedVowel-Palatalization
+V:%^VOWRaise%^PALE%^PAL%^CC2C%> VSUF-POTENTIAL_ED ; !!= * @CODE@  (6) +V+Pot+Sg3: !! tieʹđež
!! Ind+Prt+Sg3, Ind+Prt+Pl1, Ind+Prt+Pl2, Pot,

!! Weak-LoweredVowel-No-palatalization
+V:%^CC2C VSUF-CONDITIONAL_ED ; !!= * @CODE@ (7) +V+Imprt+Sg3: teâđas
!! Ind+Prs+Sg1, Ind+Prs+Sg2, Cond,  Imprt+Sg3

!! ExtraStrong-RaisedVowel-No-palatalization
+V:%^VOWRaise%^CC2CCC VSUF-IMPRT-CONNEGII_ED ; !!= * @CODE@ (8) +V+Imprt+ConNegII: tiõtˈtu
!! Imprt+ConNegII, Pass+PrfPrc

!! ExtraStrong-LoweredVowel-No-palatalization
+V:%^CC2CCC VSUF-IMPRT-PL3_ED ; !!= * @CODE@ (9) Ind.Imprt.Pl3: teâtˈtaz
!! Imprt+Pl3

!! (10) Allegro for incoative: tieʹđ»
+V+Der/škueʹtted:%^VOWRaise%^PALE%^Allegro%^PAL%^CC2C»škuätt V_PUEʹTTED ;


!+V+Imprt:			TIETTED-IMPRT ;
+V+Imprt+Sg2:%^VOWRaise%^PALE%^PAL%^CC2C K	; ! tieʹđ

+V+PrsPrc:%^VOWRaise%^PAL%>i K ; !vuõʹlǧǧi
+V+PrsPrc:%^PAL%>i K ; !vueʹlǧǧi




LEXICON V_SUIʹTTED !!= * @CODE@ suiʹtted:suitt
! seek parallell in AIBBÂD
! FORMS
! (1) +V+Inf: suiʹtted
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: soitt
! (3) +V+Ind+Prs+Pl3: soiʹtte
! (4) +V+Ind+Prt+Pl3: suiʹtte
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: vueiʹn
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: suuiʹtež
! (7) +V+Imprt+Sg3: ???sooitas
! (8) +V+Imprt+ConNegII: ???suittu
!    Pass.PrfPrc
! suiʹtted:suitt
! Stem is Strong
! Diphthong
! Vowel raising
! NEEDS WORK
!(son soitt) ??
+V:%^VOWRaise%^PALE%^PAL VSUF-INF_ED ; !1
+V: VSUF-IND-PRS-SG3_ED ; !2
+V+Ind+Prs+Pl3:%^PAL%>e K ; !3
+V:%^VOWRaise%^PAL%> VSUF-IND-PRT-PL3_ED ; !4
+V:%^VOWRaise%^PAL%^XYY2XY%> VSUF-POTENTIAL_ED ; !5 vueiʹnet
+V:%^VOWBack%^PAL%^XYY2XY%> VSUF-POTENTIAL_ED ; !5 vuõiʹnet
+V:%^XYY2XY%> VSUF-CONDITIONAL_ED ; !5 ?2 ?7
+V:%^XYY2XY VSUF-IND-PRS-CONNEG_ED ;
+V+Pot+Sg3:%^VOWBack%^PAL%^XYY2XY%>ež   K ; !6
+V+Pot+Sg3:%^VOWRaise%^PALE%^PAL%^XYY2XY%>ež   K ; !6
+V+Imprt+Sg3:%^XYY2XY%>as K ; !7
+V:%^VOWBack VSUF-IMPRT-CONNEGII_ED ; !8



LEXICON V_VUEʹLǦǦED !!= * @CODE@ vueʹlǧǧed:vuâlgg
! FORMS
! (1) +V+Inf: vueʹlǧǧed
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: vuâlgg
! (3) +V+Ind+Prs+Pl3: vueʹlǧǧe
! (4) +V+Ind+Prt+Pl3: vuõʹlǧǧe
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: vueʹlj
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: vuõʹljež
! (7) +V+Imprt+Sg3: vuâlǥas
! (8) +V+Imprt+ConNegII: vuõlggu
!    Pass.PrfPrc
! vueʹlǧǧed:vuâlgg
+V:%^PALE%^PAL VSUF-INF_ED ; !1
+V: VSUF-IND-PRS-SG3_ED ; !2
+V+Ind+Prs+Pl3:%^PALE%^PAL%>e K ; !3
+V+Ind+Prs+Pl3:%^PALÄ%^PAL%>e K ; !3
+V:%^VOWRaise%^PAL%> VSUF-IND-PRT-PL3_ED ; !4

+V:%^VOWRaise%^PAL%^XYY2XY%> VSUF-POTENTIAL_ED ; !6
+V:%^PALE%^PAL%^XYY2XY%> VSUF-POTENTIAL_ED ; !6
+V:%^XYY2XY%> VSUF-CONDITIONAL_ED ; !5 ?2 ?7
+V:%^PALE%^PAL%^XYY2XY VSUF-IND-PRS-CONNEG_ED ;
+V+Imprt+Sg3:%^XYY2XY%>as K ; !7
+V:%^VOWRaise VSUF-IMPRT-CONNEGII_ED ; !8

+V+Der/škueʹtted:%^PALE%^PAL%^XYY2XY»škuätt V_PUEʹTTED ;

!! !CLASS 1 LOW VOWEL, PALATALIZATION
!! EVEN-SYLLABLE STEMS IN -ED

LEXICON V_SOLLÂʹTTED !!= * @CODE@ sollâʹtted:sollâtt
! seek parallel in TÄTTAD
! FORMS
! (1) +V+Inf: sollâʹtted
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: sollâtt
! (3) +V+Ind+Prs+Pl3: sollâʹtte
! (4) +V+Ind+Prt+Pl3: sollõʹtte
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: sollââʹtt
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: sollõõʹttež
! (7) +V+Imprt+Sg3: sollââttas
! (8) +V+Imprt+ConNegII: sollõttu
!    Pass.PrfPrc
! (9) Ind.Imprt.Pl3: sollâttaz
! (10) Allegro for incoative: sollâʹđ»

! sollâʹtted:sollâtt
+V:%^PAL VSUF-INF_ED ; !1
+V: VSUF-IND-PRS-SG3_ED ; !2
+V+Ind+Prs+Pl3:%^PAL%>e K ; !3
+V:%^VOWRaise%^PAL%> VSUF-IND-PRT-PL3_ED ; !4
+V:%^V2VV%^VOWRaise%^PAL%> VSUF-I-POTKOND_AD ; !5
+V+Pot+Sg3:%^V2VV%^VOWRaise%^PAL%>ež   K ; !6
+V+Imprt+Sg3:%^V2VV%>as K ; !7
+V:%^V2VV%^PAL VSUF-IND-PRS-CONNEG_ED ;
+V:%^VOWRaise VSUF-IMPRT-CONNEGII_ED ; !8
+V+Der/škueʹtted:%^PAL%^CC2C%>e»škuätt V_PUEʹTTED ; 



LEXICON V_JÅÅʹTTED !!= * @CODE@ jååʹtted:jåått
! FORMS
!! Strong-LowVowel-Palatalization
+V:%^PAL VSUF-INF_ED ; !!= * @CODE@ (1) +V+Inf: jååʹtted
!! V+Inf, Ind+Prs+Pl1, Ind+Prs+Pl2, Imprt+Pl1, Imprt+Pl2
!! Actio, ActEss, PrsPrc

!! Strong-LowVowel-No-palatalization
+V: VSUF-IND-PRS-SG3_ED ; !!= * @CODE@ (2) +V+Ind+Prs+Sg3: jåått
!! +V+Ind+Prs+Sg3, Ind.Prt.ConNeg, PrfPrc

!! ExtraStrong-LowVowel-Palatalization
+V+Ind+Prs+Pl3:%^VV2V%^PAL%>e K ; !!= * @CODE@ (3) +V+Ind+Prs+Pl3: jåʹtte
!! +V+Ind+Prs+Pl3

!! ExtraStrong-RaisedVowel-Palatalization
+V:%^VV2V%^VOWRaise%> VSUF-IND-PRT-PL3_ED ; !!= * @CODE@ (4) +V+Ind+Prt+Pl3: joʹtte
!! +V+Ind+Prt+Pl3,  Ind+Prt+Sg1, Ind+Prt+Sg2, Ind+Prt+Sg4

!! Weak-StableV-Palatalization
+V:%^PAL%^CC2C VSUF-IND-PRS-CONNEG_ED ; !!= * @CODE@ (5)
!! Imprt+Sg2, Ind+Prs+ConNeg, Ind+Prs+Sg4, VAbess, GerTemp, GerInstr

!! Weak-RaisedVowel-Palatalization
+V:%^VOWRaise%^PAL%^CC2C VSUF-POTENTIAL_ED ; !!= * @CODE@  (6) +V+Pot+Sg3: j
!! Ind+Prt+Sg3, Ind+Prt+Pl1, Ind+Prt+Pl2, Pot,

!! Weak-LowVowel-No-palatalization
+V:%^CC2C VSUF-CONDITIONAL_ED ; !!= * @CODE@ (7) +V+Imprt+Sg3: jååđas
!! Ind+Prs+Sg1, Ind+Prs+Sg2, Cond,  Imprt+Sg3

!! ExtraStrong-RaisedVowel-No-palatalization
+V:%^VV2V%^VOWRaise VSUF-IMPRT-CONNEGII_ED ; !!= * @CODE@ (8) +V+Imprt+ConNegII: jottu
!! Imprt+ConNeg, Pass+PrfPrc

!! ExtraStrong-LoweredVowel-No-palatalization
+V:%^VV2V VSUF-IMPRT-PL3_ED ; !!= * @CODE@ (9) Ind.Imprt.Pl3: jåttaz
!! Imprt+Pl3

! (10) Allegro for incoative: jåʹđ»
+V+Der/škueʹtted:%^VV2V%^PAL%^CC2C»škuätt V_PUEʹTTED ;

LEXICON V_CEʹPCCED !!= * @CODE@ ceʹpcced:cepcc
! Associate with särnnad parallel
! FORMS
!! ExtraStrong-LowVowel-Palatalization
+V:%^PAL VSUF-INF_ED ; !!= * @CODE@ (1) +V+Inf: ceʹpcced
!! V+Inf, Ind+Prs+Pl1, Ind+Prs+Pl2, Imprt+Pl1, Imprt+Pl2
!! Actio, ActEss, PrsPrc

!! ExtraStrong-LowVowel-No-palatalization
+V: VSUF-IND-PRS-SG3_ED ; !!= * @CODE@ (2) +V+Ind+Prs+Sg3: cepcc
!! +V+Ind+Prs+Sg3, Ind.Prt.ConNeg, PrfPrc

!! ExtraStrong-LowVowel-Palatalization
+V+Ind+Prs+Pl3:%^PAL%>e K ; !!= * @CODE@ (3) +V+Ind+Prs+Pl3: ceʹpcce
!! +V+Ind+Prs+Pl3

!! ExtraStrong-RaisedVowel-Palatalization
+V:%^VOWRaise%^PAL%> VSUF-IND-PRT-PL3_ED ; !!= * @CODE@ (4) +V+Ind+Prt+Pl3: ciʹpcce
!! +V+Ind+Prt+Pl3,  Ind+Prt+Sg1, Ind+Prt+Sg2, Ind+Prt+Sg4

!! Weak-StableV-Palatalization
+V:%^V2VV%^PAL%^XYY2XY VSUF-IND-PRS-CONNEG_ED ; !!= * @CODE@ (5) ceeʹpc-
!! Imprt+Sg2, Ind+Prs+ConNeg, Ind+Prs+Sg4, VAbess, GerTemp, GerInstr

!! Weak-RaisedVowel-Palatalization
+V:%^V2VV%^VOWRaise%^PAL%^XYY2XY%> VSUF-POTENTIAL_ED ; !!= * @CODE@  (6) +V+Pot+Sg3: ciiʹpcež
!! Ind+Prt+Sg3, Ind+Prt+Pl1, Ind+Prt+Pl2, Pot,

!! Weak-LowVowel-No-palatalization
+V:%^V2VV%^XYY2XY VSUF-CONDITIONAL_ED ; !!= * @CODE@ (7) +V+Imprt+Sg3: ceepcas
!! Ind+Prs+Sg1, Ind+Prs+Sg2, Cond,  Imprt+Sg3

!! ExtraStrong-RaisedVowel-No-palatalization
+V:%^VOWRaise VSUF-IMPRT-CONNEGII_ED ; !!= * @CODE@ (8) +V+Imprt+ConNegII: cipccu
!! Imprt+ConNeg, Pass+PrfPrc

!! ExtraStrong-LoweredVowel-No-palatalization
+V: VSUF-IMPRT-PL3_ED ; !!= * @CODE@ (9) Ind.Imprt.Pl3: cepssaz
!! Imprt+Pl3

!! (10) Allegro for incoative: ceʹps»
+V+Der/škueʹtted:%^PAL%^XYY2XY»škuätt V_PUEʹTTED ;


LEXICON V_KÄʹDDED !!= * @CODE@ käʹdded:kädd
! seek parallel in ÄBBAD
! FORMS
! (1) +V+Inf: käʹdded
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: kädd
! (3) +V+Ind+Prs+Pl3: käʹdde
! (4) +V+Ind+Prt+Pl3: kaʹdde
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: kääʹdd
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: kaaʹddež
! (7) +V+Imprt+Sg3: kääʹddas
! (8) +V+Imprt+ConNegII: kaddu
!    Pass.PrfPrc
! (9) Ind.Imprt.Pl3: käddaz
! käʹdded:kädd
+V:%^PAL VSUF-INF_ED ; !1 käʹdded
+V: VSUF-IND-PRS-SG3_ED ; !2 kädd
+V+Ind+Prs+Pl3:%^PAL%>e K ; !3 käʹdde
+V:%^VOWRaise%^PAL%> VSUF-IND-PRT-PL3_ED ; !4 kaʹdde
+V:%^V2VV%^VOWRaise%^PAL%> VSUF-POTENTIAL_ED ; !5 kaaʹdd-
+V:%^V2VV%> VSUF-CONDITIONAL_ED ; !5 ?2 ?7 kääʹdd-
+V+Imprt+Sg3:%^V2VV%^PAL%>as K ; !7 kääʹddas
+V:%^V2VV%^PAL VSUF-IND-PRS-CONNEG_ED ;
+V: VSUF-IMPRT-CONNEGII_ED ; !8 kädd-

! (10) Allegro for incoative: käʹd»
+V+Der/škueʹtted:%^PAL%^CC2C»škuätt V_PUEʹTTED ;

LEXICON V_VEÄʹDˈDED !!= * @CODE@ veäʹdˈded:veädd
! seek parallel in LUÂDˈDAD
! FORMS
! (1) +V+Inf: veäʹdˈded
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: veädd
! (3) +V+Ind+Prs+Pl3: veäʹdˈde
! (4) +V+Ind+Prt+Pl3: vieʹdde
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: veäʹdˈd
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: vieʹddež
! (7) +V+Imprt+Sg3: veäʹdˈdas
! (8) +V+Imprt+ConNegII: viâddu
!    Pass.PrfPrc
! (9) Ind.Imprt.Pl3: veäddaz
! veäʹdˈded:veädd
+V:%^PAL VSUF-INF_ED ; !1 veäʹdˈded
+V: VSUF-IND-PRS-SG3_ED ; !2 veädd
+V+Ind+Prs+Pl3:%^PAL%>e K ; !3 veäʹdˈde
+V:%^VOWRaise%^PALẸ%^PAL%> VSUF-IND-PRT-PL3_ED ; !4 vieʹdˈde
+V:%^VOWRaise%^PALẸ%^PAL%^CC2CCC%> VSUF-POTENTIAL_ED ; !5 vieʹdd-
+V:%^CC2CCC%> VSUF-CONDITIONAL_ED ; !5 ?2 ?7 veäʹdˈd-
+V+Imprt+Sg3:%^PAL%>as K ; !7 veäʹddas
+V:%^PAL VSUF-IND-PRS-CONNEG_ED ;
+V: VSUF-IMPRT-CONNEGII_ED ; !8 veädd-

! (10) Allegro for incoative: veäʹd»
+V+Der/škueʹtted:%^PAL%^CC2C»škuätt V_PUEʹTTED ;

LEXICON V_KÂÂʹǨǨED !!= * @CODE@ kââʹǩǩed:kââkk
! 1. VERBS WITH SINGLE-SYLLABLE Ind.Prs.Sg3 (2009: ???)
! 1.3 Inf in palatalization + ed (vowel shift)
!   Raised: Inf, Ind.Prt.Pl3, Imprt.Sg2, Pot.Sg3
!   Lowered: Ind.Prs.Sg3, Ind.Prs.Pl3, Imprt.Sg3
! 1.3.1 Has Palatalization
! 1.3.1[1] (Palatalization pattern)
!   Palatalized: Inf, Ind.Prs.Pl3, Ind.Prt.Pl3, Pot.Sg3
!   Not Palatalized: Ind.Prs.Sg3, Imprt.Sg2, Imprt.Sg3
! 1.3.1[1].1 Lacks Specifically Pedagogical Gradation
! 1.3.1[1].1.1 Has Orthographic Gradation
! 1.3.1[1].1.1[231] (Diphthong + Consonant and Consonant Geminate alternation)
!   Strong grade: 
!   Extra strong grade: 
!   Weak grade: 
! FORMS
! (1)  Inf: kââʹǩǩed
! (2)  Ind.Prs.Sg3: kââkk
! (3)  Ind.Prs.Pl3: kââʹǩˈǩe
! (4)  Ind.Prt.Pl3: kõõʹǩˈǩe
! (5)  Ind.Imprt.Sg2: kââʹjj
! (6)  Ind.Pot.Sg3: kõõʹjjež
! (7)  Imprt.Sg3: kââǥǥas
! (8)  Imprt.13.ConNeg: kõkku 
! (9)  Imprt.Pl3: kâkkaz
! (9) Ind.Imprt.Pl3: kâkˈkaz
! (10) Allegro for incoative: kâʹj»
! (11)  Ger.Temp.PxSg3: kââʹjjeeʹstes
! * Yaml: __V-kaakked__
! kââʹǩǩed:kââkk
! Strong grade in penultimate syllable
+V:%^PAL VSUF-INF_ED ; !1
+V: VSUF-IND-PRS-SG3_ED ; !2
+V+Imprt+Pl3:%^VV2V%>az K ;
+V:%^KK2GG VSUF-IND-PRS-CONNEG_ED ;
+V+Ind+Prs+Pl3:%^PAL%>e K ; !3
+V:%^VOWRaise%^PAL VSUF-IND-PRT-PL3_ED ; !4
+V:%^VOWRaise%^PAL%^KK2GG VSUF-POTENTIAL_ED ; !5
+V+Pot+Sg3:%^VOWRaise%^PAL%^KK2GG%>âž   K ; !6
+V+Imprt+Sg3:%^CC2C%>as K ; !7
+V:%^VOWRaise VSUF-IMPRT-CONNEGII_ED ; !8
 !9V

+V+Der/škueʹtted:%^PAL%^CC2C»škuätt V_PUEʹTTED ;



LEXICON V_PUEʹTTED !!= * @CODE@ pueʹtted:puätt
! Stem is Strong
! Diphthong
! Vowel shift
! FORMS

!! Strong-HighVowel-Palatalization
+V:%^VOWRaise%^PALE%^PAL VSUF-INF_ED ;		!!= * @CODE@ (1) pueʹtted 
!! V+Inf, Ind+Prs+Pl1, Ind+Prs+Pl2, Imprt+Pl1, Imprt+Pl2
!! Actio, ActEss, PrsPrc

!! Strong-LoweredVowel-No-palatalization
+V: VSUF-IND-PRS-SG3_ED ; 	     		!!= * @CODE@ (2) puätt
!! +V+Ind+Prs+Sg3, Ind.Prt.ConNeg, PrfPrc

!! ExtraStrong-LoweredVowel-Palatalization
+V+Ind+Prs+Pl3:%^PAL%^CC2CCC%>e K ;     !!= * @CODE@ (3) puäʹtˈte
!! +V+Ind+Prs+Pl3

!! ExtraStrong-RaisedVowel-Palatalization
+V:%^VOWRaise%^PALE%^PAL%^CC2CCC%> VSUF-IND-PRT-PL3_ED ; !!= * @CODE@ (4) pueʹtˈte
+V:%^VOWRaise%^PALÕ%^PAL%^CC2CCC%> VSUF-IND-PRT-PL3_ED ; !!= * @CODE@ (4) puõʹtˈte
!!  +V+Ind+Prt+Pl3,  Ind+Prt+Sg1, Ind+Prt+Sg2, Ind+Prt+Sg4

!! Weak-StableV-Palatalization
+V:%^PAL%^CC2C VSUF-IND-PRS-CONNEG_ED ; !!= * @CODE@ (5) puäʹđ-
!! Imprt+Sg2, Ind+Prs+ConNeg, Ind+Prs+Sg4, VAbess, GerTemp, GerInstr

!! Weak-RaisedVowel-Palatalization
+V:%^VOWRaise%^PALE%^PAL%^CC2C%> VSUF-POTENTIAL_ED ; !!= * @CODE@ (6 pueʹđež
+Use/NG+V:%^VOWRaise%^PALÕ%^PAL%^CC2C%> VSUF-POTENTIAL_ED ; !!= * @CODE@ (6 puõʹđež
+V:%^VOWRaise%^PALÕ%^PAL%^CC2C VSUF-IND-PRT-SG3-PL1-PL2_ ; !!= * @CODE@ ( puõʹđi
+V+Pot+Sg3:%^PAL%^CC2C%>ež   K ; !!= * @CODE@ (6   puäʹđež
!! Ind+Prt+Sg3, Ind+Prt+Pl1, Ind+Prt+Pl2, Pot,

!! Weak-LoweredVowel-No-palatalization
+V:%^CC2C%> VSUF-CONDITIONAL_ED ; !!= * @CODE@ (7) puäđ-
!! Ind+Prs+Sg1, Ind+Prs+Sg2, Cond,  Imprt+Sg3
+Use/NG+V:%^PAL%^CC2C%> VSUF-CONDITIONAL_ED ; !!= * @CODE@ (5 ?2 ?7) puäʹđ-

!! ExtraStrong-RaisedVowel-No-palatalization
+V:%^VOWRaise%^PALÕ VSUF-IMPRT-CONNEGII_ED ; !!= * @CODE@ (8 puõttu
+V:%^VOWRaise VSUF-IMPRT-CONNEGII_ED ; !!= * @CODE@ (8 puåttu
!! Imprt+ConNeg, Pass+PrfPrc

!! ExtraStrong-LoweredVowel-No-palatalization (9)
+V:%^CC2CCC VSUF-IMPRT-PL3_ED ;  !!= * @CODE@ puätˈtaz
!! Imprt+Pl3

!! Allegro (10)
+V+Der/škueʹtted:%^VOWLower%^PAL%^CC2C%>e»škuätt V_PUEʹTTED ;


LEXICON V_VUEIʹNNED !!= * @CODE@ vueiʹnned:vuäinn
! FORMS
! (1) +V+Inf: vueiʹnned
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: vuäinn
! (3) +V+Ind+Prs+Pl3: vuäiʹnne
! (4) +V+Ind+Prt+Pl3: vuõiʹnne
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: vueiʹn
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: vuõiʹnež
! (7) +V+Imprt+Sg3: ???vuâlǥas
! (8) +V+Imprt+ConNegII: ???vuõlggu
!    Pass.PrfPrc
! vueiʹnned:vuäinn
! Stem is Strong
! Diphthong
! Vowel raising
! NEEDS WORK
!(son vuäinn)
+V:%^VOWRaise%^PALE%^PAL VSUF-INF_ED ; !1
+V: VSUF-IND-PRS-SG3_ED ; !2
+V+Ind+Prs+Pl3:%^PAL%>e K ; !3
+V:%^VOWRaise%^PALE%^PAL%> VSUF-IND-PRT-PL3_ED ; !4
+V:%^VOWRaise%^PALE%^PAL%^XYY2XY%> VSUF-POTENTIAL_ED ; !6 vueiʹnet
+V:%^VOWBack%^PAL%^XYY2XY%> VSUF-POTENTIAL_ED ; !6 vuõiʹnet
+V:%^XYY2XY%> VSUF-CONDITIONAL_ED ; !5 ?2 ?7
+V:%^VOWRaise%^PALE%^PAL%^XYY2XY VSUF-IND-PRS-CONNEG_ED ;
+V+Imprt+Sg3:%^XYY2XY%>as K ; !7
+V:%^VOWBack VSUF-IMPRT-CONNEGII_ED ; !8


LEXICON V_VUẸʹĐĐED !!= * @CODE@ vueʹđđed:vuäđđ
! FORMS
! (1) +V+Inf: vuẹʹđđed
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: vuäđđ
! (3) +V+Ind+Prs+Pl3: vuäʹđˈđe
! (4) +V+Ind+Prt+Pl3: vueʹđˈđe
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: vuäʹđ
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: vueʹđež
! (7) +V+Imprt+Sg3: ???vuäđas
! (8) +V+Imprt+ConNegII: ???vueđđu
!    Pass.PrfPrc
! Stem is Strong
! Diphthong
! Vowel raising
! NEEDS WORK
!(son vuäđđ)
!! Strong-HighVowel-Palatalization
+V:%^VOWRaise%^PALẸ%^PAL VSUF-INF_ED ;          !!= * @CODE@ (1) vuẹʹđđed
!! V+Inf, Ind+Prs+Pl1, Ind+Prs+Pl2, Imprt+Pl1, Imprt+Pl2
!! Actio, ActEss, PrsPrc

!! Strong-LoweredVowel-No-palatalization
+V: VSUF-IND-PRS-SG3_ED ;                       !!= * @CODE@ (2) vuäđđ
!! +V+Ind+Prs+Sg3, Ind.Prt.ConNeg, PrfPrc

!! ExtraStrong-LoweredVowel-Palatalization
+V+Ind+Prs+Pl3:%^PAL%^CC2CCC%>e K ;     !!= * @CODE@ (3) vuäʹđˈđe
!! +V+Ind+Prs+Pl3

!! ExtraStrong-RaisedVowel-Palatalization
+V:%^VOWRaise%^PALE%^PAL%^CC2CCC%> VSUF-IND-PRT-PL3_ED ; !!= * @CODE@ (4) vueʹđˈđe
!!  +V+Ind+Prt+Pl3,  Ind+Prt+Sg1, Ind+Prt+Sg2, Ind+Prt+Sg4

!! Weak-StableV-Palatalization
+V:%^PAL%^CC2C VSUF-IND-PRS-CONNEG_ED ; !!= * @CODE@ (5) vueʹđ
!! Imprt+Sg2, Ind+Prs+ConNeg, Ind+Prs+Sg4, VAbess, GerTemp, GerInstr

!! Weak-RaisedVowel-Palatalization
+V:%^VOWRaise%^PALE%^PAL%^CC2C%> VSUF-POTENTIAL_ED ; !!= * @CODE@ (6) vueʹđet
!! Ind+Prt+Sg3, Ind+Prt+Pl1, Ind+Prt+Pl2, Pot,

!! Weak-LoweredVowel-No-palatalization
+V:%^CC2C%> VSUF-CONDITIONAL_ED ; !!= * @CODE@ (7) vuäđ-
!! Ind+Prs+Sg1, Ind+Prs+Sg2, Cond,  Imprt+Sg3

!! ExtraStrong-RaisedVowel-No-palatalization
+V:%^VOWRaise%^PALÕ VSUF-IMPRT-CONNEGII_ED ; !!= * @CODE@ (8) vuõđđu
!! Imprt+ConNeg, Pass+PrfPrc

!! ExtraStrong-LoweredVowel-No-palatalization (9)
+V:%^CC2CCC VSUF-IMPRT-PL3_ED ;  !!= * @CODE@ vuäđˈđaz
!! Imprt+Pl3

!! Allegro (10)
+V+Der/škueʹtted:%^PAL%^CC2C%>e»škuätt V_PUEʹTTED ; !!= * @CODE@ vuäˈđeškuätt



LEXICON V_LEUʹǨǨED !!= * @CODE@ leuʹǩǩed:leukk
! seek parallell in MEINNAD
! FORMS
! (1) +V+Inf: leuʹǩǩed
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: vuäinn
! (3) +V+Ind+Prs+Pl3: vuäiʹnne
! (4) +V+Ind+Prt+Pl3: vuõiʹnne
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: vueiʹn
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: vuõiʹnež
! (7) +V+Imprt+Sg3: ???vuâlǥas
! (8) +V+Imprt+ConNegII: ???vuõlggu
!    Pass.PrfPrc
! leuʹǩǩed:leukk
! Stem is Strong
! Diphthong
! Vowel raising
! NEEDS WORK
!(son leukk) ??
+V:%^VOWRaise%^PALE%^PAL VSUF-INF_ED ; !1
+V: VSUF-IND-PRS-SG3_ED ; !2
+V+Ind+Prs+Pl3:%^PAL%>e K ; !3
+V:%^VOWRaise%^PALE%^PAL%> VSUF-IND-PRT-PL3_ED ; !4
+V:%^XYY2XY VSUF-IND-PRS-CONNEG_ED ; !5
+V:%^VOWRaise%^PALE%^PAL%^XYY2XY%> VSUF-POTENTIAL_ED ; !6 vueiʹnet
+V:%^VOWBack%^PAL%^XYY2XY%> VSUF-POTENTIAL_ED ; !6 vuõiʹnet
+V:%^XYY2XY%> VSUF-CONDITIONAL_ED ; !7
+V+Imprt+Sg3:%^XYY2XY%>as K ; !7
+V:%^VOWBack VSUF-IMPRT-CONNEGII_ED ; !8



LEXICON V_TÄIʹDDED !!= * @CODE@ täiʹdded:täidd
! seek parallell in MEINNAD
! FORMS
! (1) +V+Inf: täiʹdded
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: täidd
! (3) +V+Ind+Prs+Pl3: täiʹdde
! (4) +V+Ind+Prt+Pl3: taiʹdde
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: vueiʹn
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: taaiʹdež
! (7) +V+Imprt+Sg3: ???tääidas
! (8) +V+Imprt+ConNegII: ???taiddu
!    Pass.PrfPrc
! täiʹdded:täidd
! Stem is Strong
! Diphthong
! Vowel raising
! NEEDS WORK
!(son täidd) ??
+V:%^PAL VSUF-INF_ED ; !1
+V: VSUF-IND-PRS-SG3_ED ; !2
+V+Ind+Prs+Pl3:%^PAL%>e K ; !3
+V:%^VOWRaise%^PAL%> VSUF-IND-PRT-PL3_ED ; !4
+V:%^XYY2XY VSUF-IND-PRS-CONNEG_ED ; !5
+V:%^VOWRaise%^PAL%^XYY2XY%> VSUF-POTENTIAL_ED ; !6 vueiʹnet
+V:%^XYY2XY%> VSUF-CONDITIONAL_ED ; !7
+V+Imprt+Sg3:%^XYY2XY%>as K ; !7
+V:%^VOWRaise VSUF-IMPRT-CONNEGII_ED ; !8

LEXICON V_CEÄʹLǨǨED !!= * @CODE@ ceäʹlǩǩed:ceälkk
! FORMS
! (1) +V+Inf: ceäʹlǩǩed
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: ceälkk
! (3) +V+Ind+Prs+Pl3: ceäʹlǩǩe
! (4) +V+Ind+Prt+Pl3: cieʹlǩǩe
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: cieʹlj
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: cieʹljež
! (7) +V+Imprt+Sg3: ceälkas
! (8) +V+Imprt+ConNegII: ciâlkku
!    Pass.PrfPrc
! ceäʹlǩǩed:ceälkk
+V:%^PAL VSUF-INF_ED ; !1
+V: VSUF-IND-PRS-SG3_ED ; !2 ceälkk
+V+Ind+Prs+Pl3:%^PAL%>e K ; !3
+V:%^VOWRaise%^PALE%^PAL%> VSUF-IND-PRT-PL3_ED ; !4
+V:%^XYY2XY VSUF-IND-PRS-CONNEG_ED ; !5
+V:%^VOWRaise%^PALE%^PAL%^XYY2XY%> VSUF-POTENTIAL_ED ; !6
+V:%^XYY2XY%> VSUF-CONDITIONAL_ED ; !7
+V+Imprt+Sg3:%^XYY2XY%>as K ; !7
+V: VSUF-IMPRT-CONNEGII_ED ; !8

+V+Der/škueʹtted:%^PAL%^XYY2XY»škuätt V_PUEʹTTED ;

LEXICON V_MEÄʹTTED !!= * @CODE@ meäʹtted:meätt
! FORMS
! (1) +V+Inf: meäʹtted
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: meätt
! (3) +V+Ind+Prs+Pl3: meäʹtˈte
! (4) +V+Ind+Prt+Pl3: mieʹtˈte
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: mieʹđ
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: mieʹđež
! (7) +V+Imprt+Sg3: meättas
! (8) +V+Imprt+ConNegII: miâtˈtu
!    Pass.PrfPrc
! meäʹtted:meätt
+V:%^PAL VSUF-INF_ED ; !1
+V: VSUF-IND-PRS-SG3_ED ; !2 meätt
+V+Ind+Prs+Pl3:%^PAL%>e K ; !3
+V:%^VOWRaise%^PALE%^PAL%> VSUF-IND-PRT-PL3_ED ; !4
+V:%^VOWRaise%^PALE%^PAL%^XYY2XY%> VSUF-POTENTIAL_ED ; !5
+V:%^XYY2XY%> VSUF-CONDITIONAL_ED ; !5 ?2 ?7
+V:%^XYY2XY VSUF-IND-PRS-CONNEG_ED ;
+V+Pot+Sg3:%^VOWRaise%^PALE%^PAL%^XYY2XY%>ež   K ; !6
+V+Imprt+Sg3:%^XYY2XY%>as K ; !7
+V: VSUF-IMPRT-CONNEGII_ED ; !8

+V+Der/škueʹtted:%^PAL%^XYY2XY»škuätt V_PUEʹTTED ;

LEXICON V_MÄʹHSSED !!= * @CODE@ mäʹhssed:mähss
! FORMS
! (1) +V+Inf: mäʹhssed
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: mähss
! (3) +V+Ind+Prs+Pl3: mäʹhsse
! (4) +V+Ind+Prt+Pl3: maʹhsse
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: maauʹs
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: maauʹsež
! (7) +V+Imprt+Sg3: määusas
! (8) +V+Imprt+ConNegII: mahssu
!    Pass.PrfPrc
! mäʹhssed:mähss
+V:%^PAL VSUF-INF_ED ; !1
+V: VSUF-IND-PRS-SG3_ED ; !2 mähss
+V+Ind+Prs+Pl3:%^PAL%>e K ; !3
+V:%^VOWRaise%^PAL%> VSUF-IND-PRT-PL3_ED ; !4
+V:%^VOWRaise%^PAL%^XYY2VY%> VSUF-POTENTIAL_ED ; !5
+V:%^XYY2VY%> VSUF-CONDITIONAL_ED ; !5 ?2 ?7
+V:%^XYY2VY VSUF-IND-PRS-CONNEG_ED ;
+V+Pot+Sg3:%^VOWRaise%^PAL%^XYY2VY%>ež   K ; !6
+V+Imprt+Sg3:%^XYY2VY%>as K ; !7
+V: VSUF-IMPRT-CONNEGII_ED ; !8

+V+Der/škueʹtted:%^PAL%^XYY2VY»škuätt V_PUEʹTTED ;

!! DERIVED VERBS WITH PENULTIMATE VOWEL LOSS AND CHANGE

!! !CLASS 2 HIGH VOWEL, NO PALATALIZATION
LEXICON V_AALǤTED !!= * @CODE@  aalǥted:aalǥât
! lexicon ''@LEXNAME@''
+V:%^PenVow2a VSUF-VUEJTED-IND-PRS-SG3 ; ! aalǥat
+V: VSUF-VUEJTED-IND-PRS-CONNEG ;        ! aalǥât
+V:%^RmVow VSUF-VUEJTED-ELSE ;		 ! aalǥted

LEXICON V_KULSTED !!= * @CODE@ kulsted:kuulâst
+V:%^PenVow2a VSUF-VUEJTED-IND-PRS-SG3 ; ! kuulâst
+V: VSUF-VUEJTED-IND-PRS-CONNEG ;        ! kuulâst
+V:%^PenVV2V%^RmVow VSUF-VUEJTED-ELSE ;           ! kulsted
+Use/NG+V:%^RmVow VSUF-VUEJTED-ELSE ;           ! kuulsted

LEXICON V_AGSTED !!= * @CODE@ agsted:aaggâst
+V:%^PenVow2a VSUF-VUEJTED-IND-PRS-SG3 ; ! aaggâst
+V: VSUF-VUEJTED-IND-PRS-CONNEG ;        ! aaggâst
+V:%^PenVV2V%^PenCC2C%^RmVow VSUF-VUEJTED-ELSE ;           ! agsted
+Use/NG+V:%^PenVV2V%^RmVow VSUF-VUEJTED-ELSE ;           ! aggsted
+Use/NG+V:%^RmVow VSUF-VUEJTED-ELSE ;           ! aaggsted


LEXICON V_MAINSTED !!= * @CODE@ mainsted:maainâst
! inf: sted
! Ind.Prs.Sg3: -â-
! foot: 2syll
! 1.1 Inf in âd (vowel shift)
! diphthong: no
! vowel_shift: yes
! consonant_type: geminate-single
! gradation: yes3
! weak_grade: single
! strong_grade: geminate
! lexicon ''@LEXNAME@'' 
! mainsted:mainâst
! * Yaml: __V-mainsted__
+V:%^PenVow2a VSUF-VUEJTED-IND-PRS-SG3 ; ! maainast
+V: VSUF-VUEJTED-IND-PRS-CONNEG ;        ! maainâst
+V:%^PenVV2V%^RmVow VSUF-VUEJTED-ELSE ;           ! mainsted
+V+Use/NG:%^RmVow VSUF-VUEJTED-ELSE ;           ! maainsted

+V+Ind+Prt+Pl1+Use/NG:%^PenV2VV%^RmVow%>in             K  ; !WG ! maainstin
+V+Ind+Prt+Pl1+Use/NG:%^PenV2VV%^RmVow%>iim             K  ; !WG ! maainstiim
+V+Ind+Prt+Pl1+Use/NG:%^PenV2VV%^RmVow%>iin             K  ; !WG ! maainstiin
+V+Ind+Prt+Pl1+Use/NG:%^RmVow%>in             K  ; ! ! mainstin
+V+Ind+Prt+Pl1+Use/NG:%^RmVow%>iim             K  ; !WG ! maainstiim
+V+Ind+Prt+Pl1+Use/NG:%^RmVow%>iin             K  ; !WG ! maainstiin
+V+Ind+Prt+Pl2+Use/NG:%^PenV2VV%^RmVow%>iid             K  ; !WG ! mainstiid
+V+Ind+Prt+Pl2+Use/NG:%^RmVow%>iid             K  ; !WG ! mainstiid

+V+Actio:%^RmVow%>am K ; ! 2015-05-07 Is this necessary

LEXICON V_NUÕLSTED !!= * @CODE@ nuõlsted:nuõllâst
+V:%^PenVow2a VSUF-VUEJTED-IND-PRS-SG3 ; ! nuõllâst
+V: VSUF-VUEJTED-IND-PRS-CONNEG ;        ! nuõllâst
+V:%^PenCC2C%^RmVow VSUF-VUEJTED-ELSE ;           ! nuõlsted
+Use/NG+V:%^RmVow VSUF-VUEJTED-ELSE ;           ! nuõllsted

LEXICON V_TAARBŠED !!= * @CODE@ taarbšed:taarbâš
! No Gradation
! ž:š
+V:%^PenVow2a VSUF-VUEJTED-IND-PRS-SG3 ;	! taarbâš
+V:%^VC VSUF-VUEJTED-IND-PRS-CONNEG ;        	! taarbâž
+V:%^RmVow VSUF-VUEJTED-ELSE ;                  ! taarbšed

LEXICON V_ǨIÕRGGNED
! Gradation: none
! ǩiõrggned:ǩiõrggân
! (ij ǩiõrggân) %^RmVow
! ǩiõrggned:ǩiõrggân
! FORMS
! (1) +V+Inf: ǩiõrggned
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: ǩiõrggan
! (3) +V+Ind+Prs+Pl3: ǩiõrggne
! (4) +V+Ind+Prt+Pl3: ǩiõrggne
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: ǩiõrggân
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: ǩiõrggnež
! (7) +V+Imprt+Sg3: ǩiõrggnââǥǥas
! (8) +V+Imprt+ConNegII: ǩiõrggnuku
!    Pass.PrfPrc
+V:%^PenVow2a VSUF-VUEJTED-IND-PRS-SG3 ; ! ǩiõrggan
+V: VSUF-VUEJTED-IND-PRS-CONNEG ;        ! ǩiõrggân
+V:%^RmVow VSUF-VUEJTED-ELSE ;           ! ǩiõrggned



!! !CLASS 2 LOW VOWEL, NO PALATALIZATION
LEXICON V_VUEJTED !!= * @CODE@ vuejted:vuejat
! lexicon ''@LEXNAME@'' 
! FORMS
! (2) +V+Ind+Prs+Sg3: vuejat
+V:%^PenVow2a VSUF-VUEJTED-IND-PRS-SG3  ;  !2 vuejat

! (5) +V+Imprt+Sg2:  vuejat
!   V.Imprt.2.ConNeg, V.Ind.Prs.ConNeg
+V: VSUF-VUEJTED-IND-PRS-CONNEG ;

! (1) +V+Inf: vuejted
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (3) +V+Ind+Prs+Pl3: vuejte
! (4) +V+Ind+Prt+Pl3: vuejte
!   Ind.Prt.Sg1-Sg2-4
! (6) +V+Pot+Sg3: vuejtež
! (7) +V+Imprt+Sg3: vuejtââǥǥas
! (8) +V+Imprt+ConNegII: vuejtuku
!    Pass.PrfPrc

!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
+V:%^RmVow VSUF-VUEJTED-ELSE ;

LEXICON VSUF-VUEJTED-IND-PRS-SG3
+Ind+Prs+Sg3: K ;

LEXICON VSUF-VUEJTED-IND-PRS-CONNEG
+Ind+Prs+ConNeg:	K ;
+Imprt+Sg2:		K ;
+Imprt+ConNeg: 	K ;

LEXICON VSUF-VUEJTED-ELSE
+Inf:%>ed K ;
+Ind+Prs+Pl1:%>ep  K  ; 
+Ind+Prs+Pl2:%>eʹped K  ;
+Ind+Prs+Pl3:%>e     K  ;
+Ind+Prt+Pl3:%>e              K  ; ! vuejte
+Ind+Prs+Sg1:%>am  K  ;  ! vuejtam
+Ind+Prs+Sg2:%>ak  K  ; !
+Ind+Prs+Sg4:%>et K ; !

+Ind+Prt+Sg1:%>em             K  ; ! vuejtem
+Ind+Prt+Sg2:%>iǩ             K  ; ! vuejtiǩ
+Ind+Prt+Sg3:%>i              K  ; ! ! vuejti
+Ind+Prt+Pl1:%>im             K  ; ! ! vuejtim
+Ind+Prt+Pl2:%>id             K  ; ! ! vuejtid
+Ind+Prt+Sg4:%>eš K ; ! vuejteš
+Ind+Prt+ConNeg:%>am K ; ! vuejtam

+Imprt+Sg3:%>ââǥǥas          K  ; !WG
+Imprt+Pl1:%>âkap                    K  ;
+Imprt+Pl2:%>eʹǩed           K  ;
+Imprt+Pl3:%>âkaz            K  ;

+Imprt+ConNegII:%>uku K ;
+Imprt+ConNegII+Use/NG:%>uku K ;
:%>eʹ		VSUF-I-POTKOND ; !WG
+Pot+Sg3:%>ež	K ; !WG
+ActEss:%>eʹmen K ;
+Actio:%>em K ;
+Actio:%>em N_PEʹRRJEM ;
+PrsPrc:%>eei K ;
+PrsPrc+Der/N:%>ee N_TUÕĐŠTEEI ;
+PrfPrc:%>am K ;
+Pass+PrfPrc:%>ummu K ;
+Pass+PrfPrc:%>uum K ;
:e VABESS ;
 GER_ ;
+Der/mmuš:%>ummuš N_MAINSTUMMUŠ ;
+Err/Orth+Der/muš:%>umuš N_MAINSTUMMUŠ ;
+Der/škueʹtted:%>e»škuätt V_PUEʹTTED ;


LEXICON V_JÄLSTED !!= * @CODE@ jälsted:jäälast
! lexicon ''@LEXNAME@''
!
! * Yaml: __V-jaelsted__

+V:%^PenVow2a VSUF-VUEJTED-IND-PRS-SG3 ; ! jäälast
+V: VSUF-VUEJTED-IND-PRS-CONNEG ;      	 ! jäälast
+V:%^PenVV2V%^RmVow VSUF-VUEJTED-ELSE ; 		 ! jälsted
+Use/NG+V:%^RmVow VSUF-VUEJTED-ELSE ; 		 ! jälsted


LEXICON V_PÅǤSTED !!= * @CODE@ påǥsted:pååǥǥast
! lexicon ''@LEXNAME@''
! * Yaml: __V-poagsted__
+V:%^PenVow2a VSUF-VUEJTED-IND-PRS-SG3 ; ! pååǥǥast
+V: VSUF-VUEJTED-IND-PRS-CONNEG ;      	 ! pååǥǥast
+V:%^PenVV2V%^PenCC2C%^RmVow VSUF-VUEJTED-ELSE ; 		 ! påǥsted
+Use/NG+V:%^PenCC2C%^RmVow VSUF-VUEJTED-ELSE ; 	 ! påǥǥsted
+Use/NG+V:%^RmVow VSUF-VUEJTED-ELSE ; 	 ! pååǥǥsted

LEXICON V_JÅRǤSTED !!= * @CODE@ jårǥsted:jåårǥast
! lexicon ''@LEXNAME@''
! * Yaml: __V-poagsted__
+V:%^PenVow2a VSUF-VUEJTED-IND-PRS-SG3 ; ! jåårǥast
+V: VSUF-VUEJTED-IND-PRS-CONNEG ;        ! jåårǥast
+V:%^PenVV2V%^RmVow VSUF-VUEJTED-ELSE ;                 ! jårǥsted
+Use/NG+V:%^RmVow VSUF-VUEJTED-ELSE ;                 ! jåårǥsted

LEXICON V_TEÂPSTED !!= * @CODE@ teâpsted:teâppast
! lexicon ''@LEXNAME@''
! * Yaml: __V-teapsted__
+V:%^PenVow2a VSUF-VUEJTED-IND-PRS-SG3 ; ! teâppast
+V: VSUF-VUEJTED-IND-PRS-CONNEG ;        ! teâppast
+V:%^PenCC2C%^RmVow VSUF-VUEJTED-ELSE ;  ! teâpsted
+Use/NG+V:%^RmVow VSUF-VUEJTED-ELSE ;    ! teâppsted

LEXICON V_ŠORRNED !!= * @CODE@ šorrned:šorran
! (ij šorran) %^RmVow
! šorrned:šorran
! FORMS
! (1) +V+Inf: šorrned
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: šorran
! (3) +V+Ind+Prs+Pl3: šorrne
! (4) +V+Ind+Prt+Pl3: šorrne
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: šorrân
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: šorrnež
! (7) +V+Imprt+Sg3: šorrnââǥǥas
! (8) +V+Imprt+ConNegII: šorrnuku
!    Pass.PrfPrc
+V:%^PenVow2a VSUF-VUEJTED-IND-PRS-SG3 ; ! šorran
+V: VSUF-VUEJTED-IND-PRS-CONNEG ;        ! šorran
+V:%^RmVow VSUF-VUEJTED-ELSE ;  	 ! šorrned


!! !CLASS 2 HIGH VOWEL, PALATALIZATION


LEXICON V_ČIEʹǨRDED !!= * @CODE@ čieʹǩrded:čieʹǩǩerd
! FORMS
! (1) +V+Inf: čieʹǩrded
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: čieʹǩǩard
! (3) +V+Ind+Prs+Pl3: čieʹǩrdje
! (4) +V+Ind+Prt+Pl3: čieʹǩrdje
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: čieʹǩǩerd
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: kåʹmrdež
! (7) +V+Imprt+Sg3: čieʹǩrdââǥǥas
! (8) +V+Imprt+ConNegII: čieʹǩrduku
!    Pass.PrfPrc
+V:%^PenVow2a VSUF-VUEJTED-IND-PRS-SG3 ; ! čieʹǩǩard
+V: VSUF-VUEJTED-IND-PRS-CONNEG ;        ! čieʹǩǩerd
+V:%^PenCC2C%^RmVow VSUF-VUEJTED-ELSE ;  ! čieʹǩrded
+Use/NG+V:%^RmVow VSUF-VUEJTED-ELSE ;  ! čieʹǩǩrded


!! !CLASS 2 LOW VOWEL, PALATALIZATION
LEXICON V_KÅʹMRDED !!= * @CODE@ kåʹmrded:kååʹmmerd
! FORMS
! (1) +V+Inf: kåʹmrded
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: kååʹmmard
! (3) +V+Ind+Prs+Pl3: kåʹmrde
! (4) +V+Ind+Prt+Pl3: kåʹmrde
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: kååʹmmerd
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: kåʹmrdež
! (7) +V+Imprt+Sg3: kåʹmrdââǥǥas
! (8) +V+Imprt+ConNegII: kåʹmrduku
!    Pass.PrfPrc
+V:%^PenVow2a VSUF-VUEJTED-IND-PRS-SG3 ; ! kååʹmmard
+V: VSUF-VUEJTED-IND-PRS-CONNEG ;        ! kååʹmmerd
+V:%^PenVV2V%^PenCC2C%^RmVow VSUF-VUEJTED-ELSE ;  ! kåʹmrded
+Use/NG+V:%^PenCC2C%^RmVow VSUF-VUEJTED-ELSE ;  ! kåʹmmrded
+Use/NG+V:%^RmVow VSUF-VUEJTED-ELSE ;  ! kååʹmmrded

LEXICON V_ǨEʹRJJTED !!= * @CODE@ ǩeʹrjjted:ǩeʹrjjet
! lexicon ''@LEXNAME@''
! ǩeʹrjjted:ǩeʹrjjet
! * Yaml: __V-kjerjjted__
+V:%^PenVow2a VSUF-VUEJTED-IND-PRS-SG3 ; ! ǩeʹrjjat
+V: VSUF-VUEJTED-IND-PRS-CONNEG ;        ! ǩeʹrjjet
+V:%^RmVow VSUF-VUEJTED-ELSE ;  ! ǩeʹrjjted

LEXICON V_ÄUʹCSTED !!= * @CODE@ äuʹcsted:äuʹccest
! lexicon ''@LEXNAME@''
! * Yaml: __V-aeucsted__
+V:%^PenVow2a VSUF-VUEJTED-IND-PRS-SG3 ; ! äuʹccast
+V: VSUF-VUEJTED-IND-PRS-CONNEG ;        ! äuʹccest
+V:%^XYY2XY%^RmVow VSUF-VUEJTED-ELSE ;  ! äuʹcsted

LEXICON V_NJÂʹDSTED !!= * @CODE@ njâʹdsted:njâʹddest
! lexicon ''@LEXNAME@'' 
! * Yaml: __V-njadsted__
+V:%^PenVow2a VSUF-VUEJTED-IND-PRS-SG3 ; ! njââʹddest
+V: VSUF-VUEJTED-IND-PRS-CONNEG ;        ! njââʹddest
+V:%^CC2C%^RmVow VSUF-VUEJTED-ELSE ;  ! njâʹdsted
+Use/NG+V:%^RmVow VSUF-VUEJTED-ELSE ;  ! njâʹddsted


!! !CLASS 3 HIGH VOWEL, NO PALATALIZATION



!! !CLASS 3 LOW VOWEL, NO PALATALIZATION
LEXICON V_KUÂĐˈĐJED !!= * @CODE@ kuâđđjed:kuâđˈđ
! +Ind+Prs+ConNeg: kuõđđu
! FORMS
! (1) +V+Inf: kuâđđjed
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: kuâđđai
! (3) +V+Ind+Prs+Pl3: kuâđđje
! (4) +V+Ind+Prt+Pl3: kuâđđje
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: kuõđđu
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: kuâđđjež
! (7) +V+Imprt+Sg3: kuâđđjââǥǥas
! (8) +V+Imprt+ConNegII: kuâđđjuku
!    Pass.PrfPrc
! kuâđʹđjed:kuâđʹđ
+V:ai VSUF-VUEJTED-IND-PRS-SG3        ; !kuâđˈđai
+V:%^VOWRaise%>u VSUF-VUEJTED-IND-PRS-CONNEG ; ! kuõđˈđu

+V:%>j VSUF-VUEJTED-ELSE ; !!= * @CODE@ kuâđˈđjed

+V+Ind+Prt+ConNeg:%>jâmma K ; ! kuâđˈđjâmma
+V+PrfPrc:%>jâmma K ;
+V+Pass+PrfPrc:%>jum K ;


LEXICON SEDGGJED !!= * @CODE@ sedggjed:sedgg
! see KUÂĐĐJED
+V:ai VSUF-VUEJTED-IND-PRS-SG3        ; !!= * @CODE@ sedggai
+V:%^VOWRaise%>u VSUF-VUEJTED-IND-PRS-CONNEG ; !!= * @CODE@ sidggu
+V:%>j VSUF-VUEJTED-ELSE ; !!= * @CODE@ sedggjed


!! !CLASS 3 HIGH VOWEL, PALATALIZATION
LEXICON V_VÕʹLLʼJED !!= * @CODE@ võʹll’jed:võʹll
! E.Koponen pg. 31
! võʹllʼjed:võʹll
! +Ind+Prs+ConNeg: võʹlle
! võʹll’jed:võʹll
+V:e	VSUF-VUEJTED-IND-PRS-CONNEG ;	!!= * @CODE@ võʹlle
+V:ai VSUF-VUEJTED-IND-PRS-SG3 ; !!= * @CODE@ võʹllai
+V:%>j VSUF-VUEJTED-ELSE ; 

+V+Ind+Prt+Pl1:%>jin             K  ; !WG ! võʹllʼjin
+V+Ind+Prt+Pl1:%>jiim             K  ; !WG ! võʹllʼjiim
+V+Ind+Prt+Pl1:%>jiin             K  ; !WG ! võʹllʼjiin
+V+Ind+Prt+Pl2:%>jiid             K  ; !WG ! võʹllʼjiid

+V+PrfPrc:%>jâmma K ;
+V+Pass+PrfPrc:%>jum K ; !

+V+Der/škueʹtted:%>je»škuätt V_PUEʹTTED ;


!! !CLASS 3 LOW VOWEL, PALATALIZATION


!! !CLASS 3 HIGH VOWEL, NO PALATALIZATION, GH
LEXICON V_KUÕCCJED !!= * @CODE@ kuõccjed:kuõcc
! +Ind+Prs+ConNeg: kuõccâǥ
! cf. kuâđđjed, võʹllʼjed
! 
!!
! FORMS
! (1) +V+Inf: kuõccjed
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: kuõccai
! (3) +V+Ind+Prs+Pl3: kuõccje
! (4) +V+Ind+Prt+Pl3: kuõccje
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: kuõccâǥ
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: kuõccjež
! (7) +V+Imprt+Sg3: kuõccjââǥǥas
! (8) +V+Imprt+ConNegII: kuõccjuku
!    Pass.PrfPrc
! kuõccjed:kuõcc
+V+Ind+Prs+Sg3:ai K        ; ! kuõccai
+V+Ind+Prs+ConNeg:âǥ K ; ! kuõccâǥ
+V+Ind+Prs+Sg1:%>jam              K  ; !
+V+Ind+Prs+Sg2:%>jak              K  ; !
+V+Ind+Prs+Pl1:%>jep      K  ;
+V+Ind+Prs+Pl2:%>jeʹped           K  ;
+V+Ind+Prs+Pl3:%>je               K  ;
+V+Ind+Prs+Sg4:%>jet K ; !

+V+Ind+Prt+Sg1:%>jem             K  ; ! kuõccjem
+V+Ind+Prt+Sg2:%>jiǩ             K  ; ! kuõccjiǩ
+V+Ind+Prt+Sg3:%>ji              K  ; ! ! kuõccji
+V+Ind+Prt+Pl1:%>jim             K  ; ! ! kuõccjim
+V+Ind+Prt+Pl2:%>jid             K  ; ! ! kuõccjid
+V+Ind+Prt+Pl3:%>je              K  ; ! kuõccje
+V+Ind+Prt+Sg4:%>ješ K ; ! kuõccješ
+V+Ind+Prt+ConNeg:%>jam K ; ! kuõccjam
+V+Ind+Prt+ConNeg:%>jâmma K ; ! kuõccjâmma

+V+Imprt+Sg3:%>jââǥǥas          K  ; !WG
+V+Imprt+Pl1:%>jâkap                    K  ;
+V+Imprt+Pl2:%>jeʹǩed           K  ;
+V+Imprt+Pl3:%>jâkaz            K  ;

+V+Imprt+Sg2:âǥ  K ; ! kuõccâǥ
+V+Imprt+ConNeg:âǥ     K ;
+V+Imprt+ConNegII:%>juku K ;

+V:%>jeʹ         VSUF-I-POTKOND ; !WG
+V+Pot+Sg3:%>jež K ; !WG
+V+Inf:%>jed K ;
+V+ActEss:%>jeʹmen K ;
+V+Actio:%>jam K ;
+V+Actio:%>jam N_SLOOHMAN ;
+V+PrsPrc:%>jeei K ;
+V+PrsPrc+Der/N:%>jee N_TUÕĐŠTEEI ;
+V+PrfPrc:%>jam K ;
+V+Pass+PrfPrc:%>jummu K ;
+V+Pass+PrfPrc:%>juum K ;
+V+VAbess:%>jeǩâni K ;
+V:%>j GER_ ;

+V+Der/mmuš:%>jummuš N_MAINSTUMMUŠ ;
+V+Der/škueʹtted:je»škuätt V_PUEʹTTED ;


!! !CLASS 3 LOW VOWEL, NO PALATALIZATION, GH

LEXICON V_STÅÅRJED
! page 388
! ståårjed:ståår
!  (ij stååraǥ)
! Work to be done 2015-01-10
+V+Imprt+Sg2:%^IMPSG2     K ;
+V+Imprt+ConNeg:aǥ K ;
! FORMS
! (1) +V+Inf: ståårjed
!   Ind.Prs.Pl1-Pl2, Ind.Prt.ConNeg
!   Imprt.Pl1-Pl2-Pl3, ActEss, Actio, PrsPrc, PrfPrc
! (2) +V+Ind+Prs+Sg3: ståårai
! (3) +V+Ind+Prs+Pl3: ståårje
! (4) +V+Ind+Prt+Pl3: ståårje
!   Ind.Prt.Sg1-Sg2-4
! (5) +V+Imprt+Sg2: stååraǥ
!   Ind.Prs.Sg1-Sg2-4-ConNeg
!   Ind.Prt.Sg3-Pl1-Pl2
!   Imprt.2.ConNeg
!   VAbe, GerTemp, GerInstr
!   Cond-Pot
! (6) +V+Pot+Sg3: ståårjež
! (7) +V+Imprt+Sg3: ståårjââǥǥas
! (8) +V+Imprt+ConNegII: ståårjuku
!    Pass.PrfPrc
! ståårjed:ståår
+V+Ind+Prs+Sg3:ai K        ;		! ståårai
+V+Ind+Prs+ConNeg:aǥ K ;   		! ståårâǥ
+V+Ind+Prs+Sg1:%>jam              K  ; 	! ståårjam
+V+Ind+Prs+Sg2:%>jak              K  ; !
+V+Ind+Prs+Pl1:%>jep      K  ;
+V+Ind+Prs+Pl2:%>jeʹped           K  ;
+V+Ind+Prs+Pl3:%>je               K  ;
+V+Ind+Prs+Sg4:%>jet K ; !

+V+Ind+Prt+Sg1:%>jem             K  ; ! ståårjem
+V+Ind+Prt+Sg2:%>jiǩ             K  ; ! ståårjiǩ
+V+Ind+Prt+Sg3:%>ji              K  ; ! ! ståårji
+V+Ind+Prt+Pl1:%>jim             K  ; ! ! ståårjim
+V+Ind+Prt+Pl2:%>jid             K  ; ! ! ståårjid
+V+Ind+Prt+Pl3:%>je              K  ; ! ståårje
+V+Ind+Prt+Sg4:%>ješ K ; ! ståårješ
+V+Ind+Prt+ConNeg:%>jam K ; ! ståårjam
+V+Ind+Prt+ConNeg:%>jâmma K ; ! ståårjâmma

+V+Imprt+Sg3:%>jââǥǥas          K  ; ! ståårjââǥǥas
+V+Imprt+Pl1:%>jâkap            K  ;
+V+Imprt+Pl2:%>jeʹǩed           K  ;
+V+Imprt+Pl3:%>jâkaz            K  ;

+V+Imprt+Sg2:aǥ  K ; ! stååraǥ
+V+Imprt+ConNeg:aǥ     K ;
+V+Imprt+ConNegII:%>juku K ;

+V:%>jeʹ         VSUF-I-POTKOND ; !WG
+V+Pot+Sg3:%>jež K ; !WG
+V+Inf:%>jed K ;
+V+ActEss:%>jeʹmen K ;
+V+Actio:%>jam K ;
+V+Actio:%>jam N_SLOOHMAN ;
+V+PrsPrc:%>jeei K ;
+V+PrsPrc+Der/N:%>jee N_TUÕĐŠTEEI ;
+V+PrfPrc:%>jam K ;
+V+Pass+PrfPrc:%>jummu K ;
+V+Pass+PrfPrc:%>juum K ;
+V+VAbess:%>jeǩâni K ;
+V:%>j GER_ ;

+V+Der/mmuš:%>jummuš N_MAINSTUMMUŠ ;
+V+Der/škueʹtted:je»škuätt V_PUEʹTTED ;


!  -jed -ted

!!

!! !CLASS 4 HIGH VOWEL, NO PALATALIZATION

LEXICON V_SILTTEED !!= * @CODE@ siltteed:siltt
! Ind+Prs+ConNeg: -âd
! lexicon ''@LEXNAME@'' 
+V: VSUF-TEEʹMEED-IND-PRS-SG3 ;
+V:%>âd VSUF-TEEʹMEED-IND-PRS-CONNEG ;
+V: VSUF-TEEʹMEED-ELSE ; !!= * @CODE@ 

!+V+Ind+Prs:		TEEMEED-PRS ;
!+V+Ind+Prt:		TEEMEED-PRT ;
!+V+Imprt:			TEEMEED-IMPRT ;
!+V+Imprt+Sg2:%>âd		K ;
!+V:%>eʹ		VSUF-I-POTKOND ; !WG
!+V+Pot+Sg3:%>eež	K ; !WG
!+V+Inf:%>eed K ;
!+V+ActEss:%>eʹmen K ;
!+V: GER_ ;
!+V+Actio:%>eem K ;
!+V+Actio:%>eem ATTESTED-NOUN-STEM ;
!+V+PrsPrc:%>eei K ;
!+V+PrsPrc+Der/N:%>ee N_TUÕĐŠTEEI ;
!+V+PrfPrc:%>ääm K ;
!+V+Pass+PrfPrc:%>ummu K ;
!+V+Pass+PrfPrc:%>uum K ;
!+V+VAbess:%>eǩâni K ;		!WG
!+V+Imprt+ConNeg:%>ukku K ;
!+V+Der/mmuš:%>ummuš N_MAINSTUMMUŠ ;

!! !CLASS 4 LOW VOWEL, NO PALATALIZATION
LEXICON V_SOLLEED
! Ind+Prs+ConNeg: -ad
! solleed:soll
! * Yaml: __V-solleed__
! +Ind+Prs+ConNeg: sollad
! lexicon ''@LEXNAME@'' 
+V: VSUF-TEEʹMEED-IND-PRS-SG3 ;
+V:%>ad VSUF-TEEʹMEED-IND-PRS-CONNEG ;
+V: VSUF-TEEʹMEED-ELSE ; !!= * @CODE@


!! !CLASS 4 HIGH VOWEL,  PALATALIZATION

!! !CLASS 4 LOW VOWEL, PALATALIZATION

LEXICON V_TEEʹMEED
! Ind+Prs+ConNeg: Palatalization AND -ed
! teeʹmeed:teeʹm
! lexicon ''@LEXNAME@'' 
+V: VSUF-TEEʹMEED-IND-PRS-SG3 ;
+V:%>ed VSUF-TEEʹMEED-IND-PRS-CONNEG ;
+V: VSUF-TEEʹMEED-ELSE ; !!= * @CODE@


!! Not yet written








LEXICON KAMRDED-01

+V+Ind+Prs:		KAMRDED-PRS ;
+V+Ind+Prt:		KAMRDED-PRT ;
+V+Imprt:			KAMRDED-IMPRT ;
+V+Imprt+ConNeg:%>uku K ;
+V:%>eʹ		VSUF-I-POTKOND ; !WG
+V+Pot+Sg3:%>ež	K ; !WG
+V+Inf:%>ed K ;
+V+ActEss:%>eʹmen K ;
+V+Actio:%>am K ;
+V+PrsPrc:%>eei K ;
+V+PrsPrc+Der/N:%>ee N_TUÕĐŠTEEI ;
+V+PrfPrc:%>am K ;
+V+PrfPrc:%>âmma K ;
+V+Pass+PrfPrc:%>ummu K ;
+V+Pass+PrfPrc:%>um K ; !
+V+Pass+PrfPrc:%>uum K ; !
+V+VAbess:%>eǩâni K ;
+V+VAbess:%>eǩeânai K ;
+V:  GER_ ;
+V+Der/mmuš:%^PenV2VV%>ummuš N_MAINSTUMMUŠ ;



! Intermediate suffix lexica
! --------------------------


! Final suffix lexica
! -------------------

! Suffix series I
! - - - - - - - -

! assuming stem __viǯǯ__


! Present tense
! - - - - - - - -

! assuming stem _teevv_

! assuming stem _kâlmm_

! assuming stem _juʹrdd_

! assuming stem _tieʹtt_
LEXICON VSUF-TEEʹMEED-IND-PRS-SG3
+Ind+Prs+Sg3:%>ad K ; !!= * @CODE@ teeʹmad

LEXICON	VSUF-TEEʹMEED-IND-PRS-CONNEG
+Ind+Prs+ConNeg:  K ;
+Imprt+Sg2:  K ;
+Imprt+ConNeg:  K ;

LEXICON	VSUF-TEEʹMEED-ELSE
+Ind+Prs+Sg1:%>ääm             K       ; !WG _ääʹvääm_
+Ind+Prs+Sg2:%>ääk             K       ; !WG ! _ääʹvääk_
+Ind+Prs+Pl1:%>eep             K       ; ! _ääʹveep_ _tieʹttep_
+Ind+Prs+Pl2:%>eʹped           K       ; ! _ääʹveʹped_
+Ind+Prs+Pl3:%>ee              K       ; ! _ääʹvee_
+Ind+Prs+Sg4:%>eet K ; ! ääʹveet

+Ind+Prt+Sg1:%>eem             K       ; ! _ääʹveem_
+Ind+Prt+Sg2:%>iiǩ             K       ; ! _ääʹviiǩ_
+Ind+Prt+Sg3:%>ii              K       ; !WG ! _ääʹvii_
+Ind+Prt+Pl1:%>iim             K       ; !WG ! _ääʹviim_
+Ind+Prt+Use/NG+Pl1:%>iin              K       ; !WG ! _ääʹviin_ ! sub??
+Ind+Prt+Pl2:%>iid             K       ; !WG ! _ääʹviid_
+Ind+Prt+Pl3:%>ee              K       ; ! _ääʹvee_
+Ind+Prt+Sg4:%>eeš K ; ! ääʹveeš
+Ind+Prt+ConNeg:%>ääm K ;
+Ind+Prs+ConNegII:%>uku K ;
+Ind+Imprt+ConNegII:%>uku K ;

+Pot+Sg1:eʹžem K ;
+Pot+Sg2:eʹžiǩ K ;
+Pot+Sg3:eež K ;
+Pot+Pl1:eʹžep K ;
+Use/NG+Pot+Pl1:eʹžim K ;
+Use/NG+Pot+Pl1:eʹžin K ;
+Pot+Pl2:eʹžid K ;
+Pot+Pl3:eʹže K  ;
+Pot+Sg4:eʹžet K ;
+Pot+ConNeg:eʹže K ;

+Cond+Sg1:eʹčem K        ;
+Cond+Sg2:eʹčiǩ K        ;
+Cond+Sg3:eʹči K ;
+Cond+Pl1:eʹčim K        ; ! How are these related?
+Cond+Pl1+Use/NG:eʹčep K ; ! Are there substandards?
+Cond+Pl1+Use/NG:eʹčin K ; !
+Cond+Pl2:eʹčid K        ;
+Cond+Pl3:eʹče K ;
+Cond+Sg4:eʹčeš K ;
+Cond+ConNeg:eʹče K ;

+Imprt+Sg3:%>ââǥǥas          K       ; !WG
+Imprt+Pl1:%>âkap                    K       ;
+Imprt+Pl2:%>eʹǩed           K       ;
+Use/NG+Imprt+Pl2:%>eed           K       ;
+Imprt+Pl3:%>âkaz            K       ;

+Inf:eed K ;
 GER_ ;
+Actio:%>eeʹm K ;
+Actio+Nom:%>eeʹm K ;
+Actio+Acc:%>eeʹm K ;
+Actio+Gen:%>eeʹm K ;
+ActEss:%>eʹmmen K ;
+Actio:%>eeʹm ATTESTED-NOUN-STEM ;
+V+VAbess:%>eeǩâni K ; 
+PrsPrc:%>eei K ;
+PrsPrc+Der/N:%>ee N_TUÕĐŠTEEI ;
+V+Act+PrfPrc:%>ääm K ;
+V+Pass+PrfPrc:%>ummu K ;
+V+Pass+PrfPrc:%>uum K ;
+V+Der/mmuš:%>ummuš N_MAINSTUMMUŠ ;


! assuming stem _ääʹv_ _teeʹm_
LEXICON TEEMEED-PRS
 +Sg1:%>ääm		K	; !WG _ääʹvääm_ 
 +Sg2:%>ääk		K	; !WG ! _ääʹvääk_
 +Sg3:%>ad K	; ! _ääʹvad_
 +Pl1:%>eep		K	; ! _ääʹveep_ _tieʹttep_
 +Pl2:%>eʹped		K	; ! _ääʹvveʹted_
 +Pl3:%>ee		K	; ! _ääʹvee_
 +Sg4:%>eet K ; ! ääʹveet

 +ConNeg:%>ed K ; ! ääʹved


! assuming stem kååʹmmerded
LEXICON KAMRDED-PRS
 +Sg1:%>am		K	; !WG 
 +Sg2:%>ak		K	; !WG 
 +Sg3:%^INDPRSSG3 K	; 
 +Pl1:%>ep	K	; 
 +Pl2:%>eʹped		K	; 
 +Pl3:%>e		K	; 
 +Sg4:%>et K ; ! 
 +ConNeg:%^IMPSG2 K ; ! kååʹmmerd

LEXICON LEED-PRS
 +Sg1:eäm		K	; !WG
 +Sg2:eäk		K	; !WG
 +Sg3:ij		K	; 
 +Pl1:eäˈʹp	K	; ! __
 +Pl2:%>eäˈʹped		K	; ! __
 +Pl2:%>eʹped		K	; ! __
 +Pl3:%>ie		K	; ! __
 +Pl3:%>iâ		K	; ! __
 +Pl3:%>ee		K	; ! __
 +Sg4:%>eäˈʹt K ; ! 
 +ConNeg:eäkˈku K ; ! 

! Past tense
! - - - - - - - -


LEXICON TEEMEED-PRT
 +Sg1:%>eem		K	; ! _ääʹveem_
 +Sg2:%>iiǩ		K	; ! _ääʹviiǩ_
 +Sg3:%>ii		K	; !WG ! _ääʹvii_
 +Pl1:%>iim		K	; !WG ! _ääʹviim_
 +Use/NG+Pl1:%>iin		K	; !WG ! _ääʹviin_ ! sub??
 +Pl2:%>iid		K	; !WG ! _ääʹviid_
 +Pl3:%>ee		K	; ! _ääʹvee_
 +Sg4:%>eeš K ; ! ääʹveeš
 +ConNeg:%>ääm K ;


! assuming stem _kååʹmmerd_
LEXICON KAMRDED-PRT
 +Sg1:%>em             K  ; ! kåʹmrdem
 +Sg2:%>iǩ             K  ; ! kåʹmrdiǩ
 +Sg3:%>i              K  ; !WG ! kåʹmrdi
 +Pl1:%>im             K  ; !WG ! kåʹmrdim
 +Use/NG+Pl1+Pl1:%>in             K  ; !WG ! kåʹmrdin
 +Use/NG+Pl1+Pl1:%>iim             K  ; !WG ! kåʹmrdiim
 +Use/NG+Pl1+Pl1:%>iin             K  ; !WG ! kåʹmrdiin
 +Pl2:%>id             K  ; !WG ! kåʹmrdid
 +Use/NG+Pl1+Pl2:%>iid             K  ; !WG ! kåʹmrdiid
 +Pl3:%>e              K  ; ! kåʹmrde
 +Sg4:%>eš K ; ! kåʹmrdeš

LEXICON LEED-PRT
 +Sg1:%>eʹjjem             K  ; !
 +Sg2:%>eʹjjiǩ             K  ; !
 +Sg3:%>eäi              K  ; !
 +Sg3:%>eei              K  ; !
 +Sg3:%>äi              K  ; !
 +Pl1:%>eeiʹm             K  ; !
 +Pl2:%>eeiʹd             K  ; !
 +Pl3:%>eʹjje              K  ; !
 +Sg4:%>eʹjješ K ; ! 
 +ConNeg:eämˈmaž K ; !
 +ConNeg:eämˈma K ; !
 +ConNeg:eäm K ; !


! Imperative
! - - - - - - -
! teevvad
! 
!

! tieʹtted

! teeʹmeed
LEXICON TEEMEED-IMPRT
 +Sg3:%>ââǥǥas		K	; !WG
 +Pl1:%>âkap			K	;
 +Pl2:%>eʹǩed		K	;
 +Pl3:%>âkaz		K	;

LEXICON KAMRDED-IMPRT
 +Sg3:%>ââǥǥas          K  ; !WG
 +Pl1:%>âkap                    K  ;
 +Pl2:%>eʹǩed           K  ;
 +Pl3:%>âkaz            K  ;

LEXICON LEED-IMPRT
 +Sg3:%>eäǥǥas          K  ; !WG
 +Pl1:%>eäkˈkap                    K  ;
 +Pl2:%>eäkˈku           K  ;
 +Pl2:%>eäkˈkveʹted           K  ;
 +Pl3:%>eäkˈkaz            K  ;




! Potentialis & Konditionalis
! not including +Pot+Sg3
! - - - - - - - - - - - - - -
LEXICON VSUF-INF_ÂD
+Inf:%>âd K ;
+Ind+Prs+Pl1:%>âp K ;
+Ind+Prs+Pl2:%>veʹted K ;
+Ind+Prt+ConNeg:%>âm K ;
+Imprt+Pl1:%>âp K ;
+Imprt+Pl2:%>âd K ;
+ActEss:%>men K ;
+Actio:%>âm K ;
+Actio:%>âm N_ǨIÕRÂČ ; ! mõõnnâm
+PrsPrc:%>i K ;
+PrfPrc:%>âm K ;
LEXICON VSUF-IND-PRS-SG3_ÂD
+Ind+Prs+Sg3: K ;
+Imprt+Pl3:%>az K ;


LEXICON VSUF-INF_AD
+Inf:%>ad K ;
+Ind+Prs+Pl1:%>ap K ;
+Ind+Prs+Pl2:%>veʹted K ;
+Ind+Prt+ConNeg:%>am K ;
+Imprt+Pl1:%>ap K ;
+Imprt+Pl2:%>ad K ;
+ActEss:%>men K ;
+Actio:%>am K ;
+Actio:%>am N_SLOOHMAN ;
 VSUF-PRSPRC_AD ;
+PrfPrc:%>am K ;
+Der/mõš:»mõš N_LOOKKMÕŠ ;

LEXICON VSUF-IND-PRS-SG3_AD
+Ind+Prs+Sg3: K ;

!+Imprt+Pl3:%>az K ; VSUF-IMPRT-PL3_AD


LEXICON VSUF-INF_ED
+Inf:%>ed K ;
+Ind+Prs+Pl1:%>ep K ;
+Ind+Prs+Pl2:%>veʹted K ;
+Imprt+Pl1:%>ep K ;
+Imprt+Pl2:%>ed K ;
+ActEss:%>men K ;
+Actio:%>em K ;
+Actio:%>em  N_MINIʹSTTER ; ! Should there be a derivation here 2015-05-06
 VSUF-PRSPRC_ED ;



LEXICON VSUF-IND-PRS-SG3_ED
+Ind+Prs+Sg3: K ;
+Ind+Prt+ConNeg:%>am K ;
+PrfPrc:%>am K ; !HERE or ONE LEXICON UP
+Der/mõš:»mõš N_LOOKKMÕŠ ;

LEXICON VSUF-IMPRT-PL3_ED !!= * @CODE@ puätˈtaz
+Imprt+Pl3:%>az K ;

!
LEXICON VSUF-IND-PRT-PL3_ÂD
+Ind+Prt+Sg1:%>em K ;
+Ind+Prt+Sg2:%>iǩ K ;
+Ind+Prt+Pl3:%>e K ;
+Ind+Prt+Sg4:%>eš K ;
LEXICON VSUF-IND-PRT-PL3_AD
+Ind+Prt+Sg1:%>um K ;
+Ind+Prt+Sg2:%>uk K ;
+Ind+Prt+Pl3:%>u K ;
+Ind+Prt+Sg4:%>uš K ;
LEXICON VSUF-IND-PRT-PL3_ED
+Ind+Prt+Sg1:%>em K ;
+Ind+Prt+Sg2:%>iǩ K ;
+Ind+Prt+Pl3:%>e K ;
+Ind+Prt+Sg4:%>eš K ;
!
LEXICON VSUF-IND-PRT-SG3-PL1-PL2_
+Ind+Prt+Sg3:%>i K ;
+Ind+Prt+Pl1:%>im K ;
+Ind+Prt+Pl1:%>in K ;
+Ind+Prt+Pl2:%>id K ;

!
LEXICON VSUF-I-POTKOND_ÂD
 VSUF-I-POTKOND ;
 VSUF-IND-PRT-SG3-PL1-PL2_ ;
+Ind+Prs+Sg1:%>am K ;
+Ind+Prs+Sg2:%>ak K ;
+Ind+Prs+Sg4:%>ât K ;
+Ind+Prs+ConNeg: K ;

+Imprt+Sg2: K ;
+Imprt+ConNeg: K ;
 VABESS ;
 GER_ ;


LEXICON VSUF-I-POTKOND_AD
 VSUF-I-POTKOND ;
+Pot+Sg3:%>ež   K ;
+Imprt+Sg3:%>as K ;
+Ind+Prs+Sg1:%>am K ;
+Ind+Prs+Sg2:%>ak K ;
+Ind+Prs+Sg4:%>at K ;
+Ind+Prs+ConNeg: K ;
:a VSUF-IND-PRT-SG3-PL1-PL2_ ;

+Imprt+Sg2: K ;
+Imprt+ConNeg: K ;
 VABESS ;
 GER_ ;

LEXICON VSUF-POTENTIAL_ED
+Pot+Sg3:%>ež K ;
 POTENTIAL ;
 VSUF-IND-PRT-SG3-PL1-PL2_ ;


LEXICON VABESS
+VAbess:%>kani K ;
+VAbess:%>kâni K ;
+VAbess:%>ǩeânai K ;
+VAbess:%>ǩâni K ;
+VAbess:%>ǩânai K ;
+VAbess:%>ǩani K ;

LEXICON GER_
+GerInstr:%>eeʹl K ; !FIX-ME
+GerTemp:%>een K ; !FIX-ME +Ger+Ess
+GerTemp:%>eeʹst K ; !FIX-ME +Ger+Loc

LEXICON VSUF-CONDITIONAL_ED  !!= * @CODE@ (7)
+Ind+Prs+Sg1:%>am K ;
+Ind+Prs+Sg2:%>ak K ;
 CONDITIONAL ;
+Imprt+Sg3:%>as K ;

LEXICON VSUF-IND-PRS-CONNEG_ED !!= * @CODE@ (6)
+Imprt+Sg2: K ;
+Imprt+ConNeg: K ;
+Ind+Prs+ConNeg: K ;
+Ind+Prs+Sg4:%>et K ;
 VABESS ;
 GER_ ; 

+Imprt+ConNeg: K ;


LEXICON VSUF-I-POTKOND
 POTENTIAL ;
 CONDITIONAL ;

LEXICON POTENTIAL
 +Pot+Sg1:žem K	;
 +Pot+Sg2:žiǩ K	;
 +Pot+Pl1:žep K	; ! How are these related?
 +Pot+Pl1:žim K	; ! Are there sub standards?
 +Pot+Pl1:žin K	; !
 +Pot+Pl2:žid K	;
 +Pot+Pl3:že K	;
 +Pot+Sg4:žet K ;
 +Pot+ConNeg:že K ;

LEXICON CONDITIONAL
 +Cond+Sg1:čem K	;
 +Cond+Sg2:čiǩ K	;
 +Cond+Sg3:či K	;
 +Cond+Pl1:čim K	; ! How are these related?
 +Cond+Pl1+Use/NG:čep K	; ! Are there substandards? 
 +Cond+Pl1+Use/NG:čin K	; !
 +Cond+Pl2:čid K	;
 +Cond+Pl3:če K	;
 +Cond+Sg4:češ K ;
 +Cond+ConNeg:če K ;


LEXICON VSUF-IMPRT-CONNEGII_ÂD
+Imprt+ConNegII:%>u K ;
+Pass+PrfPrc:%>um K ;
LEXICON VSUF-IMPRT-CONNEGII_AD
+Imprt+ConNegII:%>u K ;
+Pass+PrfPrc:%>um K ;
LEXICON VSUF-IMPRT-PL3_AD
+Imprt+Pl3:%>az K ;

LEXICON VSUF-IMPRT-CONNEGII_ED
+Imprt+ConNegII:%>u K ;
+Pass+PrfPrc:%>um K ;

! Suffix series II

! Not written

! Other series
! - - - - - -

LEXICON VSUF-PRSPRC_AD
+PrsPrc:ai K ;
+PrsPrc+Sg+Nom:ai K ;
+PrsPrc+Pl+Nom:%^CC2C%>ai K ;
+PrsPrc+Pl+Nom:ai K ;
+PrsPrc+Der/MWN: N_PÅÅSSAI ;

LEXICON VSUF-PRSPRC_ED
+PrsPrc:i K ;
+PrsPrc+Sg+Nom:i K ;
+PrsPrc+Pl+Nom:%^CC2C%>i K ;
+PrsPrc+Pl+Nom:i K ;
+PrsPrc+Der/MWN: N_JOOʹTTI ;
