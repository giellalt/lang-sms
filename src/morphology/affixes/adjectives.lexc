
!! !!!Skolt Saami adjective declension


LEXICON A_
+A+WORK: # ;

LEXICON A_PSS-PRFPRC
! These might simply be left out of the fst
+V+Pss+PrfPrc: # ;

LEXICON A-ATTR_
+A+Attr: # ;
+A+Der/vuõtt:»vuõtt N_MUORR ; 

LEXICON A-ATTR_ys
+A+Attr:%>âs # ;
+A+Comp+Attr:»ab # ;
+A+Superl+Attr:»mõs # ;
+A+Comp:»ab NMN_AANAR ;
+A+Superl:»mõs NMN_SAJOS ;
+A+Der/vuõtt:%>âs»vuõtt N_MUORR ; 

LEXICON A-ATTR_as
+A+Attr:%>as # ;
+A+Comp+Attr:»ab # ;
+A+Superl+Attr:»mõs # ;
+A+Comp:»ab NMN_AANAR ;
+A+Superl:»mõs NMN_SAJOS ;
+A+Der/vuõtt:%>as»vuõtt N_MUORR ; 

LEXICON A-ATTR_es
+A+Attr:%>es # ;
+A+Superl+Attr:»mõs # ;
+A: ADJ-DER/COMP_ab ; !!= * @CODE@ This has a Comp+Attr
!+A+Comp:»ab NMN_AANAR ;
+A+Superl:»mõs NMN_SAJOS ;
+A+Der/vuõtt:%>es»vuõtt N_MUORR ; 


!! CLASS 1 HIGH VOWEL, NO PALATALIZATION NOMINALS
LEXICON A_PAPP !!= * @CODE@ papp:papp
!! 
:%^V2VV A-ATTR_ys ;
+A+Sg+Nom: ENDLEX ;
+A+Pl+Nom:%^V2VV ENDLEX ; 
!+A+Der/MWN:  N_PAPP ;
+A:  NMN_PAPP ;

LEXICON A_TOLL
:%^V2VV%^CC2C A-ATTR_ys ;
+A+Sg+Nom: ENDLEX ;
+A+Pl+Nom:%^V2VV%^CC2C ENDLEX ; !!= * @CODE@ Check this 2015-02-22
!+A+Der/MWN: N_TOLL ;
+A: NMN_TOLL ;

LEXICON A_ALGG !!= * @CODE@ põrtt:põrtt
!! friskk
:%^V2VV%^XYY2XY A-ATTR_ys ;
!+A+Der/MWN: N_ALGG ;
+A: NMN_ALGG ;

LEXICON A_AUTT !!= * @CODE@ autt:a%^1VOWutt
!! friskk
:%^V2VV%^XYY2XY A-ATTR_ys ;
+A: NMN_AUTT ;


LEXICON A_MUORR !!= * @CODE@ muõrr:muõrr
:%^CC2C A-ATTR_ys ;
!+A+Der/MWN: N_MUORR ;
+A: NMN_MUORR ;

LEXICON A_KUOCC !!= * @CODE@ kuõcc:kuõcc
+A+Attr:%^KK2GG%>âs # ;
+A+Comp+Attr:%^KK2GG%>»j»ab # ;
+A+Superl+Attr:%^KK2GG%>»j»ummus # ;
+A+Comp:%^KK2GG%>j»ab NMN_AANAR ;
+A+Superl:%^KK2GG%>j»ummus NMN_MAINSTUMMUSH ;
+A: NMN_LUOSS ;

LEXICON A_CHUOSHKK !!= * @CODE@ čuõškk:čuõškk
!! tuõlbb
:%^XYY2XY A-ATTR_ys ;
!+A+Der/MWN: N_CHUOSHKK ;
+A: NMN_CHUOSHKK ;

LEXICON A_MIYRKK !!= * @CODE@ sǩiâŋkk:sǩiâŋkk
!! hiâlpp
:%^XYY2XY A-ATTR_ys ;
!+A+Der/MWN: N_MIYRKK ;
+A: NMN_MIYRKK ;

LEXICON A_PLAAN !!= * @CODE@ plaan:plaan
:%^V2VV A-ATTR_ys ; !!Check this 2015-09-17
!+A+Der/MWN:  N_PLAAN ;
+A: NMN_PLAAN ;


!! CLASS 1 LOW VOWEL, NO PALATALIZATION NOMINALS
LEXICON A_PESS
+A+Attr: ENDLEX ;
+A: NMN_PESS ;

LEXICON A_VAEAEZHZH
! Gradation: None
! 1.115
!! ** Yaml: __A-vaeaezhzh__
+A+Attr: ENDLEX ;
: A-ATTR_as ;
!+A+Der/MWN: N_VAEAEZHZH ;
+A: NMN_VAEAEZHZH ;


!! CLASS 1 LOW VOWEL, PALATALIZATION, ILLATIVE IN U NOMINALS


!! CLASS 1 HIGH VOWEL, PALATALIZATION NOMINALS
LEXICON A_NUQBB  !!= * @CODE@ nuʹbb:nubb
! Gradation: None
! 1.115
!! ** Yaml: __A-nubb__
:%^V2VV%^PAL   A-ATTR_es ;
!+A+Der/MWN: N_NUQBB ;
+A: NMN_NUQBB ;

LEXICON A_KUEQLL !!= * @CODE@ kueʹll:kuâll
! vowel: diphthong
:%^PALE%^PAL%^CC2C   A-ATTR_es ;
!+A+Der/MWN: N_KUEQLL ;
+A: NMN_KUEQLL ;

LEXICON A_MUEQRJJ !!= * @CODE@ mueʹrjj:mueʹrjj
! vowel: diphthong
:%^PALE%^PAL%^XYY2XY   A-ATTR_es ;
!+A+Der/MWN: N_MUEQRJJ ;
+A: NMN_MUEQRJJ ;

!! CLASS 1 LOW VOWEL, PALATALIZATION NOMINALS

LEXICON A_VAQLL !!= * @CODE@ väʹll:väll
:%^V2VV%^PAL   A-ATTR_es ;
!+A+Der/MWN: N_VAQLL ;
+A: NMN_VAQLL ;

LEXICON A_UQCC !!= * @CODE@ uʹcc:u%^1VOWcc
:%^V2VV%^PAL   A-ATTR_es ;
+A: NMN_UQCC ;

LEXICON A_SAAQMM !!= * @CODE@ sääʹmm:säämm
:%^CC2C%^PAL   A-ATTR_es ;
!+A+Der/MWN: N_SAAQMM ;
+A: NMN_SAAQMM ;

LEXICON A_CHAAQCC !!= * @CODE@ čääʹcc:čääcc
:%^PAL%^KK2GG   A-ATTR_es ;
+A+Der/MWN: N_CHAAQCC ;
+A: NMN_CHAAQCC ;

LEXICON A_KAQLBB !!= * @CODE@ käʹlbb:kälbb âʹlǧǧ:âlgg
! vowel: monophthong
:%^V2VV%^PAL%^CC2C   A-ATTR_es ;
!+A+Der/MWN: N_KAQLBB ;
+A: NMN_KAQLBB ;

LEXICON A_HAAQSKH !!= * @CODE@ hääʹsǩ:hääsk
! vowel: monophthong
! gradation: no
:%^PAL   A-ATTR_es ;
+A: NMN_HAAQSKH ;


LEXICON A_SEQTT !!= * @CODE@ seʹtt:sẹtt
! vowel: monophthong
!! CHECKME
:%^V2VV%^PAL   A-ATTR_es ;
!+A+Der/MWN: N_SEQTT ;
+A: NMN_SEQTT ;


LEXICON A_KHEQRJJ !!= * @CODE@ ǩeʹrjj:ǩẹrjj
! vowel: monophthong
:%^V2VV%^PAL%^XYY2XY   A-ATTR_es ;
!+A+Der/MWN: N_KHEQRJJ ;
+A: NMN_KHEQRJJ ;

LEXICON A_JAUQRR !!= * @CODE@ jäuʹrr:jäurr
! vowel: monophthong
:%^V2VV%^PAL%^XYY2XY   A-ATTR_es ;
!+A+Der/MWN: N_JAUQRR ;
+A: NMN_JAUQRR ;

LEXICON A_PEIQVV !!= * @CODE@ peiʹvv:pẹivv
! vowel: monophthong
:%^V2VV%^PAL%^XYY2XY   A-ATTR_es ;
!+A+Der/MWN: N_PEIQVV ;
+A: NMN_PEIQVV ;

LEXICON A_SUEAQKHKH !!= * @CODE@ sueʹǩǩ:suäkk
!! cuẹʹǩǩ
:%^PALẸ%^PAL%^KK2GG   A-ATTR_es ;
+A+Sg+Nom:%^PALẸ%^PAL ENDLEX ;
+A+Pl+Nom:%^PALẸ%^PAL%^KK2GG ENDLEX ;
!+A+Der/MWN: N_SUEAQKHKH ;
+A: NMN_SUEAQKHKH ;

LEXICON A_KUEAQTT !!= * @CODE@ kuẹʹtt:kuätt
! vowel: diphthong
:%^PALẸ%^PAL%^CC2C   A-ATTR_es ;
!+A+Der/MWN: N_KUEAQTT ;
+A: NMN_KUEAQTT ;

LEXICON A_JEAQNNN !!= * @CODE@ jeäʹnn:jeänˈn, jeäʹǧǧ:jeägˈg
! vowel: diphthong
:%^PALẸ%^PAL%^CCC2CC   A-ATTR_es ;
!+A+Der/MWN: N_JEAQNNN ;
+A: NMN_JEAQNNN ;

LEXICON A_JEAQRMM !!= * @CODE@ jeäʹrmm:jeärmm
! vowel: diphthong
:%^PALẸ%^PAL%^XYY2XY   A-ATTR_es ;
+A: NMN_JEAQRMM ;


!! CLASS denominals in -i
!! cf. Feist (2012: 198-199)
!! These will need their own expansions
!! HOW DOES JIÕʹNNI decline?
LEXICON A_JIOQNNI !!= * @CODE@ jiõʹnni:jiõnn FIXME 2016-04
+A+Attr:%^PAL%>es #   ;
+A+Sg+Nom:%^PAL%>i  # ;
+A:%^PAL%>j ADJ-DER/COMP_ab ;
+A:%^PAL%>j ADJ-DER/SUPERL_ummus ;
LEXICON A_PAERRAI !!= * @CODE@ pärrai:pärr
+A+Attr:%>as #   ;
+A+Sg+Nom:%>ai  # ;
+A:%>j ADJ-DER/COMP_ab ;
+A:%>j ADJ-DER/SUPERL_ummus ;
LEXICON A_SAELTTAI !!= * @CODE@ sälttai:sältt
+A+Attr:%>as #   ;
+A+Sg+Nom:%>ai  # ;
+A:%>j ADJ-DER/COMP_ab ;
+A:%>j ADJ-DER/SUPERL_ummus ;
!%^RmVow%^I2J

!! CLASS 

LEXICON A_SAJOS
!! -õs ending
+A: AdjAttr-ZERO ;
+A: SG-NOM ;
+A:%{XC%}%^C2CC%>â ESS ;
+A:%{XC%}%^C2CC%>â PAR ;
+A: NMN_OBL-SAJOS ;

LEXICON A_KOALVAK
+A+Attr:%^RmVow%>õs ENDLEX ;
+A:%^RmVow ADJ-DER/COMP_kwlmmsab ;
+A:%^RmVow ADJ-DER/SUPERL_ummus ;
+A: NMN_KOALVAK ;

LEXICON A_AACCIKH
+A+Attr:%^RmVow%^PALNo%>õs ENDLEX ;
+A:%^RmVow ADJ-DER/COMP_kwlmmsab ;
+A:%^RmVow ADJ-DER/SUPERL_ummus ;
+A: NMN_AACCIKH ;

LEXICON A_KUALAZH
+A+Sg+Nom:až ENDLEX ;
+A+Pl+Nom:až ENDLEX ; !!= * @CODE@ Check this 2015-02-22
!+A+Der/MWN: N_KUALAZH ;
+A: NMN_KUALAZH ;

LEXICON A_VOONYS !!= * @CODE@ võõnâs:võnn
+A+Sg+Nom:%^V2VV%^CC2Câs ENDLEX ;
+A+Pl+Nom: ENDLEX ; !!= * @CODE@ Check this 2015-02-22
+A:s ADJ-DER/COMP_kwlmmsab ;
+A:s ADJ-DER/SUPERL_ummus ;
!+A+Der/MWN: N_VOONYS ;
+A:âs NMN_VOONYS ;

LEXICON A_OODHYS !!= * @CODE@ oođâs:ođđ
+A+Attr: ENDLEX ;
+A:s ADJ-DER/COMP_kwlmmsab ;
+A:s ADJ-DER/SUPERL_ummus ;
!+A+Der/MWN:âz NMN_VOONYS ;
+A:âs NMN_VOONYS ;

LEXICON A_KOODDYS !!= * @CODE@ kõõddâs:kõdd
+A+Attr: ENDLEX ;
+A+Sg+Nom:%^Pen%^V2VV%>âs ENDLEX ;
+A+Pl+Nom:%>âz ENDLEX ; !!= * @CODE@ Check this 2015-02-22
+A:s ADJ-DER/COMP_kwlmmsab ;
+A:s ADJ-DER/SUPERL_ummus ;
!+A+Der/MWN:âz N_VOONYS ;
+A:âs NMN_KOODDYS ;

LEXICON A_MOOCHCHYS !!= * @CODE@ mooččâs:močč
+A+Attr: ENDLEX ;
+A+Sg+Nom:%>âs ENDLEX ;
+A: ADJ-DER/COMP_aeaeb ;
+A: ADJ-DER/SUPERL_ummus ;
+A:âs NMN_KOODDYS ;

LEXICON A_JOOSKYS !!= * @CODE@ jõõskâs:jõskk
+A+Attr: ENDLEX ;
+A:s ADJ-DER/COMP_kwlmmsab ;
+A:s ADJ-DER/SUPERL_ummus ;
!+A+Der/MWN:âs NMN_TAALKYS ;
+A:âs NMN_TAALKYS ;


LEXICON A_PIIUTYS !!= * @CODE@ piiutâs:pihtt
+A+Attr: ENDLEX ;
+A:s ADJ-DER/COMP_kwlmmsab ;
+A:s ADJ-DER/SUPERL_ummus ;
+A:âs NMN_PIIUTYS ;

LEXICON A_FIINYS !!= * @CODE@ fiinâs:fijnn
+A+Attr:%^XYY2VY ENDLEX ;	! fiin
+A:s ADJ-DER/COMP_kwlmmsab ;
+A:s ADJ-DER/SUPERL_ummus ;
!+A+Der/MWN:âz NMN_FIINYS ;	! fijnnâz
+A:âs NMN_FIINYS ;	! fijnnâz


LEXICON A_RUODDYS !!= * @CODE@ ruõddâs:ruõdˈd
+A+Attr: ENDLEX ;
+A:s ADJ-DER/COMP_kwlmmsab ;
+A:s ADJ-DER/SUPERL_ummus ;
!+A+Der/MWN:âz N_RUODDYS ;
+A:âs NMN_RUODDYS ;

LEXICON A_TUOVYS !!= * @CODE@ tuõvâs:tuõvv
+A+Attr:sõs ENDLEX ;
+A:s ADJ-DER/COMP_kwlmmsab ;
+A:s ADJ-DER/SUPERL_ummus ;
!+A+Der/MWN:âz N_TUOVYS ;
+A:âs NMN_TUOVYS ;


LEXICON A_VUOIVYS !!= * @CODE@ vuõivâs:vuõivv
+A+Attr: ENDLEX ;
+A:s ADJ-DER/COMP_kwlmmsab ;
+A:s ADJ-DER/SUPERL_ummus ;
!+A+Der/MWN:âz N_VUOIVYS ;
+A:âs NMN_VUOIVYS ;


LEXICON A_KOAOAVAS !!= * @CODE@ kååvas:kåvv
+A+Attr: ENDLEX ;
+A:s ADJ-DER/COMP_kwlmmsab ;
+A:s ADJ-DER/SUPERL_ummus ;
+A:as NMN_KOAOAVAS ;

LEXICON A_KOAOADDAS !!= * @CODE@ kååddas:kåådd
+A+Attr: ENDLEX ;
+A:s ADJ-DER/COMP_kwlmmsab ;
+A:s ADJ-DER/SUPERL_ummus ;
+A:as NMN_KOAOADDAS ;

LEXICON A_TOBDDSALLASH !!= * @CODE@ tobddsallaš:tobddsallaš
+A+Attr: ENDLEX ;
+A:%^RmVow ADJ-DER/COMP_kwlmmsab ;
+A:%^RmVow ADJ-DER/SUPERL_ummus ;
+A: NMN_MAINSTUMMUSH ;


LEXICON A_JAELLASH !!= * @CODE@ jällaš:jällaš
+A+Attr:%^RmVow%>õs ENDLEX ;
+A:%^RmVow ADJ-DER/COMP_kwlmmsab ;
+A:%^RmVow ADJ-DER/SUPERL_ummus ;
!+A+Der/MWN: NMN_MAINSTUMMUSH ;
+A: NMN_MAINSTUMMUSH ;


LEXICON A_VAALMYSH !!= * @CODE@ vaalmâš:valmmâš
+A+Attr:%^RmVow%>õs ENDLEX ; !CHECKME 2016-05-05
+A:%^RmVow ADJ-DER/COMP_kwlmmsab ;
+A:%^RmVow ADJ-DER/SUPERL_ummus ;
+A: NMN_TAALKYS ;
+A:%^V2VV%^XYY2XY A2N-VUOTT ;


LEXICON A_VEEIDAS !!= * @CODE@ veeidas:veidd
+A+Attr:sõs ENDLEX ;
+A:s ADJ-DER/COMP_kwlmmsab ;
+A:s ADJ-DER/SUPERL_ummus ;
+A:as NMN_PAEAELKAS ;


LEXICON A_AANAR !!= * @CODE@ späʹsseb:späʹsseb
+A+Attr:%^RmVow%>õs ENDLEX ;
+A:%^RmVow ADJ-DER/COMP_kwlmmsab ;
+A:%^RmVow ADJ-DER/SUPERL_ummus ;
+A: NMN_AANAR ;



LEXICON A_KAAQMES  !!= * @CODE@ kääʹmes:kämm
+A+Attr:%^V2VV%^PAL ENDLEX ;
+A:s ADJ-DER/COMP_kwlmmsab ;
+A:s ADJ-DER/SUPERL_ummus ;
+A:es NMN_OAQVES ; !!= * @CODE@   kääʹmes:kämmez
+A+Der/vuõtt:%^PAL%^V2VV%>es»vuõtt N_MUORR ; 

LEXICON A_KAAQLLES  !!= * @CODE@ kääʹlles:kä%^1VOWll
+A+Attr:%^V2VV%^PAL ENDLEX ;
+A:s ADJ-DER/COMP_kwlmmsab ;
+A:s ADJ-DER/SUPERL_ummus ;
+A:es NMN_KAAQLLES ; !!= * @CODE@   kääʹmes:kämmez
+A+Der/vuõtt:%^PAL%^V2VV%>es»vuõtt N_MUORR ; 

LEXICON A_KOAQSHKHES !!= * @CODE@ kååʹšǩes:kåškk
+A+Attr:%^PAL ENDLEX ;
+A:s ADJ-DER/COMP_kwlmmsab ;
+A:s ADJ-DER/SUPERL_ummus ;
+A:es NMN_KAAQRBES ;
+A+Der/vuõtt:%^PAL%^XYY2XY%>es»vuõtt N_MUORR ; 

LEXICON A_KHEEQLES  !!= * @CODE@ ǩeeʹles:ǩeâlˈl
+A+Attr:%^Pen%PALE%^Pen%^PAL%^Pen%^CCC2C%>es ENDLEX ; !CHECKME
+A:s ADJ-DER/COMP_kwlmmsab ;
+A:s ADJ-DER/SUPERL_ummus ;
+A:es NMN_KHEEQLES ; !!= * @CODE@	ǩeeʹles:ǩeâlˈles
+A+Der/vuõtt:%^PALE%^PAL%^CCC2C%>es»vuõtt N_MUORR ; 


LEXICON A_PUAQRES  !!= * @CODE@ puäʹres:puärˈr
+A+Attr:%^PAL ENDLEX ;
+A:s ADJ-DER/COMP_kwlmmsab ;
+A:s ADJ-DER/SUPERL_ummus ;
+A:es NMN_PUAQRES ; !!= * @CODE@   puäʹres:puärˈres
+A+Der/vuõtt:%^PAL%^CCC2C%>es»vuõtt N_MUORR ; 

LEXICON A_CHUEQDHES  !!= * @CODE@ čueʹđes:čuâtˈt
+A+Attr:%^PAL ENDLEX ;
+A:s ADJ-DER/COMP_kwlmmsab ;
+A:s ADJ-DER/SUPERL_ummus ;
+A:es NMN_CHUEQDHES ; !!= * @CODE@   puäʹres:puärˈrez
+A+Der/vuõtt:%^PALE%^PAL%^CCC2C%>es»vuõtt N_MUORR ; 

LEXICON A_CHUEAQCKHES  !!= * @CODE@ čueʹcǩes:čuäckkez
+A+Attr:%^PALẸ%^PAL ENDLEX ; !CHECKME
+A:s ADJ-DER/COMP_kwlmmsab ;
+A:s ADJ-DER/SUPERL_ummus ;
+A:es NMN_CHUEAQCKHES ; !!= * @CODE@  čueʹcǩes:čuäckkez
+A+Der/vuõtt:%^PALẸ%^PAL%^XYY2XY%>es»vuõtt N_MUORR ; 


LEXICON A_PRSPRC-EEI !!= * @CODE@ tuejjeei:tuejjee
+A+Attr:jes ENDLEX ;
+A+Attr:jas ENDLEX ; !!= * @CODE@ c.f. sjm faʹlsǩeejas
+A: NMN_PRSPRC-EEI ;
+A:j ADJ-DER/COMP_kwlmmsab ;
+A:j ADJ-DER/SUPERL_ummus ;

!! WORK HERE 2015-10-14 deverbals
LEXICON A_PRSPRC-VCC-I !!= * @CODE@ cõggi:cõggi
+A: NMN_PRSPRC-VCC-I ;
+A:%^I2J ADJ-DER/COMP_kwlmmsab ;
+A:%^I2J ADJ-DER/SUPERL_ummus ;

LEXICON A_PRSPRC-VVCC-I !!= * @CODE@ jõõvvi:jõõvvi
!! ** Yaml: __A-vaeaezhzh__
+A: NMN_PRSPRC-VVCC-I ;
+A:%^I2J ADJ-DER/COMP_kwlmmsab ;
+A:%^I2J ADJ-DER/SUPERL_ummus ;
LEXICON A_PRSPRC-VVKK-I !!= * @CODE@ lookki:lookki
+A: NMN_PRSPRC-VVKK-I ;
+A:%^I2J ADJ-DER/COMP_kwlmmsab ;
+A:%^I2J ADJ-DER/SUPERL_ummus ;
LEXICON A_KOBDDI !!= * @CODE@ kobddi:kobddi SEE following A_TOBDDI diff
+A+Attr:%^RmVow # ;
+A: NMN_PRSPRC-VXYY-I ;
+A:%^I2J ADJ-DER/COMP_kwlmmsab ;
+A:%^I2J ADJ-DER/SUPERL_ummus ;

LEXICON A_PRSPRC-VWKK-I !!= * @CODE@ ǩiõssi:ǩiõssi
+A: NMN_PRSPRC-VWKK-I ;
+A:%^I2J ADJ-DER/COMP_kwlmmsab ;
+A:%^I2J ADJ-DER/SUPERL_ummus ;
LEXICON A_PRSPRC-VWCC-I !!= * @CODE@ vuõlli:vuõlli
+A: NMN_PRSPRC-VWCC-I ;
+A:%^I2J ADJ-DER/COMP_kwlmmsab ;
+A:%^I2J ADJ-DER/SUPERL_ummus ;
LEXICON A_PRSPRC-VWXYY-I !!= * @CODE@ kuõskki:kuõskki
+A: NMN_PRSPRC-VWXYY-I ;
+A:%^I2J ADJ-DER/COMP_kwlmmsab ;
+A:%^I2J ADJ-DER/SUPERL_ummus ;
LEXICON A_PRSPRC-VCC-AI !!= * @CODE@ kåččai:kåččai
+A: NMN_PRSPRC-VCC-AI ;
+A:%^RmVow%^I2J ADJ-DER/COMP_kwlmmsab ;
+A:%^RmVow%^I2J ADJ-DER/SUPERL_ummus ;
LEXICON A_PRSPRC-VVCC-AI !!= * @CODE@ peellai:peellai
+A: NMN_PRSPRC-VVCC-AI ;
+A:%^RmVow%^I2J ADJ-DER/COMP_kwlmmsab ;
+A:%^RmVow%^I2J ADJ-DER/SUPERL_ummus ;
LEXICON A_PRSPRC-VVKK-AI !!= * @CODE@ pååssai:pååssai
+A: NMN_PRSPRC-VVKK-AI ;
+A:%^RmVow%^I2J ADJ-DER/COMP_kwlmmsab ;
+A:%^RmVow%^I2J ADJ-DER/SUPERL_ummus ;
LEXICON A_PRSPRC-VXYY-AI !!= * @CODE@ särnnai:särnnai
+A: NMN_PRSPRC-VXYY-AI ;
+A:%^RmVow%^I2J ADJ-DER/COMP_kwlmmsab ;
+A:%^RmVow%^I2J ADJ-DER/SUPERL_ummus ;
LEXICON A_PRSPRC-VWCC-AI !!= * @CODE@ vuejjai:vuejjai
+A: NMN_PRSPRC-VWCC-AI ;
+A:%^RmVow%^I2J ADJ-DER/COMP_kwlmmsab ;
+A:%^RmVow%^I2J ADJ-DER/SUPERL_ummus ;
LEXICON A_PRSPRC-VWKK-AI !!= * @CODE@ reäkkai:reäkkai
+A: NMN_PRSPRC-VWKK-AI ;
+A:%^RmVow%^I2J ADJ-DER/COMP_kwlmmsab ;
+A:%^RmVow%^I2J ADJ-DER/SUPERL_ummus ;
LEXICON A_PRSPRC-VWCCC-AI !!= * @CODE@ vuäggai:vuägˈgai
+A: NMN_PRSPRC-VWCCC-AI ;
+A:%^RmVow%^I2J ADJ-DER/COMP_kwlmmsab ;
+A:%^RmVow%^I2J ADJ-DER/SUPERL_ummus ;
LEXICON A_PRSPRC-VWXYY-AI !!= * @CODE@ meälttai:meälttai
+A: NMN_PRSPRC-VWXYY-AI ;
+A:%^RmVow%^I2J ADJ-DER/COMP_kwlmmsab ;
+A:%^RmVow%^I2J ADJ-DER/SUPERL_ummus ;

LEXICON A_PRSPRC-VXYY-I !!= * @CODE@ äjjnõʹstti:äjjnõʹstti
+A:%^I2J ADJ-DER/COMP_kwlmmsab ;
+A:%^I2J ADJ-DER/SUPERL_ummus ;
+A: NMN_PRSPRC-VXYY-I ;
LEXICON A_SUQVDDI !!= * @CODE@ suʹvddi:suʹvddi
+A: NMN_SUQVDDI ;
+A:%^I2J ADJ-DER/COMP_kwlmmsab ;
+A:%^I2J ADJ-DER/SUPERL_ummus ;

LEXICON A_LIEQDHDHDHI !!= * @CODE@ lieʹđđi:lieʹđˈđi
+A: NMN_LIEQDHDHDHI ;
+A:%^I2J ADJ-DER/COMP_kwlmmsab ;
+A:%^I2J ADJ-DER/SUPERL_ummus ;
LEXICON A_PRSPRC_VWXYY-I !!= * @CODE@ vueʹstti:vueʹstti
+A: NMN_PRSPRC_VWXYY-I ;
+A:%^I2J ADJ-DER/COMP_kwlmmsab ;
+A:%^I2J ADJ-DER/SUPERL_ummus ;

LEXICON A_VUOIQNNI !!= * @CODE@ vuõiʹnni:vuõiʹnni
+A: NMN_VUOIQNNI ;
+A:%^I2J ADJ-DER/COMP_kwlmmsab ;
+A:%^I2J ADJ-DER/SUPERL_ummus ;

LEXICON A_JUURD !!= * @CODE@ juurd:jurdd
+A+Sg+Nom:%^V2VV%^XYY2XY ENDLEX ;
+A+Pl+Nom: ENDLEX ;
+A: NMN_JUURD ;


!LEXICON A_SLOOHMAN
!!! !!2. WORDS WITH TWO-SYLLABLE NOMINATIVE SINGULARS (2009: 252)
!!! !!2.3 Sg.Loc in -est. e-stems (Sg.Loc, Ess, Par).
!!! !!2.3.2 Sg.Ill in -a
!!! !!2.3.2.2 LACKS Gradation
!!! !!2.3.2.2.1 Second syllable vowel loss (Sg.Ill, Sg.Loc, Sg.Com; Ess, Par; Pl.Gen, Pl.Acc, Pl.Ill, Pl.Loc, Pl.Com, Pl.Abe)
!!! ** Yaml: __A-sloohman__
!+A+Sg+Nom: ENDLEX ;
!+A+Pl+Nom: ENDLEX ;
!+A: NMN_AANAR ;

LEXICON A_RADIO 
!! check this 2015-11-10
+A+Sg+Nom: ENDLEX ;
+A+Pl+Nom: ENDLEX ;
+A: NMN_RADIO ;

LEXICON A_NALLSHEM
!! !!2. WORDS WITH MULTI-SYLLABLE NOMINATIVE SINGULARS (2009: 293)
!! !!2.3 Sg.Loc in -est. e-stems (Sg.Loc, Ess, Par).
!! !!2.3.2 Sg.Ill in -a
!! !!2.3.2.1 Has Gradation 
!! !!2.3.2.1.1 Second syllable vowel loss (Sg.Ill, Sg.Loc, Sg.Com; Ess, Par; Pl.Obl)
!!NumContLex="650"
!! ** Yaml: __A-chaalmtem__
+A: AdjAttr-es ;
+A+Sg+Nom: ENDLEX ;
+A+Pl+Nom:%^V2VV%^PAL ENDLEX ;
+A: NMN_NALLSHEM ;

!! CLASS 11 ADJECTIVES
LEXICON A_SHIYLGGYD !!= * @CODE@ šiâlggâd:šiâlgg
+A:%^VOWRaise%^PALE%^PAL AdjAttr-es ;
+A: NMN_VISKKYD ;
+A:âd A2N-VUOTT ;
+A: ADJ-DER/COMP_aeaeb ;
+A: ADJ-DER/SUPERL_ummus ;

LEXICON A_VISKKYD !!= * @CODE@ viskkâd:viskk
+A:%^PAL AdjAttr-es ;
+A: NMN_VISKKYD ;
+A:âd A2N-VUOTT ;
+A: ADJ-DER/COMP_aeaeb ;
+A: ADJ-DER/SUPERL_ummus ;


LEXICON A_MOASHSHAD !!= * @CODE@ måššad:måšš
+A: AdjAttr-es ;
+A: NMN_MOASHSHAD ;
+A:ad A2N-VUOTT ;
+A: ADJ-DER/COMP_aeaeb ;
+A: ADJ-DER/SUPERL_ummus ;


LEXICON A_AQKHKHED !!= * @CODE@ âʹǩǩed:âʹǩǩ
+A: AdjAttr-es ;
+A: NMN_AQKHKHED ;
+A:ed A2N-VUOTT ;
+A: ADJ-DER/COMP_aeaeb ;
+A: ADJ-DER/SUPERL_ummus ;


LEXICON A20
+A+Pred+Pl: # ; 
+A+Comp:»ab # ; 
+A+Superl:»mõs # ; 

LEXICON A_MEERSAZH
!! säʹmmlaž:säʹmml
+A:až AdjAttr-ZERO ;
+A: ADJ-DER/COMP_kwlmmsab ;
+A: ADJ-DER/SUPERL_ummus ;
+A+Der/MWN:  N_MEERSAZH ;



LEXICON A30
!+A: # ; same form
+A+Comp:»ab # ; !!NPAL
+A+Superl:»ummus # ; !!NPAL

LEXICON DenominalAdjsCShort
+Der/teʹm:»tẹ%^1VOWm A_NALLSHEM ;
!+Der/A+Pred+Sg:»teʹm # ;
!+Der/A+Pred+Pl:»teʹm # ;
+Der/A+Der/teʹm:»teʹm A2N-VUOTT ;
+Der/A+Der/teʹm:»teʹm AdjAttr-es ; ! 2013-09-19 the +Attr removed to avoid double tags
+Der/t+A:»t AdjDerComp ; ! 2013-09-19 the +Comp removed to avoid double tags
+Der/t+A:»t AdjDerSuperl-ummus ; ! 2013-09-19 the +Superl removed to avoid double tags
+Der/tõõvvâd:»tõõvv IV_LAUKKOOLLYD ; !!= * @CODE@ privative = carative + translative

LEXICON AdjAttr-ZERO
+Attr: ENDLEX ;
+Attr: A2N-VUOTT ;

LEXICON AdjAttr-es
+Attr:%>es ENDLEX ; 
+Attr:%>es A2N-VUOTT ;

LEXICON A2N-VUOTT
+Der/vuõtt:»vuõtt N_MUORR ;


LEXICON AdjDerComp
+Comp+Attr:»ab # ; 

LEXICON A_IS_ADJ-DER/COMP_ab
+A: ADJ-DER/COMP_ab ;
!! Class 12 Feist 163
LEXICON ADJ-DER/COMP_ab
+Comp+Attr:»ab # ;
+Comp:»ab SG-NOM ;
+Comp:»ab SG-ABE/ACC/GEN ;
+Comp:»ab PL-NOM ;
+Comp:»ba SG-ILL ; !!= * @CODE@ Sammallahti (1991: 188)
+Comp:»be SG-LOC ; !!= * @CODE@ Sammallahti (1991: 188)
+Comp:»b SG-COM_IN ; !!= * @CODE@ Sammallahti (1991: 188)
+Comp:»b PL-OBL ; !!= * @CODE@ Sammallahti (1991: 188)
+Comp:»õʹbbe SG-ILL ;
+Comp:»õbâ SG-LOC ;
+Comp:»õb SG-COM_IN ;
+Comp:»õb PL-OBL ;
+Comp:»õbâ ESS/PAR ;
+Comp+Ess:»ben K ;

LEXICON A_IS_ADJ-DER/COMP_aeaeb
+A: ADJ-DER/COMP_aeaeb ;
LEXICON ADJ-DER/COMP_aeaeb
+Comp+Attr:»ääb # ;
+Comp:»ääb SG-NOM ;
+Comp:»ääb SG-ABE/ACC/GEN-PL-NOM ;
+Comp:»uʹbbe SG-ILL ;
+Comp:»ubu SG-LOC ;
+Comp:»ubu SG-COM_IN/PL-OBL ;
+Comp:»ubu ESS/PAR ;

LEXICON A_IS_ADJ-DER/COMP_kwlmmsab
+A: ADJ-DER/COMP_kwlmmsab ;
!! Sammallahti 2010: 151
LEXICON ADJ-DER/COMP_kwlmmsab
+Comp+Attr:»ab # ;
+Comp:»ab SG-NOM ;
+Comp:»ab SG-ABE/ACC/GEN ;
+Comp:»ab PL-NOM ;
+Comp:»õʹbbe SG-ILL ;
+Use/NG+Comp:»uʹbbe SG-ILL ;
+Comp:»õbâ SG-LOC ;
+Use/NG+Comp:»ubu SG-LOC ;
+Comp:»õb SG-COM_IN ;
+Use/NG+Comp:»ubu SG-COM_IN ;
+Comp:»õb PL-OBL ;
+Use/NG+Comp:»ubu PL-OBL ;
+Comp:»õbâ ESS/PAR ;
+Use/SpellNoSugg+Use/NG+Comp:»ubâ ESS/PAR ;
+Use/SpellNoSugg+Use/NG+Comp:»ubu ESS/PAR ;

LEXICON A_IS_ADJ-DER/SUPERL_mws
+A+Superl+Attr:»mõs # ;
+A+Superl:»mõs NMN_SAJOS ;

LEXICON A_IS_ADJ-DER/SUPERL_ummus
+A: ADJ-DER/SUPERL_ummus ;
LEXICON ADJ-DER/SUPERL_ummus
!+Der/ummus+Superl+Attr:»ummus # ;
!+Der/ummus+Superl:»ummus NMN_MAINSTUMMUSH ;
!+Der/umus+Superl+Attr:»umus # ;
!+Der/umus+Superl:»umus NMN_MAINSTUMMUSH ;
+Superl+Attr:»ummus # ;
+Superl:»ummus NMN_MAINSTUMMUSH ;
+Use/NG+Superl+Attr:»umus # ;
+Use/NG+Superl:»umus NMN_MAINSTUMMUSH ;
+Use/NG+Superl+Attr:»õmâs # ;
+Use/NG+Superl:»õmâs NMN_MAINSTUMMUSH ;
+Use/NG+Superl+Attr:»umâs # ;
+Use/NG+Superl:»umâs NMN_MAINSTUMMUSH ;


LEXICON AdjDerSuperl-mos
!+Der/mõs+Superl:»mõs # ; 
+Superl:»mõs # ; 

LEXICON AdjDerSuperl-mus
!+Der/mus+Superl:»mus # ; 
+Superl:»mus # ; 
LEXICON AdjDerSuperl-ummus
!+Der/ummus+Superl:»ummus # ; 
+Superl:»ummus # ; 

LEXICON AdjNom-as
+Attr+Sg+Gen:%>âz # ;
+Attr+Sg+Acc:%>âz # ;
+Attr+Sg+Ill:%>sa # ;
+Attr+Sg+Loc:%>sest # ;
+Attr+Sg+Com:%>sin # ;
+Attr+Ess:%>sen # ;
+Attr+Par:%>sed # ; 
+Attr+Sg+Abe:%>âztää # ;
+Attr+Pl+Nom:%>âz # ;
+Attr+Pl+Gen:%>si # ;
+Attr+Pl+Acc:%>sid # ;
+Attr+Pl+Ill:%>sid # ;
+Attr+Pl+Loc:%>sin # ;
+Attr+Pl+Com:%>sivuiʹm # ;
+Attr+Pl+Abe:%>sitää # ;


!! N›A derivation in +Der/N2A
LEXICON N2A_LUOQSSI
!! 1A (Feist 2011: 198-199)
+A+Attr:%>es K ;
+A:%>i SG-NOM ;
+A:%>i SG-ABE/ACC/GEN ;
+A:%>i PL-NOM ;
+A:%>j%>e SG-LOC ;
+A:%>j SG-COM_IN ;
+A:%>j%>e ESS/PAR ;
+A:%>j PL-OBL ;
+A:j ADJ-DER/COMP_ab ;
+A:j ADJ-DER/SUPERL_ummus ;

LEXICON N2A_REAEB3H3HAI
!! 1B (Feist 2011: 198-199)
+A+Attr:%>as # ;
+A:%>ai SG-NOM ;
+A:%>ai SG-ABE/ACC/GEN ;
+A:%>ai PL-NOM ;
+A:%>j%>e SG-LOC ;
+A:%>j SG-COM_IN ;
+A:%>j%>e ESS/PAR ;
+A:%>j PL-OBL ;
+A:%>j ADJ-DER/COMP_kwlmmsab ;
+A:%>j ADJ-DER/SUPERL_ummus ;


!!+Der/N2A: N2A_CHAECCAI ;                !!N»A: 
LEXICON N2A_CHAECCAI
!! 1C (Feist 2011: 198-199)
+Der/N2A+A+Attr:%>as # ;
+Der/N2A+A:%>ai SG-NOM ;
+Der/N2A+A:%>ai SG-ABE/ACC/GEN ;
+Der/N2A+A:%>ai PL-NOM ;
+Der/N2A+A:%>j%>e SG-LOC ;
+Der/N2A+A:%>j SG-COM_IN ;
+Der/N2A+A:%>j%>e ESS/PAR ;
+Der/N2A+A:%>j PL-OBL ;
+Der/N2A+A:%>j ADJ-DER/COMP_kwlmmsab ;
+Der/N2A+A:%>j ADJ-DER/SUPERL_ummus ;


