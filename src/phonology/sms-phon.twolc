! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or feedback@divvun.no

!! !!!Skolt Sámi TWOLC file
!
! $Id$
!
!

!! !!!Introduction
!! The rule file for Skolt Saami is divided into 5 main types:
!! 
!! # Definitions
!! # Consonant shift rules (tbw)
!! # Vowel alternation rules
!! # Consonant gradation rules
!! # Rules for cleaning up and composing end result


!! !!!Alphabets, sets and definitions

!! !!Alphabet

Alphabet 
  a b c d e f g h i j k l m n o p q r s t u v w x y z !!= * @CODE@
  A B C D E F G H I J K L M N O P Q R S T U V W X Y Z !!= * @CODE@
  ä å â õ      !!= * @CODE@ 
  Ä Å Â Õ       !!= * @CODE@
  č ǩ ǯ ǧ ž đ ǥ ʒ š ŋ					!!= * @CODE@
  Č Ǩ Ǯ Ǧ Ž Đ Ǥ Ʒ Š Ŋ					!!= * @CODE@
  ẹ   	      	   !!= * @CODE@ Pedagogical purposes
  Ẹ		   !!= * @CODE@ Pedagogical purposes

  æ ø ö á é í					!!= * @CODE@
  Æ Ø Ö Á						!!= * @CODE@
  Á É Ó Ú Í À È Ò Ù Ì Ë Ü Ï Â Ê Ô Û Î Ã Ý þ Ñ Ð		!!= * @CODE@
  á é ó ú í à è ò ù ì ë ü ï â ê ô û î ã ý þ ñ ð ß ª ß ç	!!= * @CODE@


 ʹ  !! * MODIFIER LETTER PRIME: U+02B9 suprasegmental palatization
 ʼ  !! * MODIFIER LETTER APOSTROPHE: U+02BC Syllable marker that appears in writing 
 ˈ !! * MODIFIER LETTER VERTICAL LINE: U+02C8 as overlong consonant marker that does not appear in writing except for pedagogical purposes

!  for consonant lengthening
%{XC%}:m
%{XC%}:v

%{XC%}:n
%{XC%}:d
%{XC%}:r
%{XC%}:l

%{XC%}:k


!! !! Trigger symbols

 %^RmVow:0 !!= * @CODE@ for removing vowels e, â, a before final consonant in stem
 %^UltRmVow:0 !!= * @CODE@ for removing vowels e, â, a after final consonant in stem
 %^PenRmVow:0 !!= * @CODE@ for removing penultimate vowels e, â, a, i.e. vowel befor final stem consonant/consonants
 %^1VOW:0 %^2VOW:0 %^3VOW:0 %^4VOW:0 %^5VOW:0 !!= * @CODE@ Vow triggers

 %^VOWLower:0 !!= * @CODE@ i>e, õ>â, â>ä, u>o, o>å
 %^VOWRaise:0 !!= * @CODE@ e>i, ä>e 
 %^PenVOWRaise:0 !!= * @CODE@ e>i, ä>e
 %^PenVOWLower:0 !!= * @CODE@ i>e, õ>â, â>ä, u>o, o>å
 %^LowerI2E:0	 !!= * @CODE@ this is for diphthong in mieʹlǩǩ:meâlkka
 %^PenLowerI2E:0	 !!= * @CODE@ this is for diphthong in mieʹlǩǩ:meâlkka
 %^VOWBack:0	     !!= * @CODE@ Backing e>õ, e>â vuäinn BUT vuõiʹni
 %^PenVOWBack:0	     !!= * @CODE@ Backing e>õ, e>â vuäinn BUT vuõiʹni

 %^VOWFront:0	     !!= * @CODE@ Fronting
 %^PAL:0 %^PALNo:0 %^PALE:0 %^PALÄ:0 %^PALẸ:0 %^PALÂ:0 %^PALÕ:0   !!= * @CODE@ Palatalisation
 %^PenPAL:0 %^PenPALNo:0 %^PenPALE:0 %^PenPALÄ:0 %^PenPALẸ:0 %^PenPALÂ:0  !!= * @CODE@ Penultimate Palatalisation
%^PenPALÕ:0 %^PenPALÂ:0
%^PALK:0     !!= * @CODE@ causes k:ǩ and g:ǧ

 %^VC:0         !!= * @CODE@ Voicing s:z š:ž
 %^DeVC:0         !!= * @CODE@ devoicing z:s ž:š
 %^Â:0		!!= * @CODE@ Indicates placement of â in stem

!!  !! CHARACTERISTIC BREAKDOWN 2015-02-17
%^VV2V:0        !!= * @CODE@ Shortens vowel 
%^V2VV:0 	!!= * @CODE@ Lengthens vowel
%^CC2C:0 	!!= * @CODE@ Shortens consonant
%^C2CC:0 	!!= * @CODE@ Lengthens consonant
%^CC2CCC:0 	!!= * @CODE@ Strong to Extra Strong consonant
%^C2CCC:0 	!!= * @CODE@ Weak to Extra strong consonant
%^CCC2C:0 	!!= * @CODE@ Extra Strong to Weak consonant
%^CCC2CC:0 	!!= * @CODE@ Extra Strong to Strong consonant
%^XYYY2XYY:0 	!!= * @CODE@ Weakens Extra strong to strong consonant cluster
%^XYYY2XY:0 	!!= * @CODE@ Weakens Extra strong to weak consonant cluster
%^XYY2XY:0 	!!= * @CODE@ Shortens consonant cluster
%^XY2XYY:0 	!!= * @CODE@ Lengthens consonant cluster
%^XY2XYYY:0 	!!= * @CODE@ Lengthens consonant cluster
%^XYY2VY:0	!!= * @CODE@ Consonant cluster to vowel and consonant sijdd:siid
%^XYY2VYY:0	!!= * @CODE@ Consonant cluster to vowel and geminate uvdd:ouʹdd
%^KKK2GG:0	!!= * @CODE@ Geminate weakening 
%^KK2GG:0	!!= * @CODE@ Geminate weakening jokk:jooǥǥ
%^GG2KK:0	!!= * @CODE@ Geminate strengthening
%^GG2KKK:0	!!= * @CODE@ Geminate extra strengthening čuâǥǥas:čuõkˈksa


%^PenVV2V:0     !!= * @CODE@ Shortens vowel in penultimate syllable
%^PenV2VV:0     !!= * @CODE@ Lengthens vowel in penultimate syllable
%^PenCC2C:0     !!= * @CODE@ Shortens consonant in penultimate syllable
%^PenCCC2C:0        !!= * @CODE@ Extra strong to weak consonant in penultimate syllable
%^PenCCC2CC:0        !!= * @CODE@ Extra strong to strong consonant in penultimate syllable
%^PenCC2CCC:0     !!= * @CODE@ Extra Lengthens consonant in penultimate syllable
%^PenC2CC:0     !!= * @CODE@ Lengthens consonant in penultimate syllable
%^PenC2CCC:0 	!!= * @CODE@ Lengthens consonant in penultimate syllable
%^PenXYY2VY:0	!!= * @CODE@ Vhss:VVus in penultimate syllable
%^PenXYY2VYY:0	!!= * @CODE@ consonant cluster to Vowel and Geminate
%^PenVY2XYY:0	!!= * @CODE@ VVus:Vhss in penultimate syllable
%^PenXY2XYY:0	!!= * @CODE@ 
%^PenXYY2XY:0	!!= * @CODE@ 
%^PenXYYY2XYY:0	!!= * @CODE@ Extra strong to strong consonant cluster
%^PenXYYY2XY:0	!!= * @CODE@ Extra strong to weak consonant cluster
%^PenXYY2XYYY:0	!!= * @CODE@ Extra Lengthens consonant cluster
%^PenKKK2GG:0      !!= * @CODE@ Geminate weakening
%^PenKK2GG:0       !!= * @CODE@ Geminate weakening jokk:jooǥǥ
%^PenGG2KK:0       !!= * @CODE@ Geminate strengthening
%^PenGG2KKK:0      !!= * @CODE@ Geminate extra strengthening čuâǥǥas:čuõkˈksa


%^J2I:0		!!= * @CODE@ in tuâjj:tuâi
%^I2J:0		!!= * @CODE@ in agent names

!! !! Gradation triggers 2015.01.23
 %^XSt2St:0	!!= * @CODE@ Causes weakening from extra strong to strong
 %^XSt2W:0	!!= * @CODE@ Causes weakening from extra strong to weak
 %^St2W:0	!!= * @CODE@ Causes weakening from strong to weak
 %^W2St:0	!!= * @CODE@ Causes strengthening from weak to strong
 %^W2XSt:0	!!= * @CODE@ Causes strengthening from weak to extra strong
 %^St2XSt:0	!!= * @CODE@ Causes strengthening from strong to extra strong

!! Other vowel length and consonant length will be phased out
 %^VOWSH:0	  !!= * @CODE@
 %^VOWXSH:0	  !!= * @CODE@ Extra shortens vowel in relation to consonant
 %^VOWXL:0    !!= * @CODE@ Extra lengthens vowel in relation to consonant in nouns, for example
 %^CXLong:0 %^CLong:0 %^CShort:0 !!= * @CODE@ Lengthening, shortening

 X3:0  !!= * @CODE@ causes weak grade
 %{VU%}:u         !!= * @CODE@ used with stuuʹl
 %{Â1%}:0 !!= * @CODE@ used with "mainsted" to tell (a story)
 %{A1%}:0 !!= * @CODE@ used with "vuejted" to drive (causative)
 %{E1%}:0 !!= * @CODE@ used with "kåʹmrded" to bow
 %{%ʹ%}:0 !!= * @CODE@ archiform for MODIFIER LETTER ACUTE ACCENT used in stems where the lemma does not contain a MODIFIER LETTER ACUTE ACCENT and yet the paradigm requires one.
 %{EÂ%}:e !!= * @CODE@ archiform for ie:eâ variation
 %^IMPSG2:0	!!= * @CODE@ 
 %^INDPRSSG3:0  !!= * @CODE@ 
 %^PenVow2a:0	!!= * @CODE@ Changes penultimate vowel to a; e:a â:a
 %^FRICG:0	!!= * @CODE@ 
 %^VOWTense:0   !!= * @CODE@ â:e
 %^VOWLax:0     !!= * @CODE@ e:â 

!! Literal quotes and angles must be escaped (cf morpheme boundaries below):

 »7     !! * »
 «7     !! * «
 %[%>%] !! * >
 %[%<%] !! * <

!! Morpheme boundaries:
 «  ! Derivational prefix
 »  ! Derivational suffix
 %< ! Inflectional prefix
 %> ! Inflectional suffix
 #  ! Word boundary for both lexicalised and dynamic compounds
 %^ ! (exceptional) soft hyphenation point
 %- ! Hyphen for question particle

!! End of alphabet definitions
 ;


!! !!Sets
Sets 

! Letter =
 Cns	= b c d f g h j k l m n p q r s t v w x z đ ʒ ǯ č ŋ ǩ ǧ ǥ š ž ; !!= * @CODE@
 CNoV	= b c d f g h j k l m n p q r s t w x z đ ʒ ǯ č ŋ ǩ ǧ ǥ š ž ;   !!= * @CODE@

 CNoJ	= b c d f g h k l m n p q r s t v w x z đ ʒ ǯ č ŋ ǩ ǧ ǥ š ž ;   !!= * @CODE@
 Vow	= a e i o u å ä â õ           	      	      	      	        !!= * @CODE@
          A E I O U Å Ä Â Õ ;	    				        !!= * @CODE@

 CNoJV  = b c d f g h k l m n p q r s t w x z đ ʒ ǯ č ŋ ǩ ǧ ǥ š ž ;     !!= * @CODE@
 CNoDLRZ = b c f g h j k m n p q s t v w x z đ ǯ č ŋ ǩ ǧ ǥ š ž ;        !!= * @CODE@


!! !!Definitions

Definitions
 DummiesBeforeConsLength = [%^V2VV: |%^VV2V: |%^VOWRaise:| %^VOWLower:| %^PAL: | %^PALNo: |%^PALÂ: |%^PALÕ:| %^PALE: | %^PALÄ: | %^PALẸ: ] ;
 DummiesBeforeVowelHeight = [( %^V2VV: |%^VV2V: )] ; 

!! {{{
ShortV2XL = [0:a a|0:ä a:ä] ;  !!= * @CODE@

LongV2SH = [a:0 a|â:0 â|ä:0 ä|e:0 e|i:0 i|å:0 å|o:0 o|u:0 u|õ:0 õ|a:0 a:ä|â:0 â:õ|ä:0 ä:a|ä:0 ä:a|e:0 e:i|i:0 i:e|å:0 å:o|o:0 o:u|u:0 u:o|õ:0 õ:â] ; !!= * @CODE@ 

Diphth = [e â|e:i â:õ|e ä|e:i â:e|e:i ä:â|e:i ä:e|i â|i:e â:ä|i â:e|i e|i:e e:â|i:e e:ä|i e:õ|i õ|i:e õ:â|i:e õ:ä|u â|u â:õ|u e|u e:â|u â:e|u e:õ|u e:ä|u õ|u õ:â|u õ:e|u å|u å:ä|u å:e| u å:ẹ|u å:õ|u ä|u ä:å|u ä:ẹ|u ä:e] ; !!= * @CODE@ 

DbVow = [a a|a:ä a:ä|i i|i:e i:e|o o|o:å o:å|õ õ|õ:â õ:â|u u|u:o u:o|â â|â:õ â:õ|e e|e e:ä|e ä:e|e:i e:i|å å|å:o å:o|ä ä|ä:a ä:a] ; !!= * @CODE@ 

2DbVow = [0:a a|0:a ä:a|0:i i|0:i e:i|0:o o|0:o u:o|0:o å:o|0:õ õ|0:õ â:õ|0:u u|0:u o:u|0:â â|0:e e|0:å å|0:å o:å|0:ä ä] ; !!= * @CODE@ 

AllDbVow = [DbVow|2DbVow] ; !!= * @CODE@ 

2SgVow = [a:0 a|a:0 a:ä|i:0 i|i:0 i:e|o:0 o|o:0 o:å|õ:0 õ|õ:0 õ:â|u:0 u|u:0 u:o|â:0 â|â:0 â:õ|e:0 e|e:0 e:i|å:0 å|å:0 å:o| ä:0 ä|ä:0 ä:a] ; !!= * @CODE@ 

SurfLongVow = [Diphth|AllDbVow] ; !!= * @CODE@ 

2ShortVow = [Diphth|2SgVow] ; !!= * @CODE@ 


CC = [b b|c c|č č|ǯ ǯ|d d|đ đ|g g|ǧ ǧ|k k|ǩ ǩ|l l|m m|n n|p p|r r|s s|š š|t t|v v|h t t|n n j] ; !!= * @CODE@

LongC = [k: k:|s: s:|š: š:|c: c:|č: č:|m m:|v v:|đ đ:|n n:|l l:|r r:] ; !!= * @CODE@ 
!! }}}

!! preceded by two vowels VxVx, VxVy
!! {{{
DiphthwithCC = [k:ǥ k:ǥ|s:z s:z|š:ž š:ž|c:ʒ c:ʒ|č:j č:j|m m:0|v v:0|đ đ:0|n n:0|l l:0|r r:0] ; !!= * @CODE@
!! }}}

!! preceded by two vowels
!! {{{
CC2CforPrecedingVV = [k:ǥ k:ǥ|k:j k:j|ǩ:j ǩ:j|ǩ:ǥ ǩ:ǥ|s:z s:z|š:ž š:ž|c:ʒ c:ʒ|č:j č:j|m m:0|v v:0|đ đ:0|n n:0|l l:0|p:v p:0|t:đ t:0|r r:0] ; !!= * @CODE@ 
!! }}}

!! preceded by one vowel Vx=>VxVx påll=>pååll
!|ǧ:j ǧ:0|ǧ:ǥ ǧ:0 
!! {{{
CCforPrecedingV2VV =  [b b|p p|m m|v v| d d|t t|đ đ|n n|l l|r r|s s|š š|c c|č č| ǯ ǯ | k k|ǩ ǩ|g g|ǧ ǧ|ǧ:g ǧ:g|ǧ:ǥ ǧ:ǥ ] ; !!= * @CODE@ 

iCC2iCforPrecedingV2VV = [ i (ʹ:|0:ʹ) [b b:0|ʒ ʒ:0|d d:0|g g:0|k k:0|ǩ ǩ:0|l l:0|m m:0|n n:0|p p:0|r r:0|t t:0|v v:0]] ; !!= * @CODE@ 

uCC2uCforPrecedingV2VV = [ [u|%{VU%}:u] (ʹ:|0:ʹ) [ʒ ʒ:0|d d:0|g g:0|k k:0|ǩ ǩ:0|l l:0|m m:0|n n:0|p p:0|r r:0|t t:0]] ; !!= * @CODE@ 
!! }}}

!! extra long vowel (u or i)
!! {{{
CC2CafterV2VVandUI = [uCC2uCforPrecedingV2VV|iCC2iCforPrecedingV2VV] ;  !!= * @CODE@ 
!! }}}

!! extra long vowel (d or l or r)
!! {{{
CC2CforPrecedingV2VV = [l [b b:0|ǧ:j ǧ:0|ǧ:ǥ ǧ:0|m m:0|t t:0]|m p p:0|p c c:0|r [d d:0|n n:0]|s [k: k:0|ǩ: ǩ:0|t t:0]] ; !!= * @CODE@ 
!! }}}

!! extra long consonant (d or l or r)
!! {{{
C2CCafterVV2V = [l [b 0:b|j:ǧ 0:ǧ|ǥ:ǧ 0:ǧ|m 0:m|t 0:t]|m p 0:p|p c 0:c|r d 0:d|s [:k 0:k|:ǩ 0:ǩ|t 0:t]] ; !!= * @CODE@ 
!! }}}

!! Long consonant cluster
LongCluster = [l [d d |g g|ǧ ǧ|m m|t t]|m p p|p c c|r d d |s [k k|ǩ ǩ|t t]] ;

!! Shortening consonant cluster
XYY2XYCluster = [l [m m:|b b:|p p:| d d:|t t:|g g:|ǧ: ǧ:|k: k:|ǩ: ǩ:]|m [p p:| j j: ]|p c c:|r [d d:|g: g:|ǧ: ǧ:|k: k:|ǩ: ǩ:|č: č:|t t: | j j: ] |s [k k:|ǩ ǩ:|t t:]] ;


!! Short consonant cluster


!! Onset consonant or word boundary
OnSetC = [:Cns|#] ;


!! following morpheme or word boundary
!! {{{
RBound = [#|%>] ; !!= * @CODE@ 
!! }}}

!! relative lengthening of vowel right context following vowel:vowel
! 0:õ <=> OnSetC _ :õ RelVowLengthening

!! {{{
RelVowLengthening = [(ʹ:|0:ʹ) [CCforPrecedingV2VV  %^V2VV:|CC2CforPrecedingVV %^V2VV:|Cns: (%{XC%}:) %^V2VV:|CC2CforPrecedingV2VV (a: i:) %^V2VV: |CC2CafterV2VVandUI %^V2VV: |XYY2XYCluster %^V2VV:] ] ; !!= * @CODE@
!RelVowLengthening = [(ʹ:|0:ʹ) [CCforPrecedingV2VV  %^V2VV:|Cns: (%{XC%}:| : :? ) %^V2VV:|CC2CforPrecedingV2VV (a: i:) %^V2VV: |CC2CafterV2VVandUI %^V2VV: |XYY2XYCluster %^V2VV:] ] ;
!! }}}

!! relative lengthening of vowel right context following vowel:vowel
!! 0:i <=> OnSetC _ :i RelVowLengtheningNOTi
!! {{{
RelVowLengtheningNOTi = [(ʹ:|0:ʹ) [CCforPrecedingV2VV  %^V2VV:|[CC2CforPrecedingV2VV|uCC2uCforPrecedingV2VV] %^V2VV: %^CC2C:|CC2CforPrecedingVV %^CC2C: ]] ; !!= * @CODE@
!! }}}





!! relative lengthening of vowel right context following vowel:vowel
!! 0:u <=> OnSetC _ :u RelVowLengtheningNOTu
!! {{{
RelVowLengtheningNOTu = [(ʹ:|0:ʹ) [CCforPrecedingV2VV  %^PenV2VV: %^CShort:|[iCC2iCforPrecedingV2VV|CC2CforPrecedingV2VV] %^V2VV: %^CC2C:|CC2CforPrecedingVV %^CC2C:]] ; !!= * @CODE@
!! }}}


!! possible triggers before VOWLower and VOWRaise
!! {{{
PrecedeVOWLowerRaise = [([%^VOWSH:|%^VOWXSH:] %^CLong:|[%^PenV2VV:|%^VOWXL:] %^CShort:)] ; !!= * @CODE@ 
!! }}}

BetweenStemAndHeight = [( %^VV2V: |%^V2VV:)] ;

!! possible triggers before PALE PALÄ
BetweenStemAndPALAllo = [ BetweenStemAndHeight (%^VOWLower:|%^VOWRaise:|%^LowerI2E:)] ;

!! possible triggers between stem and PALNo and PAL
BetweenStemAndPALNo = [ BetweenStemAndHeight [(%^VOWLower:|%^VOWRaise:|%^LowerI2E:) (%^PALÄ:|%^PALE:|%^PALẸ:|%^PALÕ:|%^PALÂ:)| %^VOWBack: ] ] ;

!! possible triggers between word end and consonant grade
BetweenStemAndConsGrade = [ BetweenStemAndHeight ((%^VOWRaise:|%^VOWLower:|%^LowerI2E:) ((%^PALÂ:|%^PALÕ:) %^PALNo:|(%^PALÕ:|%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:)| %^VOWBack: (%^PALNo:|%^PAL:) ) ] ;


!! possible triggers between VowHeight and word end
!! {{{
BetweenVowHeightAndMorph = [(%^VOWFront:|%^VOWBack:) (%^VOWTense:|%^VOWLax:) (X3:) ((%^PALE:|%^PALÄ:|%^PALẸ:|%^PALÕ:) %^PAL:|(%^PALÂ:|%^PALÕ:) %^PALNo:)] ; !!= * @CODE@ 
!! }}}

BetweenPALNoAndMorph = [(%^CC2C:|%^C2CC:|%^XY2XYY:|%^XYY2XY:|%^KK2GG:|%^GG2KK:|%^CC2CCC:|%^C2CCC:|%^CCC2C:|%^CCC2CC:|%^XYYY2XYY:|%^XYYY2XY:|%^XY2XYYY:|%^XYY2VY:|%^XYY2VYY:|%^KKK2GG:|%^GG2KKK:) RBound ] ;

PenBetweenStemAndPALNo = [(%^PenV2VV:|%^PenVV2V:) (%^PenVOWRaise:|%^PenVOWLower:|%^PenVOWBack:) ] ;

PenBetweenStemAndConsGrade = [(%^PenV2VV:|%^PenVV2V:) ((%^PenVOWRaise:|%^PenVOWLower:) ((%^PenPALÂ:|%^PenPALÕ:) %^PenPALNo:|(%^PenPALÕ:|%^PenPALE:|%^PenPALÄ:|%^PenPALẸ:) %^PenPAL:)| %^PenVOWBack: (%^PenPALNo:|%^PenPAL:) ) ] ;

PenBetweenStemAndVowelLoss = [(%^PenV2VV:|%^PenVV2V:) [(%^PenVOWRaise:|%^PenVOWLower:) ((%^PenPALÂ:|%^PenPALÕ:) %^PenPALNo:|(%^PenPALE:|%^PenPALÄ:|%^PenPALẸ:|%^PenPALÕ:) %^PenPAL:)| %^PenVOWBack: (%^PenPALNo:|%^PenPAL:)] ([%^PenC2CC:|%^PenXY2XYY:]|%^PenCC2C:|%^PenXYY2XY:|%^PenCC2CCC:)] ;


PenBetweenStemAndStemFinalVoicing = [(%^PenV2VV:|%^PenVV2V:) [(%^PenVOWRaise:|%^PenVOWLower:) ((%^PenPALÂ:|%^PenPALÕ:) %^PenPALNo:|(%^PenPALÕ:|%^PenPALE:|%^PenPALÄ:|%^PenPALẸ:) %^PenPAL:)| %^PenVOWBack: (%^PenPALNo:|%^PenPAL:) ] ([%^PenC2CC:|%^PenXY2XYY:]|%^PenCC2C:|%^PenXYY2XY:|%^PenCC2CCC:) (%^RmVow:)] ;

PenBetweenPALNoAndMorph = [(%^PenCC2C:|%^PenC2CC:|%^PenXYY2XY:|%^PenXYY2XYYY:|%^PenKK2GG:|%^PenGG2KK:|%^PenCC2CCC:|%^PenC2CCC:|%^PenCCC2C:|%^PenCCC2CC:|%^PenXYYY2XYY:|%^PenXYYY2XY:|%^PenXY2XYY:|%^PenXYY2VY:|%^PenXYY2VYY:|%^PenKKK2GG:|%^PenGG2KKK:) RBound ] ;


!! neutral to vowel height and backness
!! {{{
NeutrHighBack = [(%^VOWLower:|%^VOWRaise:) (%^VOWFront:|%^VOWBack:)] ; !!= * @CODE@ 
!! }}}

!! neutral to vowel length
VNeutrGrade = [(%^VV2V: |%^V2VV: )];
!! neutral to consonant length
CNeutrGrade = [(%^CC2C: |[%^C2CC:|%^XY2XYY:] |%^C2CCC: |%^CC2CCC: |%^KK2GG: |%^KKK2GG: |%^XYY2VY: |%^XYY2VYY:|%^CCC2CC: )] ;
!! neutral to vowel and consonant length
!! {{{
NeutrGrade = [VNeutrGrade |CNeutrGrade] ; !!= * @CODE@ 
!! }}}



!! * X3 = C Vx Cx Cx     C Vx Vy Cx ˈ Cx
!! * X2 = C Vx Vx Cx Cx  C Vx Vy Cx Cx
!! * X1 = C Vx Vx Cx     C Vx Vy Cx


!! !!!Rules
Rules 

!! !!Vowel shortening rules 



"Vowel shortening â:0"
!! __@RULENAME@__ - 
 â:0 <=>
          OnSetC _ â: (u) (ʹ:|0:ʹ) Cns: [ Cns:*|:Cns*]  %^VV2V:  ;
!!čââʹđ+N+Sg+Ill: __heart/sydän__
!€# čââʹđ0%^VV2V%^PALNo%^C2CC%>ma
!€0 č0â0đđ000%>ma
    OnSetC   _ (:ʼ) [Cns:+|i:|n j|l m|s ǩ:|s t|š ǩ:|š m] PenBetweenStemAndVowelLoss %^RmVow: ;
!         [Cns:+|:Cns+] _ [Cns|i:|n j|l m|s ǩ:|s t|š ǩ:|š m] PenBetweenStemAndVowelLoss %^RmVow: ;

      Cns: (:Cns*)  _ (:ʼ) Cns:+ PenBetweenStemAndConsGrade %^RmVow: ;



"Even syllabic verbs I, vowel shortening e:0"
!! __@RULENAME@__ - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
 e:0 <=>
   OnSetC _ e: (ʹ:|0:ʹ) [LongC|Cns: :Cns*] %^VV2V:  ;
!! teevvad+V+Prt+4:
!€# teevv%^VV2V%^VOWRaise%>uš
!€0 t0ivv00%>uš
 OnSetC _ e: (ʹ:|0:ʹ) [LongC|Cns: :Cns*] [%^VOWSH: %^CLong:|%^VOWXSH: (%^CLong:)] ;
       Cns:  _ (0:ʼ) [Cns:+|i:|n j|l m|s ǩ:|s t|š ǩ:|š m] PenBetweenStemAndVowelLoss %^RmVow: ; 
!! Ââvel+N+Prop+Sg+Loc  __Ivalo__
!€# Ââvel%^RmVow%>est
!€# Ââv0l0%>est

 OnSetC _ e: (u) (ʹ:|0:ʹ) Cns: [ Cns:*|:Cns*]  %^VV2V:  ;

 OnSetC _ e: (ʹ:|0:ʹ)  Cns: [ Cns:*|:Cns*]   Vow: Cns:+ %^PenVV2V: ;
!!eʹǩrded+V+Inf
!€# eeʹǩǩerd%^PenVV2V%^PenCC2C%^RmVow%>ed
!€0 0eʹǩ00rd000%>ed
!! deriving cieʹǩǩes+N+Sg+Ill: ceäkˈksa

      Cns: (:Cns*)  _ (:ʼ) Cns:+ PenBetweenStemAndConsGrade %^RmVow: ;



"Even syllabic verbs I, vowel shortening å:0"
!! __@RULENAME@__ - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
 å:0 <=>
 OnSetC _ å: (u) (ʹ:|0:ʹ) Cns: [ Cns:*|:Cns*]  %^VV2V:  ;
         OnSetC _ å: (ʹ:|0:ʹ) Cns: (:Cns+|Cns:+) Vow: [Cns:+ |n j|l m|s ǩ:|s t|š ǩ:|š m]  %^PenVV2V: ;
!! sååbbar+N+Sg+Gen __meeting__
!€# sååbbar%^PenVV2V
!€0 s0åbbar0
      Cns: (:Cns*)  _ Cns:+ PenBetweenStemAndConsGrade %^RmVow: ;



"Even syllabic verbs I, vowel shortening õ:0"
!! __@RULENAME@__ - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
 õ:0 <=>
 OnSetC _ õ: (ʹ:|0:ʹ) [LongC|Cns: :Cns*] %^VV2V: ;
!€# mõõ0nn%^VV2V%^PAL%>e
!€0 m0õʹnn00%>e
!€# mõõ0nn%^VV2V%^VOWLower%^PAL%>e
!€0 m0âʹnn000%>e
         OnSetC _ õ: (ʹ:|0:ʹ) Cns: (:Cns+|Cns:+) Vow: [Cns:+|n j|l m|s ǩ:|s t|š ǩ:|š m]  %^PenVV2V: ;
!! kõõnjâl+N+Sg+Gen __tear__
!€# kõõn0jâl%^PenVV2V%^PenC2CC
!€0 k0õnnjâl000

 OnSetC _ õ: (u) (ʹ:|0:ʹ) Cns: [ Cns:*|:Cns*]  %^VV2V:  ;

      Cns: (:Cns*)  _ Cns:+ PenBetweenStemAndConsGrade %^RmVow: ;


"Even syllabic verbs I, vowel shortening u:0"
!! __@RULENAME@__ - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
 u:0 <=> OnSetC _ u: (ʹ:|0:ʹ) [C2CCafterVV2V|Cns: :Cns+] (i:) %^VV2V: ;
!! juurd+N+Ess __thought__
!€# juurd0%^VV2V%^C2CC%>j%>en
!€0 j0urdd00%>j%>en
      OnSetC  _ u: (ʹ:|0:ʹ) Cns:+ Vow: [Cns:+|n j|l m|s ǩ:|s t|š ǩ:|š m] %^PenVV2V: ;
      Cns: (:Cns*)  _ Cns:+ PenBetweenStemAndConsGrade %^RmVow: ;
!! mainstummuš+N+Sg+Ill:
!€# mainstum0uš%^PenC2CC%^RmVow%>a
!€0 mainstumm0š00%>a


"Even syllabic verbs I, vowel shortening i:0"
!! __@RULENAME@__ - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
 i:0 <=>
         OnSetC _ i: (ʹ:|0:ʹ) [LongC|Cns: :Cns*] %^VV2V: ;
!! viikkâd+V+Ind+Prs+Pl3
!€# viikk%^VV2V%^VOWLower%^PAL%>e
!€0 v0eʹǩǩ000%>e
         Cns _ %^UltRmVow%> ǥ a ; ! +Sg+Ill N_HÕʹPPI
        OnSetC  _ [Cns:+|n j|l m|s ǩ:|s t|š ǩ:|š m] PenBetweenStemAndVowelLoss %^RmVow: ;
!         [Cns:+|:Cns+] _ [Cns:+|n j|l m|s ǩ:|s t|š ǩ:|š m] PenBetweenStemAndVowelLoss %^RmVow: ;
!! prääʹzniǩ+N+Sg+Ill: __celebration/juhla__
!€# prääʹzniǩ%^PenPALNo%^RmVow%^PALNo%>a
!€0 prää0zn0k000%>a

         OnSetC _ i: (ʹ:|0:ʹ) Cns:+ Vow: [Cns:+|n j|l m|s ǩ:|s t|š ǩ:|š m]  %^PenVV2V: ; ! iilâsǩ:ilska

 OnSetC _ i: (u) (ʹ:|0:ʹ) Cns: [ Cns:*|:Cns*]  %^VV2V:  ;

      Cns: (:Cns*)  _ Cns:+ PenBetweenStemAndConsGrade %^RmVow: ;


"Even syllabic verbs I, vowel shortening o:0"
!! __@RULENAME@__ - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
 o:0 <=> 
    OnSetC _ o: (ʹ:|0:ʹ) [LongC|Cns: :Cns*] %^VV2V: ;
!€# šoomm%^VV2V%^VOWRaise%>u
!€0 š0omm00%>u

  OnSetC _ o: (u) (ʹ:|0:ʹ) Cns: [ Cns:*|:Cns*]  %^VV2V:  ;
!! ooumaž+N+Sg+Gen
!€# ooum0%^VV2V%^C2CCu
!€0 0oumm00u

         [Cns:+|:Cns+] _ [Cns:+|i:|n j|l m|s ǩ:|s t|š ǩ:|š m]  %^RmVow: ; 

      Cns: (:Cns*)  _ Cns:+ PenBetweenStemAndConsGrade %^RmVow: ;


"Even syllabic verbs I, vowel shortening a:0 "
!! __@RULENAME@__ - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
 a:0 <=> OnSetC _ a: (ʹ:|0:ʹ) [LongC|Cns: :Cns*] %^VV2V: ;
!€# saa0kk%^VV2V%^PAL%>e
!€0 s0aʹǩǩ00%>e
 OnSetC _ a: (ʹ:|0:ʹ) [LongC|Cns: :Cns*] Vow: Cns: [Cns:*|:Cns*] %^PenVV2V: ;
       OnSetC   _ (:ʼ) [Cns:+|i:|n j|l m|s ǩ:|s t|š ǩ:|š m] PenBetweenStemAndVowelLoss %^RmVow: ; 
!!Aanar+N+Prop+Sg+Ill: __Inari/Enare__
!€# Aanar%^RmVow%>a
!€# Aan0r0%>a
         OnSetC _ a: (ʹ:|0:ʹ) Cns:+ Vow: [Cns:+|n j|l m|s ǩ:|s t|š ǩ:|š m]  %^PenVV2V: ; 
   OnSetC :Vow+ (ʹ:|0:ʹ) [LongC|Cns:+ - [Cns:* [l|n]]] _ i: %^RmVow: %> Vow: ; 

 OnSetC _ a: (u) (ʹ:|0:ʹ) Cns: [ Cns:*|:Cns*]  %^VV2V:  ;
      Cns: (:Cns*)  _ (:ʼ) Cns:+ PenBetweenStemAndConsGrade %^RmVow: ;

  OnSetC _ (:ʼ) Cns:+ %^RmVow:  ;
!!mättʼted+V+Inf: __teach/opettaa__
!€# mätta0t%^RmVow%>ed
!€0 mätt0ʼt0%>ed




"vowel to syllable mark a:ʼ"
!! __@RULENAME@__ - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
!! {{{
 a:ʼ <=> OnSetC :Vow+ (ʹ:|0:ʹ)  Cns:* [l|n]  _ i: %> Vow: ;
!! }}}



"ZERO to syllable mark 0:ʼ"
!! __@RULENAME@__ - used in zeeʹtt+N+Sg+Abe: zeeʹttʼtää
 0:ʼ <=>
     [ :n | :l ] ([%^C2CC:|%^XY2XYY:]) %> _ j ;
     n Vow: _ n %^RmVow: ;
     t Vow: _ t %^RmVow: ;
!!mättʼted+V+Inf: __teach/opettaa__
!€# mätta0t%^RmVow%>ed
!€# mätt0ʼt0%>ed
     j Vow: _ j %^RmVow: ;
!!jåʹttʼted+V+Inf
        m (ˈ:) m BetweenStemAndConsGrade (%^CCC2CC:) %>  _ m ; 
        v (ˈ:) v BetweenStemAndConsGrade (%^CCC2CC:) %>  _ v ; 
        s (ˈ:) s BetweenStemAndConsGrade (%^CCC2CC:) %>  _ s ; 
        t (ˈ:) t BetweenStemAndConsGrade (%^CCC2CC:) %>  _ t ; 
        k (ˈ:) k BetweenStemAndConsGrade (%^CCC2CC:) %>  _ k ; 
!€# ijss%>0sa
!€# ijss%>ʼsa

"ZERO to hyphen minus 0:- in this rule"
!! __@RULENAME@__ - used in Kääzzkõsraajõstuâjj-joouk
 0:%- <=>
        Cx  _ #  Cx ;
	where Cx in (b c d f g h j k l m n p q r s t v w x z đ ʒ ǯ č ŋ ǩ ǧ ǥ š ž) ; 





"Even syllabic verbs I, vowel shortening ä:0"
!! __@RULENAME@__ - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
 ä:0 <=> OnSetC _ ä: Cns: :Cns* %^VV2V:  ;
!!mään+N+Sg+Gen __moon__
!€# mään%{XC%}%^VV2V%^VOWRaise%^C2CC%>u
!€0 m0ann00%>u
   OnSetC _ ä: (ʹ:|0:ʹ) [LongC|Cns: :Cns*|Cns:+] %^VV2V:  ;
         OnSetC _ ä: (ʹ:|0:ʹ) Cns: (:Cns+|Cns:+) Vow: [Cns:+|n j|l m|s ǩ:|s t|š ǩ:|š m]  %^PenVV2V: ;

 OnSetC _ ä: (u) (ʹ:|0:ʹ) Cns: [ Cns:*|:Cns*]  %^VV2V:  ;




!! !!Vowel alternation rules

!! !VOWEL SHIFT

!2015-05-20 What is this?(tbw)

!! !RAISING

!! deriving +Ind+Prt+Sg1, +Ind+Prt+Sg2 +Ind+Prt+Pl3 in teevvad:tivvu

"Even syllabic verbs I, vowel raising o:u"
!! __@RULENAME@__ - Adding +Prt+Pl3 for ed verbs, Removing u: and i:  second element due to njeiddad njeiddu
 o:u <=>  OnSetC  o: _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  BetweenStemAndHeight %^VOWRaise: %> [e|u] ;
       OnSetC  :0 _ [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  (%^VV2V:) %^VOWRaise: RBound ;
!€# šoomm%^VV2V%^VOWRaise%>u
!€0 š0omm00%>u

       OnSetC o:0 _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  BetweenStemAndHeight %^VOWRaise: ((%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) %> [e|u] ;
       OnSetC 0:u _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  (%^V2VV: %^CShort:) %^VOWRaise: (%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL: %> [e|u] ;
           OnSetC _ (u:) (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  (%^VOWSH: %^CLong:|%^V2VV:) %^VOWRaise: ((%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) %> [e|u] ;


"Even syllabic verbs I, vowel raising å:o"
!! __@RULENAME@__ - Adding +Prt+Pl3 for ed verbs, Removing u: and i:  second element due to njeiddad njeiddu
 å:o <=>  OnSetC _ å:o (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  %^VOWRaise: ; 
          OnSetC [å:o|0:o] _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] BetweenStemAndHeight  %^VOWRaise: ;
           [å:o|0:|å:0] _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] BetweenStemAndHeight %^VOWRaise: ;
!€# k0åʹll%^V2VV%^VOWRaise%>in
!€0 kooʹll000%>in
          OnSetC _ (u:) (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  %^VOWRaise: ((%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) %> [e|u] ;



"Even syllabic verbs I, vowel raising e:i"
!! __@RULENAME@__ - Adding +Prt+Pl3 for ed verbs, Removing u: and i:  second element due to njeiddad njeiddu
 e:i <=>
  OnSetC _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] %^VOWRaise: ;
!€# pess%^VOWRaise%>u
!€0 piss0%>u
  OnSetC [0:i] _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] %^V2VV: %^VOWRaise: ;
!€# p0ess%^V2VV%^VOWRaise%>už
!€0 piiss00%>už
  OnSetC :0 _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] %^VV2V: %^VOWRaise: ;
!€# teevv%^VV2V%^VOWRaise%>uš
!€0 t0ivv00%>uš
  OnSetC  [e:|e:0|0:i] _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  BetweenStemAndHeight %^VOWRaise: ;
  OnSetC _ (u:) (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] BetweenStemAndHeight %^VOWRaise: ;
! diphthong raising with e:i at onset - deriving +Ind+Prt+Pl3 in reäkkad > riâkku, veârrad > viõrru
  _ [ä:|â: ] (u) (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  BetweenStemAndHeight  %^VOWRaise: ;
!!reâugg+N+Sg+Ill
!€# reâugg%^VOWRaise%>u
!€0 riõugg0%>u


"Even syllabic verbs I, vowel raising â:õ" 
!! __@RULENAME@__ - Adding +Prt+Pl3 for ed verbs, Removing u: and i:  second element due to njeiddad njeiddu
 â:õ <=>
          OnSetC u   _    (i)  (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+|Cns:+ :%ˈ Cns:+| Cns:+ (%ˈ:|:%ˈ) :Cns] (%{XC%}:) %^VOWRaise:  (%^PALNo:|%^PAL:) BetweenPALNoAndMorph ;
!€# ruâk0k%^VOWRaise%^CC2CCC%>u
!€0 ruõkˈk00%>u
  OnSetC â:  _      (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  BetweenStemAndHeight %^VOWRaise: ;
  OnSetC _   â:   (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  %^VOWRaise: ;
!!mââʹnn+N+Pl+Acc: __egg/muna__
!€# mââʹnn%^VOWRaise%^CC2C%>id
!€0 mõõʹn00%>id
          OnSetC â:0 _      (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  %^VV2V: %^VOWRaise: (%^PAL:) ;
                 0:õ _      (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  %^V2VV: %^VOWRaise: ;
          OnSetC     _ (u:) (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]   %^VOWRaise: (%^PAL:) ;
          OnSetC u   _   (i)   (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+|Cns:+ :%ˈ Cns:+] %^VOWRaise: (%^PALNo:|%^PAL:) BetweenPALNoAndMorph  ;
          OnSetC [ e: | i]  _   (u)   (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+|Cns:+ 0:%ˈ Cns:+] %^VOWRaise: (%^PALNo:|%^PAL:) BetweenPALNoAndMorph ;
!!reâugg+N+Sg+Ill
!€# reâugg%^VOWRaise%>u
!€0 riõugg0%>u



"Even syllabic verbs I, diphthong raising beginning with u ä:å"
!! __@RULENAME@__ - deriving +Ind+Prt+Pl3 in kuâsˈsad > kuõsˈsu, kuärŋŋad kuårŋŋu
!! {{{
 ä:å <=> OnSetC u  _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] BetweenStemAndHeight %^VOWRaise: %> u ;
!! }}}


"Even syllabic verbs I, diphthong raising beginning with e:i ä:â"
!! __@RULENAME@__ - deriving +Ind+Prt+Pl3 in reäkkad > riâkku, veârrad > viõrru
!! {{{
 ä:â <=> OnSetC e:  _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+|Cns:+ 0:%ˈ Cns:+]  BetweenStemAndHeight %^VOWRaise: %> u ;
!! }}}



"Even syllabic verbs I, diphthong raising beginning with e:i ä:e"
!! __@RULENAME@__ 
 ä:e <=> OnSetC [u|e:i]  _ (i|u) :ʹ [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+|Cns:+ 0:%ˈ Cns:+]  BetweenStemAndHeight %^VOWRaise: %^PALE: %^PAL: ;
!€# čuä0ckk%^CONSH%^PALE%^PAL%>es
!€0 čueʹcǩ0000%>es

"Even syllabic verbs I, diphthong raising beginning with e:i ä:ẹ"
!! __@RULENAME@__ 
 ä:ẹ <=> OnSetC [u|e:i]  _ (i|u) :ʹ [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+|Cns:+ 0:%ˈ Cns:+]  BetweenStemAndHeight %^VOWRaise: %^PALẸ: %^PAL: ;

"diphthong backing beginning with u ä:õ"
!! __@RULENAME@__
 ä:õ <=>  u _ (i|u) (:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+|Cns:+ 0:%ˈ Cns:+]  BetweenStemAndHeight %^VOWBack: ;
          u _ (i|u) (:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+|Cns:+ 0:%ˈ Cns:+]  BetweenStemAndHeight %^VOWRaise: %^PALÕ:  ;

"Even syllabic verbs I, diphthong raising beginning with ä:a u"
!! __@RULENAME@__ - deriving läullad > laullum
 ä:a <=> [ ä: | OnSetC (0:) ] _ Cns: [%{XC%}: | Cns:* | :Cns* ] BetweenStemAndHeight %^VOWRaise: ;
!€# mään%{XC%}%^VV2V%^VOWRaise%^C2CC%>u
!€0 m0ann00%>u
!€# äldd%^VOWRaise%>u
!€0 aldd0%>u
!€# 0äldd%^V2VV%^VOWRaise%^CC2C%>u
!€0 aald000%>u
 [ ä: | OnSetC (0:) ] _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+|Cns:+ 0:%ˈ Cns:+] BetweenStemAndHeight %^VOWRaise: ;
 OnSetC _ ä: (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+|Cns:+ 0:%ˈ Cns:+] %^VOWRaise: ;
!!čääʹcc+N+Pl+Acc __water__
!€# čääʹcc%^VOWRaise%^CC2C%>id
!€0 čaaʹʒʒ00%>id
  OnSetC (0:a)  _ u  (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+|Cns:+ 0:%ˈ Cns:+] BetweenStemAndHeight %^VOWRaise: %>  u ;
         OnSetC  ä:    _    (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+|Cns:+ 0:%ˈ Cns:+] BetweenStemAndHeight %^VOWRaise: [%^C2CC:|%^XY2XYY:]  %>  a ; 
         OnSetC  ä:    _    (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+|Cns:+ 0:%ˈ Cns:+] BetweenStemAndHeight %^VOWRaise: ;




!! !LOWERING 

! Where: VIGGAD +Ind+Prs+Sg3, +Ind+Prs+Pl3
!        TIETTED 
! long and single vowels



"Even syllabic verbs I, vowel lowering o:å"
!! __@RULENAME@__ - deriving +Ind+Prs+Sg3, +Ind+Prs+Pl3 in poorrâd
 o:å <=> OnSetC    _ o:å (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  %^VOWLower:  ;
         OnSetC o: _     (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^VV2V:) %^VOWLower:  ;
!€# poo0rr%^V2VV%^VOWLower%^PAL%>e
!€0 p0åʹrr000%>e
         OnSetC    _     (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]   %^VOWLower: ;
! juʹrdded+V+Cond+Sg1: joordčem
         OnSetC 0:å _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] %^V2VV:   %^VOWLower: ;
! juʹrdded > joordam
         OnSetC    _ (u:|i:) (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] %^VOWLower: ;
         OnSetC    _ ((ʹ:) (v:u|j:i)) (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] %^VOWLower: ;



"Even syllabic verbs I, vowel lowering i:e"
!! __@RULENAME@__ - deriving +Ind+Prs+Sg3, +Ind+Prs+Pl3 in viǯǯâd
 i:e <=>
   OnSetC _ i:e (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^V2VV:)  %^VOWLower: ;
!€# v0iǯǯ%^V2VV%^VOWLower%>as 
!€0 veeǯǯ00%>as
         OnSetC _ [õ:â|õ:ä|â:ä|e:ä|e:â] (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (e: s:)  %^VOWLower: ;
           OnSetC i: _   (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^VV2V:) %^VOWLower: ;
           OnSetC  _   (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  %^VOWLower: ;
! juʹrdded+V+Cond+Sg1: joordčem
!           OnSetC 0:e _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] V2VV:  %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) RBound ;
           OnSetC 0: _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] %^V2VV:  %^VOWLower: ;
!€# v0iǯǯ%^V2VV%^VOWLower%>as
!€0 veeǯǯ00%>as

! juʹrdded > joordam
           OnSetC _ (u:|i:) (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] %^VOWLower: ;
           OnSetC _ ((ʹ:) (v:u|j:i)) (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] %^VOWLower: ;
  OnSetC _ â: (ʹ:|0:ʹ) Cns: [Cns:* |:Cns*] %^LowerI2E: ;
!!mieʹlǩǩ+N+Sg+Ill: __milk/maito__
!€# miâlkk%^LowerI2E%>a
!€0 meâlkk0%>a

"Even syllabic verbs I, vowel lowering a:ä"
!! __@RULENAME@__ - deriving +Ind+Prs+Sg3, +Ind+Prs+Pl3 in viǯǯâd
!! {{{
 a:ä <=> OnSetC    _ a:ä (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) RBound ;
         OnSetC a: _     (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] BetweenStemAndHeight                      %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) RBound ;
         OnSetC    _     (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] BetweenStemAndHeight                      %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) RBound ;
! juʹrdded+V+Cond+Sg1: joordčem
         OnSetC 0:ä _    (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^PenV2VV: %^CShort:)  %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) RBound ;
! juʹrdded > joordam
         OnSetC     _ (u:|i:) (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^PenV2VV: %^CShort:) %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) RBound ;
         OnSetC     _ ((ʹ:) (v:u|j:i)) (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] BetweenStemAndHeight %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) RBound ;
!! }}}


!"Even syllabic verbs I, vowel raising a:ä"
! __@RULENAME@__ - Adding +Prt+Pl3 for ed verbs, Removing u: and i:  second element due to njeiddad njeiddu
! a:ä <=> OnSetC a: _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  BetweenStemAndHeight %^VOWRaise: %> [e|u] ; 
!         OnSetC a:0 _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  BetweenStemAndHeight %^VOWRaise: ((%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) %> [e|u] ;
!         OnSetC 0:ä _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  (%^PenV2VV: %^CShort:) %^VOWRaise: (%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL: %> [e|u] ;
!  OnSetC _ (u:) (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  (%^VOWSH: %^CLong:|%^PenV2VV:) %^VOWRaise: ((%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) %> [e|u] ;



"Even syllabic verbs I, vowel lowering u:o"
!! __@RULENAME@__ - deriving +Ind+Prs+Sg3, +Ind+Prs+Pl3 in uʹvdded
 u:o <=>
        OnSetC (0:) _ (ʹ:) v: (0:ʹ) [n:+ |d:+|l:+|r:+|ʒ:+|ǯ:+|j:+] (%^V2VV:) %^VOWLower:  ;

!! uʹvdded+V+Cond+Pl3 __to give__
!€# 0uvdd%^V2VV%^VOWLower%^XYY2VY%>če
!€0 ooud0000%>če
!! vuʹvll+N+Sg+Ill: voulla
!€# vuʹvll%^VOWLower%^PALNo%^XYY2VYY%>a
!€0 vo0ull000%>a
  OnSetC _ :o (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  %^VOWLower: (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) RBound ;
        OnSetC u: _   (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+|r d d:] VNeutrGrade   %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) RBound ;
       OnSetC (0:o) _  (ʹ:| 0:ʹ) [CNoV: Cns:*|Cns:+ (%ˈ:|:%ˈ) Cns:+] BetweenStemAndHeight %^VOWLower: ; 
!       OnSetC (0:o) _  (ʹ:| 0:ʹ) [CNoV: Cns:*|Cns:+ (%ˈ:|:%ˈ) Cns:+] BetweenStemAndHeight %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) RBound ; 
!!juʹrdded+V+Ind+Prs+Sg1 __think__
!€# j0urdd%^V2VV%^VOWLower%^CC2C%>am
!€0 joord0000%>am
! uʹvdded+V+Ind+Pl3: ouʹdde 

 OnSetC  _ (ʹ:)  v:u (0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^PenV2VV: %^CShort:) %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) RBound ;
! OnSetC (0:) _ (ʹ:) [CNoV: Cns:*|Cns:+ (%ˈ:|:%ˈ) Cns:+] VNeutrGrade %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) RBound ;
 OnSetC (0:) _ %{VU%}: (ʹ:|0:ʹ) Cns:+  ;
!!stuuʹl+N+Sg+Ill: __chair/tuoli__
!€# st0u%{VU%}ʹl%^V2VV%^VOWLower%^PALNo%>a
!€0 stoou0l000%>a
! OnSetC 0: _ [ %{VU%}:u (ʹ:|0:ʹ) [d|l|r|ʒ]]  %^VOWLower: ;
! juʹrdded+V+Cond+Sg1: joordčem
 OnSetC 0: _ (ʹ:|0:ʹ) [CNoV: Cns:*|Cns:+ (%ˈ:|:%ˈ) Cns:+]  %^PenV2VV:  %^CShort:    %^VOWLower: ; 
! juʹrdded > joordam




"Even syllabic verbs I, vowel lowering õ:â "
!! __@RULENAME@__ - deriving +Ind+Prs+Sg3, +Ind+Prs+Pl3 in viǯǯâd
 õ:â <=>
       i: _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] %^VOWLower: [ RBound |%^PAL:|%^PALNo:] ;
!!riõkkâd+V+Ind+Prs+Sg3 __to whip__
!€# riõkˈk%^VOWLower
!€0 reâkˈk0
       õ: _  (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^VV2V:) %^VOWLower: ;
!€# mõõ0nn%^VV2V%^VOWLower%^PAL%>e
!€0 m0âʹnn000%>e
      _ õ: (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  %^VOWLower: ;
!€# mõõnn%^VOWLower%^CC2C%>as
!€0 mâân000%>as

! juʹrdded > joordam
   OnSetC _ (u:|i:) (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^PenV2VV: %^CShort:) %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) RBound ; 
   OnSetC _ ((ʹ:) (v:u|j:i)) (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] BetweenStemAndHeight %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) RBound ;
! diphthong opening after u
        u _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] %^VOWLower: [ RBound |%^PAL:|%^PALNo:] ;
!! kuõskkâd+V+Ind+Prs+Sg3
!€# kuõskk%^VOWLower
!€0 kuâskk0


!! !diphthongs

"Even syllabic verbs I, diphthong opening after u å:ä"
!! __@RULENAME@__ - deriving +Ind+Prs+Sg3, in kuåccâd kuäʹcce
!! = a>ä lowering clockwise
 å:ä <=> u _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] BetweenStemAndHeight %^VOWLower: (%^PALNo:|%^PAL:) BetweenPALNoAndMorph ;
!€# suåkk%^VOWLower%^CC2C
!€0 suäǥǥ00


"Even syllabic verbs I, diphthong opening after i:e e:ä"
!! __@RULENAME@__ - deriving +Ind+Prs+Sg3, in čiõkkâd
!! {{{
 e:ä <=> [i:|u] _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (e: s:) (%^PenV2VV: %^CShort:|%^VOWSH: %^CLong:|%^VOWXSH: %^CLong:) %^VOWLower: (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) [RBound|%^RmVow:|%^VC:] ;
! vueʹlǧǧed+V+Ind+Prs+Pl3: vuäʹlǧǧa
!! }}}

"Vowel in second syllable e:a"
!! __@RULENAME@__ - deriving cieʹǩǩes+N+Sg+Gen: ceäkˈkaz
 e:a <=> 
   _ (ʹ:|0:ʹ) [Cns:+] PenBetweenStemAndVowelLoss %^PenVow2a: ;
!!cieʹǩǩes+N+Sg+Gen
!€# ciâk0kes%^PenVOWLower%^PenCC2CCC%^PenVow2a%^VC
!€0 ceäkˈkaz0000



"Even syllabic verbs I, diphthong opening i:e â:ä"
!! __@RULENAME@__ - deriving +Ind+Prs+Sg3, in čiõkkâd
 â:ä <=>
 u _ i (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] %^VOWLower: ;
!!vueiʹnned+V+Ind+Prs+Sg3
!€# vuâinn%^VOWLower
!€0 vuäinn0
 i: _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] %^VOWLower: (%^PALNo:| %^PAL:)  ;
! i: _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^PenV2VV: %^CShort:) %^VOWLower: (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) RBound ;



"Even syllabic verbs I, diphthong opening after i:e e:â"
!! __@RULENAME@__ - deriving +Ind+Prs+Sg3, in pi%{EÂ%}ʹǩǩ:peâkka
!! {{{
 %{EÂ%}:â <=> i: _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^PenV2VV: %^CShort:) %^VOWLower: (X3:) %^PALNo:  RBound ;
!! }}}

"Even syllabic verbs I, diphthong opening after u e:â"
!! __@RULENAME@__ - deriving vueʹlǧǧed+V+Ind+Prs+Pl3: vuâʹlǧǧe
!! {{{
 e:â <=> [u|i:] _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (e: s:) (%^PenV2VV: (%^FRICG:) %^CShort:|%^VOWSH: (%^CLong:)) %^VOWLower: %^VOWBack: ;
!! }}}

!! !Vowel backing

"Even syllabic verbs I, diphthong opening after u e:õ"
!! __@RULENAME@__ - deriving vueʹlǧǧed+V+Ind+Prt+Sg1: vuõʹlǧǧem
!! {{{
 e:õ <=> u _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^PenV2VV: %^CShort:|%^VOWSH: (%^CLong:)) %^VOWBack: ;
!! }}}

" u å:õ"
!! __@RULENAME@__ -
!! = a>ä lowering clockwise
 å:õ <=> u _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] %^VOWRaise: ;
!€# puått%^VOWRaise%>u
!€0 puõtt0%>u



!! !Vowel Palatalization
"diphthong allophonic realization in palatalization u å:e"
!! __@RULENAME@__ - deriving 
 å:e <=> u _ :ʹ  [Cns:+|Cns:+ 0:%ˈ Cns:+]  %^PALE: %^PAL: ;
!€# puå0v0v%^PALE%^PAL%^CC2CCC%>e
!€0 pueʹvˈv000%>e


"diphthong allophonic realization in palatalization u å:ẹ"
!! __@RULENAME@__ - deriving 
 å:ẹ <=> u _ :ʹ  [Cns:+|Cns:+ 0:%ˈ Cns:+] %^PALẸ: %^PAL: ;
!€# suå0kk%^PALẸ%^PAL%^CC2C
!€0 suẹʹjj000

!! !Vowel Lowering and Fronting

"Even syllabic verbs I, diphthong opening õ:ä after i:e"
!! __@RULENAME@__ - deriving +Ind+Prs+Sg3, in čiõkkâd
 õ:ä <=>
 i:e _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  BetweenStemAndHeight %^VOWLower: %^PALÄ: ;
 u _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  BetweenStemAndHeight %^VOWLower: %^PALÄ: ;




!! !SECONDARY FRONTING


 
"Even syllabic verbs I, secondary vowel fronting with PAL u õ:e"
!! __@RULENAME@__ - deriving +Ind+Prs+Pl3 in VIQQAD: kuõskkâd >kueʹsǩǩe
 õ:e <=>
  u _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] %^VOWLower: %^PALE: ; ! deriving +V+Inf in vueʹlǧǧed

         i _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] %^VOWLower: %^PALE: ; 
! - deriving +Ind+Prs+Sg3, in kuõskkâd kueʹsǩǩe CHECK THIS



"Even syllabic verbs I, secondary u > v "
! __@RULENAME@__ 
 u:v <=> u _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] %^VY2XYY ;
!!muuʒ+N+Sg+Gen 
!€# muuʒ0%^VY2XYY%>i
!€0 muvʒʒ0%>i

!! !RELATIVE VOWEL LENGTHENING

!! vowel lengthening and consonant shortening, %^PenV2VV and %^CShort 

"Even syllabic verbs I, relative vowel lengthening 0:â"
!! __@RULENAME@__ - deriving +V+Inf in TIETTED: uudd > uʹvdded
 0:â <=> OnSetC _ :â (u:|i:|%{VU%}:) (ʹ:|0:ʹ)  Cns: [Cns:*|:Cns*] %^V2VV: ;
!Distinguishing mainstam and maainstam
      OnSetC _ â (u:|i:) (ʹ:|0:ʹ)  Cns: [Cns:*|:Cns*]  Vow: Cns:+  %^PenV2VV: ;
      e _ Cns: Cns:+ %^PALÂ: %^PALNo: ;

"Relative vowel lengthening 0:Vx"
! __@RULENAME@__
 0:Vx <=> OnSetC _ :Vx (u:|i:|%{VU%}:) (ʹ:|0:ʹ)  Cns: [Cns:*|:Cns*] %^V2VV: ;
  OnSetC _ :Vx (u:|i:|%{VU%}:) (ʹ:|0:ʹ)  Cns: [Cns:*|:Cns*] Vow: Cns:+  %^PenV2VV: ;
                where Vx in (a ä å e o õ) ;


!"Even syllabic verbs I, relative vowel lengthening 0:e"
! __@RULENAME@__ - deriving +V+Inf in TIETTED: uudd > uʹvdded
! 0:e <=> OnSetC _ e RelVowLengthening ;
!€# čââʹlmteʹm%^V2VV
!€0 čââʹlmte0m0
!        OnSetC _ e (i) (ʹ:|0:ʹ) Cns: (Cns:*|:Cns*) %^V2VV: ;
!€# p0ess%^V2VV
!€0 peess0
!         OnSetC _ i:e RelVowLengthening %^VOWLower: ;
!         OnSetC _ i:e RelVowLengthening %^VOWLower: ;
!€# v0iǯǯ%^V2VV%^VOWLower%>as 
!€0 veeǯǯ00%>as

!Distinguishing mainstam and maainstam
!      Cns: _ e (u:|i:) (ʹ:|0:ʹ)  Cns:+ (Vow:) Cns:+  %^V2VV: ; 
!€# n0eiʹbb%^V2VV%^CC2C
!€0 neeiʹb000
!      [ n | š ] _ e (ʹ:|0:ʹ) ǩ ǩ %^V2VV: ;
!€# kaađn0e0ǩǩ%^V2VV%^PAL
!€0 kaađneeʹǩǩ00

!"Even syllabic verbs I, relative vowel lengthening 0:å"
! __@RULENAME@__ - deriving
! 0:å <=>
!   OnSetC _ [å|o:å] (u:|i:) RelVowLengthening ;
!   OnSetC _ å (ʹ:|0:ʹ) Cns:+ %^V2VV: ;
!påll+N+Sg+Gen ____ 
!€# p0åll%^V2VV
!€0 pååll0

!Distinguishing mainstam and maainstam
!Vowel lengthening in INDPRSSG3 and IMPSG2 0:å kååʹmmerd, maainâst
!      Cns: _ å (u:|i:) (ʹ:|0:ʹ)  Cns: [ Cns:* | :Cns* ] [e:|a:|â:] Cns:+  %^PenV2VV: ;


!"Even syllabic verbs I, relative vowel lengthening 0:õ"
! __@RULENAME@__ - deriving +V+Inf in TIETTED: uudd > uʹvdded
! 0:õ <=>
!     OnSetC _ :õ RelVowLengthening ;
!âʹlǧǧ+N+Pl+Gen: __boy/poika__
!€# 0âʹlǧǧ%^V2VV%^CC2C%>i
!€0 õõʹlj000%>i
!Distinguishing mainstam and maainstam
!      Cns: _ õ (u:|i:) (ʹ:|0:ʹ)  Cns: Vow: Cns:+  %^PenV2VV: ; 

!   OnSetC _ õ (ʹ:|0:ʹ) Cns:+ %^V2VV: ; 
!€# v0õrr%^V2VV%^CC2C
!€0 võõr000
!€# s0õll%^V2VV%^CC2C
!€0 sõõl00
!€# mättʼt0õss%^V2VV%^CC2C
!€0 mättʼtõõzz00
!€# s0õhss%^V2VV%^XYY2VY
!€0 sõõus000

!"Even syllabic verbs I, relative vowel lengthening 0:ä"
! __@RULENAME@__ - deriving +V+Inf in TIETTED: uudd > uʹvdded
! 0:ä <=> OnSetC _ ä (i|u) (ʹ:|0:ʹ) Cns:+ %^V2VV: ;
! OnSetC _ ä  (ʹ:|0:ʹ) Cns:+ %^V2VV: ([%^CC2C:|%^XYY2XY:]) RBound ;
!€# 0äldd%^V2VV%^CC2C
!€0 ääld000
!€# j0äuʹrr%^V2VV%^CC2C
!€0 jääuʹr000
! OnSetC _ [å:ä|ä|a:ä] RelVowLengthening ; 
!Distinguishing mainstam and maainstam
!- kååʹmmerd, maainâst Vowel lengthening in INDPRSSG3 and IMPSG2
!     _ ä (i) (ʹ:|0:ʹ) Cns:+ Vow: Cns:+ [%^PenV2VV:|%^IMPSG2:|%^INDPRSSG3:] ;




!"Even syllabic verbs I, relative vowel lengthening 0:a"
! __@RULENAME@__ - deriving +V+Inf in MAINSTED: maainstam, mainstam
! 0:a <=>
!  OnSetC _ :a (i|u) (ʹ:|0:ʹ) Cns:+ %^V2VV: ;
!€# 0äldd%^V2VV%^VOWRaise%^CC2C%>už
!€0 aald0000%>už
!€# dur0ak%^V2VV
!€0 duraak0

!Distinguishing mainstam and maainstam
!  OnSetC _ a  (i|u)  (ʹ:|0:ʹ) Cns:+ Vow: Cns:+  %^PenV2VV: ;
!€# m0ainâst%^PenV2VV
!€0 maainâst0
!   [ b | ʒ | (ǯ) ǯ | (g) g | i b | i n |(k) k |l|m |(n) n | n v|r g|r| r t | u d|u l| u n | u ŋ | u r] _ s t (%^PenV2VV:) %^IMPSG2: ;
!   [ ʹ đ đ | ʹ ǩ |ʹ l|ʹ l ǩ |ʹ m m |ʹ p] _ s t %^IMPSG2: ;
!   Cns: _ a  i  (ʹ:|0:ʹ) Cns: Vow: Cns:+  %^PenV2VV: ; 
!   Cns: _ a  i  Cns: Vow: Cns:+  %^PenV2VV: ;
!   Cns: _ a (u:|i:) (ʹ:|0:ʹ)  Cns:+ Vow: Cns:+  [%^PenV2VV:|%^IMPSG2:|%^INDPRSSG3:] ; 




!"Even syllabic verbs I, relative vowel lengthening 0:o"
! __@RULENAME@__ -  simultaneous lengthening and lowering: juʹrdded > joordam juʹrdded+V+Ind+Prs+Sg1
! 0:o <=>
!   OnSetC _ [:o  RelVowLengthening | u:o [%{VU%}:u|(ʹ:) v:] (ʹ:|0:ʹ) [d|l|r|ʒ] (%^V2VV:) %^VOWLower: | :o  (ʹ:) v:  (0:ʹ) [d d:|l l:|r r:|ʒ ʒ:] %^V2VV:  %^VOWLower:] ;
!€# 0uvdd%^V2VV%^VOWLower%^XYY2VY%>če
!€0 ooud0000%>če
!juʹrdded+V+Ind+Prs+Sg1 __think__
!€# j0urdd%^V2VV%^VOWLower%^CC2C%>am
!€0 joord0000%>am

!  OnSetC _ å:o  (ʹ:|0:ʹ) Cns:+  %^V2VV: %^VOWRaise: ;
!€# k0åʹll%^V2VV%^VOWRaise%>in
!€0 kooʹll000%>in
!   OnSetC _ o (ʹ:|0:ʹ) Cns:+ [ %^V2VV: ((%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:|%^PALNo:) [%^CC2C:|%^XYY2XY:] ] ; 
! - deriving toll+N+Sg+Gen: tool
!€# t0oll%^V2VV%^CC2C
!€0 tool000
!€# t0o0ll%^V2VV%^PAL%^CC2C%>e
!€0 tooʹl0000%>e






"Even syllabic nouns I, relative vowel lengthening i"
!! __@RULENAME@__ - ǩiđđ:ǩiiđ
 0:i <=> OnSetC _ :i RelVowLengthening  ;
  OnSetC _ i  (ʹ:|0:ʹ) CNoJ: Vow: Cns:+  %^PenV2VV: ;
!! pirsted+V+Ind+Prs+ConNeg
!€# p0irâst%^PenV2VV
!€0 piirâst0
   OnSetC _ e:i [Cns:+|Cns:+ ((%ˈ:|:%ˈ)|:%ˈ) Cns:+] %^V2VV: %^VOWRaise: ;
!€# p0ess%^V2VV%^VOWRaise%>už
!€0 piiss00%>už
   # Cns:* _ i (ʹ:|0:ʹ) Cns:+ [ %^V2VV: ((%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:|%^PALNo:) [%^CC2C:|%^XYY2XY:]] ;
!€# ǩ0iđđ%^V2VV%^CC2C
!€0 ǩiiđ00



"Even syllabic nouns, relative vowel extra lengthening u not followed by v"
!! __@RULENAME@__ - declension of nouns kunn > kuun
 0:u <=> OnSetC _ [o:u|u] Cns: Cns:* %^V2VV: ((%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:|%^PALNo:) BetweenPALNoAndMorph ;
 OnSetC _ [o:u|u] RelVowLengtheningNOTu ;
           Cns: _  :u (ʹ:|0:ʹ)  CNoV: Vow: Cns:+   %^PenV2VV: [%>|#|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:] ;



!! simultaneous lengthening and raising, hmm: xfst ordering might be easier



!! !VOWEL DUMMY LOSS


!! !SEMI VOWELS

 
"Even syllabic nouns, for j>i"
!! __@RULENAME@__ - sijdd > siid This will need a special extra-lengthening rule
 j:i <=> OnSetC i: (%{ʹ%}:) _ (ʹ:) [ d d:|s s:] BetweenStemAndConsGrade %^XYY2VY: ;
!€# ijss%^VOWXL%^CShort%>âs
!€0 iis000%>âs
  Vow: (ʹ:|:ʹ) _ j: BetweenStemAndConsGrade %^J2I: [%^CC2C:|%^XYY2XY:] ;

!€# sââʹjj%^PALNo%^J2I%^CC2C
!€0 sââ0i00
!!tuâjj+N+Sg+Acc __work__
!€# tuâjj%^J2I%^CC2C
!€0 tuâi000

"Even syllabic verbs,  for v>u"
!! __@RULENAME@__ - uvdd > ouʹdde
 v:u <=>  (0:o) u: (ʹ:) _ (0:ʹ) [ǯ ǯ: |ʒ ʒ:|d d:|l l:|r r:] BetweenStemAndConsGrade [%^XYY2VY: |%^CCC2CC:|%^XYY2VYY:] ;
!€# uv0dd%^PAL%^XYY2VY
!€0 uuʹd000
!€# uvdd%^VOWLower%^XYY2VYY
!€0 oudd00

 "Even syllabic nouns, for h>u"
!! __@RULENAME@__ - luhtt  s s: ...
 h:u <=>
 Vow: (%{ʹ%}:) _ (ʹ:|0:ʹ) [s s:|š š:|t t:|c c:|č č:] BetweenStemAndConsGrade %^XYY2VY: ;
! Vow: (%{ʹ%}:) _ (ʹ:|0:ʹ) [s s:|š š:|t t:|c c:|č č:] [%^PenV2VV: %^CShort:|%^VOWXL:|%^VOWSH: %^CLong:  %^VOWLower: (%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:] ;
!!uhss+N+Der/Dim+N+Sg+Gen __door__
!€# uh0ss%^PAL%^XYY2VY%>e
!€0 uuʹs000%>e

"trisyllabic verbs and doer derivations, i>j"
!! __@RULENAME@__ -
 i:j <=> _ PenBetweenStemAndVowelLoss (%^RmVow:) %^I2J: %>: Vow ;


!! !VOWELS TENSE vs LAX 2012-11-28


"Even-syllabic nouns, for â:e"
!! __@RULENAME@__ -  ǩiâpp:ǩieʹpˈpe
 â:e <=> i _ :ʹ [Cns:+|Cns:+ 0:%ˈ Cns:+]  %^PALE: %^PAL: ;
!! +N+Sg+Ill
!€# miâ0rr%^PALE%^PAL%>e
!€0 mieʹrr00%>e
!€# piâ0ss%^PALE%^PAL
!€0 pieʹss00
      u _ (:ʹ) [Cns:+|Cns:+ 0:%ˈ Cns:+] %^PALE: %^PAL: ;
!€# kuâ0ll%^PALE%^PAL
!€0 kueʹll00
  [u|e:i]  _ (i|u) :ʹ [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+|Cns:+ 0:%ˈ Cns:+]  BetweenStemAndHeight %^VOWRaise: %^PALE: %^PAL: ;
!!tieʹtted+V+Imprt+Sg2: __know/tietää__
!€# teâ0tt%^VOWRaise%^PALE%^PAL%^CC2C
!€0 tieʹđ00000

"Even-syllabic nouns, for â:ẹ"
!! __@RULENAME@__ 
 â:ẹ <=> i _ :ʹ [Cns:+|Cns:+ 0:%ˈ Cns:+]  %^PALẸ: %^PAL: ;
!!pieʹll+N+Sg+Nom: __half__
!€# piâ0ll%^PALẸ%^PAL
!€0 piẹʹll00

!! !VOWEL and ZERO ALTERNATION


!"Realization for %{E1%} in e "
!!! __@RULENAME@__ - kååʹmmerd
!!! {{{
! %{E1%}:e <=> _ (ʹ:|0:ʹ) Cns:+ (%^PenV2VV:) %^IMPSG2: ;
!!! }}}

!"Realization for %{A1%} in a " 
!!! __@RULENAME@__ - kåʹmrded, vuejat, maainâst
!!! {{{
! %{A1%}:a <=> _ (ʹ:|0:ʹ) Cns:+ (%^PenV2VV:) %^IMPSG2: ;
!              _ (ʹ:|0:ʹ|ʼ:) [i|Cns:+] (%^PenV2VV:) %^INDPRSSG3: ;
!!! }}}

"Realization for â in a"
!! __@RULENAME@__ -
 â:a <=> _ (ʹ:|0:ʹ) [Cns:+] (%^PenV2VV:) %^PenVow2a: ;
!!ǩiõrggned+V+Ind+Prs+Sg3
!€# ǩiõrggân%^PenVow2a
!€0 ǩiõrggan0

!! !THE NON-ORTHOGRAPHIC SYLLABLE 


"Loss of ʼ when preceded by vowel"
!! __@RULENAME@__ - This is a temporary solution to  "ʼ" in võʹllʼjed
!! {{{
 ʼ:0 <=> :Vow _ ; 
!! }}}

"%{A1%}:ʼ when subseqent syllable has vowel v"
!! __@RULENAME@__ - This is a temporary solution to  "ʼ" in võʹllʼjed 2013-08-29
!! {{{
%{A1%}:ʼ <=> [l l|n n] _ i: %> Vow: ; 
!! }}}

!! !PALATALIZATION


!2013-09-11"Even-syllabic verbs I, Palatalization mark between V and C"
!2013-09-11 __@RULENAME@__ - used +Ind+Prs+Pl3, +Ind+Prt+Pl3
!2013-09-11 %{ʹ%}:ʹ <=> OnSetC (Vow:0|:Vow) Vow:Vow _ [Cns:Cns Cns:*|Cns:Cns+ (%ˈ:|:%ˈ) Cns:+|Cns:Cns+ 0:%ˈ Cns:+] (%^VOWSH: (%^CLong:)|%^VOWTense:|%^PenV2VV: %^CShort:) (%^VOWLower:|%^VOWRaise:) (%^VOWFront:|%^VOWTense:0) (X3:) (%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL: RBound ;



"0:ʹ as transfer from left of v:u"
!! __@RULENAME@__ - used +Ind+Prs+Pl3, uʹvdded+V+Ind+Prs+Pl3: ouʹdde
0:ʹ <=>      v:u _ [ʒ ʒ:|d d:|l l:|r r:]       BetweenStemAndPALNo  %^PAL: [%^XYY2VY:|%^XYY2VYY:|%^XYY2XY:] ;
!€# uv0dd%^PAL%^XYY2VY
!€0 uuʹd000
 
         ʹ:0 v:u _ [ʒ ʒ:|d d:|l l:|r r:]       BetweenStemAndPALNo  ( %^PAL:) RBound ;
              h:u _ [c c:|č č:|s s:|š š:|t t:]  BetweenStemAndPALNo  %^PAL: ;
             Vow: _ [Cns:+|Cns:+ (%ˈ:|0:%ˈ) Cns:+] (i:) BetweenStemAndPALNo  %^PAL: ;
!€# saa0kk%^VV2V%^PAL%>e
!€0 s0aʹǩǩ00%>e
!€# mõõ0nn%^VV2V%^VOWLower%^PAL%>e
!€0 m0âʹnn000%>e
!€# kuâ0ll%^PALE%^PAL
!€0 kueʹll00


!! The left context allows for:
!! Vowel shortening, and j/v > Vow 



 
"Even-syllabic verbs I, Palatalization of g:ǧ"
!! __@RULENAME@__ -
 g:ǧ <=> Vow: (ʹ:|0:ʹ) (Cns:)     _ (ˈ:0|0:ˈ) g: BetweenStemAndPALNo (%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL: (%^CCC2CC:) RBound ;
           Vow: (ʹ:|0:ʹ) (Cns:) g: (ˈ:0|0:ˈ) _   BetweenStemAndPALNo (%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL: (%^CCC2CC:) RBound ;

    Vow: 0: _ BetweenStemAndPALNo %^PAL: ;
!!bioloog+N+Sg+Ill __biologist__
!€# bioloo0g%^PAL%>e
!€0 biolooʹǧ0%>e


"Even-syllabic verbs I, Palatalization of k:ǩ"
!! __@RULENAME@__ -
 k:ǩ <=>
 Vow: :ʹ Cns:   _ k: BetweenStemAndPALNo (%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL: ([%^CC2C:|%^XYY2XY:]) RBound ;
!!mieʹlǩǩ+N+Sg+Acc: __milk/maito__
!€# miâ0lkk%^PALE%^PAL%^CC2C
!€0 mieʹlǩ0000
 Vow: :ʹ        _ k: BetweenStemAndPALNo (%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL: RBound ;
! Vow: (ʹ:|0:ʹ) (Cns:)     _ k: (%^VV2V:|%^VOWSH: (%^CLong:)) NeutrHighBack (X3:) (%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL: RBound ;
  Vow: 0:ʹ (Cns:) k: _   BetweenStemAndPALNo (%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL: RBound ;
!           Vow: (ʹ:|0:ʹ) (Cns:) k: _     (%^VV2V:|%^VOWSH: (%^CLong:)) NeutrHighBack (X3:) (%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL: RBound ;
!with insertion of palatal before
           Vow: (ʹ:|0:ʹ) (Cns:)     _ k: (%^VV2V:|%^VOWSH: (%^CLong:)) NeutrHighBack (X3:) (%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL: RBound ;
           Vow: (ʹ:|0:ʹ) (Cns:) k: _     (%^VV2V:|%^VOWSH: (%^CLong:)) NeutrHighBack (X3:) (%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL: RBound ;

!                where Cx in (g k)
!                      Cy in (ǧ ǩ)
!                matched ;
!€# jo0kk%^PAL%>e
!€0 joʹǩǩ0%>e
!€# saa0kk%^VV2V%^PAL%>e
!€0 s0aʹǩǩ00%>e

"Even-syllabic nouns I, Depalatalization of ǧ and weakening of grade to ǥ"
!! __@RULENAME@__ - vueʹlǧǧed+V+Ind+Prs+Sg1: vuâlǥam
 ǧ:ǥ <=> Vow: (ʹ:0) (Cns:)    _ ǧ: BetweenStemAndPALNo %^PALNo: [%^CC2C:|%^XYY2XY:]  ;

!! derive vueʹlǧǧed+V+Imp+Pl3: vuâlǥǥaz

"Even-syllabic nouns I,  ǧ and weakening of grade to j"
!! __@RULENAME@__ - vueʹlǧǧed+V+Ind+Prs+4: vueʹljet  âʹlǧǧ+N+Sg+Gen: ââʹlj
 ǧ:j <=> [ ʹ | :ʹ] Cns: _ ǧ: BetweenStemAndPALNo ((%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) [%^CC2C:|%^XYY2XY:] ;
        [i|u] [ ʹ | :ʹ] _ ǧ: BetweenStemAndPALNo ((%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) [%^CC2C:|%^XYY2XY:] ;
!!äiʹǧǧ+N+Sg+Gen: __time/aika__
!€# 0äiʹǧǧ%^V2VV%^CC2C
!€0 ääiʹj000

"Even-syllabic nouns I, Depalatalization of ǧǧ"
!! __@RULENAME@__ - vueʹlǧǧed+V+Ind+Prs+Sg3: vuâlgg
 ǧ:g <=> Vow: (ʹ:) (Cns:)          _ (%ˈ:) ǧ: BetweenStemAndPALNo  %^PALNo: RBound ;
         Vow: (ʹ:) (Cns:) ǧ: (%ˈ:) _        BetweenStemAndPALNo %^PALNo: RBound ;
!€# vueʹlǧǧ%^PALNo
!Maybe this is not needed

"Even-syllabic nouns I, Depalatalization of ǩ:k"
!! __@RULENAME@__ -
 ǩ:k <=> 
  Vow: (ʹ:) (Cns)  _ (%ˈ:|:%ˈ) ǩ: BetweenStemAndPALNo    %^PALNo: RBound ;
  Vow: (ʹ:) (Cns) ǩ: (%ˈ:|:%ˈ) _  BetweenStemAndPALNo %^PALNo: RBound ;
!!mäʹtǩǩ+N+Sg+Ill
!€# mäʹtǩǩ%^PALNo%>a
!€0 mä0tkk0%>a
!  Vow: (ʹ:) (Cns)  _ (%ˈ:|:%ˈ) ǩ: (e: s:) BetweenStemAndPALNo    %^PALNo: RBound ;
!  Vow: (ʹ:) (Cns) ǩ: (%ˈ:|:%ˈ) _    (e: s:) BetweenStemAndPALNo %^PALNo: RBound ;
 [Vow: | Cns:] (%ˈ:|:%ˈ) _ PenBetweenStemAndVowelLoss (%^RmVow:) %^PALNo: ;
!!prääʹzniǩ+N+Sg+Ill: __celebration/juhla__
!€# prääʹzniǩ%^PenPALNo%^RmVow%^PALNo%>a
!€0 prää0zn0k000%>a


"Even-syllabic nouns, removing palatalization in -est +Loc nouns"
!! __@RULENAME@__ - removing palatalization in +Sg+Ill,  pieʹss:peässa
   ʹ:0 <=>  [Vow:|:Vow] _ [:Cns :Cns+|Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] BetweenStemAndPALNo %^PALNo: [RBound|%^UltRmVow:|%^VC:] ;
!€# čââʹlmteʹm%{XC%}%^CLong%^PALNo%>a
!€0 čââʹlmte0mm00%>a
     _ [:Cns :Cns+|Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] BetweenStemAndPALNo %^PALNo: ;
!€# čââʹlmteʹm0%^PALNo%^C2CC%>a
!€0 čââʹlmte0mm00%>a
   [Vow:|:Vow] _ [:Cns :Cns+|Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] %^RmVow: %^PALNo: [RBound|%^UltRmVow:|%^VC:] ;
!!stroitteʹl+N+Sg+Ill: __plant/laitos__
!€# stroitteʹl%^RmVow%^PALNo%>a
!€0 stroitt00l00%>a
  [Vow:|:Vow] _ [:Cns :Cns+|Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] Vow: Cns:+ PenBetweenStemAndPALNo %^PenPALNo:  ;
!!prääʹzniǩ+N+Sg+Ill: __celebration/juhla__
!€# prääʹzniǩ%^PenPALNo%^RmVow%^PALNo%>a
!€0 prää0zn0k000%>a

!jurdded
                    Vow: _ CNoV: Cns:+ BetweenStemAndPALNo %^PALNo: ; 
                         _ v:u ; 



!! !!Consonant QUANTITY CHANGE gradation rules

!! !Weakening Consonant Cluster

!! dealing with relative length changes
!! mõõnnâd : mõʹnne : mõõn 

!"Even syllabic verbs I, cg in relation to Vowel length"
! !Vow Cx Cx:0 

"Even syllabic verbs I, cg m:0"
!! __@RULENAME@__ - used with  +Imp+Sg2, +Ind+Prs+ConNeg, 
 m:0 <=>
 Vow: (ʹ:|0:ʹ) (s|r|l|p|d:)  m: _ BetweenStemAndConsGrade [%^CC2C:|%^XYY2XY:] ;
!€# sääʹmm%^CC2C
!€0 sääʹm00
         Vow: (ʹ:) h: (ʹ:|0:ʹ)      m: _ BetweenStemAndConsGrade [%^CC2C:|%^XYY2XY:] ;
         m: _ Vow: Cns:+ PenBetweenStemAndConsGrade [%^PenCC2C:|%^PenXYY2XY:] ;
!!kåʹmrded+V+Ind+Prs+Sg3 __bow__
!€# k0åʹmmerd%^PenV2VV%^PenVow2a
!€0 kååʹmmard00

"Even syllabic verbs I, cg for b"
!! __@RULENAME@__ - used with  neiʹbb+N+Sg+Gen: neeiʹb
 b:0 <=> Vow:  [i (ʹ:|0:ʹ)|(ʹ:|0:ʹ) l] b _   BetweenStemAndConsGrade [%^CC2C:|%^XYY2XY:] ;
!€# n0eiʹbb%^V2VV%^CC2C
!€0 neeiʹb000


"Even syllabic verbs I, second consonant loss p:0"
!! __@RULENAME@__ -
 p:0 <=> Vow: (ʹ:|0:ʹ) (Cns:) p: _ BetweenStemAndConsGrade [%^CC2C:|%^XYY2XY:] ;
!€# ǩiâpp%^CC2C
!€0 ǩiâv00
          2SgVow (ʹ:|0:ʹ)            p _ Vow: [Cns:+|n j|l m|s ǩ:|s t|š ǩ:|š m] PenBetweenStemAndConsGrade  [%^PenCC2C:|%^PenXYY2XY:] ;  


"Even syllabic verbs I, cg v:0"
!! __@RULENAME@__ - used with  +Imp+Sg2, +Ind+Prs+ConNeg, teevvad > teev

 v:0 <=>
 Vow: (i) (ʹ:|0:ʹ) v: _    BetweenStemAndConsGrade [%^CC2C:|%^XYY2XY:] ;
!!peiʹvv+N+Sg+Gen
!€# p0eiʹvv%^V2VV%^CC2C
!€0 peeiʹv000
 Vow: (i) (ʹ:|0:ʹ) v: _  Vow: Cns: [Cns:* |:Cns*]  PenBetweenStemAndConsGrade [%^PenCC2C:|%^PenXYY2XY:] ;
!!peiʹvv+N+Sg+Nom: __day/päivä__
!€# tuõvvâz%^PenCC2C%^DeVC
!€0 tuõv0âs00



"f:0"
!! __@RULENAME@__ 

 f:0 <=>
      f: _    BetweenStemAndConsGrade [%^CC2C:|%^XYY2XY:] ;
!!kaaʹff+N+Sg+Gen __coffee__
!€# kaaʹff%^CC2C
!€0 kaaʹf0


"Even syllabic verbs I, cg n:0"
!! __@RULENAME@__ - used with  +Imp+Sg2, +Ind+Prs+ConNeg, jiõnn:jiõn
 n:0 <=> 
      n: _    BetweenStemAndConsGrade [%^CC2C:|%^XYY2XY:] ;
!!päʹrnn
         Vow:  (ʹ:) h: (ʹ:|0:ʹ) n: _ BetweenStemAndConsGrade [%^CC2C:|%^XYY2XY:] ; 
      Vow:  (ʹ:|0:ʹ)  (s|r|l|p|d:) n:  _ (a: i:) (%^PenV2VV:|%^VOWXL:) %^CShort: ;
      n: _  Vow: Cns:+  PenBetweenStemAndConsGrade [%^PenCC2C:|%^PenXYY2XY:] ;
          

"Even syllabic nouns I, with extra lengthening of vowel ij>ii/uv>uu and dd>d"
!! __@RULENAME@__ - used with  +N+Sg+Nom > +N+Sg+Gen, sijdd > siid

 d:0 <=> d _ BetweenStemAndConsGrade [%^CC2C:|%^XYY2XY:] ;
!€# 0äldd%^V2VV%^CC2C
!€0 ääld000
!€# 0äldd%^V2VV%^VOWRaise%^CC2C%>už
!€0 aald0000%>už
 [v:| j:] (0:ʹ) d _  BetweenStemAndConsGrade  [%^XYY2VY:] ;
!€# uv0dd%^PAL%^XYY2VY
!€0 uuʹd000

 ! juʹrdded > joordam

!! uʹvdded+V+Ind+Prs+4: uuʹdet

! njeiddad > njeeid
! 2012-06-12 v:u allows for uʹvdded
!! - idd:id, +Imp+Sg2, +Ind+Prs+ConNeg double consonants following vowel and "i" at coda that become single consonants in gradation
         d _ Vow: [Cns:+|n j|l m|s ǩ:|s t|š ǩ:|š m]  [%^PenCC2C:|%^PenXYY2XY:] ; 





"Even syllabic verbs I, cg for ʒ"
!! __@RULENAME@__ - used with  pääʹʒʒelm+N+Sg+Ill: päʹʒlma
 ʒ:0 <=>
      u: (ʹ:) v:u (0:ʹ) ʒ _  BetweenStemAndConsGrade %^XYY2VY: ;
        2SgVow (ʹ:|0:ʹ) ʒ _ Vow: [Cns:+|n j|l m|s ǩ:|s t|š ǩ:|š m]  PenBetweenStemAndConsGrade  [%^PenCC2C:|%^PenXYY2XY:] ;


"ǯ:0"
!! __@RULENAME@__ - 
 ǯ:0 <=>
      u: (ʹ:) [ v: (0:ʹ)|Cns:] ǯ _  BetweenStemAndConsGrade [%^CC2C:|%^XYY2XY:] ;
!!kuʹvǯǯ+N+Sg+Gen
!€# kuʹvǯǯ%^CC2C
!€0 kuʹvǯ00
        2SgVow (ʹ:|0:ʹ) ǯ _ Vow: [Cns:+|n j|l m|s ǩ:|s t|š ǩ:|š m] PenBetweenStemAndConsGrade  [%^PenCC2C:|%^PenXYY2XY:] ;

"Even syllabic verbs I, cg for đ" 
!! __@RULENAME@__ - used with  ǩiđđ:ǩiiđ
 đ:0 <=>
 Vow: (ʹ:|0:ʹ) đ: _    BetweenStemAndConsGrade %^CC2C: ;
!€# ǩ0iđđ%^V2VV%^CC2C
!€0 ǩiiđ00
     	  [DbVow|Diphth] (ʹ:|0:ʹ) đ: _ Vow: Cns:  PenBetweenStemAndConsGrade [%^PenCC2C:|%^PenXYY2XY:] ;


"Even syllabic verbs I, cg for r" 
!! __@RULENAME@__ - used with  võrr:võõr
 r:0 <=>
 Vow: (i|u) (ʹ:|0:ʹ) r: _    BetweenStemAndConsGrade [%^CC2C:|%^XYY2XY:] ;
!  [SurfLongVow (ʹ:|0:ʹ)|u: (ʹ:) v:u (0:ʹ)] r: _  [%^V2VV: ((%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:|%^PALNo:) [%^CC2C:|%^XYY2XY:]] ;
!€# v0õrr%^V2VV%^CC2C
!€0 võõr000
!€# paarr%^CC2C
!€0 paar00
!€# j0äuʹrr%^V2VV%^CC2C
!€0 jääuʹr000
 Vow: (i|u) (ʹ:|0:ʹ) r: _  Vow: Cns:  PenBetweenStemAndConsGrade [%^PenCC2C:|%^PenXYY2XY:] ;


"Even syllabic verbs I, cg for l"
!! __@RULENAME@__ - used with  vuʹvll+N+Sg+Gen: vuuʹl
 l:0 <=>
 Vow: (ʹ:|0:ʹ) l: _    BetweenStemAndConsGrade [%^CC2C:|%^XYY2XY:] ;
!! deriving 
!           SurfLongVow (ʹ:|0:ʹ) l: _  [%^V2VV: ((%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:|%^PALNo:) %^CC2C:] ;
!€# t0oll%^V2VV%^CC2C
!€0 tool00
   u: (ʹ:) v:u (0:ʹ) l: _  %^V2VV: ((%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:|%^PALNo:) [%^CC2C:|%^XYY2XY:] ;
!- used with  vuʹvll+N+Sg+Gen: vuuʹl
!€# vuʹv0ll%^V2VV%^CC2C
!€0 vu0uʹl00

!! deriving påll+N+Sg+Gen: pååll
         [[DbVow|Diphth]|Vow: u] (ʹ:|0:ʹ) l: _  [%^St2W:|%^PenV2VV: %^CShort:] ; 
!€# kuâ0ll%^PenV2VV%^CShort%^PAL
!€0 kueʹl0000

"j:0 "
!! __@RULENAME@__ 
 j:0 <=>
     j: _    BetweenStemAndConsGrade (%^J2I:) [%^CC2C:|%^XYY2XY:|%^XYY2VY:] ;
!!ǩeʹrjj+N+Pl+Nom __book__
!€# ǩ0eʹrjj%^V2VV%^CC2C
!€0 ǩeeʹrj000
!!tuâjj+N+Sg+Acc __work__
!€# tuâjj%^J2I%^CC2C
!€0 tuâi000


"Even syllabic verbs I, cg for g"
!! __@RULENAME@__
 g:0 <=>  2SgVow (ʹ:|0:ʹ) g _ Vow: [Cns:+|n j|l m|s ǩ:|s t|š ǩ:|š m] PenBetweenStemAndConsGrade [%^PenCC2C:|%^PenXYY2XY:] ;
!! cõõggâlm+N+Sg+Ill
!€# cõõggâlm%^PenVV2V%^PenCC2C%^RmVow%>a
!€0 c0õg00lm000%>a
          Vow:  (ʹ:|0:ʹ)  [r|l|d:] g: _    BetweenStemAndConsGrade [%^CC2C:|%^XYY2XY:] ;

"Even syllabic verbs I, cg for ǧ"
!! __@RULENAME@__ - used with  vueʹlǧǧed+V+Ind+Prs+Sg1: vuâlǥam
 ǧ:0 <=> Vow: [ [ :ʹ| ʹ: ] Cns: | [u:|i:] [ :ʹ| ʹ: ] ] [ǧ:j|ǧ:] _ BetweenStemAndConsGrade [%^CC2C:|%^XYY2XY:] ;
!!äiʹǧǧ+N+Sg+Gen: __time/aika__
!€# 0äiʹǧǧ%^V2VV%^CC2C
!€0 ääiʹj000


!! GEMINATE TO WEAK QUALITY GEMINATE
"Even syllabic nouns I, with extra lengthening of vowel V>VV and KK>ǤǤ"
!! __@RULENAME@__ 
!! used with  +N+Sg+Nom > +N+Sg+Gen for cases like
!! lookki > looǥǥi.
 k:ǥ <=>
 Vow:  k: _   BetweenStemAndPALNo %^KK2GG: ;
         Vow:   _ k: BetweenStemAndPALNo %^KK2GG: ;

  OnSetC [DbVow|Diphth]  k: _ (i:) BetweenStemAndPALNo %^KK2GG: ;
  OnSetC [DbVow|Diphth]  _  k: (i:) BetweenStemAndPALNo %^KK2GG: ;
  Vow:    k: _    (i:) %^VOWXL: (%^CShort:) ;
  Vow:       _ k: (i:) %^VOWXL: (%^CShort:) ;
  :Vow  k: _ (%^V2VV:) %^KK2GG: ;
  :Vow  _ k: (%^V2VV:) %^KK2GG: ;
!€# j0okk%^V2VV%^KK2GG
!€0 jooǥǥ00
  :Vow  _ k: BetweenStemAndPALNo %^CC2C: ;
!!loǥškueʹtted __begin to read__
!€# lookk%^VV2V%^CC2C»škueʹtted
!€0 l0oǥ000»škueʹtted

"Even syllabic nouns I, with extra lengthening of vowel V>VV and k:j"
!! __@RULENAME@__ 
!! used with
!!  > .
 k:j <=>   
 Vow: :ʹ k: _  BetweenStemAndPALAllo (%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL: %^KK2GG: ;
         Vow: :ʹ    _ k: BetweenStemAndPALAllo (%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL: %^KK2GG: ;

  OnSetC [DbVow|Diphth] :ʹ k:   _ BetweenStemAndPALAllo (%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL: %^KK2GG: ;
  OnSetC [DbVow|Diphth] :ʹ  _  k: BetweenStemAndPALAllo (%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL: %^KK2GG: ; 

  OnSetC  SurfLongVow   :ʹ k: (%ˈ:) _          %^VOWXL: (%^CShort:) ; 
  OnSetC  SurfLongVow   :ʹ          _ (%ˈ:) k: %^VOWXL: (%^CShort:) ; 

"Even syllabic nouns I, with extra lengthening of vowel V>VV and ǩ:j"
!! __@RULENAME@__ 
!! used with
!!  > .
 ǩ:j <=>   
 Vow: (ʹ:|0:ʹ) ǩ: _    BetweenStemAndPALAllo ((%^PALÄ:|%^PALẸ:|%^PALE:) %^PAL:) %^KK2GG: ;
         Vow: (ʹ:|0:ʹ)    _ ǩ: BetweenStemAndPALAllo ((%^PALÄ:|%^PALẸ:|%^PALE:) %^PAL:) %^KK2GG: ;

  OnSetC [DbVow|Diphth] (ʹ:|0:ʹ) ǩ:   _ BetweenStemAndPALAllo ((%^PALÄ:|%^PALẸ:|%^PALE:) %^PAL:) %^KK2GG: ;
  OnSetC [DbVow|Diphth] (ʹ:|0:ʹ)  _  ǩ: BetweenStemAndPALAllo ((%^PALÄ:|%^PALẸ:|%^PALE:) %^PAL:) %^KK2GG: ; 

  OnSetC  SurfLongVow   (ʹ:|0:ʹ) ǩ: (%ˈ:) _ PALAllo ((%^PALÄ:|%^PALẸ:|%^PALE:) %^PAL:)  %^KKK2GG: ; 
  OnSetC  SurfLongVow   (ʹ:|0:ʹ)          _ (%ˈ:) ǩ: PALAllo ((%^PALÄ:|%^PALẸ:|%^PALE:) %^PAL:) %^KKK2GG: ; 


"Even syllabic nouns I, with extra lengthening of vowel V>VV and ǩ:ǥ"
!! __@RULENAME@__ 
!! used with
!!  > .
 ǩ:ǥ <=>   
 Vow: (ʹ:) ǩ: (%ˈ:) _    BetweenStemAndPALAllo %^PALNo: [%^KK2GG:|%^KKK2GG:] ;
 Vow: (ʹ:) _ (%ˈ:) ǩ: BetweenStemAndPALAllo %^PALNo: [%^KK2GG:|%^KKK2GG:] ;



"Even syllabic verbs I, Voicing š:ž" 
!! __@RULENAME@__ - ss:zz, +Imp+Sg2, +Ind+Prs+ConNeg, double consonants at coda become voiced in gradation
 š:ž <=> 
 Vow: (ʹ:|0:ʹ) š: _    BetweenStemAndConsGrade %^KK2GG: ;
         Vow: (ʹ:|0:ʹ)    _ š: BetweenStemAndConsGrade %^KK2GG: ;
         Vow: (ʹ:|0:ʹ) š:       _          %^PenV2VV:   %^CShort: ;
         OnSetC [Diphth|DbVow] (ʹ:|0:ʹ)          _       š: %^PenV2VV:   %^CShort: ; 
         OnSetC   Diphth       (ʹ:|0:ʹ) š:  %ˈ:  _   BetweenStemAndConsGrade %^KKK2GG: ;
          OnSetC   Diphth      (ʹ:|0:ʹ) _   %ˈ:  š:  BetweenStemAndConsGrade %^KKK2GG: ;
         OnSetC   AllDbVow     (ʹ:|0:ʹ) š: (%ˈ:) _          %^VOWXL: (%^CShort:) ;
         OnSetC   AllDbVow     (ʹ:|0:ʹ)          _ (%ˈ:) š: %^VOWXL: (%^CShort:) ;
!€# kõõččm0õšš%^V2VV%^KK2GG
!€0 kõõččmõõžž00
      Vow:   _ PenBetweenStemAndStemFinalVoicing %^VC: ;
!!mainstummuš+N+Sg+Gen:
!€# mainstum0uš%^PenC2CC%^VC
!€0 mainstummuž00

"Even syllabic verbs I, Voicing c:ʒ"
!! __@RULENAME@__ - cc:zz, +Imp+Sg2, +Ind+Prs+ConNeg, double consonants at coda become voiced in gradation
 c:ʒ <=> Vow: (ʹ:|0:ʹ) c: _    BetweenStemAndConsGrade %^KK2GG: ;
         Vow: (ʹ:|0:ʹ)    _ c: BetweenStemAndConsGrade %^KK2GG: ;
!€# čääʹcc%^KK2GG
!€0 čääʹʒʒ00
                                   _ %^VC: ; 

"Even syllabic verbs I, Voicing č:j"
!! __@RULENAME@__ - ss:zz, +Imp+Sg2, +Ind+Prs+ConNeg, double consonants at coda become voiced in gradation
 č:j <=> Vow: (ʹ:|0:ʹ) č: _    BetweenStemAndConsGrade %^KK2GG: ;
         Vow: (ʹ:|0:ʹ)    _ č: BetweenStemAndConsGrade %^KK2GG: ;
                                             _ %^VC: ;


"Even syllabic verbs I, Voicing after long vowel or diphthong s:z s:z" 
!! __@RULENAME@__ - ss:zz, +Imp+Sg2, +Ind+Prs+ConNeg, double consonants at coda become voiced in gradation
 s:z <=> Vow: (ʹ:|0:ʹ) s:  _  BetweenStemAndConsGrade  %^KK2GG: ; 
         Vow: (ʹ:|0:ʹ) _   s: BetweenStemAndConsGrade  %^KK2GG: ; 
!€# mättʼt0õss%^V2VV%^KK2GG
!€0 mättʼtõõzz00
         Vow:  (ʹ:|0:ʹ) s:  %ˈ:  _  BetweenStemAndConsGrade %^KKK2GG: ; 
         Vow:  (ʹ:|0:ʹ)  _ %ˈ:   s: BetweenStemAndConsGrade %^KKK2GG: ; 

!! deriving cieʹǩǩes+N+Sg+Gen: ceäkˈkaz
   :Vow+ (ʹ:|0:ʹ) [Cns: :Cns*|Cns:+ (%ˈ:|:%ˈ) Cns:+] Vow: _ PenBetweenStemAndStemFinalVoicing %^VC: ; 


"Devoicing z:s"
!! __@RULENAME@__ 
 z:s <=>  [Cns: :Cns*|Cns:+ (%ˈ:|:%ˈ) Cns:+] Vow: _ PenBetweenStemAndStemFinalVoicing %^DeVC: ;
!!aaunâs+N+Sg+Nom: __material/aine__
!€# 0aunnâz%^PenV2VV%^PenXYY2XY%^DeVC
!€0 aaun0âs000

"Devoicing ž:š"
!! __@RULENAME@__ 
 ž:š <=> Cns Vow: _ PenBetweenStemAndStemFinalVoicing %^DeVC:;
!€# p0õttâž%^PenV2VV%^PenCC2C%^DeVC
!€0 põõđ0âš000



"Even syllabic verbs I, second consonant loss t:0"
!! __@RULENAME@__ -
 t:0 <=> Vow:  (i|u)   (ʹ:|0:ʹ) ([b|p|v|f|n|s|c|r|l|š|č|j|k]) t: _ BetweenStemAndConsGrade  [%^CC2C:|%^XYY2XY:] ;
         Vow: (%{ʹ%}:) h: (ʹ:|0:ʹ)           t: _ BetweenStemAndConsGrade  %^XYY2VY: ;
!!autt+N+Pl+Nom __car/auto__
!€# 0autt%^V2VV%^CC2C
!€0 aaut000


"Even syllabic verbs I, third consonant loss" 
!! __@RULENAME@__ - lgg:lǥ, Vow Ca Cx Cx:0, +Imp+Sg2, +Ind+Prs+ConNeg, double consonants following consonant at coda that become single consonants in gradation
 Cx:0 <=>
       Vow: [ (ʹ:|0:ʹ)  Cns: | [u|i] (ʹ:|0:ʹ) ] Cx: _ BetweenStemAndConsGrade [%^CC2C:|%^XYY2XY:] ;
       Vow:  (ʹ:|0:ʹ) [h:|j:] (ʹ:|0:ʹ) Cx:  _ BetweenStemAndConsGrade %^XYY2VY: ;
       Vow: [ (ʹ:|0:ʹ)  Cns: | [u|i] (ʹ:|0:ʹ) ] Cx: _ Vow: Cns:+ PenBetweenStemAndConsGrade [%^PenCC2C:|%^PenXYY2XY:] ;
       Vow:  (ʹ:|0:ʹ) [h:|j:] (ʹ:|0:ʹ) Cx:  _ Vow: Cns:+ PenBetweenStemAndConsGrade %^PenXYY2VY: ;
                where Cx in (c č s š) ;



!"Even syllabic verbs I, cg for s"
! __@RULENAME@__ 
! s:0 <=>  i: (%{ʹ%}:) j: (ʹ:|0:ʹ) s _ BetweenStemAndConsGrade %^XYY2VY: ;
!€# ijss%^VOWXL%^CShort%>âs
!€0 iis000%>âs
!      Vow:  (ʹ:|0:ʹ)  [r|l|p] s: _ BetweenStemAndConsGrade [%^CC2C:|%^XYY2XY:] ;
!      Vow:  (%{ʹ%}:) h: (ʹ:|0:ʹ) s:  _ BetweenStemAndConsGrade [%^CC2C:|%^XYY2XY:] ;
!      Vow: (%{ʹ%}:) h: (ʹ:|0:ʹ) s:  _ BetweenStemAndConsGrade %^XYY2VY: ;
!uhss+N+Sg+Gen __door__
!€# uhss%^XYY2VY
!€0 uus00
!"Even syllabic verbs I, cg for š"
! __@RULENAME@__
! š:0 <=>
!      Vow:  (ʹ:|0:ʹ)  [r|l|p] š: _ BetweenStemAndConsGrade [%^CC2C:|%^XYY2XY:] ;
!      Vow:  (%{ʹ%}:) h: (ʹ:|0:ʹ) š:  _ BetweenStemAndConsGrade [%^CC2C:|%^XYY2XY:] ;
!      Vow: (%{ʹ%}:) h: (ʹ:|0:ʹ) š:  _ BetweenStemAndConsGrade %^XYY2VY: ;
!šapšš+N+Sg+Gen __white fish/siika__
!€# š0apšš%^V2VV%^CC2C
!€0 šaapš000






"Consonant loss ŋ:0"
!! __@RULENAME@__ -
 ŋ:0 <=>
 Vow: (ʹ:|0:ʹ) ([Cns - ŋ]) ŋ: _    BetweenStemAndConsGrade [%^CC2C:|%^XYY2XY:] ;
 Vow: (ʹ:|0:ʹ) ([Cns - ŋ]) ŋ: _  Vow: Cns:+  PenBetweenStemAndConsGrade [%^PenCC2C:|%^PenXYY2XY:] ;



"Even syllabic verbs I, t>đ" 
!! __@RULENAME@__ - tt:đ
!! {{{

 t:đ  <=> Vow: (ʹ:|0:ʹ) _ t:0 BetweenStemAndConsGrade [%^CC2C:|%^CCC2C:] ;
!! }}}


"Even syllabic nouns I, p>v" 
!! __@RULENAME@__ - pp:v
 p:v  <=> Vow: (ʹ:|0:ʹ) _ p:0  %^CC2C: ;
!€# ǩiâpp%^CC2C
!€0 ǩiâv00


"Even syllabic verbs I, second consonant loss k:0"
!! __@RULENAME@__ -
 k:0 <=>   k:  _ BetweenStemAndConsGrade  [ %^CC2C: | %^XYY2XY: ] ;
!!loǥškueʹtted __begin to read__
!€# lookk%^VV2V%^CC2C»škueʹtted
!€0 l0oǥ000»škueʹtted
!!mieʹlǩǩ+N+Sg+Acc: __milk/maito__
!€# miâ0lkk%^PALE%^PAL%^CC2C
!€0 mieʹlǩ0000


"Even syllabic verbs I, second consonant loss ǩ:0"
!! __@RULENAME@__ -
 ǩ:0 <=>
  Vow:  (ʹ:|0:ʹ)  [[i|u] (ʹ:|0:ʹ) |c|ŋ|s|r|l|p] ǩ:  _ BetweenStemAndConsGrade  [%^CC2C:|%^XYY2XY:] ;

    ǩ:  _ Vow: Cns:+ PenBetweenStemAndConsGrade  [%^PenCC2C:|%^PenXYY2XY:] ;
!!eʹǩrded+V+Inf
!€# eeʹǩǩerd%^PenVV2V%^PenCC2C%^RmVow%>ed
!€0 0eʹǩ00rd000%>ed


"Even syllabic verbs I, lgg>lǥ" 
!! __@RULENAME@__ - vueʹlǧǧed+V+Ind+Prs+Sg2 => vuâlǥak
 g:ǥ  <=> Vow: (ʹ:0|%{ʹ%}:0) [l|r|d:]    _ g: BetweenStemAndConsGrade [%^CC2C:|%^XYY2XY:] ;


!vueʹlǧǧed+V+Imp+Pl3: vuâlǥǥaz
!"Even syllabic verbs I, lgg>lǥǥ" ! lgg:lǥǥ

"Odd syllabic nouns I, lǥ>lg"
!! __@RULENAME@__ - ǩeâlǥal+N+Sg+Gen: ǩeâlggal
ǥ:g <=>
 Vow: (ʹ:0)  l _ 0:  BetweenStemAndConsGrade %^XY2XYY:  ;

 Vow: (ʹ:0)  l _ 0: Vow: Cns:* PenBetweenStemAndConsGrade %^PenXY2XYY:  ;

!€# ǩeâlǥ0al%^PenXY2XYY
!€0 ǩeâlggal0

"even syllabic verbs I, 'lǧǧ>'lj" 
!! __@RULENAME@__ - 
 g:j  <=> Vow:   :ʹ [l|r|d:]  _ g:0 BetweenStemAndPALNo %^PAL: %^XYY2XY: ;
!vueʹlǧǧed+V+Ind+Prt+Pl1 => vuõʹljim
!€# vuâ0lgg%^VOWRaise%^PAL%^CC2C%>im
!€# vuõʹlj0000%>im






!! !!Rules for cleaning up and composing end result

!! * Strengthening Consonant Cluster
!! * V shortening and C lengthening 
!! * %^C2CC is generally used in conjunction with a preceding %^VV2V
!! * In verbs this will be observed in finite forms
!! * In derivational morphology, e.g. -škuetted, this is not the case

"Orthographic Consonant lengthening Weak to strong or extra strong %{XC%}:m"
!! __@RULENAME@__
 %{XC%}:m <=>  Vow: (ʹ:|0:ʹ) m _ BetweenStemAndConsGrade [[%^C2CC:|%^XY2XYY:]|%^C2CCC:] ;
!€# čââʹlmteʹm%{XC%}%^PALNo[%^C2CC:|%^XY2XYY:]%>a
!€0 čââʹlmte0mm000a

"Orthographic Consonant lengthening Weak to strong or extra strong %{XC%}:v"
!! __@RULENAME@__
 %{XC%}:v <=>  Vow: (ʹ:|0:ʹ) v _ BetweenStemAndConsGrade [[%^C2CC:|%^XY2XYY:]|%^C2CCC:] ;


"Orthographic Consonant lengthening Weak to strong or extra strong %{XC%}:n"
!! __@RULENAME@__
 %{XC%}:n <=>  Vow: (ʹ:|0:ʹ) n _ BetweenStemAndConsGrade [[%^C2CC:|%^XY2XYY:]|%^C2CCC:] ;
!€# mään%{XC%}%^VV2V%^VOWRaise%^C2CC%>u
!€0 m0ann0000u

"Orthographic Consonant lengthening Weak to strong or extra strong %{XC%}:d"
!! __@RULENAME@__
 %{XC%}:d <=>  Vow: (ʹ:|0:ʹ) d _ BetweenStemAndConsGrade [[%^C2CC:|%^XY2XYY:]|%^C2CCC:] ;

"Orthographic Consonant lengthening Weak to strong or extra strong %{XC%}:r"
!! __@RULENAME@__
 %{XC%}:r <=>  Vow: (ʹ:|0:ʹ) r _ BetweenStemAndConsGrade [[%^C2CC:|%^XY2XYY:]|%^C2CCC:] ;
!€# puär0%{XC%}%^VOWRaise%^C2CCC%>u
!€0 puårˈr000u

"Orthographic Consonant lengthening Weak to strong or extra strong %{XC%}:l"
!! __@RULENAME@__
 %{XC%}:l <=>  Vow: (ʹ:|0:ʹ) l (0:ˈ) _ BetweenStemAndConsGrade [ [%^C2CC:|%^XY2XYY:] | %^C2CCC:] ;
!!suâl+N+Pl+Nom __island__
!€# suâl0%{XC%}%^VOWRaise%^C2CCC%>u
!€0 suõlˈl000u

"Orthographic Consonant lengthening Weak to strong or extra strong %{XC%}:k"
!! __@RULENAME@__
 %{XC%}:k <=>  Vow: (ʹ:|0:ʹ) k _ %^W2XSt: ;




"Orthographic Consonant lengthening in INDPRSSG3 and IMPSG2 0:m"
!! __@RULENAME@__ - kåʹmrded+V+Imp+Sg2: kååʹmmerd 
 0:m <=> Vow:Vow (ʹ:|0:ʹ)  m _ Vow: Cns:+ PenBetweenStemAndConsGrade [%^PenC2CC:|%^PenXY2XYY:]  ;
! Vow:Vow (ʹ:|0:ʹ)  m _ Vow: Cns:+ PenBetweenStemAndConsGrade [%^PenC2CC:|%^PenXY2XYY:] [%^IMPSG2:|%^INDPRSSG3:] ;
        m _ BetweenStemAndConsGrade [%^C2CC:|%^XY2XYY:] ;

!€# ooum0%^VV2V%^C2CCu
!€0 0oumm00u
!€# čââʹlmteʹm0%^PALNo%^C2CC%>a
!€0 čââʹlmte0mm000a

"Consonant lengthening 0:b"
!! __@RULENAME@__ - 
 0:b <=>  2ShortVow  (ʹ:|0:ʹ) [l] b _  BetweenStemAndConsGrade [%^C2CC:|%^XY2XYY:] ;
  2ShortVow  (ʹ:|0:ʹ) [l] b _ Vow: Cns PenBetweenStemAndConsGrade [%^PenC2CC:|%^PenXY2XYY:] ;


"Consonant lengthening 0:p"
!! __@RULENAME@__ - 
 0:p <=>  2ShortVow  (ʹ:|0:ʹ) [m|l] p _ BetweenStemAndConsGrade [%^C2CC:|%^XY2XYY:] ;
           2ShortVow  (ʹ:|0:ʹ) [m|l] p _ Vow: Cns PenBetweenStemAndConsGrade [%^PenC2CC:|%^PenXY2XYY:] ;	       

"Consonant lengthening 0:v"
!! __@RULENAME@__ - 
 0:v <=>
  Vow:  ( i:|u:) (ʹ:|0:ʹ) (Cns:) v _ BetweenStemAndConsGrade [%^C2CC:|%^XY2XYY:] ;
  Vow:  ( i:|u:) (ʹ:|0:ʹ) (Cns:) v _ Vow: Cns:* PenBetweenStemAndConsGrade [%^PenC2CC:|%^PenXY2XYY:] ;
!           where Cx in (b p v) ;
!!kååvas+N+Sg+Gen: ____
!€# kååv0as%^PenVV2V%^PenC2CC%^VC
!€0 k0åvvaz000

"Orthographic Consonant lengthening in INDPRSSG3 and IMPSG2 0:n"
!! __@RULENAME@__
 0:n <=>  Vow: (ʹ:|0:ʹ) n _ (j)  BetweenStemAndConsGrade [%^C2CC:|%^XY2XYY:] ;
!€# mään0%^VV2V%^VOWRaise%^C2CC%>u
!€0 m0ann0000u
 OnSetC  2ShortVow (ʹ:|0:ʹ) n _ (j) Vow: Cns PenBetweenStemAndConsGrade [%^PenC2CC:|%^PenXY2XYY:] ;

!!kõõnjâl+N+Sg+Gen __tear__
!€# kõõn0jâl%^PenVV2V%^PenC2CC
!€0 k0õnnjâl00


"Consonant lengthening 0:d"
!! __@RULENAME@__ - juurd+N+Sg+Gen: jurddi
 0:d <=> Vow: (ʹ:|:ʹ) (r) d _ (i:)  BetweenStemAndConsGrade [%^C2CC:|%^XY2XYY:] ;
    Vow: (ʹ:|:ʹ) (r) d _ Vow: Cns  PenBetweenStemAndConsGrade [%^PenC2CC:|%^PenXY2XYY:] ;

"Consonant lengthening 0:t"
!! __@RULENAME@__ - 
 0:t <=> 2ShortVow (ʹ:) (s) :t _ BetweenStemAndConsGrade [%^C2CC:|%^XY2XYY:] ;
      2ShortVow (ʹ:) (s) :t _ Vow: Cns  PenBetweenStemAndConsGrade [%^PenC2CC:|%^PenXY2XYY:] ;


"Consonant lengthening 0:ǯ"
!! __@RULENAME@__ - 
 0:ǯ <=>  2ShortVow  (ʹ:|:ʹ) Cns ǯ _ BetweenStemAndConsGrade [%^C2CC:|%^XY2XYY:] ;
         2ShortVow  (ʹ:|:ʹ) Cns ǯ _ Vow: Cns  PenBetweenStemAndConsGrade [%^PenC2CC:|%^PenXY2XYY:] ;

"Consonant lengthening 0:c"
!! __@RULENAME@__ - 
 0:c <=>  2ShortVow  (ʹ:|:ʹ) Cns c _ BetweenStemAndConsGrade [%^C2CC:|%^XY2XYY:] ;
         2ShortVow  (ʹ:|:ʹ) Cns c _ Vow: Cns  PenBetweenStemAndConsGrade [%^PenC2CC:|%^PenXY2XYY:] ;

"Consonant lengthening 0:đ"
!! __@RULENAME@__ - čââʹđ+N+Sg+Gen: čâđđam
 0:đ <=> Vow:  (ʹ:)  đ _ BetweenStemAndConsGrade [%^C2CC:|%^XY2XYY:] ;
!!čââʹđ+N+Sg+Ill: __heart/sydän__
!€# čââʹđ0%^VV2V%^PALNo%^C2CC%>ma
!€0 č0â0đđ000%>ma
         Vow:  (ʹ:)  đ _ Vow: Cns  PenBetweenStemAndConsGrade [%^PenC2CC:|%^PenXY2XYY:] ;


"Consonant lengthening 0:l"
!! __@RULENAME@__ 
 0:l <=>
 :Vow (ʹ:|:ʹ) l (0:%ˈ) _ BetweenStemAndConsGrade [ [%^C2CC:|%^XY2XYY:]|%^C2CCC:] ;
 :Vow (ʹ:|:ʹ) l (0:%ˈ) _ Vow: Cns  PenBetweenStemAndConsGrade [ [%^PenC2CC:|%^PenXY2XYY:]|%^PenC2CCC:] ;
!!suâl+N+Par __island/saari__
!€# suâl0%^VOWRaise%^C2CC%>ud 
!€0 suõll00%>ud


"Consonant lengthening 0:ŋ"
!! __@RULENAME@__
 0:ŋ <=>   ŋ _ BetweenStemAndConsGrade [ %^C2CC:|%^XY2XYY:|%^C2CCC:] ;
           ŋ _ Vow: Cns:+ PenBetweenStemAndConsGrade [%^PenC2CC:|%^PenXY2XYY:|%^PenC2CCC:] ;


"Consonant lengthening 0:g"
!! __@RULENAME@__ 
 0:g <=>  2ShortVow   l ǥ: _ BetweenStemAndConsGrade [ [%^C2CC:|%^XY2XYY:]|%^C2CCC:] ;
  2ShortVow   l ǥ: _ Vow: Cns PenBetweenStemAndConsGrade [ [%^PenC2CC:|%^PenXY2XYY:]|%^PenC2CCC:] ;
!!ǩeâlǥal+N+Sg+Gen ____
!€# ǩeâlǥ0al%^PenC2CC
!€0 ǩeâlggal0

"Consonant lengthening 0:ǧ"
!! __@RULENAME@__ - 
!! {{{
 0:ǧ <=>  2ShortVow  :ʹ  l ǥ: _ Vow: Cns:+ PenBetweenStemAndPALNo (%^PenPALE:|%^PenPALÄ:|%^PenPALẸ:) %^PenPAL: %^PenXY2XYY: ;
!! }}}

"Consonant lengthening 0:ǩ"
!! __@RULENAME@__ - 
!! {{{
 0:ǩ <=>   :ʹ  l ǩ _ Vow: Cns:+ PenBetweenStemAndPALNo (%^PenPALE:|%^PenPALÄ:|%^PenPALẸ:) %^PenPAL: %^PenXY2XYY: ;
!! }}}

"Consonant lengthening 0:k"
!! __@RULENAME@__ - 
 0:k <=> Vow (ʹ:) [l|n|ŋ|r|s|š|t] [k| ǩ:k] _ BetweenStemAndConsGrade [%^C2CC:|%^XY2XYY:] ;
!€# durak0%^C2CC%>a
!€0 durakk00a
   Vow (ʹ:) [l|n|ŋ|r|s|š|t] [k| ǩ:k] _ Vow: Cns:+ PenBetweenStemAndConsGrade [%^PenC2CC:|%^PenXY2XYY:] ;


!! !!CONSONANT QUALITY CHANGE

"Consonant quality change j:ǧ"
!! __@RULENAME@__ - 
 j:ǧ <=>   :ʹ  Cns: _ 0: Vow: Cns:+ PenBetweenStemAndPALNo ((%^PenPALE:|%^PenPALÄ:|%^PenPALẸ:) %^PenPAL:) %^PenXY2XYY: ;

"Consonant quality change ǥ:ǧ"
!! __@RULENAME@__ 
 ǥ:ǧ <=>  :ʹ  Cns: _ 0: Vow: Cns:+ PenBetweenStemAndPALNo (%^PenPALE:|%^PenPALÄ:|%^PenPALẸ:) %^PenPAL: %^PenXY2XYY: ;
!Dummy

"Consonant quality change ǥ:k"
!! __@RULENAME@__ 
 ǥ:k <=>  Vow:  ǥ: 0: _ %^GG2KKK: ;
          Vow:  _  0: ǥ: %^GG2KKK: ;
!!čuâǥǥas+N+Sg+Ill  __road__
!€# čuâǥ0ǥ%^GG2KKK%^VOWRaise%>sa
!€0 čuõkˈk00%>sa


!! !Pedagogical X3 length mark after diphthongs in vertical line %ˈ 

!2015-05-20 What is this?(tbw)


!! !Adding X3 length mark
"Consonant X3 lengthening after diphthong in vertical line %ˈ "
!! __@RULENAME@__ - used in +N+Sg+Ill __jeäll:jiâlˈlu, __
!!b c č ǯ d đ g ǧ k ǩ l m n p r s š t v also htˈt nˈnj
 0:%ˈ <=> Diphth (ʹ:|0:ʹ)     b  _ b    [ BetweenStemAndConsGrade %^CC2CCC:| Vow: Cns:+ PenBetweenStemAndConsGrade %^PenCC2CCC:] ;
          Diphth (ʹ:|0:ʹ)     c  _ c    [ BetweenStemAndConsGrade %^CC2CCC:| Vow: Cns:+ PenBetweenStemAndConsGrade %^PenCC2CCC:] ;
          Diphth (ʹ:|0:ʹ)     č  _ č    [ BetweenStemAndConsGrade %^CC2CCC:| Vow: Cns:+ PenBetweenStemAndConsGrade %^PenCC2CCC:] ;
          Diphth (ʹ:|0:ʹ)     ǯ  _ ǯ    [ BetweenStemAndConsGrade %^CC2CCC:| Vow: Cns:+ PenBetweenStemAndConsGrade %^PenCC2CCC:] ;
          Diphth (ʹ:|0:ʹ)     d  _ d    [ BetweenStemAndConsGrade %^CC2CCC:| Vow: Cns:+ PenBetweenStemAndConsGrade %^PenCC2CCC:] ;
          Diphth (ʹ:|0:ʹ)     đ  _ đ    [ BetweenStemAndConsGrade %^CC2CCC:| Vow: Cns:+ PenBetweenStemAndConsGrade %^PenCC2CCC:] ;
          Diphth (ʹ:|0:ʹ)     g: _ g:   [ BetweenStemAndConsGrade %^CC2CCC:| Vow: Cns:+ PenBetweenStemAndConsGrade %^PenCC2CCC:] ;
          Diphth (ʹ:|0:ʹ)     ǥ: _ ǥ:   [ BetweenStemAndConsGrade [%^CC2CCC:|%^GG2KKK:]| Vow: Cns:+ PenBetweenStemAndConsGrade %^PenCC2CCC:] ;
          Diphth (ʹ:|0:ʹ)     ǧ: _ ǧ:   [ BetweenStemAndConsGrade %^CC2CCC:| Vow: Cns:+ PenBetweenStemAndConsGrade %^PenCC2CCC:] ;
          Diphth (ʹ:|0:ʹ)     k: _ k:   [ BetweenStemAndConsGrade %^CC2CCC:| Vow: Cns:+ PenBetweenStemAndConsGrade %^PenCC2CCC:] ;
          Diphth (ʹ:|0:ʹ)     ǩ: _ ǩ:   [ BetweenStemAndConsGrade %^CC2CCC:| Vow: Cns:+ PenBetweenStemAndConsGrade %^PenCC2CCC:] ;
          Diphth (ʹ:|0:ʹ)     l  _ :l    [ BetweenStemAndConsGrade %^CC2CCC:| Vow: Cns:+ PenBetweenStemAndConsGrade %^PenCC2CCC:] ;
!€# kuâl0l%^CC2CCC%>a
!€0 kuâlˈl00a
          Diphth (ʹ:|0:ʹ)     m  _ :m    [ BetweenStemAndConsGrade %^CC2CCC:| Vow: Cns:+ PenBetweenStemAndConsGrade %^PenCC2CCC:] ;
          Diphth (ʹ:|0:ʹ)     n  _ :n (j)[ BetweenStemAndConsGrade %^CC2CCC:| Vow: Cns:+ PenBetweenStemAndConsGrade %^PenCC2CCC:] ;
          Diphth (ʹ:|0:ʹ)     p  _ p    [ BetweenStemAndConsGrade %^CC2CCC:| Vow: Cns:+ PenBetweenStemAndConsGrade %^PenCC2CCC:] ;
          Diphth (ʹ:|0:ʹ)     r  _ :r    [ BetweenStemAndConsGrade %^CC2CCC:| Vow: Cns:+ PenBetweenStemAndConsGrade %^PenCC2CCC:] ;
          Diphth (ʹ:|0:ʹ)     s  _ s    [ BetweenStemAndConsGrade %^CC2CCC:| Vow: Cns:+ PenBetweenStemAndConsGrade %^PenCC2CCC:] ;
          Diphth (ʹ:|0:ʹ)     š  _ š    [ BetweenStemAndConsGrade %^CC2CCC:| Vow: Cns:+ PenBetweenStemAndConsGrade %^PenCC2CCC:] ;
          Diphth (ʹ:|0:ʹ) (h) t _ t     [ BetweenStemAndConsGrade %^CC2CCC:| Vow: Cns:+ PenBetweenStemAndConsGrade %^PenCC2CCC:] ;
          Diphth (ʹ:|0:ʹ)     v _ v     [ BetweenStemAndConsGrade %^CC2CCC:| Vow: Cns:+ PenBetweenStemAndConsGrade %^PenCC2CCC:] ;






!! !Removing X3 length mark
"Removing Consonant X3 length mark after diphthong in vertical line %ˈ LEFT ARROW"
!! __@RULENAME@__ - deriving 
!! b c č ǯ d đ g ǧ k ǩ l m n p r s š t v also htˈt nˈnj
! BetweenStemAndConsGrade [%^C2CC:|%^XY2XYY:]
!! {{{ 
 %ˈ:0 <= Diphth (ʹ:|0:ʹ)     b _ b     BetweenStemAndConsGrade %^CCC2CC: ;
         Diphth (ʹ:|0:ʹ)     c _ c     BetweenStemAndConsGrade %^CCC2CC: ;
         Diphth (ʹ:|0:ʹ)     č _ č     BetweenStemAndConsGrade %^CCC2CC: ;
         Diphth (ʹ:|0:ʹ)     ǯ _ ǯ     BetweenStemAndConsGrade %^CCC2CC: ;
         Diphth (ʹ:|0:ʹ)     d _ d     BetweenStemAndConsGrade %^CCC2CC: ;
         Diphth (ʹ:|0:ʹ)     đ _ đ     BetweenStemAndConsGrade %^CCC2CC: ;
         Diphth (ʹ:|0:ʹ)     g _ g     BetweenStemAndConsGrade %^CCC2CC: ;
         Diphth (ʹ:|0:ʹ)     ǧ _ ǧ     BetweenStemAndConsGrade %^CCC2CC: ;
         Diphth (ʹ:|0:ʹ)     k _ k     BetweenStemAndConsGrade %^CCC2CC: ;
         Diphth (ʹ:|0:ʹ)     ǩ _ ǩ     BetweenStemAndConsGrade %^CCC2CC: ;
         Diphth (ʹ:|0:ʹ)     l _ l     BetweenStemAndConsGrade %^CCC2CC: ;
         Diphth (ʹ:|0:ʹ)     m _ m     BetweenStemAndConsGrade %^CCC2CC: ;
         Diphth (ʹ:|0:ʹ)     n _ n (j) BetweenStemAndConsGrade %^CCC2CC: ;
         Diphth (ʹ:|0:ʹ)     p _ p     BetweenStemAndConsGrade %^CCC2CC: ;
         Diphth (ʹ:|0:ʹ)     r _ r     BetweenStemAndConsGrade %^CCC2CC: ;
         Diphth (ʹ:|0:ʹ)     s _ s     BetweenStemAndConsGrade %^CCC2CC: ;
         Diphth (ʹ:|0:ʹ)     š _ š     BetweenStemAndConsGrade %^CCC2CC: ;
         Diphth (ʹ:|0:ʹ) (h) t _ t     BetweenStemAndConsGrade %^CCC2CC: ;
         Diphth (ʹ:|0:ʹ)     v _ v     BetweenStemAndConsGrade %^CCC2CC: ;
!! }}}



! OUT-DATED
!"Realization for %{Â1%} in â " 
!!! __@RULENAME@__ - maainâst
! %{Â1%}:â <=> Cns _ Cns:+ (%^PenV2VV:) %^IMPSG2: ;
!!€# m0ain%{Â1%}st%^PenV2VV%^IMPSG2
!!€0 maainâst00
!


!"Realization for %{E1%} in a" 
!!! __@RULENAME@__ - 
!!! {{{
! %{E1%}:a <=> _ (ʹ:|0:ʹ) [i|Cns:+] (%^PenV2VV:) %^INDPRSSG3: ;
!!! }}}

!"Realization for %{Â1%} in a" 
!!! __@RULENAME@__ - 
! %{Â1%}:a <=> _ (ʹ:|0:ʹ|ʼ:) [i|Cns:+] (%^PenV2VV:) %^INDPRSSG3: ;
!!€# m0ain%{Â1%}st%^PenV2VV%^INDPRSSG3
!!€0 maainast00
         

