! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or divvun@samediggi.no

!! !!!Skolt Sámi TWOLC file
!
! $Id$
!
!

!! !!!Introduction
!! The rule file for Skolt Saami is divided into 5 main types:
!! 
!! # Definitions
!! # Consonant shift rules (tbw)
!! # Vowel alternation rules
!! # Consonant gradation rules
!! # Rules for cleaning up and composing end result


!! !!! Alphabets, sets and definitions

!! !! Alphabet

Alphabet 
  a b c d e f g h i ï j k l m n o p q r s t u v w x y z !!= * @CODE@
  æ ä ø ö å á é í										!!= * @CODE@
  Å Ä Ö Ø Æ Á											!!= * @CODE@
  Á É Ó Ú Í À È Ò Ù Ì Ë Ü Ï Â Ê Ô Û Î Ã Ý þ Ñ Ð			!!= * @CODE@
  á é ó ú í à è ò ù ì ë ü ï â ê ô û î ã ý þ ñ ð ß ª ß ç	!!= * @CODE@
  č ǩ ǯ ǧ ž đ ǥ ʒ š ŋ									!!= * @CODE@
  Č Ǩ Ǯ Ǧ Ž Đ Ǥ Ʒ Š Ŋ									!!= * @CODE@
  õ â													!!= * @CODE@
  Õ Â													!!= * @CODE@

 %ˈ !! * Vertical line: as overlong consonant marker that does not appear in writing 
 ʼ    !! * Upper right single quotation mark: Syllable marker that appears in writing 
 ˊ  !! * MODIFIER LETTER ACUTE ACCENT: suprasegmental palatalization

!! !! Trigger symbols
  
 %^1VOW:0 %^2VOW:0 %^3VOW:0 %^4VOW:0 %^5VOW:0 !!= * @CODE@ Vow triggers

 %^VOWLower:0 !!= * @CODE@ i>e, õ>â, â>ä, u>o, o>å
 %^VOWRaise:0 !!= * @CODE@ e>i, ä>e 
 %^VOWSH:0	  !!= * @CODE@
 %^VOWL:0     !!= * @CODE@ Lengthens vowel in relation to consonant
 %^VOWXL:0    !!= * @CODE@ Extra lengthens vowel in relation to consonant in nouns, for example
 %^CXLong:0 %^CLong:0 %^CShort:0 !!= * @CODE@ Lengthening, shortening
 %^VOWFront:0	     !!= * @CODE@ Fronting
 %^PAL:0 %^PALNo:0   !!= * @CODE@ Palatalisation
 X3:0  !!= * @CODE@ causes weak grade
 QÂ1:0 !!= * @CODE@ used with "mainsted" to tell (a story)
 QA1:0 !!= * @CODE@ used with "vuejted" to drive (causative)
 QE1:0 !!= * @CODE@ used with "kåˊmrded" to bow
 %{%ˊ%}:0 !!= * @CODE@ archiform for MODIFIER LETTER ACUTE ACCENT used in stems where the lemma does not contain a MODIFIER LETTER ACUTE ACCENT and yet the paradigm requires one.
 %^IMPSG2:0	    !!= * @CODE@ 
 %^INDPRSSG3:0  !!= * @CODE@ 
 %^FRICG:0	    !!= * @CODE@ 
 %^VOWTense:0   !!= * @CODE@ 

!! Literal quotes and angles must be escaped (cf morpheme boundaries below):

 »7     !! * »
 «7     !! * «
 %[%>%] !! * >
 %[%<%] !! * <

! Morpheme boundaries:
 «  ! Derivational prefix
 »  ! Derivational suffix
 %< ! Inflectional prefix
 %> ! Inflectional suffix
 #  ! Word boundary for both lexicalised and dynamic compounds
 %^ ! (exceptional) soft hyphenation point

! End of alphabet definitions
  ;


!! !!Sets
Sets 

! Letter =
 C	= b c d f g h j k l m n p q r s t v w x z đ ʒ ǯ č ŋ ǩ ǧ ǥ š ž ; !!= * @CODE@
 V	= a e i o u å ä â õ ;										    !!= * @CODE@

 CNoJV        = b c d f g h j k l m n p q r s t v w x z đ ʒ ǯ č ŋ ǩ ǧ ǥ š ž ;    !!= * @CODE@


!! !! Definitions

Definitions 

LongC = [ k: k: | s: s: | š: š: | c: c: | č: č: | m m: | v v: | đ đ: | n n: | l l: | r r: ] ;


Rules !!= !!! @CODE@

!! !! Consonant shift rules 
!! (tbw)



!"ˊ:0 rule"
! %ˊ:0 <=> V: _ C:+ (X3:) [ %^VOWSH: | %^1VOW: | %^2VOW: ] ;

! "Even syllabic verbs I, ˊ:0 blocking"
! %ˊ:0 /<= V: _ C:+ (X3:) [ %^3VOW: | %^4VOW: | %^5VOW: ] ;


"Even syllabic verbs I, vowel shortening"
!! __@RULENAME@__ - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
 Vx:0 <=> Vx: _ (%ˊ: | %{%ˊ%}: ) LongC %^VOWSH: %^CLong: (%^VOWLower: |%^VOWRaise: ) (%^VOWFront: ) (X3:) (%^PAL: |%^PALNo: ) [ %>: | #: ]  ;
! 2012-06-01 Vx: _ (%ˊ) C:+ (X3:) [ %^VOWSH: | %^2VOW: | %^3VOW: | %^4VOW: ] ;
 		where Vx in (â e å õ u i o a ä) ;

! 2013-08-29
"Even syllabic verbs I, diphthong shortening consonant lengthening"
!! __@RULENAME@__ - used in +N+Sg+Ill __jeäll:jiâlˈlu, __
 0:%ˈ <=> [ e: ä: | e: â: | i: â: | i: e: | u: â: | u: e: ] (%ˊ: | %{%ˊ%}: ) Cx: _ Cx: ( j ) [ %^VOWSH: %^CLong: | %^VOWTense: ] (%^VOWLower: |%^VOWRaise: ) (%^VOWFront: ) (X3:) (%^PAL: |%^PALNo: ) [ %>: | #: ]  ;
                where Cx in (c đ k ǩ l m n p r s t v) ;





!! !!Vowel alternation rules

!! !VOWEL SHIFT

!! !RAISING

!! deriving +Ind+Prt+Sg1, +Ind+Prt+Sg2 +Ind+Prt+Pl3 in teevvad:tivvu

"Even syllabic verbs I, vowel raising"
!! __@RULENAME@__ - Adding +Prt+Pl3 for ed verbs, Removing u: and i:  second element due to njeiddad njeiddu
 Vx:Vy <=>   _ Vx:  (%ˊ: | %{%ˊ%}: ) [ C:+ | C:+ %ˈ: C:+ ]  (%^VOWSH: %^CLong: ) %^VOWRaise: %>: [ e | u ]  ; 
             _ Vx:0 (%ˊ: | %{%ˊ%}: ) [ C:+ | C:+ %ˈ: C:+ ]  (%^VOWSH: %^CLong: ) %^VOWRaise: (%^PAL: ) %>: [ e | u ] ;
             _ 0:Vy (%ˊ: | %{%ˊ%}: ) [ C:+ | C:+ %ˈ: C:+ ]  (%^VOWL: %^CShort: ) %^VOWRaise: %^PAL: %>: [ e | u ] ;
  [ C | #: ] _ ( u: ) (%ˊ: | %{%ˊ%}: ) [ C:+ | C:+ %ˈ: C:+ ]  (%^VOWSH: %^CLong: | %^VOWL: ) %^VOWRaise: (%^PAL: ) %>: [ e | u ]  ;

              where Vx in (o å ä)
                    Vy in (u o a)
              matched ;

"Even syllabic verbs I, vowel raising"
!! __@RULENAME@__ - Adding +Prt+Pl3 for ed verbs, Removing u: and i:  second element due to njeiddad njeiddu
 e:i <=>   _ e:  (%ˊ: | %{%ˊ%}: ) [ C:+ | C:+ %ˈ: C:+ ]  (%^VOWSH: %^CLong: ) %^VOWRaise: %>: [ e | u ]  ; 
             _ e:0 (%ˊ: | %{%ˊ%}: ) [ C:+ | C:+ %ˈ: C:+ ]  (%^VOWSH: %^CLong: ) %^VOWRaise: (%^PAL: ) %>: [ e | u ] ;
             _ 0:i (%ˊ: | %{%ˊ%}: ) [ C:+ | C:+ %ˈ: C:+ ]  (%^VOWL: %^CShort: ) %^VOWRaise: %^PAL: %>: [ e | u ] ;
  [ C | #: ] _ ( u: ) (%ˊ: | %{%ˊ%}: ) [ C:+ | C:+ %ˈ: C:+ ]  (%^VOWSH: %^CLong: | %^VOWL: ) %^VOWRaise: (%^PAL: ) %>: [ e | u ]  ;
!! diphthong raising with e:i at onset - deriving +Ind+Prt+Pl3 in reäkkad > riâkku, veârrad > viõrru
  _ [ ä:â | â:õ  ] (%ˊ: | %{%ˊ%}: ) [ C:+ | C:+ %ˈ: C:+ | C:+ 0:%ˈ C:+ ]  (%^VOWSH: %^CLong: ) %^VOWRaise: %>: u ;



"Even syllabic verbs I, vowel raising â:õ" 
!! __@RULENAME@__ - Adding +Prt+Pl3 for ed verbs, Removing u: and i:  second element due to njeiddad njeiddu
 â:õ <=>   _ â:  (%ˊ: | %{%ˊ%}: ) [ C:+ | C:+ %ˈ: C:+ ]  (%^VOWSH: %^CLong: ) %^VOWRaise: %>: [ e | u ]  ; 
             _ â:0 (%ˊ: | %{%ˊ%}: ) [ C:+ | C:+ %ˈ: C:+ ]  (%^VOWSH: %^CLong: ) %^VOWRaise: (%^PAL: ) %>: [ e | u ] ;
             _ 0:õ (%ˊ: | %{%ˊ%}: ) [ C:+ | C:+ %ˈ: C:+ ]  (%^VOWL: %^CShort: ) %^VOWRaise: %^PAL: %>: [ e | u ] ;
  [ C | #: ] _ ( u: ) (%ˊ: | %{%ˊ%}: ) [ C:+ | C:+ %ˈ: C:+ ]  (%^VOWSH: %^CLong: | %^VOWL: ) %^VOWRaise: (%^PAL: ) %>: [ e | u ]  ;
 u  _ (%ˊ: | %{%ˊ%}: ) [ C:+ | C:+ %ˈ: C:+ | C:+ :%ˈ C:+ ] ( %^VOWSH: %^CLong: ) %^VOWRaise: %>: u ;
 e:  _ (%ˊ: | %{%ˊ%}: ) [ C:+ | C:+ %ˈ: C:+ | C:+ 0:%ˈ C:+ ]  (%^VOWSH: %^CLong: ) %^VOWRaise: %>: u ;



"Even syllabic verbs I, diphthong raising beginning with u ä:å"
!! __@RULENAME@__ - deriving +Ind+Prt+Pl3 in kuâs'sad > kuõs'su, kuärŋŋad kuårŋŋu
 ä:å <=> u  _ (%ˊ: | %{%ˊ%}: ) [ C:+ | C:+ %ˈ: C:+ ] ( %^VOWSH: %^CLong: ) %^VOWRaise: %>: u ;


"Even syllabic verbs I, diphthong raising beginning with e:i ä:â"
!! __@RULENAME@__ - deriving +Ind+Prt+Pl3 in reäkkad > riâkku, veârrad > viõrru
 ä:â <=> e:  _ (%ˊ: | %{%ˊ%}: ) [ C:+ | C:+ %ˈ: C:+ | C:+ 0:%ˈ C:+ ]  ( %^VOWSH: %^CLong: ) %^VOWRaise: %>: u ;




!! !LOWERING 

! Where: VIGGAD +Ind+Prs+Sg3, +Ind+Prs+Pl3
!        TIETTED 
! long and single vowels


"Even syllabic verbs I, vowel lowering"
!! __@RULENAME@__ - deriving +Ind+Prs+Sg3, +Ind+Prs+Pl3 in viǯǯâd
 Vx:Vy <=>  _ Vx: (%ˊ: | %{%ˊ%}: ) [ C:+ | C:+ %ˈ: C:+ ] ( %^VOWSH: %^CLong: | %^VOWL: %^CShort: )  %^VOWLower: (%^VOWFront:) (X3:) (%^PALNo: | %^PAL:) [ %>: | #: ] ;
        Vx: _     (%ˊ: | %{%ˊ%}: ) [ C:+ | C:+ %ˈ: C:+ ] ( %^VOWL: %^CShort: )                      %^VOWLower: (%^VOWFront:) (X3:) (%^PALNo: | %^PAL:) [ %>: | #: ] ;
 [#: | C: ] _     (%ˊ: | %{%ˊ%}: ) [ C:+ | C:+ %ˈ: C:+ ] ( %^VOWSH: %^CLong: )                      %^VOWLower: (%^VOWFront:) (X3:) (%^PALNo: | %^PAL:) [ %>: | #: ] ;
! juˊrdded+V+Cond+Sg1: joordčem
 [#: | C: ] _ 0:Vy (%ˊ: | %{%ˊ%}: ) [ C:+ | C:+ %ˈ: C:+ ] ( %^VOWL: %^CShort: )  %^VOWLower: (%^VOWFront:) (X3:) (%^PALNo: | %^PAL:) [ %>: | #: ] ;
! juˊrdded > joordam
 [ #: | C ] _ ( u: | i: ) (%ˊ: | %{%ˊ%}: ) [ C:+ | C:+ %ˈ: C:+ ] ( %^VOWL: %^CShort: ) %^VOWLower: (%^VOWFront: ) (X3:) (%^PALNo: | %^PAL:) [ %>: | #: ] ;
 [ #: | C ] _ ((%ˊ: | %{%ˊ%}: ) ( v:u | j:i )) (%ˊ: | %{%ˊ%}: ) [ C:+ | C:+ %ˈ: C:+ ] ( %^VOWSH: %^CLong: ) %^VOWLower: (%^VOWFront: ) (X3:) (%^PALNo: | %^PAL:) [ %>: | #: ] ;
              where Vx in (u i o a)
                    Vy in (o e å ä)
              matched ;

"Even syllabic verbs I, vowel lowering õ:â "
!! __@RULENAME@__ - deriving +Ind+Prs+Sg3, +Ind+Prs+Pl3 in viǯǯâd
 õ:â <=>  _ õ: (%ˊ: | %{%ˊ%}: ) [ C:+ | C:+ %ˈ: C:+ ] ( %^VOWSH: %^CLong: | %^VOWL: %^CShort: )  %^VOWLower: (%^VOWFront:) (X3:) (%^PALNo: | %^PAL:) [ %>: | #: ] ;
        õ: _     (%ˊ: | %{%ˊ%}: ) [ C:+ | C:+ %ˈ: C:+ ] ( %^VOWL: %^CShort: )                      %^VOWLower: (%^VOWFront:) (X3:) (%^PALNo: | %^PAL:) [ %>: | #: ] ;
 [#: | C: ] _     (%ˊ: | %{%ˊ%}: ) [ C:+ | C:+ %ˈ: C:+ ] ( %^VOWSH: %^CLong: )                      %^VOWLower: (%^VOWFront:) (X3:) (%^PALNo: | %^PAL:) [ %>: | #: ] ;
! juˊrdded+V+Cond+Sg1: joordčem
 [#: | C: ] _ 0:â (%ˊ: | %{%ˊ%}: ) [ C:+ | C:+ %ˈ: C:+ ] ( %^VOWL: %^CShort: )  %^VOWLower: (%^VOWFront:) (X3:) (%^PALNo: | %^PAL:) [ %>: | #: ] ;
! juˊrdded > joordam
 [ #: | C ] _ ( u: | i: ) (%ˊ: | %{%ˊ%}: ) [ C:+ | C:+ %ˈ: C:+ ] ( %^VOWL: %^CShort: ) %^VOWLower: (%^VOWFront: ) (X3:) (%^PALNo: | %^PAL:) [ %>: | #: ] ;
 [ #: | C ] _ ((%ˊ: | %{%ˊ%}: ) ( v:u | j:i )) (%ˊ: | %{%ˊ%}: ) [ C:+ | C:+ %ˈ: C:+ ] ( %^VOWSH: %^CLong: ) %^VOWLower: (%^VOWFront: ) (X3:) (%^PALNo: | %^PAL:) [ %>: | #: ] ;
!! diphthong opening after u
!! - deriving +Ind+Prs+Sg3, in kuõskkâd kueˊsǩǩe
 u _ (%ˊ: | %{%ˊ%}: ) [ C:+ | C:+ %ˈ: C:+ ] ( %^VOWL: ( %^FRICG:0 ) %^CShort: )  %^VOWLower: (X3:) (%^PALNo: | %^PAL:) [ %>: | #: ] ;
!! diphthong opening õ:â after i:e
!! - deriving +Ind+Prs+Sg3, in čiõkkâd
i: _ (%ˊ: | %{%ˊ%}: ) [ C:+ | C:+ %ˈ: C:+ ] ( %^VOWL: %^CShort: )  %^VOWLower: (X3:)  %^PALNo: [ %>: | #: ] ;

! diphthongs

"Even syllabic verbs I, diphthong opening after u"
!! __@RULENAME@__ - deriving +Ind+Prs+Sg3, in kuåccâd kuäˊcce
 å:ä <=> u _ (%ˊ: | %{%ˊ%}: ) [ C:+ | C:+ %ˈ: C:+ ] ( %^VOWL: ( %^FRICG:0 ) %^CShort: )  %^VOWLower: (X3:) (%^PALNo: | %^PAL:) [ %>: | #: ] ;


"Even syllabic verbs I, diphthong opening after i:e"
!! __@RULENAME@__ - deriving +Ind+Prs+Sg3, in čiõkkâd
 Vx:Vy <=> i: _ (%ˊ: | %{%ˊ%}: ) [ C:+ | C:+ %ˈ: C:+ ] ( %^VOWL: %^CShort: )  %^VOWLower: (X3:) (%^PAL:) [ %>: | #: ] ;
! i: _ (%ˊ: | %{%ˊ%}: ) [ C:+ | C:+ %ˈ: C:+ ] ( %^VOWL: %^CShort: )  %^VOWLower: (X3:) (%^PALNo: | %^PAL:) [ %>: | #: ] ;
             where Vx in (â e)
                   Vy in (ä ä)
             matched ;

"Even syllabic verbs I, diphthong opening after i:e e:â"
!! __@RULENAME@__ - deriving +Ind+Prs+Sg3, in pieˊǩǩ:peâkka
 e:â <=> i: _ (%ˊ: | %{%ˊ%}: ) [ C:+ | C:+ %ˈ: C:+ ] ( %^VOWL: %^CShort: )  %^VOWLower: (X3:) %^PALNo:  [ %>: | #: ] ;



"Even syllabic verbs I, diphthong opening õ:ä after i:e"
!! __@RULENAME@__ - deriving +Ind+Prs+Sg3, in čiõkkâd
 õ:ä <=> i: _ (%ˊ: | %{%ˊ%}: ) [ C:+ | C:+ %ˈ: C:+ ] ( %^VOWL: %^CShort: )  %^VOWLower: (X3:)  %^PAL: [ %>: | #: ] ;


"Even syllabic verbs I, diphthong opening i:e before õ:, å:, e:"
!! __@RULENAME@__ - used in conjunction with diphthong opening
 i:e <=> _ [õ:â | õ:ä | â:ä | e:ä | e:â ] (%ˊ: | %{%ˊ%}: ) [ C:+ | C:+ %ˈ: C:+ ] ( %^VOWL: %^CShort: )  %^VOWLower: (X3:) (%^PALNo: | %^PAL:) [ %>: | #: ] ;



!! !SECONDARY FRONTING


 
"Even syllabic verbs I, secondary vowel fronting with PAL u õ:e"
!! __@RULENAME@__ - deriving +Ind+Prs+Pl3 in VIQQAD: kuõskkâd >kueˊsǩǩe
 õ:e <=> u _ (%ˊ: | %{%ˊ%}: ) [ C:+ | C:+ %ˈ: C:+ ]  (%^VOWSH: %^CLong: | %^VOWL: %^CShort: )  %^VOWFront: (X3:) %^PAL: [ %>: | #: ] ; ! deriving +V+Inf in vueˊlǧǧed
         i _ (%ˊ: | %{%ˊ%}: ) [ C:+ | C:+ %ˈ: C:+ ] %^VOWFront: %^PAL: %>: ;
 


"Even syllabic verbs I, secondary vowel fronting with PAL"
!! __@RULENAME@__ - deriving +Ind+Prs+Pl3 in VIQQAD: čiõkkâd >čeäˊǩ'ǩe, also TIETTED vueˊlǧǧed+V+Ind+Prs+Pl3: vuäˊlǧǧe
 õ:ä <=> [ i:e | u ] _ (%ˊ: | %{%ˊ%}: ) [ C:+ | C:+ %ˈ: C:+ ]  %^VOWSH: ( %^FRICG:0 ) %^CLong: %^VOWLower: %^VOWFront: (X3:) %^PAL: [ %>: | #: ] ;


! "Even syllabic verbs I, secondary u > v PAL"
! __@RULENAME@__ - deriving +V+Inf in TIETTED: _uudd_ >uˊvdded
! u:v <=> u _ (%ˊ: | %{%ˊ%}: ) [ C:+ | C:+ %ˈ: C:+ ] %^VOWFront: %^PAL: %>: ;

!! !RELATIVE VOWEL LENGTHENING

!! vowel lengthening and consonant shortening, %^VOWL and %^CShort 


"Even syllabic verbs I, relative vowel lengthening"
!! __@RULENAME@__ - deriving +V+Inf in TIETTED: uudd > uˊvdded
 0:Vx <=> C: Vx: _ [( u: | i: ) (%ˊ: | %{%ˊ%}: ) [ C:+ | C:+ %ˈ: C:+ ] ] %^VOWL: (%^CShort:) [ %>: | #: | %^PAL: ] ;
      C: Vx: _ ( u: | i: ) (%ˊ: | %{%ˊ%}: )  C: [ QÂ1: ] C:+   %^VOWL: [ %>: | #: | %^PAL: ] ;
             where Vx in (â e å õ o a ä) ; 
             
!! 2012-06-05 removing "i" and "u" because viikkâd renders viiiǥǥam


"Even syllabic nouns I, relative vowel lengthening i"
!! __@RULENAME@__ - ǩiđđ:ǩiiđ
 0:i <=> C: i: _ [(%ˊ: | %{%ˊ%}: ) [ C:+ | C:+ %ˈ: C:+ ] ] [ %^VOWL: | %^VOWXL: ] (%^CShort:) [ %>: | #: | %^PAL: ] ;
    C: i: _ ( %ˊ: | %{%ˊ%}: )  C: [ QÂ1: ] C:+  [ %^VOWL: | %^VOWXL: ] [ %>: | #: | %^PAL: ] ;



"Even syllabic nouns, relative vowel extra lengthening"
!! __@RULENAME@__ - deriving +V+Inf in TIETTED: uudd >uˊvdded
 0:Vx <=> [ C: | #: ] Vx: _ [ (%ˊ: | %{%ˊ%}: ) [ C:+ | C:+ %ˈ: C:+ ] ] %^VOWXL: [ %>: | #: ]  ;
             where Vx in (â e å õ o a ä) ;


"Even syllabic nouns, relative vowel extra lengthening u not followed by v"
!! __@RULENAME@__ - declension of nouns kunn > kuun
 0:u <=> [ C: | #: ] u: _ (%ˊ: | %{%ˊ%}: ) [[ s: s: | n n: ] %^VOWXL: | CNoJV C:+ %^VOWL: %^CShort: ] [ %>: | #: ] ;
!! relative lengthening of u
          C: u: _ [ (%ˊ: | %{%ˊ%}: ) [ C:+ | C:+ %ˈ: C:+ ] ] %^VOWL: (%^CShort:) [ %>: | #: | ( %^VOWRaise: ) %^PAL: ] ;
      C: u: _ (%ˊ: | %{%ˊ%}: )  C: [ QÂ1: ] C:+   %^VOWL: [ %>: | #: | %^PAL: ] ;




!! simultaneous lengthening and raising, hmm: xfst ordering might be easier

"Even syllabic verbs I, relative vowel lengthening and raising"
!! __@RULENAME@__ - conjugation of ceˊpcced > ciiˊpcež
 0:Vy <=> [ #: | C: ] Vx:Vy _ (%ˊ: | %{%ˊ%}: ) C:+ %^VOWL: %^CShort: %^VOWRaise: %^PAL: %>: e ;
             where Vx in (e â å ä o)
                   Vy in (i õ o a u)
             matched ;




"Even syllabic verbs I, relative vowel lengthening and lowering"
!! __@RULENAME@__ -  simultaneous lengthening and lowering: juˊrdded > joordam juˊrdded+V+Ind+Prs+Sg1
 0:Vy <=> [ C: | #: ] Vx:Vy _ (%ˊ: | %{%ˊ%}: ) C:+ %^VOWL: %^CShort: %^VOWLower:  ;
             where Vx in (i õ o a)
                   Vy in (e â å ä)
             matched ;

"Even syllabic verbs I, relative vowel lengthening and lowering u:o 0:o"
!! __@RULENAME@__ -  simultaneous lengthening and lowering: juˊrdded > joordam juˊrdded+V+Ind+Prs+Sg1
 0:o <=> [ C: | #: ] u:o _ (%ˊ: | %{%ˊ%}: ) C:+ %^VOWL: %^CShort: %^VOWLower:  ;


"Vowel lengthening in INDPRSSG3 and IMPSG2"
!! __@RULENAME@__ - kååˊmmerd, maainâst
 0:Vx <=> Vx _ (i) ( %ˊ ) :C+ [ QE1: | QA1: | QÂ1: ] C:+ [ %^IMPSG2: | %^INDPRSSG3: ] ;
        where Vx in (a å ä) ;

!! !VOWEL DUMMY LOSS


"Vowel dummy loss when not INDPRSSG3 and IMPSG2"
!! __@RULENAME@__ - QE1
 Vx:0 <=> _ C:+ %>: ;
       where Vx in (QE1) ;


!! !SEMI VOWELS

 
"Even syllabic nouns, for j>i"
!! __@RULENAME@__ - sijdd > siid This will need a special extra-lengthening rule
 j:i <=> [ C: | #: ] i: (%{%ˊ%}: ) _ (%ˊ: | %{%ˊ%}: ) d d: [ %^VOWL: %^CShort: | %^VOWXL: | %^VOWLower: | %^VOWSH: %^CLong:  %^VOWLower: %^PAL: ];


"Even syllabic verbs,  for v>u"
!! __@RULENAME@__ - uvdd > ouˊdde
 v:u <=> [ C: | #: ] u: ( 0:u | 0:o ) (%{%ˊ%}: ) _ (%ˊ: | %{%ˊ%}: ) d d: [ %^VOWL: %^CShort: | %^VOWXL: | %^VOWLower: | %^VOWSH: %^CLong:  %^VOWLower: (%^VOWFront: ) %^PAL: ];


 "Even syllabic nouns, for h>u"
!! __@RULENAME@__ - luhtt  s s: ...
 h:u <=> V: (%{%ˊ%}: ) _ (%ˊ: | %{%ˊ%}: ) [ s s: | š š: | t t: | c c: | č č: ] [ %^VOWL: %^CShort: | %^VOWXL: | %^VOWSH: %^CLong:  %^VOWLower: %^PAL: ] ;


"trisyllabic verbs and doer derivations, i>j"
!! __@RULENAME@__ -
 i:j <=> _ %> V ;

!! !VOWELS TENSE vs LAX 2012-11-28


"Even-syllabic nouns, for â:e"
!! __@RULENAME@__ - miârr:mieˊrre
 â:e <=> i _ ( %ˊ | %{%ˊ%}:  ) [ C:+ | C:+ 0:%ˈ C:+ ] %^VOWTense:0 ;
        

!! !VOWEL and ZERO ALTERNATION


"Realization for QE1 in e " 
!! __@RULENAME@__ - kåˊmrded, vuejat, maainâst
 Vx:Vy <=> _ ( %ˊ ) C:+ %^IMPSG2: ;
         where Vx in (QE1 QA1 QÂ1)
               Vy in (e a â)
         matched ;

"Realization for QE1 in a" 
!! __@RULENAME@__ - 
 Vx:Vy <=> _ ( %ˊ | ’: ) [ i | C:+ ] %^INDPRSSG3: ;
         where Vx in (QE1 QA1 QÂ1)
               Vy in (a a a)
         matched ;
         
!! !THE NON-ORTHOGRAPHIC SYLLABLE 


"Loss of _’_ when preceded by vowel"
!! __@RULENAME@__ - This is a temporary solution to  "’" in võˊll’jed
 ’:0 <=> :V _  ; 

"QA1:’ when subseqent syllable has vowel v"
!! __@RULENAME@__ - This is a temporary solution to  "’" in võˊll’jed 2013-08-29
QA1:’ <=> [ l l ] _ i: %>: V: ; 

!! !PALATALIZATION


"Even-syllabic verbs I, Palatalization mark between V and C"
!! __@RULENAME@__ - used +Ind+Prs+Pl3, +Ind+Prt+Pl3
 %{%ˊ%}:%ˊ <=> [ #: | C: ] V:V ( V:0 | :V ) _ [ C:+ | C:+ %ˈ: C:+ | C:+ 0:%ˈ C:+ ] ( %^VOWSH: ( %^CLong: ) | %^VOWTense: | %^VOWL: %^CShort: ) ( %^VOWLower: | %^VOWRaise: ) ( %^VOWFront: | %^VOWTense:0 ) (X3:) %^PAL: [ %>: | #: ] ;


!! The left context allows for:
!! Vowel shortening, and j/v > V 



 
"Even-syllabic verbs I, Palatalization of gg and kk"
!! __@RULENAME@__ -
 Cx:Cy <=> V: (%ˊ: | %{%ˊ%}: ) (C:)     _ Cx: ( %^VOWSH: (%^CLong: ) ) ( %^VOWLower: | %^VOWRaise: ) (%^VOWFront: ) (X3:) %^PAL: [ %>: | #: ] ;
           V: (%ˊ: | %{%ˊ%}: ) (C:) Cx: _     ( %^VOWSH: (%^CLong: ) ) ( %^VOWLower: | %^VOWRaise: ) (%^VOWFront: ) (X3:) %^PAL: [ %>: | #: ] ;
                where Cx in (g k)
                      Cy in (ǧ ǩ)
                matched ;

"Even-syllabic nouns I, Depalatalization of ǧǧ and ǩǩ"
!! __@RULENAME@__ -
 Cx:Cy <=> V: (%ˊ: | %{%ˊ%}: ) (C:)     _ (%ˈ: ) Cx: ( %^VOWSH: (%^CLong: ) ) ( %^VOWLower: | %^VOWRaise: ) (X3:) %^PALNo: [ %>: | #: ] ;
           V: (%ˊ: | %{%ˊ%}: ) (C:) Cx: (%ˈ: ) _     ( %^VOWSH: (%^CLong: ) ) ( %^VOWLower: | %^VOWRaise: ) (X3:) %^PALNo: [ %>: | #: ] ;
                where Cx in (ǧ ǩ)
                      Cy in (g k)
                matched ;



"Even-syllabic nouns, removing palatalization in -est +Loc nouns"
!! __@RULENAME@__ - removing palatalization in +Sg+Ill,  pieˊss:peässa
   %ˊ:0 <=> V: _ [ C:+ | C:+ %ˈ: C:+ ] ( %^VOWSH: %^CLong: | %^VOWL: %^CShort: ) ( %^VOWLower: | %^VOWRaise: ) (%^VOWFront: ) (X3:) %^PALNo: [ %>: | #: ] ;


!  "Even-syllabic verbs I, removing palatalization in -ed verbs"
! __@RULENAME@__ - removing palatalization Prs+Sg3
!   %ˊ:0 <=> V: _ [ C:+ | C:+ %ˈ: C:+ ] ( %^VOWSH: %^CLong: | %^VOWL: %^CShort: ) ( %^VOWLower: | %^VOWRaise: ) (%^VOWFront: ) (X3:) %^PALNo: [ %>: | #: ] ;




! "Even syllabic verbs I, vowel position I > II-III"
!  Vx:Vy <=> (C:+) _ V: (ˊ:) C:+ (X3:) [ %^2VOW: | %^3VOW: ] ;
!  		where Vx in (â o e å ä)
!  			  Vy in (õ u i o a)
!  		matched ;
! 
! "Even syllabic verbs I, vowel position II > I"
!  Vx:Vy <=> (C:+) _ V: ˊ:0 C:+ (X3:) %^1VOW: ;
!  		   (C:+) V: _ ˊ:0 C:+ (X3:) %^1VOW: ;
!  		   (C:+) _ ˊ:0 C:+ %^1VOW: ;
!  		where Vx in (õ u i o a)
!  			  Vy in (â o e å ä)
!  		matched ;


!  2012-06-01 This appears to depart from a vowel with "ˊ"
!  so it must be III > IV
! "Even syllabic verbs I, vowel position II > IV"
!  Vx:Vy <=> (C:+) _ V:0 ˊ C:+ %^4VOW: ;
!  		where Vx in (õ u i o a)
!  			  Vy in (â o e å ä)
!  		matched ;
! 
! "Even syllabic verbs I, diftong position I > II â:õ"
!  â:õ <=> (C:+) [ e: | u: ] _ (ˊ:) C:+ (X3:) [ %^2VOW: | %^3VOW: ] ;
! 
! "Even syllabic verbs I, diftong position I > II ä:a"
!  ä:a <=> (C:+) [ e: | u: ] _ (ˊ:) C:+ (X3:) %^2VOW: ;
! 
! "Even syllabic verbs I, diftong position I > II ä:e"
!  ä:e <=> (C:+) [ e: | u: ] _ (ˊ:) C:+ (X3:) %^3VOW: ;
! 
! 
! "Even syllabic verbs I, diftong position II > I & IV i:e"
!  i:e <=> (C:+) _ [ õ:â | a:ä ] (ˊ) C:+ (X3:) [ %^1VOW: | %^4VOW: ] ;
! 
! "Even syllabic verbs I, diftong position II > I & IV õ:â"
!  Vx:Vy <=> (C:+) [ i:e | u: ] _ (ˊ) C:+ (X3:) [ %^1VOW: | %^4VOW: ] ;
!  		where Vx in (õ a)
!  			  Vy in (â ä)
!  		matched ;
! 
! "Even syllabic verbs I, diftong position II > III a:e"
!  a:e <=> (C:+) [ i: | u: ] _ (ˊ) C:+ (X3:) [ %^3VOW: ] ;
! 
! 
! !e:â e:õ
! "Even syllabic verbs I, diftong position V > I & IV i:e"
!  i:e <=> (C:+) _ [e: | â:] ˊ: C:+ (X3:) [ %^1VOW: | %^4VOW: ] ;
! 
! "Even syllabic verbs I, diftong position V > I & IV e:â"
!  e:â <=> (C:+) [i: | u:] _ ˊ: C:+ (X3:) [ %^1VOW: | %^4VOW: ] ;
! 
! "Even syllabic verbs I, diftong position V > I & IV â:ä"
!  â:ä <=> (C:+) [i: | u:] _ ˊ: C:+ (X3:) [ %^1VOW: | %^4VOW: ] ;
! 
! "Even syllabic verbs I, diftong position V > II & III e:õ"
!  e:õ <=> (C:+) [ i: | u: ] _ ˊ: C:+ (X3:) [ %^2VOW: | %^3VOW: ] ;
! 
! ! 2012-06-01 Unresolved <= conflict with respect to 'â:õ' vs. 'â:a'
! ! between "Even syllabic verbs I, diftong position I > II â:õ"
! ! and "Even syllabic verbs I, diftong position V > II & III â:a"
! !TEMPORARY REMOVAL
! !"Even syllabic verbs I, diftong position V > II & III â:a"
! ! â:a <=> (C:+) [ i: | u: ] _ ˊ: C:+ (X3:)   %^2VOW: ;
! !
! !2012-06-01
! ! Warning: Unresolved <= conflict with respect to 'â:õ' vs. 'â:e'
! !    between "Even syllabic verbs I, diftong position I > II â:õ"
! !        and "Even syllabic verbs I, diftong position V > II & III â:e"
! ! TEMPORARY REMOVAL
! !"Even syllabic verbs I, diftong position V > II & III â:e"
! ! â:e <=> (C:+) [ i: | u: ] _ ˊ C:+ (X3:) %^3VOW: ;


!! !!Consonant gradation rules

!! !Weakening Consonant Cluster

!! dealing with relative length changes
!! mõõnnâd : mõˊnne : mõõn 

!"Even syllabic verbs I, cg in relation to Vowel length"
! V Cx Cx:0 


"Even syllabic verbs I, cg"
!! __@RULENAME@__ - used with  +Imp+Sg2, +Ind+Prs+ConNeg, teevvad > teev
 Cx:0 <=> :V (%ˊ: | %{%ˊ%}: ) Cx _ (%^VOWL: ) %^CShort: ;
           V: :V  (%ˊ: | %{%ˊ%}: ) Cx _ %^VOWXL: ; 
 		where Cx in (đ l r v) ;

"Even syllabic verbs I, cg m:0"
!! __@RULENAME@__ - used with  +Imp+Sg2, +Ind+Prs+ConNeg, 
 m:0 <=> :V (%ˊ: | %{%ˊ%}: ) Cx _ (%^VOWL: ) %^CShort: ;
           V: :V  (%ˊ: | %{%ˊ%}: ) Cx _ %^VOWXL: ; 
          [ V: | :V ] (%ˊ: | %{%ˊ%}: )  [ s | r | l | p | d: ] Cx:  _ %^VOWL: %^CShort: ;
          V: (%{%ˊ%}: ) h: (%ˊ: | %{%ˊ%}: ) Cx: _ %^VOWXL: ;
          
"Even syllabic verbs I, cg n:0"
!! __@RULENAME@__ - used with  +Imp+Sg2, +Ind+Prs+ConNeg, jiõnn:jiõn
 n:0 <=> :V (%ˊ: | %{%ˊ%}: ) Cx _ (%^VOWL: ) %^CShort: ;
           V: :V  (%ˊ: | %{%ˊ%}: ) Cx _ %^VOWXL: ; 
          [ V: | :V ] (%ˊ: | %{%ˊ%}: )  [ s | r | l | p | d: ] Cx:  _ %^VOWL: %^CShort: ;
          V: (%{%ˊ%}: ) h: (%ˊ: | %{%ˊ%}: ) Cx: _ %^VOWXL: ;
          

! "Even syllabic verbs I, cg vv:v"
! v:0 <=> :V (%ˊ: | %{%ˊ%}: ) v _ (%^VOWL: ) %^CShort: ; 

"Even syllabic nouns I, with extra lengthening of vowel V>VV and SS>ZZ"
!! __@RULENAME@__ 
!! used with  +N+Sg+Nom > +N+Sg+Gen for cases like
!! {{kuss > kuuzz}}.
!! {{{
Cx:Cy <=> [#: | C: ] V: ( 0:V | V:V ) (%ˊ: | %{%ˊ%}: ) Cx: _ %^VOWXL: ; !!= @CODE@
          [#: | C: ] V: ( 0:V | V:V ) (%ˊ: | %{%ˊ%}: ) _ Cx: %^VOWXL: ; !!= @CODE@
                where Cx in (k s)                          !!= @CODE@
                      Cy in (ǥ z)                          !!= @CODE@
                matched ;                                  !!= @CODE@
!! }}}


"Even syllabic nouns I, with extra lengthening of vowel ij>ii/uv>uu and dd>d"
!! __@RULENAME@__ - used with  +N+Sg+Nom > +N+Sg+Gen, sijdd > siid
 Cx:0 <=> [ u: (0:u | 0:o ) (%ˊ: ) v: | i: (%ˊ: ) j: ] ( %{%ˊ%}: ) Cx _ %^VOWXL: ;
 ! juˊrdded > joordam
          [ V: | :V ] (%ˊ: | %{%ˊ%}: ) CNoJV Cx _ (%^VOWL: ) %^CShort: ;
                where Cx in (d) ;




"Even syllabic verbs I, Voicing" 
!! __@RULENAME@__ - ss:zz, +Imp+Sg2, +Ind+Prs+ConNeg, double consonants at coda become voiced in gradation
 Cx:Cy <=> V: (%ˊ: | %{%ˊ%}: ) Cx: _ (%^VOWL:) %^CShort: ;
           V: (%ˊ: | %{%ˊ%}: ) _ Cx: (%^VOWL:) %^CShort: ; !This was X3
                where Cx in (s š c č k)
                      Cy in (z ž ʒ j ǥ)
                matched ;


"Even syllabic verbs I, third consonant loss" 
!! __@RULENAME@__ - lgg:lǥ, V Ca Cx Cx:0, +Imp+Sg2, +Ind+Prs+ConNeg, double consonants following consonant at coda that become single consonants in gradation
 Cx:0 <=> [ V: | :V ] (%ˊ: | %{%ˊ%}: )  [ s | r | l | p | d: ] Cx:  _ %^VOWL: %^CShort: ;
          V: (%{%ˊ%}: ) h: (%ˊ: | %{%ˊ%}: ) Cx: _ %^VOWXL: ;
                where Cx in (k ǩ g ǧ ŋ t č c s) ;

! 
! njeiddad > njeeid
! 2012-06-12 _v:u_ allows for uˊvdded
"Even syllabic verbs I, third consonant loss idd:id" 
!! __@RULENAME@__ - idd:id, +Imp+Sg2, +Ind+Prs+ConNeg double consonants following vowel and "i" at coda that become single consonants in gradation
 d:0 <=> V: (0:V) [ i | v:u ] (%ˊ: | %{%ˊ%}: ) d: _ %^VOWL: %^CShort: ;


"Even syllabic verbs I, t>đ" 
!! __@RULENAME@__ - tt:đ
 t:đ  <=> V: (%ˊ: | %{%ˊ%}: ) _ t:0 [ %^VOWL: %^CShort: | %^VOWXL: ]  ;

"Even syllabic nouns I, p>v" 
!! __@RULENAME@__ - pp:v
 p:v  <=> V: (%ˊ: | %{%ˊ%}: ) _ p:0 [ %^VOWL: %^CShort: | %^VOWXL: ]  ;

"Even syllabic verbs I, second consonant loss"
!! __@RULENAME@__ -
 Cx:0 <=> V: (%ˊ: | %{%ˊ%}: ) Cx: _ [ %^VOWL: %^CShort: | %^VOWXL: ] ;
                where Cx in (p t) ; ! Extend

! 
"Even syllabic verbs I, lgg>lǥ" 
!! __@RULENAME@__ - vueˊlǧǧed+V+Ind+Prs+Sg2 => vuâlǥak
 g:ǥ  <=> V: (%ˊ:0 | %{%ˊ%}:0 ) [ l | r | d: ]  _ g: %^VOWL:  %^CShort: ;
 V: (%ˊ:0 | %{%ˊ%}:0 ) [ l | r | d: ]  _ g: %^VOWL: ( %^FRICG:0 ) %^CShort: ;
          V: (%ˊ:0 | %{%ˊ%}:0 ) [ l | r | d: ]  g: _ %^VOWL: %^FRICG:0 %^CShort: ;

!vueˊlǧǧed+V+Imp+Pl3: vuâlǥǥaz
!"Even syllabic verbs I, lgg>lǥǥ" ! lgg:lǥǥ



"Even syllabic verbs I, 'lǧǧ>'lj" 
!! __@RULENAME@__ - vueˊlǧǧed+V+Ind+Prt+Pl1 => vuõˊljim
 g:j  <=> V:  :%ˊ [ l | r | d: ]  _ g:0 %^VOWL: %^CShort: ( %^VOWLower: | %^VOWRaise: ) (%^VOWFront: ) (X3:) %^PAL: ;


!! !!Rules for cleaning up and composing end result

!! * Strengthening Consonant Cluster
!! * V shortening and C lengthening 
!! * %^CLong is generally used in conjunction with a preceding %^VOWSH
!! * In verbs this will be observed in finite forms
!! * In derivational morphology, e.g. -škuetted, this is not the case



"Orthographic Consonant lengthening in INDPRSSG3 and IMPSG2"
!! __@RULENAME@__ - kååˊmmerd 
 0:Cx <=> V: ( :V ) ( %ˊ ) :C* Cx _ [ QE1: ] C:+ [ %^IMPSG2: | %^INDPRSSG3: ] ;
            where Cx in (m) ;



! 
! ! 2009: 20
! "Even-syllabic nouns, In gg or ǧǧ"
! __@RULENAME@__ -
!  Cx:Cy <=> V: [ (ˊ:) [ d: | l | r ]| [ u: | i: ] (ˊ:) ] _ X3: ;
!               where Cx in (ǧ g)
!                     Cy in (j ǥ)
!               matched ;
! 
! ! ǩeäˊdǧǧ: ǩieˊđjest
! ! ǩeäˊdǧǧ: ǩeäđǥaž
! 
! "xx:x rule"
! __@RULENAME@__ -
!  Cx:0 <=> V ( ˊ: ) Cx _ V X3:0 ;
! ! to be refined…
! 
! ! goal:
! ! mõõnnâd:mõõˊnn	POORRÂD ;
! ! 
! 
