! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or divvun@samediggi.no

!! !!!Skolt Sámi TWOLC file
!
! $Id$
!
!

!! !!!Introduction
!! The rule file for Skolt Saami is divided into 5 main types:
!! 
!! # Definitions
!! # Consonant shift rules (tbw)
!! # Vowel alternation rules
!! # Consonant gradation rules
!! # Rules for cleaning up and composing end result


!! !!!Alphabets, sets and definitions

!! !!Alphabet

Alphabet 
  a b c d e f g h i j k l m n o p q r s t u v w x y z !!= * @CODE@
  A B C D E F G H I J K L M N O P Q R S T U V W X Y Z !!= * @CODE@
  ä å â õ      !!= * @CODE@ 
  Ä Å Â Õ       !!= * @CODE@
  č ǩ ǯ ǧ ž đ ǥ ʒ š ŋ					!!= * @CODE@
  Č Ǩ Ǯ Ǧ Ž Đ Ǥ Ʒ Š Ŋ					!!= * @CODE@
  ẹ   	      	   !!= * @CODE@ Pedagogical purposes
  Ẹ		   !!= * @CODE@ Pedagogical purposes

  æ ø ö á é í					!!= * @CODE@
  Æ Ø Ö Á						!!= * @CODE@
  Á É Ó Ú Í À È Ò Ù Ì Ë Ü Ï Â Ê Ô Û Î Ã Ý þ Ñ Ð		!!= * @CODE@
  á é ó ú í à è ò ù ì ë ü ï â ê ô û î ã ý þ ñ ð ß ª ß ç	!!= * @CODE@


 ʹ  !! * MODIFIER LETTER PRIME: U+02B9 suprasegmental palatization
 ʼ  !! * MODIFIER LETTER APOSTROPHE: U+02BC Syllable marker that appears in writing 
 ˈ !! * MODIFIER LETTER VERTICAL LINE: U+02C8 as overlong consonant marker that does not appear in writing except for pedagogical purposes

!  for consonant lengthening
%{XC%}:m
%{XC%}:v

%{XC%}:n
%{XC%}:r
%{XC%}:l

%{XC%}:k


!! !! Trigger symbols

 %^RmVow:0 !!= * @CODE@ for removing vowels e, â, a in second syllable before consonant  
 %^1VOW:0 %^2VOW:0 %^3VOW:0 %^4VOW:0 %^5VOW:0 !!= * @CODE@ Vow triggers

 %^VOWLower:0 !!= * @CODE@ i>e, õ>â, â>ä, u>o, o>å
 %^VOWRaise:0 !!= * @CODE@ e>i, ä>e 
 %^VOWBack:0	     !!= * @CODE@ Backing e>õ, e>â
 %^VOWFront:0	     !!= * @CODE@ Fronting
 %^PAL:0 %^PALNo:0 %^PALE:0 %^PALÄ:0 %^PALẸ:0   !!= * @CODE@ Palatalisation
 %^VC:0         !!= * @CODE@ Voicing s:z š:ž
 %^Â:0		!!= * @CODE@ Indicates placement of â in stem

!!  !! CHARACTERISTIC BREAKDOWN 2015-02-17
%^VV2V:0        !!= * @CODE@ Shortens vowel 
%^V2VV:0 	!!= * @CODE@ Lengthens vowel
%^CC2C:0 	!!= * @CODE@ Shortens consonant
%^C2CC:0 	!!= * @CODE@ Lengthens consonant
%^CC2CCC:0 	!!= * @CODE@ Strong to Extra Strong consonant
%^C2CCC:0 	!!= * @CODE@ Weak to Extra strong consonant
%^CCC2C:0 	!!= * @CODE@ Extra Strong to Weak consonant
%^CCC2CC:0 	!!= * @CODE@ Extra Strong to Strong consonant
%^XYYY2XYY:0 	!!= * @CODE@ Weakens Extra strong to strong consonant cluster
%^XYYY2XY:0 	!!= * @CODE@ Weakens Extra strong to weak consonant cluster
%^XYY2XY:0 	!!= * @CODE@ Shortens consonant cluster
%^XY2XYY:0 	!!= * @CODE@ Lengthens consonant cluster
%^XYY2VY:0	!!= * @CODE@ Consonant cluster to vowel and consonant sijdd:siid
%^XYY2VYY:0	!!= * @CODE@ Consonant cluster to vowel and geminate uvdd:ouʹdd

%^PenVV2V:0     !!= * @CODE@ Shortens vowel in penultimate syllable
%^PenV2VV:0     !!= * @CODE@ Lengthens vowel in penultimate syllable
%^PenCC2C:0     !!= * @CODE@ Shortens consonant in penultimate syllable
%^PenC2CC:0     !!= * @CODE@ Lengthens consonant in penultimate syllable

!! !! Gradation triggers 2015.01.23
 %^XSt2St:0	!!= * @CODE@ Causes weakening from extra strong to strong
 %^XSt2W:0	!!= * @CODE@ Causes weakening from extra strong to weak
 %^St2W:0	!!= * @CODE@ Causes weakening from strong to weak
 %^W2St:0	!!= * @CODE@ Causes strengthening from weak to strong
 %^W2XSt:0	!!= * @CODE@ Causes strengthening from weak to extra strong
 %^St2XSt:0	!!= * @CODE@ Causes strengthening from strong to extra strong

!! Other vowel length and consonant length will be phased out
 %^VOWSH:0	  !!= * @CODE@
 %^VOWXSH:0	  !!= * @CODE@ Extra shortens vowel in relation to consonant
 %^VOWXL:0    !!= * @CODE@ Extra lengthens vowel in relation to consonant in nouns, for example
 %^CXLong:0 %^CLong:0 %^CShort:0 !!= * @CODE@ Lengthening, shortening

 X3:0  !!= * @CODE@ causes weak grade
 %{VU%}:u         !!= * @CODE@ used with stuuʹl
 %{Â1%}:0 !!= * @CODE@ used with "mainsted" to tell (a story)
 %{A1%}:0 !!= * @CODE@ used with "vuejted" to drive (causative)
 %{E1%}:0 !!= * @CODE@ used with "kåʹmrded" to bow
 %{%ʹ%}:0 !!= * @CODE@ archiform for MODIFIER LETTER ACUTE ACCENT used in stems where the lemma does not contain a MODIFIER LETTER ACUTE ACCENT and yet the paradigm requires one.
 %{EÂ%}:e !!= * @CODE@ archiform for ie:eâ variation
 %^IMPSG2:0	!!= * @CODE@ 
 %^INDPRSSG3:0  !!= * @CODE@ 
 %^FRICG:0	!!= * @CODE@ 
 %^VOWTense:0   !!= * @CODE@ â:e
 %^VOWLax:0     !!= * @CODE@ e:â 

!! Literal quotes and angles must be escaped (cf morpheme boundaries below):

 »7     !! * »
 «7     !! * «
 %[%>%] !! * >
 %[%<%] !! * <

!! Morpheme boundaries:
 «  ! Derivational prefix
 »  ! Derivational suffix
 %< ! Inflectional prefix
 %> ! Inflectional suffix
 #  ! Word boundary for both lexicalised and dynamic compounds
 %^ ! (exceptional) soft hyphenation point
 %- ! Hyphen for question particle

!! End of alphabet definitions
 ;


!! !!Sets
Sets 

! Letter =
 Cns	= b c d f g h j k l m n p q r s t v w x z đ ʒ ǯ č ŋ ǩ ǧ ǥ š ž ; !!= * @CODE@
 CNoV	= b c d f g h j k l m n p q r s t w x z đ ʒ ǯ č ŋ ǩ ǧ ǥ š ž ;   !!= * @CODE@

 CNoJ	= b c d f g h k l m n p q r s t v w x z đ ʒ ǯ č ŋ ǩ ǧ ǥ š ž ;   !!= * @CODE@
 Vow	= a e i o u å ä â õ           	      	      	      	        !!= * @CODE@
          A E I O U Å Ä Â Õ ;	    				        !!= * @CODE@

 CNoJV  = b c d f g h k l m n p q r s t w x z đ ʒ ǯ č ŋ ǩ ǧ ǥ š ž ;     !!= * @CODE@
 CNoDLRZ = b c f g h j k m n p q s t v w x z đ ǯ č ŋ ǩ ǧ ǥ š ž ;        !!= * @CODE@


!! !!Definitions

Definitions
 DummiesBeforeConsLength = [%^V2VV: |%^VV2V: |%^VOWRaise:| %^VOWLower:| %^PAL: | %^PALNo: | %^PALE: | %^PALÄ: | %^PALẸ: ] ;
 DummiesBeforeVowelHeight = [( %^V2VV: |%^VV2V: )] ; 

!! {{{
ShortV2XL = [0:a a|0:ä a:ä] ;  !!= * @CODE@

LongV2SH = [a:0 a|â:0 â|ä:0 ä|e:0 e|i:0 i|å:0 å|o:0 o|u:0 u|õ:0 õ|a:0 a:ä|â:0 â:õ|ä:0 ä:a|ä:0 ä:a|e:0 e:i|i:0 i:e|å:0 å:o|o:0 o:u|u:0 u:o|õ:0 õ:â] ; !!= * @CODE@ 

Diphth = [e â|e:i â:õ|e ä|e:i â:e|e:i ä:â|e:i ä:e|i â|i:e â:ä|i â:e|i e|i:e e:â|i:e e:ä|i e:õ|i õ|i:e õ:â|i:e õ:ä|u â|u â:õ|u e|u e:â|u â:e|u e:õ|u e:ä|u õ|u õ:â|u õ:e|u å|u å:ä|u å:e| u å:ẹ|u ä|u ä:å|u ä:ẹ|u ä:e] ; !!= * @CODE@ 

DbVow = [a a|a:ä a:ä|i i|i:e i:e|o o|o:å o:å|õ õ|õ:â õ:â|u u|u:o u:o|â â|â:õ â:õ|e e|e e:ä|e ä:e|e:i e:i|å å|å:o å:o|ä ä|ä:a ä:a] ; !!= * @CODE@ 

2DbVow = [0:a a|0:a ä:a|0:i i|0:i e:i|0:o o|0:o u:o|0:o å:o|0:õ õ|0:õ â:õ|0:u u|0:u o:u|0:â â|0:e e|0:å å|0:å o:å|0:ä ä] ; !!= * @CODE@ 

AllDbVow = [DbVow|2DbVow] ; !!= * @CODE@ 

2SgVow = [a:0 a|a:0 a:ä|i:0 i|i:0 i:e|o:0 o|o:0 o:å|õ:0 õ|õ:0 õ:â|u:0 u|u:0 u:o|â:0 â|â:0 â:õ|e:0 e|e:0 e:i|å:0 å|å:0 å:o| ä:0 ä|ä:0 ä:a] ; !!= * @CODE@ 

SurfLongVow = [Diphth|AllDbVow] ; !!= * @CODE@ 

2ShortVow = [Diphth|2SgVow] ; !!= * @CODE@ 


CC = [b b|c c|č č|ǯ ǯ|d d|đ đ|g g|ǧ ǧ|k k|ǩ ǩ|l l|m m|n n|p p|r r|s s|š š|t t|v v|h t t|n n j] ; !!= * @CODE@

LongC = [k: k:|s: s:|š: š:|c: c:|č: č:|m m:|v v:|đ đ:|n n:|l l:|r r:] ; !!= * @CODE@ 
!! }}}

!! preceded by two vowels VxVx, VxVy
!! {{{
DiphthwithCC = [k:ǥ k:ǥ|s:z s:z|š:ž š:ž|c:ʒ c:ʒ|č:j č:j|m m:0|v v:0|đ đ:0|n n:0|l l:0|r r:0] ; !!= * @CODE@
!! }}}

!! preceded by two vowels
!! {{{
CC2CforPrecedingVV = [k:ǥ k:ǥ|k:j k:j|ǩ:j ǩ:j|ǩ:ǥ ǩ:ǥ|s:z s:z|š:ž š:ž|c:ʒ c:ʒ|č:j č:j|m m:0|v v:0|đ đ:0|n n:0|l l:0|p:v p:0|t:đ t:0|r r:0] ; !!= * @CODE@ 
!! }}}

!! preceded by one vowel Vx=>VxVx påll=>pååll
!|ǧ:j ǧ:0|ǧ:ǥ ǧ:0 
!! {{{
CCforPrecedingV2VV =  [b b|p p|m m|v v| d d|t t|đ đ|n n|l l|r r|s s|š š|c c|č č| ǯ ǯ | k k|ǩ ǩ|g g|ǧ ǧ|ǧ:g ǧ:g|ǧ:ǥ ǧ:ǥ ] ; !!= * @CODE@ 

iCC2iCforPrecedingV2VV = [ i (ʹ:|0:ʹ) [b b:0|ʒ ʒ:0|d d:0|g g:0|k k:0|ǩ ǩ:0|l l:0|m m:0|n n:0|p p:0|r r:0|t t:0|v v:0]] ; !!= * @CODE@ 

uCC2uCforPrecedingV2VV = [ [u|%{VU%}:u] (ʹ:|0:ʹ) [ʒ ʒ:0|d d:0|g g:0|k k:0|ǩ ǩ:0|l l:0|m m:0|n n:0|p p:0|r r:0|t t:0]] ; !!= * @CODE@ 
!! }}}

!! extra long vowel (u or i)
!! {{{
CC2CafterV2VVandUI = [uCC2uCforPrecedingV2VV|iCC2iCforPrecedingV2VV] ;  !!= * @CODE@ 
!! }}}

!! extra long vowel (d or l or r)
!! {{{
CC2CforPrecedingV2VV = [l [b b:0|ǧ:j ǧ:0|ǧ:ǥ ǧ:0|m m:0|t t:0]|m p p:0|p c c:0|r [d d:0|n n:0]|s [k: k:0|ǩ: ǩ:0|t t:0]] ; !!= * @CODE@ 
!! }}}

!! extra long consonant (d or l or r)
!! {{{
C2CCafterVV2V = [l [b 0:b|j:ǧ 0:ǧ|ǥ:ǧ 0:ǧ|m 0:m|t 0:t]|m p 0:p|p c 0:c|r d 0:d|s [:k 0:k|:ǩ 0:ǩ|t 0:t]] ; !!= * @CODE@ 
!! }}}

!! Long consonant cluster
LongCluster = [l [d d |g g|ǧ ǧ|m m|t t]|m p p|p c c|r d d |s [k k|ǩ ǩ|t t]] ;

!! Shortening consonant cluster
XYY2XYCluster = [l [m m:|b b:|p p:| d d:|t t:|g g:|ǧ: ǧ:|k: k:|ǩ: ǩ:]|m [p p:| j j: ]|p c c:|r [d d:|g: g:|ǧ: ǧ:|k: k:|ǩ: ǩ:|č: č:|t t: | j j: ] |s [k k:|ǩ ǩ:|t t:]] ;


!! Short consonant cluster


!! Onset consonant or word boundary
OnSetC = [Cns|#] ;


!! following morpheme or word boundary
!! {{{
RBound = [#|%>] ; !!= * @CODE@ 
!! }}}

!! relative lengthening of vowel right context following vowel:vowel
! 0:õ <=> OnSetC _ :õ RelVowLengthening

!! {{{
RelVowLengthening = [(ʹ:|0:ʹ) [CCforPrecedingV2VV  %^V2VV:|CC2CforPrecedingVV %^V2VV:|Cns: (%{XC%}:) %^V2VV:|CC2CforPrecedingV2VV (a: i:) %^V2VV: |CC2CafterV2VVandUI %^V2VV: |XYY2XYCluster %^V2VV:] ] ; !!= * @CODE@
!RelVowLengthening = [(ʹ:|0:ʹ) [CCforPrecedingV2VV  %^V2VV:|Cns: (%{XC%}:| : :? ) %^V2VV:|CC2CforPrecedingV2VV (a: i:) %^V2VV: |CC2CafterV2VVandUI %^V2VV: |XYY2XYCluster %^V2VV:] ] ;
!! }}}

!! relative lengthening of vowel right context following vowel:vowel
!! 0:i <=> OnSetC _ :i RelVowLengtheningNOTi
!! {{{
RelVowLengtheningNOTi = [(ʹ:|0:ʹ) [CCforPrecedingV2VV  %^V2VV:|[CC2CforPrecedingV2VV|uCC2uCforPrecedingV2VV] %^V2VV: %^CC2C:|CC2CforPrecedingVV %^CC2C: ]] ; !!= * @CODE@
!! }}}





!! relative lengthening of vowel right context following vowel:vowel
!! 0:u <=> OnSetC _ :u RelVowLengtheningNOTu
!! {{{
RelVowLengtheningNOTu = [(ʹ:|0:ʹ) [CCforPrecedingV2VV  %^PenV2VV: %^CShort:|[iCC2iCforPrecedingV2VV|CC2CforPrecedingV2VV] %^V2VV: %^CC2C:|CC2CforPrecedingVV %^CC2C:]] ; !!= * @CODE@
!! }}}


!! possible triggers before VOWLower and VOWRaise
!! {{{
PrecedeVOWLowerRaise = [([%^VOWSH:|%^VOWXSH:] %^CLong:|[%^PenV2VV:|%^VOWXL:] %^CShort:)] ; !!= * @CODE@ 
!! }}}

!! possible triggers between stem and PALNo and PAL
BetweenStemAndPALNo = [( %^VV2V: |%^V2VV:) (%^VOWLower:|%^VOWRaise:) (%^PALÄ:|%^PALE:|%^PALẸ:)] ;
! [([%^St2XSt:|%^St2W:|%^XSt2St:|%^XSt2W:|%^W2St:|%^W2XSt:]|[%^VOWSH:|%^VOWXSH:] (%^CLong:)|[%^PenV2VV:|%^VOWXL:] (%^FRICG:) (%^CShort:)) (%^VOWLower:|%^VOWRaise:) (%^VOWFront:|%^VOWBack:) (%^VOWTense:|%^VOWLax:) (X3:)] ;

!! possible triggers before PALE PALÄ
BetweenStemAndPALAllo = [( %^VV2V: |%^V2VV:)] ;
![([%^St2XSt:|%^St2W:|%^XSt2St:|%^XSt2W:|%^W2St:|%^W2XSt:]|[%^VOWSH:|%^VOWXSH:] (%^CLong:)|[%^PenV2VV:|%^VOWXL:] (%^FRICG:) (%^CShort:)) ] ; 

BetweenStemAndHeight = [( %^VV2V: |%^V2VV:)] ;

!! possible triggers between VowHeight and word end
!! {{{
BetweenVowHeightAndMorph = [(%^VOWFront:|%^VOWBack:) (%^VOWTense:|%^VOWLax:) (X3:) ((%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:|%^PALNo:)] ; !!= * @CODE@ 
!! }}}

!! possible triggers between word end and consonant grade
BetweenStemAndConsGrade = [(%^V2VV:|%^VV2V:) (%^VOWRaise:|%^VOWLower:) (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:)] ;

PenBetweenStemAndConsGrade = [(%^PenV2VV:|%^PenVV2V:)] ;

!! neutral to vowel height and backness
!! {{{
NeutrHighBack = [(%^VOWLower:|%^VOWRaise:) (%^VOWFront:|%^VOWBack:)] ; !!= * @CODE@ 
!! }}}

!! neutral to vowel length
VNeutrGrade = [(%^VV2V: |%^V2VV: )];
!! neutral to consonant length
CNeutrGrade = [(%^CC2C: |%^C2CC: |%^C2CCC: |%^CC2CCC: |%^CCC2C: |%^CCC2CC: )] ;
!! neutral to vowel and consonant length
!! {{{
NeutrGrade = [VNeutrGrade |CNeutrGrade] ; !!= * @CODE@ 
!! }}}



!! * X3 = C Vx Cx Cx     C Vx Vy Cx ' Cx
!! * X2 = C Vx Vx Cx Cx  C Vx Vy Cx Cx
!! * X1 = C Vx Vx Cx     C Vx Vy Cx


!! !!!Rules
Rules 

!! !!Vowel shortening rules 
!! (tbw)



"Even syllabic verbs I, vowel shortening â:0"
!! __@RULENAME@__ - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
!! {{{
 â:0 <=> OnSetC _ â: (ʹ:|0:ʹ) [LongC|Cns:+] [%^VOWSH: %^CLong:|%^VOWXSH: (%^CLong:)] ;
         [Cns:+|:Cns+] _ [Cns|i:|n j|l m|s ǩ:|s t|š ǩ:|š m] (%^VOWSH: (%^CLong:)|%^VC:) %^RmVow: ;

 OnSetC _ â: u (ʹ:|0:ʹ) Cns: Cns:*  %^VV2V:  ;

!! }}}

"Even syllabic verbs I, vowel shortening e:0"
!! __@RULENAME@__ - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
 e:0 <=>
   OnSetC _ e: (ʹ:|0:ʹ) [LongC|Cns: :Cns*] %^VV2V:  ;
!! teevvad+V+Prt+4:
!€# teevv%^VV2V%^VOWRaise%>uš
!€0 t0ivv000uš
 OnSetC _ e: (ʹ:|0:ʹ) [LongC|Cns: :Cns*] [%^VOWSH: %^CLong:|%^VOWXSH: (%^CLong:)] ;
         [Cns:+|:Cns+] _ [Cns|i:|n j|l m|s ǩ:|s t|š ǩ:|š m] (%^VOWSH: (%^CLong:)|%^VC:) %^RmVow: ; 
         OnSetC _ e: (ʹ:|0:ʹ) Cns:+ Vow: Cns %^VOWSH: ; 
!! deriving cieʹǩǩes+N+Sg+Ill: ceäkˈksa
  OnSetC :Vow+ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] _  s: [%^VOWSH: %^CLong:| %^VOWXSH: %^CLong:]  %^VOWLower: (%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:)  %^RmVow: ; 

 OnSetC _ e: u (ʹ:|0:ʹ) Cns: Cns:*  %^VV2V:  ;


"Even syllabic verbs I, vowel shortening å:0"
!! __@RULENAME@__ - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
!! {{{
 å:0 <=> OnSetC _ å: (ʹ:|0:ʹ) [LongC|Cns: :Cns*] [%^VOWSH: %^CLong:|%^VOWXSH: (%^CLong:)] ;

 OnSetC _ å: u (ʹ:|0:ʹ) Cns: Cns:*  %^VV2V:  ;

!! }}}

"Even syllabic verbs I, vowel shortening õ:0"
!! __@RULENAME@__ - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
 õ:0 <=>
 OnSetC _ õ: (ʹ:|0:ʹ) [LongC|Cns: :Cns*] %^VV2V: ;
!€# mõõ0nn%^VV2V%^PAL%>e
!€0 m0õʹnn000e
!€# mõõ0nn%^VV2V%^VOWLower%^PAL%>e
!€0 m0âʹnn0000e
         OnSetC _ õ: (ʹ:|0:ʹ) Cns:+ Vow: [Cns|n j|l m|s ǩ:|s t|š ǩ:|š m]  %^VOWSH: ;

 OnSetC _ õ: u (ʹ:|0:ʹ) Cns: Cns:*  %^VV2V:  ;

"Even syllabic verbs I, vowel shortening u:0"
!! __@RULENAME@__ - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
!! {{{
 u:0 <=> OnSetC _ u: (ʹ:|0:ʹ) [C2CCafterVV2V|Cns: :Cns+] (i:) [%^VOWSH: %^CLong:|%^VOWXSH: (%^CLong:)] ;
         OnSetC _ u: (ʹ:|0:ʹ) Cns:+ Vow: [Cns|n j|l m|s ǩ:|s t|š ǩ:|š m] [%^VOWSH:|%^RmVow:] ;

!! }}}

"Even syllabic verbs I, vowel shortening i:0"
!! __@RULENAME@__ - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
 i:0 <=>
         OnSetC _ i: (ʹ:|0:ʹ) Cns:+ %^VV2V: ;
!!viikkâd+V+Ind+Prs+Pl3
!€# viikk%^VV2V%^VOWLower%^PAL%>e
!€0 v0eʹǩǩ0000e
 OnSetC _ i: (%ʹ:|0:%ʹ) [LongC|Cns: :Cns*] [%^VOWSH: %^CLong:|%^VOWXSH: (%^CLong:)] ;
         Cns _ %> ǥ a ; ! +Sg+Ill N_HÕʹPPI
         [Cns:+|:Cns+] _ [Cns|n j|l m|s ǩ:|s t|š ǩ:|š m] (%^VOWSH: (%^CLong:)|%^VC:) %^RmVow: ;
         OnSetC _ i: (ʹ:|0:ʹ) Cns:+ Vow: [Cns|n j|l m|s ǩ:|s t|š ǩ:|š m]  %^VOWSH: ; ! iilâsǩ:ilska

 OnSetC _ i: u (ʹ:|0:ʹ) Cns: Cns:*  %^VV2V:  ;


"Even syllabic verbs I, vowel shortening o:0"
!! __@RULENAME@__ - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
 o:0 <=> 
    OnSetC _ o: (ʹ:|0:ʹ) [LongC|Cns: :Cns*] %^VV2V: ;
!€# šoomm%^VV2V%^VOWRaise%>u
!€0 š0omm000u

 OnSetC _ o: u (ʹ:|0:ʹ) Cns: Cns:*  %^VV2V:  ;
!! ooumaž+N+Sg+Gen
!€# ooum0%^VV2V%^C2CCu
!€0 0oumm00u

         [Cns:+|:Cns+] _ [Cns|i:|n j|l m|s ǩ:|s t|š ǩ:|š m]  %^RmVow: ; 



"Even syllabic verbs I, vowel shortening a:0"
!! __@RULENAME@__ - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
 a:0 <=> OnSetC _ a: (ʹ:|0:ʹ) [LongC|Cns: :Cns*] %^VV2V: ;
!€# saa0kk%^VV2V%^PAL%>e
!€0 s0aʹǩǩ000e
 OnSetC _ a: (ʹ:|0:ʹ) [LongC|Cns: :Cns*] [%^VOWSH: %^CLong:|%^VOWXSH: (%^CLong:)] ;
         [Cns:+|:Cns+] _ [Cns|i:|n j|l m|s ǩ:|s t|š ǩ:|š m] (%^VOWSH: (%^CLong:)|%^VC:) %^RmVow: ; 
         OnSetC _ a: (%ʹ:|0:%ʹ) Cns:+ Vow: [Cns|n j|l m|s ǩ:|s t|š ǩ:|š m]  %^VOWSH: ; 
   OnSetC :Vow+ (%ʹ:|0:%ʹ) [LongC|Cns:+ - [Cns:* [l|n]]] _ i: %> Vow: ; 

 OnSetC _ a: u (ʹ:|0:ʹ) Cns: Cns:*  %^VV2V:  ;


"vowel to syllable mark a:ʼ"
!! __@RULENAME@__ - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
!! {{{
 a:ʼ <=> OnSetC :Vow+ (%ʹ:|0:%ʹ)  Cns:* [l|n]  _ i: %> Vow: ;
!! }}}


"ZERO to syllable mark 0:ʼ"
!! __@RULENAME@__ - used in zeeʹtt+N+Sg+Abe: zeeʹttʼtää
 0:ʼ <=>
     [ :n | :l ] (%^C2CC:) %> _ j ;
        s (ˈ:) s BetweenStemAndConsGrade (%^CCC2CC:) %>  _ s ; 
        t (ˈ:) t BetweenStemAndConsGrade (%^CCC2CC:) %>  _ t ; 
        k (ˈ:) k BetweenStemAndConsGrade (%^CCC2CC:) %>  _ k ; 
!€# ijss%>0sa
!€# ijss0ʼsa


"Even syllabic verbs I, vowel shortening ä:0"
!! __@RULENAME@__ - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
 ä:0 <=> OnSetC _ ä: Cns: %{XC%}: %^VV2V:  ;
!€# mään%{XC%}%^VV2V: %^C2CC%^VOWRaise%>u
!€0 m0ann000u
   OnSetC _ ä: (ʹ:|0:ʹ) [LongC|Cns: :Cns*|Cns:+] [%^VOWSH: %^CLong:|%^VOWXSH: (%^CLong:)] ;
         OnSetC _ ä: (ʹ:|0:ʹ) Cns:+ Vow: [Cns|i:|n j|l m|s ǩ:|s t|š ǩ:|š m] %^VOWSH: %^CLong: ;

 OnSetC _ ä: u (ʹ:|0:ʹ) Cns: Cns:*  %^VV2V:  ;




!! !!Vowel alternation rules

!! !VOWEL SHIFT

!! !RAISING

!! deriving +Ind+Prt+Sg1, +Ind+Prt+Sg2 +Ind+Prt+Pl3 in teevvad:tivvu

"Even syllabic verbs I, vowel raising o:u"
!! __@RULENAME@__ - Adding +Prt+Pl3 for ed verbs, Removing u: and i:  second element due to njeiddad njeiddu
 o:u <=>  OnSetC  o: _ (%ʹ:|0:%ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  (%^VOWSH: %^CLong:) %^VOWRaise: %> [e|u] ;
       OnSetC  :0 _ [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  (%^VV2V:) %^VOWRaise: RBound ;
!€# šoomm%^VV2V%^VOWRaise%>u
!€0 š0omm000u

          OnSetC o:0 _ (%ʹ:|0:%ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  (%^VOWSH: %^CLong:) %^VOWRaise: ((%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) %> [e|u] ;
          OnSetC 0:u _ (%ʹ:|0:%ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  (%^V2VV: %^CShort:) %^VOWRaise: (%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL: %> [e|u] ;
  OnSetC _ (u:) (%ʹ:|0:%ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  (%^VOWSH: %^CLong:|%^V2VV:) %^VOWRaise: ((%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) %> [e|u] ;


"Even syllabic verbs I, vowel raising å:o"
!! __@RULENAME@__ - Adding +Prt+Pl3 for ed verbs, Removing u: and i:  second element due to njeiddad njeiddu
 å:o <=>  OnSetC _ å:o (%ʹ:|0:%ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  %^VOWRaise: ; 
          OnSetC [å:o|0:o] _ (%ʹ:|0:%ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] VNeutrGrade  %^VOWRaise: ;
          OnSetC 0: _ (ʹ:|0:ʹ) CCforPrecedingV2VV %^V2VV: %^VOWRaise: ;
!€# k0åʹll%^V2VV%^VOWRaise%>in
!€0 kooʹll0000in
          OnSetC _ (u:) (%ʹ:|0:%ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  %^VOWRaise: ((%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) %> [e|u] ;



"Even syllabic verbs I, vowel raising e:i"
!! __@RULENAME@__ - Adding +Prt+Pl3 for ed verbs, Removing u: and i:  second element due to njeiddad njeiddu
 e:i <=>
!!
  OnSetC _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] %^VOWRaise: ;
!€# pess%^VOWRaise%>u
!€0 piss00u
  OnSetC [0:i] _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] %^V2VV: %^VOWRaise: ;
!€# p0ess%^V2VV%^VOWRaise%>už
!€0 piiss000už
  OnSetC :0 _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] %^VV2V: %^VOWRaise: ;
!€# teevv%^VV2V%^VOWRaise%>uš
!€0 t0ivv000uš
  OnSetC  [e:|e:0|0:i] _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  PrecedeVOWLowerRaise %^VOWRaise: ;
  OnSetC _ (u:) (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] PrecedeVOWLowerRaise %^VOWRaise: ;
! diphthong raising with e:i at onset - deriving +Ind+Prt+Pl3 in reäkkad > riâkku, veârrad > viõrru
  _ [ä:|â: ] (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  PrecedeVOWLowerRaise  %^VOWRaise: ;



"Even syllabic verbs I, vowel raising â:õ" 
!! __@RULENAME@__ - Adding +Prt+Pl3 for ed verbs, Removing u: and i:  second element due to njeiddad njeiddu
 â:õ <=>
          OnSetC u   _      (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+|Cns:+ :%ˈ Cns:+| Cns:+ (%ˈ:|:%ˈ) :Cns] (%{XC%}:) %^VOWRaise:  ;
!€# ruâk0k%^VOWRaise%^CC2CCC%>u
!€0 ruõkˈk000u
  OnSetC â:  _      (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  (%^VOWSH: %^CLong:) %^VOWRaise: ;
          OnSetC â:0 _      (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  (%^VOWSH: %^CLong:) %^VOWRaise: (%^PAL:) ;
                 0:õ _      (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]           VNeutrGrade %^VOWRaise: ;
          OnSetC     _ (u:) (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]   %^VOWRaise: (%^PAL:) ;
          OnSetC u   _      (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+|Cns:+ :%ˈ Cns:+] ([%^VOWSH:|%^VOWXSH:] %^CLong:) %^VOWRaise: ;
          OnSetC e:  _      (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+|Cns:+ 0:%ˈ Cns:+]  (%^VOWSH: %^CLong:) %^VOWRaise: ;



"Even syllabic verbs I, diphthong raising beginning with u ä:å"
!! __@RULENAME@__ - deriving +Ind+Prt+Pl3 in kuâs'sad > kuõs'su, kuärŋŋad kuårŋŋu
!! {{{
 ä:å <=> OnSetC u  _ (%ʹ:|0:%ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^VOWSH: %^CLong:) %^VOWRaise: %> u ;
!! }}}


"Even syllabic verbs I, diphthong raising beginning with e:i ä:â"
!! __@RULENAME@__ - deriving +Ind+Prt+Pl3 in reäkkad > riâkku, veârrad > viõrru
!! {{{
 ä:â <=> OnSetC e:  _ (%ʹ:|0:%ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+|Cns:+ 0:%ˈ Cns:+]  PrecedeVOWLowerRaise %^VOWRaise: %> u ;
!! }}}



"Even syllabic verbs I, diphthong raising beginning with e:i ä:e"
!! __@RULENAME@__ 
 ä:e <=> OnSetC [u|e:i]  _ :ʹ [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+|Cns:+ 0:%ˈ Cns:+]  PrecedeVOWLowerRaise %^VOWRaise: %^PALE: %^PAL: ;
!€# čuä0ckk%^CONSH%^PALE%^PAL%>es
!€# čueʹcǩ00000es

"Even syllabic verbs I, diphthong raising beginning with e:i ä:ẹ"
!! __@RULENAME@__ 
 ä:ẹ <=> OnSetC [u|e:i]  _ :ʹ [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+|Cns:+ 0:%ˈ Cns:+]  PrecedeVOWLowerRaise %^VOWRaise: %^PALẸ: %^PAL: ;


"Even syllabic verbs I, diphthong raising beginning with ä:a u"
!! __@RULENAME@__ - deriving läullad > laullum
 ä:a <=> [ ä: | OnSetC (0:) ] _ Cns: [%{XC%}: |Cns:*] (%^VV2V: | %^V2VV:) %^VOWRaise: ;
!€# mään%{XC%}%^VV2V%^VOWRaise%^C2CC%>u
!€0 m0ann000u
!€# äldd%^VOWRaise%>u
!€0 aldd00u
!€# 0äldd%^V2VV%^VOWRaise%^CC2C%>u
!€0 aald0000u
 [ ä: | OnSetC (0:) ] _ (%ʹ:|0:%ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+|Cns:+ 0:%ˈ Cns:+] DummiesBeforeVowelHeight %^VOWRaise: ;
 OnSetC _ ä: (%ʹ:|0:%ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+|Cns:+ 0:%ˈ Cns:+] DummiesBeforeVowelHeight %^VOWRaise: ;
!€# čääʹcc%^VOWRaise%^CC2C%>id
!€0 čaaʹʒʒ000id
  OnSetC (0:a)  _ u  (%ʹ:|0:%ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+|Cns:+ 0:%ˈ Cns:+] (%^VOWSH: %^CLong:) %^VOWRaise: %>  u ;
         OnSetC  ä:    _    (%ʹ:|0:%ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+|Cns:+ 0:%ˈ Cns:+] %^VOWSH: %^CLong:  %>  a ; 
         OnSetC  ä:    _    (%ʹ:|0:%ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+|Cns:+ 0:%ˈ Cns:+] %^VOWRaise: ;
         OnSetC        _ ä: (%ʹ:|0:%ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+|Cns:+ 0:%ˈ Cns:+] %^VOWRaise: ; 


!! !LOWERING 

! Where: VIGGAD +Ind+Prs+Sg3, +Ind+Prs+Pl3
!        TIETTED 
! long and single vowels



"Even syllabic verbs I, vowel lowering o:å"
!! __@RULENAME@__ - deriving +Ind+Prs+Sg3, +Ind+Prs+Pl3 in poorrâd
 o:å <=> OnSetC    _ o:å (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  %^VOWLower:  ;
         OnSetC o: _     (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^VV2V:) %^VOWLower:  ;
!€# poo0rr%^V2VV%^VOWLower%^PAL%>e
!€0 p0åʹrr0000e
         OnSetC    _     (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]   %^VOWLower: ;
! juʹrdded+V+Cond+Sg1: joordčem
         OnSetC 0:å _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] %^V2VV:   %^VOWLower: ;
! juʹrdded > joordam
         OnSetC    _ (u:|i:) (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] %^VOWLower: ;
         OnSetC    _ ((ʹ:) (v:u|j:i)) (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] %^VOWLower: ;



"Even syllabic verbs I, vowel lowering i:e"
!! __@RULENAME@__ - deriving +Ind+Prs+Sg3, +Ind+Prs+Pl3 in viǯǯâd
 i:e <=>
   OnSetC _ i:e (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^V2VV:)  %^VOWLower: ;
!€# v0iǯǯ%^V2VV%^VOWLower%>as 
!€0 veeǯǯ000as
         OnSetC _ [õ:â|õ:ä|â:ä|e:ä|e:â] (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (e: s:)  %^VOWLower: ;
           OnSetC i: _   (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^VV2V:) %^VOWLower: ;
           OnSetC  _   (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  %^VOWLower: ;
! juʹrdded+V+Cond+Sg1: joordčem
!           OnSetC 0:e _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] V2VV:  %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) RBound ;
           OnSetC 0: _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] %^V2VV:  %^VOWLower: ;
!€# v0iǯǯ%^V2VV%^VOWLower%>as
!€0 veeǯǯ000as

! juʹrdded > joordam
           OnSetC _ (u:|i:) (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] %^VOWLower: ;
           OnSetC _ ((ʹ:) (v:u|j:i)) (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] %^VOWLower: ;



"Even syllabic verbs I, vowel lowering a:ä"
!! __@RULENAME@__ - deriving +Ind+Prs+Sg3, +Ind+Prs+Pl3 in viǯǯâd
!! {{{
 a:ä <=> OnSetC    _ a:ä (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) RBound ;
         OnSetC a: _     (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^VOWSH: %^CLong:)                      %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) RBound ;
         OnSetC    _     (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^VOWSH: %^CLong:)                      %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) RBound ;
! juʹrdded+V+Cond+Sg1: joordčem
         OnSetC 0:ä _    (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^PenV2VV: %^CShort:)  %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) RBound ;
! juʹrdded > joordam
         OnSetC     _ (u:|i:) (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^PenV2VV: %^CShort:) %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) RBound ;
         OnSetC     _ ((ʹ:) (v:u|j:i)) (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^VOWSH: %^CLong:) %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) RBound ;
!! }}}


!"Even syllabic verbs I, vowel raising a:ä"
!! __@RULENAME@__ - Adding +Prt+Pl3 for ed verbs, Removing u: and i:  second element due to njeiddad njeiddu
! a:ä <=> OnSetC a: _ (%ʹ:|0:%ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  (%^VOWSH: %^CLong:) %^VOWRaise: %> [e|u] ; 
!         OnSetC a:0 _ (%ʹ:|0:%ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  (%^VOWSH: %^CLong:) %^VOWRaise: ((%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) %> [e|u] ;
!         OnSetC 0:ä _ (%ʹ:|0:%ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  (%^PenV2VV: %^CShort:) %^VOWRaise: (%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL: %> [e|u] ;
!  OnSetC _ (u:) (%ʹ:|0:%ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  (%^VOWSH: %^CLong:|%^PenV2VV:) %^VOWRaise: ((%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) %> [e|u] ;



"Even syllabic verbs I, vowel lowering u:o"
!! __@RULENAME@__ - deriving +Ind+Prs+Sg3, +Ind+Prs+Pl3 in uʹvdded
 u:o <=>  OnSetC _ :o (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^PenV2VV: %^CShort:)  %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) RBound ;
        OnSetC u: _   (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+|r d d:] VNeutrGrade   %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) RBound ;
       OnSetC (0:o) _  (ʹ:| 0:ʹ) [CNoV: Cns:*|Cns:+ (%ˈ:|:%ˈ) Cns:+] VNeutrGrade %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) RBound ; 
! uʹvdded+V+Ind+Pl3: ouʹdde 
 OnSetC (0:) _ [(ʹ:) v: (0:ʹ)|%{VU%}:u (ʹ:|0:ʹ)] [d:+|l:+|r:+|ʒ:+] (%^V2VV:) %^VOWLower: (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) [%^CCC2C:|%^CCC2CC:] ;
!€# 0uvdd%^V2VV%^VOWLower%^CCC2C%>če
!€# ooud00000če

!! vuʹvll+N+Sg+Ill: voulla
 OnSetC  _ (ʹ:)  v:u (0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^PenV2VV: %^CShort:) %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) RBound ;
 OnSetC (0:) _ (ʹ:) [CNoV: Cns:*|Cns:+ (%ˈ:|:%ˈ) Cns:+] VNeutrGrade %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) RBound ;
!! stuuʹl+N+Sg+Ill: stooula
 OnSetC 0: _ [ %{VU%}:u (ʹ:|0:ʹ) [d|l|r|ʒ]]  %^VOWLower: ;
! juʹrdded+V+Cond+Sg1: joordčem
 OnSetC 0: _ (ʹ:|0:ʹ) [CNoV: Cns:*|Cns:+ (%ˈ:|:%ˈ) Cns:+]  %^PenV2VV:  %^CShort:    %^VOWLower: ; 
! juʹrdded > joordam




"Even syllabic verbs I, vowel lowering õ:â "
!! __@RULENAME@__ - deriving +Ind+Prs+Sg3, +Ind+Prs+Pl3 in viǯǯâd
 õ:â <=>
       i: _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] %^VOWLower: [ RBound |%^PAL:|%^PALNo:] ;
!!riõkkâd+V+Ind+Prs+Sg3 'to whip'
!€# riõkˈk%^VOWLower
!€0 reâkˈk0
       õ: _  (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^VV2V:) %^VOWLower: ;
!€# mõõ0nn%^VV2V%^VOWLower%^PAL%>e
!€0 m0âʹnn0000e
      _ õ: (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  %^VOWLower: ;
!€# mõõnn%^VOWLower%^CC2C%>as
!€0 mâân0000as

! juʹrdded > joordam
   OnSetC _ (u:|i:) (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^PenV2VV: %^CShort:) %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) RBound ; 
   OnSetC _ ((ʹ:) (v:u|j:i)) (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^VOWSH: %^CLong:) %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) RBound ;
! diphthong opening after u
        u _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] %^VOWLower: [ RBound |%^PAL:|%^PALNo:] ;
!! kuõskkâd+V+Ind+Prs+Sg3
!€# kuõskk%^VOWLower
!€# kuâskk0


! diphthongs

"Even syllabic verbs I, diphthong opening after u å:ä"
!! __@RULENAME@__ - deriving +Ind+Prs+Sg3, in kuåccâd kuäʹcce
!! = a>ä lowering clockwise
 å:ä <=> u _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] %^VOWLower: ;
!€# suåkk%^VOWLower%^CC2C
!€0 suäǥǥ00


"Even syllabic verbs I, diphthong opening after i:e e:ä"
!! __@RULENAME@__ - deriving +Ind+Prs+Sg3, in čiõkkâd
!! {{{
 e:ä <=> [i:|u] _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (e: s:) (%^PenV2VV: %^CShort:|%^VOWSH: %^CLong:|%^VOWXSH: %^CLong:) %^VOWLower: (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) [RBound|%^RmVow:|%^VC:] ;
! vueʹlǧǧed+V+Ind+Prs+Pl3: vuäʹlǧǧa
!! }}}        

"Vowel in second syllable e:a"
!! __@RULENAME@__ - deriving cieʹǩǩes+N+Sg+Gen: ceäkˈkaz
!! {{{
 e:a <=> OnSetC :Vow+ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] _ s: [%^VOWSH: %^CLong:|%^VOWXSH: %^CLong:] %^VOWLower: (%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) %^VC: ;
!! }}}        

"Even syllabic verbs I, diphthong opening i:e â:ä"
!! __@RULENAME@__ - deriving +Ind+Prs+Sg3, in čiõkkâd
!! {{{
 â:ä <=> i: _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^PenV2VV: %^CShort:) %^VOWLower: (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) RBound ;
!! }}}


"Even syllabic verbs I, diphthong opening after i:e e:â"
!! __@RULENAME@__ - deriving +Ind+Prs+Sg3, in pi%{EÂ%}ʹǩǩ:peâkka
!! {{{
 %{EÂ%}:â <=> i: _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^PenV2VV: %^CShort:) %^VOWLower: (X3:) %^PALNo:  RBound ;
!! }}}

"Even syllabic verbs I, diphthong opening after u e:â"
!! __@RULENAME@__ - deriving vueʹlǧǧed+V+Ind+Prs+Pl3: vuâʹlǧǧe
!! {{{
 e:â <=> [u|i:] _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (e: s:) (%^PenV2VV: (%^FRICG:) %^CShort:|%^VOWSH: (%^CLong:)) %^VOWLower: %^VOWBack: ;
!! }}}

!! !Vowel backing

"Even syllabic verbs I, diphthong opening after u e:õ"
!! __@RULENAME@__ - deriving vueʹlǧǧed+V+Ind+Prt+Sg1: vuõʹlǧǧem
!! {{{
 e:õ <=> u _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^PenV2VV: %^CShort:|%^VOWSH: (%^CLong:)) %^VOWBack: ;
!! }}}

!! !Vowel Palatalization
"diphthong allophonic realization in palatalization u å:e"
!! __@RULENAME@__ - deriving 
 å:e <=> u _ :ʹ  [Cns:+|Cns:+ 0:%ˈ Cns:+] BetweenStemAndPALAllo %^PALE: %^PAL: ;
!€# puå0v0v%^PALE%^PAL%^CC2CCC%>e
!€# pueʹvˈv0000e

!! !Vowel Palatalization
"diphthong allophonic realization in palatalization u å:ẹ"
!! __@RULENAME@__ - deriving 
 å:ẹ <=> u _ :ʹ  [Cns:+|Cns:+ 0:%ˈ Cns:+] BetweenStemAndPALAllo %^PALẸ: %^PAL: ;
!€# suå0kk%^PALẸ%^PAL%^CC2C
!€0 suẹʹjj000

!! !Vowel Lowering and Fronting

"Even syllabic verbs I, diphthong opening õ:ä after i:e"
!! __@RULENAME@__ - deriving +Ind+Prs+Sg3, in čiõkkâd
 õ:ä <=>
 i:e _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  BetweenStemAndHeight %^VOWLower: %^PALÄ: ;
 u _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  BetweenStemAndHeight %^VOWLower: %^PALÄ: ;




!! !SECONDARY FRONTING


 
"Even syllabic verbs I, secondary vowel fronting with PAL u õ:e"
!! __@RULENAME@__ - deriving +Ind+Prs+Pl3 in VIQQAD: kuõskkâd >kueʹsǩǩe
 õ:e <=>
  u _ (%ʹ:|0:%ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] %^VOWLower: %^PALE: ; ! deriving +V+Inf in vueʹlǧǧed

         i _ (%ʹ:|0:%ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] %^VOWLower: %^PALE: ; 
! - deriving +Ind+Prs+Sg3, in kuõskkâd kueʹsǩǩe CHECK THIS



! "Even syllabic verbs I, secondary u > v PAL"
! __@RULENAME@__ - deriving +V+Inf in TIETTED: uudd >uʹvdded
! u:v <=> u _ (%ʹ:|0:%ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] %^VOWFront: (%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL: %> ;


!! !RELATIVE VOWEL LENGTHENING

!! vowel lengthening and consonant shortening, %^PenV2VV and %^CShort 


"Even syllabic verbs I, relative vowel lengthening 0:â"
!! __@RULENAME@__ - deriving +V+Inf in TIETTED: uudd > uʹvdded
!! {{{
 0:â <=> OnSetC _ :â RelVowLengthening ;
! Cns: _ â [(u:|i:) (%ʹ:|0:%ʹ) [u l l:|Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]]  %^PenV2VV: (%^CShort:) [%>|#|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:] ;
!Distinguishing mainstam and maainstam
      Cns: _ â (u:|i:) (ʹ:|0:ʹ)  Cns: %{Â1%}: Cns:+  %^PenV2VV: ;
!      OnSetC _ â [(ʹ:|0:ʹ) [u l l:|Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]]  %^VOWXL: ;
! Even syllabic verbs I, relative vowel lengthening and lowering 0:â
!simultaneous lengthening and lowering: juʹrdded > joordam juʹrdded+V+Ind+Prs+Sg1
!   OnSetC _ õ:â (ʹ:|0:ʹ) Cns:+ %^PenV2VV: %^CShort: %^VOWLower: ;
!! }}}


"Even syllabic verbs I, relative vowel lengthening 0:e"
!! __@RULENAME@__ - deriving +V+Inf in TIETTED: uudd > uʹvdded
 0:e <=> OnSetC _ e RelVowLengthening ;
!€# čââʹlmteʹm%^V2VV
!€0 čââʹlmte0m0
        OnSetC _ e (ʹ:|0:ʹ) Cns:+ %^V2VV: ;
!€# p0ess%^V2VV
!€0 peess0
         OnSetC _ i:e RelVowLengthening %^VOWLower: ;
!         OnSetC _ i:e RelVowLengthening %^VOWLower: ;
!€# v0iǯǯ%^V2VV%^VOWLower%>as 
!€0 veeǯǯ000as


!Distinguishing mainstam and maainstam
      Cns: _ e (u:|i:) (ʹ:|0:ʹ)  Cns:+ (%{Â1%}:) Cns:+  %^V2VV: ; 
!€# n0eiʹbb%^V2VV%^CC2C
!€0 neeiʹb000
      [ n | š ] _ e (ʹ:|0:ʹ) ǩ ǩ %^V2VV: ;
!€# kaađn0e0ǩǩ%^V2VV%^PAL
!€0 kaađneeʹǩǩ00

"Even syllabic verbs I, relative vowel lengthening 0:å"
!! __@RULENAME@__ - deriving
!! {{{
 0:å <=>
!! - deriving påll+N+Sg+Gen: pååll
   OnSetC _ :å RelVowLengthening RBound ;
!Distinguishing mainstam and maainstam
!Vowel lengthening in INDPRSSG3 and IMPSG2 0:å kååʹmmerd, maainâst
      Cns: _ å (u:|i:) (ʹ:|0:ʹ)  Cns:+ [%{Â1%}:|%{A1%}:|%{E1%}:] Cns:+  [%^PenV2VV:|%^IMPSG2:|%^INDPRSSG3:] ;
!! }}}

"Even syllabic verbs I, relative vowel lengthening 0:õ"
!! __@RULENAME@__ - deriving +V+Inf in TIETTED: uudd > uʹvdded
 0:õ <=>
!! âʹlǧǧ+N+Pl+Gen: õõʹlji
     OnSetC _ :õ RelVowLengthening ;
!Distinguishing mainstam and maainstam
      Cns: _ õ (u:|i:) (ʹ:|0:ʹ)  Cns: %{Â1%}: Cns:+  %^PenV2VV: ; 

   OnSetC _ õ (ʹ:|0:ʹ) Cns:+ %^V2VV: ; 
!€# v0õrr%^V2VV%^CC2C
!€0 võõr000
!€# s0õll%^V2VV%^CC2C
!€0 sõõl00
!€# mättʼt0õss%^V2VV%^CC2C
!€0 mättʼtõõzz00


"Even syllabic verbs I, relative vowel lengthening 0:ä"
!! __@RULENAME@__ - deriving +V+Inf in TIETTED: uudd > uʹvdded
 0:ä <=> OnSetC _ ä (u|i) (ʹ:|0:ʹ) Cns:+ %^V2VV: ;
! OnSetC _ ä  (ʹ:|0:ʹ) Cns:+ %^V2VV: (%^CC2C:) RBound ;
!€# 0äldd%^V2VV%^CC2C
!€0 ääld000
!€# j0äuʹrr%^V2VV%^CC2C
!€0 jääuʹr000
 OnSetC _ [å:ä|ä|a:ä] RelVowLengthening ; 
!Distinguishing mainstam and maainstam
!- kååʹmmerd, maainâst Vowel lengthening in INDPRSSG3 and IMPSG2
     _ ä (i) (ʹ:|0:ʹ) Cns:+ [%{E1%}:|%{A1%}:|%{Â1%}:] Cns:+ [%^PenV2VV:|%^IMPSG2:|%^INDPRSSG3:] ;




"Even syllabic verbs I, relative vowel lengthening 0:a"
!! __@RULENAME@__ - deriving +V+Inf in MAINSTED: maainstam, mainstam
 0:a <=>
  OnSetC _ :a [RelVowLengthening]  ;
!€# 0äldd%^V2VV%^VOWRaise%^CC2C%>už
!€0 aald00000už
!€# dur0ak%^V2VV
!€0 duraak0

!Distinguishing mainstam and maainstam
  OnSetC _ a  i  (ʹ:|0:ʹ) Cns: [ %{Â1%}: | %{A1%}: | %{E1%}:] Cns:+  %^PenV2VV: ;
!€# m0ain%{Â1%}st%^PenV2VV%^IMPSG2
!€0 maainâst0
!   [ b | ʒ | (ǯ) ǯ | (g) g | i b | i n |(k) k |l|m |(n) n | n v|r g|r| r t | u d|u l| u n | u ŋ | u r] _ s t (%^PenV2VV:) %^IMPSG2: ;
!   [ ʹ đ đ | ʹ ǩ |ʹ l|ʹ l ǩ |ʹ m m |ʹ p] _ s t %^IMPSG2: ;

   Cns: _ a  i  (ʹ:|0:ʹ) Cns: %{Â1%}: Cns:+  %^PenV2VV: ; 
   Cns: _ a  i  Cns: %{Â1%}: Cns:+  %^PenV2VV: ;
   Cns: _ a (u:|i:) (ʹ:|0:ʹ)  Cns:+ [%{Â1%}:|%{A1%}:|%{E1%}:] Cns:+  [%^PenV2VV:|%^IMPSG2:|%^INDPRSSG3:] ; 




"Even syllabic verbs I, relative vowel lengthening 0:o"
!! __@RULENAME@__ -  simultaneous lengthening and lowering: juʹrdded > joordam juʹrdded+V+Ind+Prs+Sg1
 0:o <=>
!!
   OnSetC _ [:o  RelVowLengthening | u:o [%{VU%}:u|(ʹ:) v:] (ʹ:|0:ʹ) [d|l|r|ʒ] (%^V2VV:) %^VOWLower: | :o  (ʹ:) v:  (0:ʹ) [d d:|l l:|r r:|ʒ ʒ:] %^V2VV:  %^VOWLower:] ;
!€# 0uvdd%^V2VV%^VOWLower%^CCC2C%>če
!€# ooud00000če

  OnSetC _ å:o  (ʹ:|0:ʹ) CCforPrecedingV2VV %^V2VV: %^VOWRaise: ;
!€# k0åʹll%^V2VV%^VOWRaise%>in
!€0 kooʹll0000in
   OnSetC _ o (ʹ:|0:ʹ) Cns:+ [ %^V2VV: ((%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:|%^PALNo:) %^CC2C: ] ; 
!! - deriving toll+N+Sg+Gen: tool
!€# t0oll%^V2VV%^CC2C
!€0 tool000
!€# t0o0ll%^V2VV%^PAL%^CC2C%>e
!€0 tooʹl00000e

!2013-09-13!      OnSetC _ o (u:|i:) (ʹ:|0:ʹ) [[Cns:+|Cns:+ ((%ˈ:|:%ˈ)|:%ˈ) Cns:+]|Cns: %{Â1%}: Cns:+] [%^PenV2VV:|%^VOWXL:] ;


"Even syllabic nouns I, relative vowel lengthening i"
!! __@RULENAME@__ - ǩiđđ:ǩiiđ
 0:i <=> OnSetC _ :i RelVowLengthening  ;
  OnSetC _ i  (ʹ:|0:ʹ) CNoJ: [ %{Â1%}: | %{A1%}: | %{E1%}:] Cns:+  %^PenV2VV: ;
!! pirsted+V+Ind+Prs+ConNeg
!€# p0ir%{Â1%}st%^PenV2VV%^IMPSG2
!€0 piirâst00
   OnSetC _ e:i [Cns:+|Cns:+ ((%ˈ:|:%ˈ)|:%ˈ) Cns:+] %^V2VV: %^VOWRaise: ;
!€# p0ess%^V2VV%^VOWRaise%>už
!€0 piiss000už
   # Cns:* _ i (ʹ:|0:ʹ) Cns:+ [ %^V2VV: ((%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:|%^PALNo:) %^CC2C:] ;
!€# ǩ0iđđ%^V2VV%^CC2C
!€0 ǩiiđ00



"Even syllabic nouns, relative vowel extra lengthening u not followed by v"
!! __@RULENAME@__ - declension of nouns kunn > kuun
!! {{{
 0:u <=> OnSetC _ [o:u|u] Cns: Cns:* %^V2VV: ((%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:|%^PALNo:) %^CC2C: ;
 OnSetC _ [o:u|u] RelVowLengtheningNOTu ;
           Cns: _  :u (%ʹ:|0:%ʹ)  CNoV: %{Â1%}: Cns:+   %^PenV2VV: [%>|#|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:] ;
!! }}}
! (ʹ:|0:ʹ) [[s: s:|n n:] %^VOWXL:|CNoV: Cns:+ %^PenV2VV: %^CShort:] RBound ;
! relative lengthening of u
!  Cns: _ u: [(%ʹ:|0:%ʹ) [CNoV:+|CNoV:+ (%ˈ:|:%ˈ) Cns:+]] [%^PenV2VV:|%^VOWXL:] (%^CShort:) [%>|#|(%^VOWRaise:) (%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:] ;
!OnSetC _ o:u (%ʹ:|0:%ʹ) Cns:+ %^PenV2VV: %^CShort: %^VOWRaise: (%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL: %> e ;
!OnSetC _ o:u (%ʹ:|0:%ʹ) Cns:+ %^PenV2VV: %^CShort: %^VOWRaise: ;


!! simultaneous lengthening and raising, hmm: xfst ordering might be easier



!! !VOWEL DUMMY LOSS


!! !SEMI VOWELS

 
"Even syllabic nouns, for j>i"
!! __@RULENAME@__ - sijdd > siid This will need a special extra-lengthening rule
 j:i <=> OnSetC i: (%{ʹ%}:) _ (ʹ:) [ d d:|s s:] BetweenStemAndConsGrade %^XYY2VY: ;
!€# ijss%^VOWXL%^CShort%>âs
!€0 iis0000âs
  Vow:Vow ʹ: _ j: %^PALNo:  %^CC2C: # ;
!€# sââʹjj%^PALNo%^CC2C
!€0 sââ0i00

"Even syllabic verbs,  for v>u"
!! __@RULENAME@__ - uvdd > ouʹdde
 v:u <=> OnSetC (0:o) u: (ʹ:) _ (0:ʹ) [ʒ ʒ:|d d:|l l:|r r:] BetweenStemAndConsGrade [%^CCC2C: |%^CCC2CC:] ;
!€# uv0dd%^PAL%^CCC2C
!€0 uuʹd000
!€# uvdd%^VOWLower%^CCC2CC
!€0 oudd00

 "Even syllabic nouns, for h>u"
!! __@RULENAME@__ - luhtt  s s: ...
!! {{{
 h:u <=> V: (%{ʹ%}:) _ (ʹ:|0:ʹ) [s s:|š š:|t t:|c c:|č č:] [%^PenV2VV: %^CShort:|%^VOWXL:|%^VOWSH: %^CLong:  %^VOWLower: (%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:] ;
!! }}}

"trisyllabic verbs and doer derivations, i>j"
!! __@RULENAME@__ -
!! {{{
 i:j <=> _ %>: Vow ;
!! }}}

!! !VOWELS TENSE vs LAX 2012-11-28


"Even-syllabic nouns, for â:e"
!! __@RULENAME@__ -  ǩiâpp:ǩieʹpˈpe
 â:e <=> i _ :ʹ [Cns:+|Cns:+ 0:%ˈ Cns:+] BetweenStemAndPALAllo %^PALE: %^PAL: ;
!! +N+Sg+Ill
!€# miâ0rr%^PALE%^PAL%>e
!€0 mieʹrr000e
!€# piâ0ss%^PALE%^PAL
!€0 pieʹss00
      u _ (:ʹ) [Cns:+|Cns:+ 0:%ˈ Cns:+] BetweenStemAndPALAllo %^PALE: %^PAL: ;
!€# kuâ0ll%^PALE%^PAL
!€0 kueʹll00

!! !VOWEL and ZERO ALTERNATION


"Realization for %{E1%} in e "
!! __@RULENAME@__ - kååʹmmerd
!! {{{
 %{E1%}:e <=> _ (ʹ:|0:ʹ) Cns:+ (%^PenV2VV:) %^IMPSG2: ;
!! }}}

"Realization for %{A1%} in a " 
!! __@RULENAME@__ - kåʹmrded, vuejat, maainâst
!! {{{
 %{A1%}:a <=> _ (ʹ:|0:ʹ) Cns:+ (%^PenV2VV:) %^IMPSG2: ;
              _ (ʹ:|0:ʹ|ʼ:) [i|Cns:+] (%^PenV2VV:) %^INDPRSSG3: ;
!! }}}

"Realization for %{Â1%} in â " 
!! __@RULENAME@__ - maainâst
 %{Â1%}:â <=> Cns _ Cns:+ (%^PenV2VV:) %^IMPSG2: ;
!€# m0ain%{Â1%}st%^PenV2VV%^IMPSG2
!€0 maainâst00



"Realization for %{E1%} in a" 
!! __@RULENAME@__ - 
!! {{{
 %{E1%}:a <=> _ (ʹ:|0:ʹ) [i|Cns:+] (%^PenV2VV:) %^INDPRSSG3: ;
!! }}}

"Realization for %{Â1%} in a" 
!! __@RULENAME@__ - 
 %{Â1%}:a <=> _ (ʹ:|0:ʹ|ʼ:) [i|Cns:+] (%^PenV2VV:) %^INDPRSSG3: ;
!€# m0ain%{Â1%}st%^PenV2VV%^INDPRSSG3
!€0 maainast00
         
!! !THE NON-ORTHOGRAPHIC SYLLABLE 


"Loss of ʼ when preceded by vowel"
!! __@RULENAME@__ - This is a temporary solution to  "ʼ" in võʹllʼjed
!! {{{
 ʼ:0 <=> :Vow _ ; 
!! }}}

"%{A1%}:ʼ when subseqent syllable has vowel v"
!! __@RULENAME@__ - This is a temporary solution to  "ʼ" in võʹllʼjed 2013-08-29
!! {{{
%{A1%}:ʼ <=> [l l|n n] _ i: %> Vow: ; 
!! }}}

!! !PALATALIZATION


!2013-09-11!"Even-syllabic verbs I, Palatalization mark between V and C"
!2013-09-11!!! __@RULENAME@__ - used +Ind+Prs+Pl3, +Ind+Prt+Pl3
!2013-09-11! %{ʹ%}:ʹ <=> OnSetC (Vow:0|:Vow) Vow:Vow _ [Cns:Cns Cns:*|Cns:Cns+ (%ˈ:|:%ˈ) Cns:+|Cns:Cns+ 0:%ˈ Cns:+] (%^VOWSH: (%^CLong:)|%^VOWTense:|%^PenV2VV: %^CShort:) (%^VOWLower:|%^VOWRaise:) (%^VOWFront:|%^VOWTense:0) (X3:) (%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL: RBound ;



"0:ʹ as transfer from left of v:u"
!! __@RULENAME@__ - used +Ind+Prs+Pl3, uʹvdded+V+Ind+Prs+Pl3: ouʹdde
0:ʹ <=>      v:u _ [ʒ ʒ:|d d:|l l:|r r:]       BetweenStemAndPALNo  %^PAL: [%^CCC2C:|%^CCC2CC:] ;
!€# uv0dd%^PAL%^CCC2C
!€0 uuʹd000
 
         ʹ:0 v:u _ [ʒ ʒ:|d d:|l l:|r r:]       BetweenStemAndPALNo  ( %^PAL:) RBound ;
              h:u _ [c c:|č č:|s s:|š š:|t t:]  BetweenStemAndPALNo  %^PAL: ;
             Vow: _ [Cns:+|Cns:+ (%ˈ:|0:%ˈ) Cns:+] (i:) BetweenStemAndPALNo  %^PAL: ;
!€# saa0kk%^VV2V%^PAL%>e
!€0 s0aʹǩǩ000e
!€# mõõ0nn%^VV2V%^VOWLower%^PAL%>e
!€0 m0âʹnn0000e
!€# kuâ0ll%^PALE%^PAL
!€0 kueʹll00


!! The left context allows for:
!! Vowel shortening, and j/v > Vow 



 
"Even-syllabic verbs I, Palatalization of g:ǧ and k:ǩ"
!! __@RULENAME@__ -
 Cx:Cy <=> Vow: (ʹ:|0:ʹ) (Cns:)     _ Cx: (%^VV2V:|%^VOWSH: (%^CLong:)) NeutrHighBack (X3:) (%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL: RBound ;
           Vow: (ʹ:|0:ʹ) (Cns:) Cx: _     (%^VV2V:|%^VOWSH: (%^CLong:)) NeutrHighBack (X3:) (%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL: RBound ;
!with insertion of palatal before
           Vow: (ʹ:|0:ʹ) (Cns:)     _ Cx: (%^VV2V:|%^VOWSH: (%^CLong:)) NeutrHighBack (X3:) (%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL: RBound ;
           Vow: (ʹ:|0:ʹ) (Cns:) Cx: _     (%^VV2V:|%^VOWSH: (%^CLong:)) NeutrHighBack (X3:) (%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL: RBound ;

                where Cx in (g k)
                      Cy in (ǧ ǩ)
                matched ;
!€# jo0kk%^PAL%>e
!€0 joʹǩǩ00e
!€# saa0kk%^VV2V%^PAL%>e
!€0 s0aʹǩǩ000e

"Even-syllabic nouns I, Depalatalization of ǧ and weakening of grade to ǥ"
!! __@RULENAME@__ - vueʹlǧǧed+V+Ind+Prs+Sg1: vuâlǥam
!! {{{
 ǧ:ǥ <=> l    _ ǧ: [%^PenV2VV:|%^VOWXL:] (%^FRICG:) %^CShort: NeutrHighBack %^PALNo: ;

!! }}}
!! derive vueʹlǧǧed+V+Imp+Pl3: vuâlǥǥaz

"Even-syllabic nouns I,  ǧ and weakening of grade to j"
!! __@RULENAME@__ - vueʹlǧǧed+V+Ind+Prs+4: vueʹljet  âʹlǧǧ+N+Sg+Gen: ââʹlj
!! {{{
 ǧ:j <=> l _ ǧ: [%^PenV2VV: %^CShort:|%^VOWXL: (%^CShort:)] NeutrHighBack ((%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) RBound ;
!! }}}

"Even-syllabic nouns I, Depalatalization of ǧǧ"
!! __@RULENAME@__ - vueʹlǧǧed+V+Ind+Prs+Sg3: vuâlgg
 ǧ:g <=> Vow: (ʹ:|0:ʹ) (Cns:)          _ (%ˈ:) ǧ: BetweenStemAndPALNo  %^PALNo: RBound ;
         Vow: (ʹ:|0:ʹ) (Cns:) ǧ: (%ˈ:) _        BetweenStemAndPALNo %^PALNo: RBound ;
!€# vueʹlǧǧ%^PALNo
!Maybe this isn't needed

"Even-syllabic nouns I, Depalatalization of ǩ:k"
!! __@RULENAME@__ -
 ǩ:k <=>   !!= * @CODE@
  Vow: (%ʹ:|0:%ʹ)  _ (%ˈ:|:%ˈ) ǩ: (e: s:) BetweenStemAndPALNo    %^PALNo: RBound ;
  Vow: (%ʹ:|0:%ʹ) ǩ: (%ˈ:|:%ˈ) _    (e: s:) BetweenStemAndPALNo %^PALNo: RBound ;
  Vow:0                     _                       (%^VOWSH: %^CLong:)  %^RmVow: %> a ;	
  Vow: (%ʹ:|0:%ʹ) l   _ (%ˈ:|:%ˈ) ǩ: (e: s:) BetweenStemAndPALNo    %^PALNo: RBound ;
  Vow: (%ʹ:|0:%ʹ) l ǩ: (%ˈ:|:%ˈ) _    (e: s:) BetweenStemAndPALNo %^PALNo: RBound ;
  Vow:0           [s|š]            _                       (%^VOWSH: %^CLong:)  %^RmVow: %> a ;	

"Even-syllabic nouns, removing palatalization in -est +Loc nouns"
!! __@RULENAME@__ - removing palatalization in +Sg+Ill,  pieʹss:peässa
   ʹ:0 <=>  [Vow:|:Vow] _ [:Cns :Cns+|Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (e: s:) BetweenStemAndPALNo %^PALNo: [RBound|%^RmVow:|%^VC:] ;
!€# čââʹlmteʹm%{XC%}%^CLong%^PALNo%>a
!€0 čââʹlmte0mm000a
     _ [:Cns :Cns+|Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] BetweenStemAndPALNo %^PALNo: ;
!€# čââʹlmteʹm0%^PALNo%^C2CC%>a
!€0 čââʹlmte0mm000a

!jurdded
                    Vow: _ CNoV: Cns:+ BetweenStemAndPALNo %^PALNo: ; 
                         _ v:u ; 



!! !!Consonant QUANTITY CHANGE gradation rules

!! !Weakening Consonant Cluster

!! dealing with relative length changes
!! mõõnnâd : mõʹnne : mõõn 

!"Even syllabic verbs I, cg in relation to Vowel length"
!! !Vow Cx Cx:0 

"Even syllabic verbs I, cg for b"
!! __@RULENAME@__ - used with  neiʹbb+N+Sg+Gen: neeiʹb
 b:0 <=> SurfLongVow  [i (ʹ:|0:ʹ)|(ʹ:|0:ʹ) l] b _   BetweenStemAndConsGrade %^CC2C: ;
!€# n0eiʹbb%^V2VV%^CC2C
!€0 neeiʹb000

"Even syllabic verbs I, cg m:0"
!! __@RULENAME@__ - used with  +Imp+Sg2, +Ind+Prs+ConNeg, 
 m:0 <=> SurfLongVow (ʹ:|0:ʹ)  (s|r|l|p|d:) m: _ BetweenStemAndConsGrade %^CC2C: ;
 SurfLongVow (ʹ:|0:ʹ) m: _    BetweenStemAndConsGrade %^CC2C: ;
!€# sääʹmm%^CC2C
!€0 sääʹm00
         SurfLongVow (ʹ:) h: (ʹ:|0:ʹ)      m: _ BetweenStemAndConsGrade %^CC2C: ;
          


"Even syllabic verbs I, cg v:0"
!! __@RULENAME@__ - used with  +Imp+Sg2, +Ind+Prs+ConNeg, teevvad > teev

 v:0 <=>
 SurfLongVow (ʹ:|0:ʹ) v: _    BetweenStemAndConsGrade %^CC2C: ;

! SurfLongVow (i) (ʹ:|0:ʹ) v: _ (%^PenV2VV:|%^VOWXL:) %^CShort: ;

"Even syllabic nouns I, with extra lengthening of vowel ij>ii/uv>uu and dd>d"
!! __@RULENAME@__ - used with  +N+Sg+Nom > +N+Sg+Gen, sijdd > siid

 d:0 <=> d _ BetweenStemAndConsGrade %^CC2C: ;
!€# 0äldd%^V2VV%^CC2C
!€0 ääld000
!€# 0äldd%^V2VV%^VOWRaise%^CC2C%>už
!€0 aald00000už
 SurfLongVow (ʹ:|0:ʹ) d: _    BetweenStemAndConsGrade %^CC2C: ;
 [(0:o) u: (ʹ:) v:|i: (ʹ:) j:] (0:ʹ) d _  BetweenStemAndConsGrade  [%^CCC2C:] ;
!€# uv0dd%^PAL%^CCC2C
!€0 uuʹd000

 ! juʹrdded > joordam
          SurfLongVow (ʹ:|0:ʹ) CNoJV  d _ [%^PenV2VV: %^CShort:|%^VOWXL: (%^CShort:)] ;
                    u: (ʹ:) v:u (0:ʹ) d _  %^VOWXL: (%^CShort:) ; 
!! uʹvdded+V+Ind+Prs+4: uuʹdet
                     u (ʹ:) v:u (0:ʹ) d _  %^PenV2VV:  %^CShort: ; 
! njeiddad > njeeid
! 2012-06-12 v:u allows for uʹvdded
!! - idd:id, +Imp+Sg2, +Ind+Prs+ConNeg double consonants following vowel and "i" at coda that become single consonants in gradation
        2SgVow (ʹ:|0:ʹ) d _ Vow: [Cns|n j|l m|s ǩ:|s t|š ǩ:|š m]  %^VOWSH: %^CLong: %^RmVow: ; 

"Even syllabic verbs I, cg n:0"
!! __@RULENAME@__ - used with  +Imp+Sg2, +Ind+Prs+ConNeg, jiõnn:jiõn
 n:0 <=> 
 SurfLongVow (ʹ:|0:ʹ) n: _    BetweenStemAndConsGrade %^CC2C: ;
      SurfLongVow  (ʹ:|0:ʹ)  (s|r|l|p|d:) n:  _ (a: i:) (%^PenV2VV:|%^VOWXL:) %^CShort: ;
         SurfLongVow  (ʹ:) h: (ʹ:|0:ʹ) n: _ BetweenStemAndConsGrade %^CC2C: ; 
          
"Even syllabic verbs I, cg for ʒ"
!! __@RULENAME@__ - used with  pääʹʒʒelm+N+Sg+Ill: päʹʒlma
!! {{{
 ʒ:0 <=>
!2013-09-19    SurfLongVow (ʹ:|0:ʹ) ʒ _ %^VOWXL: (%^CShort:) ;
      u: (ʹ:) v:u (0:ʹ) ʒ _  BetweenStemAndConsGrade %^CCC2C: ;
        2SgVow (ʹ:|0:ʹ) ʒ _ Vow: [Cns|n j|l m|s ǩ:|s t|š ǩ:|š m]  %^VOWSH:  %^CLong: %^RmVow: ;
!! }}}

"Even syllabic verbs I, cg for s"
!! __@RULENAME@__ 
 s:0 <=>  i: (%{ʹ%}:) j: (ʹ:|0:ʹ) s _ BetweenStemAndConsGrade %^XYY2VY: ;
!€# ijss%^VOWXL%^CShort%>âs
!€0 iis0000âs
      SurfLongVow  (ʹ:|0:ʹ)  [r|l|p] s: _ BetweenStemAndConsGrade %^CC2C: ;
      SurfLongVow  (%{ʹ%}:) h: (ʹ:|0:ʹ) s:  _ BetweenStemAndConsGrade %^CC2C: ;

 

"Even syllabic verbs I, cg for đ" 
!! __@RULENAME@__ - used with  ǩiđđ:ǩiiđ
 đ:0 <=>
 SurfLongVow (ʹ:|0:ʹ) đ: _    BetweenStemAndConsGrade %^CC2C: ;

!  Vow: (ʹ:|0:ʹ) đ: _ [%^V2VV: ((%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:|%^PALNo:) %^CC2C:] ;
!€# ǩ0iđđ%^V2VV%^CC2C
!€0 ǩiiđ00
     	  [DbVow|Diphth] (ʹ:|0:ʹ) đ: _ %^PenV2VV: %^CShort: ;


"Even syllabic verbs I, cg for r" 
!! __@RULENAME@__ - used with  võrr:võõr
 r:0 <=>
 SurfLongVow (u|i) (ʹ:|0:ʹ) r: _    BetweenStemAndConsGrade %^CC2C: ;
!  [SurfLongVow (ʹ:|0:ʹ)|u: (ʹ:) v:u (0:ʹ)] r: _  [%^V2VV: ((%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:|%^PALNo:) %^CC2C:] ;
!€# v0õrr%^V2VV%^CC2C
!€0 võõr000
!€# paarr%^CC2C
!€0 paar00
!€# j0äuʹrr%^V2VV%^CC2C
!€0 jääuʹr000

          [DbVow|Diphth] (ʹ:|0:ʹ) r: _  [%^St2W:|%^PenV2VV: %^CShort:] ; 


"Even syllabic verbs I, cg for l"
!! __@RULENAME@__ - used with  vuʹvll+N+Sg+Gen: vuuʹl
 l:0 <=>
 SurfLongVow (ʹ:|0:ʹ) l: _    BetweenStemAndConsGrade %^CC2C: ;
!! deriving 
!           SurfLongVow (ʹ:|0:ʹ) l: _  [%^V2VV: ((%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:|%^PALNo:) %^CC2C:] ;
!€# t0oll%^V2VV%^CC2C
!€0 tool00
   u: (ʹ:) v:u (0:ʹ) l: _  %^V2VV: ((%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:|%^PALNo:) %^CC2C: ;
!- used with  vuʹvll+N+Sg+Gen: vuuʹl
!€# vuʹv0ll%^V2VV%^CC2C
!€0 vu0uʹl00

!! deriving påll+N+Sg+Gen: pååll
         [[DbVow|Diphth]|SurfLongVow u] (ʹ:|0:ʹ) l: _  [%^St2W:|%^PenV2VV: %^CShort:] ; 
!€# kuâ0ll%^PenV2VV%^CShort%^PAL
!€0 kueʹl0000

"j:0 "
!! __@RULENAME@__ 
 j:0 <=>
 SurfLongVow (ʹ:|0:ʹ) Cns:* j: _    BetweenStemAndConsGrade %^CC2C: ;
!€# ǩ0eʹrjj%^V2VV%^CC2C
!€0 ǩeeʹrj000


"Even syllabic verbs I, cg for g"
!! __@RULENAME@__ - used with  cõõggâlm+N+Sg+Ill: cõglma
 g:0 <=>  2SgVow (ʹ:|0:ʹ) g _ Vow: [Cns|n j|l m|s ǩ:|s t|š ǩ:|š m]  %^VOWSH: %^CLong:  %^RmVow: ;
          SurfLongVow  (ʹ:|0:ʹ)  [r|l|d:] g: _    BetweenStemAndConsGrade %^CC2C: ;

"Even syllabic verbs I, cg for ǧ"
!! __@RULENAME@__ - used with  vueʹlǧǧed+V+Ind+Prs+Sg1: vuâlǥam
 ǧ:0 <=> SurfLongVow (ʹ:|0:ʹ) l [ǧ:j|ǧ:] _    BetweenStemAndConsGrade %^CC2C: ;


! "Even syllabic verbs I, cg vv:v"
! v:0 <=> :Vow (ʹ:|0:ʹ) v _ (%^PenV2VV:) %^CShort: ; 

"Even syllabic nouns I, with extra lengthening of vowel V>VV and KK>ǤǤ"
!! __@RULENAME@__ 
!! used with  +N+Sg+Nom > +N+Sg+Gen for cases like
!! lookki > looǥǥi.
 k:ǥ <=>
 SurfLongVow  k: _   BetweenStemAndConsGrade %^CC2C: ;
         SurfLongVow   _ k: BetweenStemAndConsGrade %^CC2C: ;

  OnSetC [DbVow|Diphth]  k: _ (i:) BetweenStemAndPALNo %^CC2C: ;
  OnSetC [DbVow|Diphth]  _  k: (i:) BetweenStemAndPALNo %^CC2C: ;
  OnSetC SurfLongVow    k: _    (i:) %^VOWXL: (%^CShort:) ;
  OnSetC SurfLongVow       _ k: (i:) %^VOWXL: (%^CShort:) ;
  :Vow  k: _ (%^V2VV:) %^CC2C: ;
  :Vow  _ k: (%^V2VV:) %^CC2C: ;
!€# j0okk%^V2VV%^CC2C
!€0 jooǥǥ00

"Even syllabic nouns I, with extra lengthening of vowel V>VV and k:j"
!! __@RULENAME@__ 
!! used with
!!  > .
 k:j <=>   
 SurfLongVow :ʹ k: _  BetweenStemAndPALAllo (%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL: %^CC2C: ;
         SurfLongVow :ʹ    _ k: BetweenStemAndPALAllo (%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL: %^CC2C: ;

  OnSetC [DbVow|Diphth] :ʹ k:   _ BetweenStemAndPALAllo (%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL: %^CC2C: ;
  OnSetC [DbVow|Diphth] :ʹ  _  k: BetweenStemAndPALAllo (%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL: %^CC2C: ; 

  OnSetC  SurfLongVow   :ʹ k: (%ˈ:) _          %^VOWXL: (%^CShort:) ; 
  OnSetC  SurfLongVow   :ʹ          _ (%ˈ:) k: %^VOWXL: (%^CShort:) ; 

"Even syllabic nouns I, with extra lengthening of vowel V>VV and ǩ:j"
!! __@RULENAME@__ 
!! used with
!!  > .
 ǩ:j <=>   
 SurfLongVow (ʹ:|0:ʹ) ǩ: _    BetweenStemAndPALAllo ((%^PALÄ:|%^PALẸ:|%^PALE:) %^PAL:) %^CC2C: ;
         SurfLongVow (ʹ:|0:ʹ)    _ ǩ: BetweenStemAndPALAllo ((%^PALÄ:|%^PALẸ:|%^PALE:) %^PAL:) %^CC2C: ;

  OnSetC [DbVow|Diphth] (ʹ:|0:ʹ) ǩ:   _ BetweenStemAndPALAllo ((%^PALÄ:|%^PALẸ:|%^PALE:) %^PAL:) %^CC2C: ;
  OnSetC [DbVow|Diphth] (ʹ:|0:ʹ)  _  ǩ: BetweenStemAndPALAllo ((%^PALÄ:|%^PALẸ:|%^PALE:) %^PAL:) %^CC2C: ; 

  OnSetC  SurfLongVow   (ʹ:|0:ʹ) ǩ: (%ˈ:) _ PALAllo ((%^PALÄ:|%^PALẸ:|%^PALE:) %^PAL:)  %^CCC2C: ; 
  OnSetC  SurfLongVow   (ʹ:|0:ʹ)          _ (%ˈ:) ǩ: PALAllo ((%^PALÄ:|%^PALẸ:|%^PALE:) %^PAL:) %^CCC2C: ; 


"Even syllabic nouns I, with extra lengthening of vowel V>VV and ǩ:ǥ"
!! __@RULENAME@__ 
!! used with
!!  > .
 ǩ:ǥ <=>   
 Vow: (ʹ:) ǩ: (%ˈ:) _    BetweenStemAndPALAllo %^PALNo: [%^CC2C:|%^CCC2C:] ;
 Vow: (ʹ:) _ (%ˈ:) ǩ: BetweenStemAndPALAllo %^PALNo: [%^CC2C:|%^CCC2C:] ;



"Even syllabic verbs I, Voicing š:ž" 
!! __@RULENAME@__ - ss:zz, +Imp+Sg2, +Ind+Prs+ConNeg, double consonants at coda become voiced in gradation
 š:ž <=> 
 SurfLongVow (ʹ:|0:ʹ) š: _    BetweenStemAndConsGrade %^CC2C: ;
         SurfLongVow (ʹ:|0:ʹ)    _ š: BetweenStemAndConsGrade %^CC2C: ;
 OnSetC [Diphth|DbVow] (ʹ:|0:ʹ) š:       _          %^PenV2VV:   %^CShort: ;
         OnSetC [Diphth|DbVow] (ʹ:|0:ʹ)          _       š: %^PenV2VV:   %^CShort: ; 
         OnSetC   Diphth       (ʹ:|0:ʹ) š:  %ˈ:  _          %^VOWXL: (%^CShort:) ;
         OnSetC   Diphth       (ʹ:|0:ʹ)          _ %ˈ:   š: %^VOWXL: (%^CShort:) ;
         OnSetC   AllDbVow     (ʹ:|0:ʹ) š: (%ˈ:) _          %^VOWXL: (%^CShort:) ;
         OnSetC   AllDbVow     (ʹ:|0:ʹ)          _ (%ˈ:) š: %^VOWXL: (%^CShort:) ;
!€# kõõččm0õšš%^VOWXL%^CShort
!€0 kõõččmõõžž00
!         _ %^VC: ;

"Even syllabic verbs I, Voicing c:ʒ"
!! __@RULENAME@__ - cc:zz, +Imp+Sg2, +Ind+Prs+ConNeg, double consonants at coda become voiced in gradation
 c:ʒ <=> SurfLongVow (ʹ:|0:ʹ) c: _    BetweenStemAndConsGrade %^CC2C: ;
         SurfLongVow (ʹ:|0:ʹ)    _ c: BetweenStemAndConsGrade %^CC2C: ;
!€# čääʹcc%^CC2C
!€0 čääʹʒʒ00
                                   _ %^VC: ; 

"Even syllabic verbs I, Voicing č:j"
!! __@RULENAME@__ - ss:zz, +Imp+Sg2, +Ind+Prs+ConNeg, double consonants at coda become voiced in gradation
 č:j <=> OnSetC SurfLongVow (ʹ:|0:ʹ) č: _    BetweenStemAndConsGrade %^CC2C: ;
         OnSetC SurfLongVow (ʹ:|0:ʹ)    _ č: BetweenStemAndConsGrade %^CC2C: ;
                                             _ %^VC: ;


"Even syllabic verbs I, Voicing after long vowel or diphthong s:z s:z" 
!! __@RULENAME@__ - ss:zz, +Imp+Sg2, +Ind+Prs+ConNeg, double consonants at coda become voiced in gradation
 s:z <=> OnSetC [Diphth|AllDbVow] (ʹ:|0:ʹ) s:  _  BetweenStemAndConsGrade  %^CC2C: ; 
         OnSetC [Diphth|AllDbVow] (ʹ:|0:ʹ) _   s: BetweenStemAndConsGrade  %^CC2C: ; 
!€# mättʼt0õss%^V2VV%^CC2C
!€0 mättʼtõõzz00
         OnSetC   [Diphth|AllDbVow]  (ʹ:|0:ʹ) s:  %ˈ:  _  BetweenStemAndConsGrade %^CCC2C: ; 
         OnSetC   [Diphth|AllDbVow]  (ʹ:|0:ʹ)  _ %ˈ:   s: BetweenStemAndConsGrade %^CCC2C: ; 

!! deriving cieʹǩǩes+N+Sg+Gen: ceäkˈkaz
  OnSetC :Vow+ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] e: _ BetweenStemAndPALNo (%^PALNo:|%^PAL:) %^VC: ; 


         


"Even syllabic verbs I, third consonant loss" 
!! __@RULENAME@__ - lgg:lǥ, Vow Ca Cx Cx:0, +Imp+Sg2, +Ind+Prs+ConNeg, double consonants following consonant at coda that become single consonants in gradation
!! {{{
 Cx:0 <=> SurfLongVow  (ʹ:|0:ʹ)  [s|r|l|p|d:] Cx: _ BetweenStemAndConsGrade %^CC2C: ;
          SurfLongVow  (%{ʹ%}:) h: (ʹ:|0:ʹ) Cx:  _ BetweenStemAndConsGrade %^CC2C: ;
                where Cx in (ŋ č c) ;
!! }}}

"Even syllabic verbs I, t>đ" 
!! __@RULENAME@__ - tt:đ
!! {{{

 t:đ  <=> SurfLongVow (ʹ:|0:ʹ) _ t:0 BetweenStemAndConsGrade [%^CC2C:|%^CCC2C:] ;
!! }}}


"Even syllabic nouns I, p>v" 
!! __@RULENAME@__ - pp:v
 p:v  <=> SurfLongVow (ʹ:|0:ʹ) _ p:0  %^CC2C: ;
!€# ǩiâpp%^CC2C
!€0 ǩiâv00



"Even syllabic verbs I, second consonant loss t:0"
!! __@RULENAME@__ -
!! {{{
 t:0 <=> SurfLongVow              (ʹ:|0:ʹ) ([s|r|l]) t: _ BetweenStemAndConsGrade  %^CC2C: ;
         SurfLongVow (%{ʹ%}:) h: (ʹ:|0:ʹ)           t: _ BetweenStemAndConsGrade  %^CC2C: ;
!! }}}

"Even syllabic verbs I, second consonant loss p:0"
!! __@RULENAME@__ -
 p:0 <=> SurfLongVow (ʹ:|0:ʹ) (Cns:) p: _ %^CC2C: ;
!€# ǩiâpp%^CC2C
!€0 ǩiâv00
          2SgVow (ʹ:|0:ʹ)            p _ Vow: [Cns|n j|l m|s ǩ:|s t|š ǩ:|š m]  %^VOWSH:  %^RmVow: ;  

"Even syllabic verbs I, second consonant loss k:0"
!! __@RULENAME@__ -
!! {{{
 k:0 <=> SurfLongVow  (ʹ:|0:ʹ)  [s|r|l|p] k:  _ BetweenStemAndConsGrade  %^CC2C: ;
!       SurfLongVow  (%{ʹ%}:) h: (ʹ:|0:ʹ) k: _ BetweenStemAndConsGrade  %^CC2C: ;
!! }}}

"Even syllabic verbs I, second consonant loss ǩ:0"
!! __@RULENAME@__ -
!! {{{
 ǩ:0 <=>
  SurfLongVow  (ʹ:|0:ʹ)  [s|r|l|p] ǩ:  _ BetweenStemAndConsGrade  %^CC2C: ;
!          SurfLongVow  (%{ʹ%}:) h: (ʹ:|0:ʹ) ǩ: _ (%^PenV2VV:|%^VOWXL:) %^CShort: ;
!! }}}


"Even syllabic verbs I, lgg>lǥ" 
!! __@RULENAME@__ - vueʹlǧǧed+V+Ind+Prs+Sg2 => vuâlǥak
 g:ǥ  <=> SurfLongVow (ʹ:0|%{ʹ%}:0) [l|r|d:]    _ g: BetweenStemAndConsGrade %^CC2C: ;
!          SurfLongVow (ʹ:0|%{ʹ%}:0) [l|r|d:] g: _    (%^PenV2VV:|%^VOWXL:) %^FRICG:0 %^CShort: ; 


!vueʹlǧǧed+V+Imp+Pl3: vuâlǥǥaz
!"Even syllabic verbs I, lgg>lǥǥ" ! lgg:lǥǥ

"Odd syllabic nouns I, lǥ>lg"
!! __@RULENAME@__ - ǩeâlǥal+N+Sg+Gen: ǩeâlggal
!! {{{
ǥ:g <=> 2ShortVow   l _ 0: Vow: Cns [%^VOWSH: %^CLong:|%^VOWXSH: (%^CLong:)]  ;
!! }}}
!€# ǩeâlǥ0al
!€0 ǩeâlggal

"even syllabic verbs I, 'lǧǧ>'lj" 
!! __@RULENAME@__ - 
 g:j  <=> SurfLongVow   :ʹ [l|r|d:]  _ g:0 BetweenStemAndPALNo %^PAL: %^CC2C: ;
!vueʹlǧǧed+V+Ind+Prt+Pl1 => vuõʹljim
!€# vuâ0lgg%^VOWRaise%^PAL%^CC2C%>im
!€# vuõʹlj00000im







!! !!Rules for cleaning up and composing end result

!! * Strengthening Consonant Cluster
!! * V shortening and C lengthening 
!! * %^CLong is generally used in conjunction with a preceding %^VOWSH
!! * In verbs this will be observed in finite forms
!! * In derivational morphology, e.g. -škuetted, this is not the case

"Orthographic Consonant lengthening Weak to strong or extra strong %{XC%}:m"
!! __@RULENAME@__
 %{XC%}:m <=>  Vow: (ʹ:|0:ʹ) m _ [%^W2XSt:|%^CLong:] %^PALNo: ;
!€# čââʹlmteʹm%{XC%}%^CLong%^PALNo%>a
!€0 čââʹlmte0mm000a

"Orthographic Consonant lengthening Weak to strong or extra strong %{XC%}:v"
!! __@RULENAME@__
 %{XC%}:v <=>  Vow: (ʹ:|0:ʹ) v _ %^W2XSt: ;


"Orthographic Consonant lengthening Weak to strong or extra strong %{XC%}:n"
!! __@RULENAME@__
 %{XC%}:n <=>  Vow: (ʹ:|0:ʹ) n _ BetweenStemAndConsGrade [%^C2CC:|%^C2CCC:] ;
!€# mään%{XC%}%^VV2V%^VOWRaise%^C2CC%>u
!€0 m0ann0000u

"Orthographic Consonant lengthening Weak to strong or extra strong %{XC%}:r"
!! __@RULENAME@__
 %{XC%}:r <=>  Vow: (ʹ:|0:ʹ) r _ %^W2XSt: ;
!€# puär0%{XC%}%^W2XSt%^VOWRaise%>u
!€0 puårˈr000u

"Orthographic Consonant lengthening Weak to strong or extra strong %{XC%}:l"
!! __@RULENAME@__
 %{XC%}:l <=>  Vow: (ʹ:|0:ʹ) l (0:ˈ) _ BetweenStemAndConsGrade %^C2CC: ;
!€# suâl0%{XC%}%^W2XSt%^VOWRaise%>u
!€0 suõlˈl000u

"Orthographic Consonant lengthening Weak to strong or extra strong %{XC%}:k"
!! __@RULENAME@__
 %{XC%}:k <=>  Vow: (ʹ:|0:ʹ) k _ %^W2XSt: ;




"Orthographic Consonant lengthening in INDPRSSG3 and IMPSG2 0:m"
!! __@RULENAME@__ - kåʹmrded+V+Imp+Sg2: kååʹmmerd 
 0:m <=> Vow:Vow (ʹ:|0:ʹ)  m _ [%{E1%}:] Cns:+ PenBetweenStemAndConsGrade %^PenC2CC: [%^IMPSG2:|%^INDPRSSG3:] ;
        m _ BetweenStemAndConsGrade %^C2CC: ;

!€# ooum0%^VV2V%^C2CCu
!€0 0oumm00u
!€# čââʹlmteʹm0%^PALNo%^C2CC%>a
!€0 čââʹlmte0mm000a

"Consonant lengthening 0:b"
!! __@RULENAME@__ - 
 0:b <=>  2ShortVow  (ʹ:|0:ʹ) [l] b _  BetweenStemAndConsGrade %^C2CC: ;
  2ShortVow  (ʹ:|0:ʹ) [l] b _ Vow: Cns PenBetweenStemAndConsGrade %^PenC2CC: ;


"Consonant lengthening 0:p"
!! __@RULENAME@__ - 
!! {{{
 0:p <=>  2ShortVow  (ʹ:|0:ʹ) [m|l] p _ Vow: Cns [%^VOWSH: %^CLong:|%^VOWXSH: (%^CLong:)] (%^PALNo:) ; 
!! }}}

"Orthographic Consonant lengthening in INDPRSSG3 and IMPSG2 0:n"
!! __@RULENAME@__
 0:n <=>  Vow: (ʹ:|0:ʹ) n _ %^W2XSt: ;
!€# mään0%^W2XSt%^VOWRaise%>u
!€0 m0ann000u
 OnSetC  2ShortVow (ʹ:|0:ʹ) n _ (j â: l) [%^VOWSH: %^CLong:|%^VOWXSH: (%^CLong:)] ; 


"Consonant lengthening 0:d"
!! __@RULENAME@__ - juurd+N+Sg+Gen: jurddi
!! {{{
 0:d <=> 2ShortVow (ʹ:) (r) d _ (i:)  BetweenStemAndConsGrade %^C2CC: ;
!! }}}

"Consonant lengthening 0:t"
!! __@RULENAME@__ - 
!! {{{
 0:t <=> 2ShortVow (ʹ:) (s) :t _ BetweenStemAndConsGrade %^C2CC: ;
!! }}}

"Consonant lengthening 0:c"
!! __@RULENAME@__ - 
!! {{{
 0:c <=>  2ShortVow  (ʹ:|:ʹ) p c _ BetweenStemAndConsGrade %^C2CC: ;
!! }}}

"Consonant lengthening 0:đ"
!! __@RULENAME@__ - čââʹđ+N+Sg+Gen: čâđđam
!! {{{
 0:đ <=>  2ShortVow  (ʹ:)  đ _ BetweenStemAndConsGrade %^C2CC: ;
!! }}}

"Consonant lengthening 0:l"
!! __@RULENAME@__ 
 0:l <=>
 :Vow (ʹ:|:ʹ) l (0:%ˈ) _ BetweenStemAndConsGrade [ %^C2CC:|%^C2CCC:] ;
! :Vow (ʹ:|:ʹ) l (0:%ˈ) _ (e: s:) BetweenStemAndConsGrade [ %^C2CC:|%^C2CCC:] ;
!€# suâl0%^VOWRaise%^C2CC%>ud 
!€0 suõll000ud
!  2ShortVow  (ʹ:)  l (0:%ˈ) _ (e: s:) [%^VOWSH: %^CLong:|%^VOWXSH: (%^CLong:)] ; 


"Consonant lengthening 0:g"
!! __@RULENAME@__ - ǩeâlǥal+N+Sg+Gen: ǩeâlggal
!! {{{
 0:g <=>  2ShortVow   l ǥ: _ Vow: Cns [%^VOWSH: %^CLong:|%^VOWXSH: (%^CLong:)] ;
!! }}}

"Consonant lengthening 0:ǧ"
!! __@RULENAME@__ - 
!! {{{
 0:ǧ <=>  2ShortVow  :ʹ  l ǥ: _ Vow: Cns [%^VOWSH: %^CLong:|%^VOWXSH: (%^CLong:)] ((%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) ;
!! }}}

"Consonant lengthening 0:ǩ"
!! __@RULENAME@__ - 
!! {{{
 0:ǩ <=>  2ShortVow  :ʹ  l ǩ _ Vow: Cns [%^VOWSH: %^CLong:|%^VOWXSH: (%^CLong:)] ((%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) ;
!! }}}

"Consonant lengthening 0:k"
!! __@RULENAME@__ - 
 0:k <=> Vow (ʹ:) (l|n|ŋ|r|s|š|t) :k _ BetweenStemAndConsGrade %^C2CC: ;
!€# durak0%^C2CC%>a
!€0 durakk00a
!  2ShortVow  (ʹ:) [l|s|š] :k _ Vow: Cns [%^VOWSH: %^CLong:|%^VOWXSH: (%^CLong:)] (%^PALNo:) ;


!! !!CONSONANT QUALITY CHANGE

"Consonant quality change j:ǧ"
!! __@RULENAME@__ - 
 j:ǧ <=>  2ShortVow  :ʹ  l _ 0: Vow: Cns [%^VOWSH: %^CLong:|%^VOWXSH: (%^CLong:)] ((%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) ;

"Consonant quality change ǥ:ǧ"
!! __@RULENAME@__ 
 ǥ:ǧ <=>  2ShortVow  :ʹ  l _ 0: Vow: Cns [%^VOWSH: %^CLong:|%^VOWXSH: (%^CLong:)] (%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL: ;
!Dummy

"Devoicing ž:š"
!! __@RULENAME@__
 ž:š <=> Cns %{Â1%}:0 _ ;
!€# taarb%{Â1%}ž%>am
!€0 taarb0š0am

!! !Pedagogical X3 length mark after diphthongs in vertical line %ˈ 

!! !Adding X3 length mark
"Consonant X3 lengthening after diphthong in vertical line %ˈ "
!! __@RULENAME@__ - used in +N+Sg+Ill __jeäll:jiâlˈlu, __
!!b c č ǯ d đ g ǧ k ǩ l m n p r s š t v also htˈt nˈnj
 0:%ˈ <=> Diphth (ʹ:|0:ʹ)     b  _ b     BetweenStemAndConsGrade %^CC2CCC: ;
          Diphth (ʹ:|0:ʹ)     c  _ c     BetweenStemAndConsGrade %^CC2CCC: ;
          Diphth (ʹ:|0:ʹ)     č  _ č     BetweenStemAndConsGrade %^CC2CCC: ;
          Diphth (ʹ:|0:ʹ)     ǯ  _ ǯ     BetweenStemAndConsGrade %^CC2CCC: ;
          Diphth (ʹ:|0:ʹ)     d  _ d     BetweenStemAndConsGrade %^CC2CCC: ;
          Diphth (ʹ:|0:ʹ)     đ  _ đ     BetweenStemAndConsGrade %^CC2CCC: ;
          Diphth (ʹ:|0:ʹ)     g: _ g:    BetweenStemAndConsGrade %^CC2CCC: ;
          Diphth (ʹ:|0:ʹ)     ǧ: _ ǧ:    BetweenStemAndConsGrade %^CC2CCC: ;
          Diphth (ʹ:|0:ʹ)     k: _ k:    BetweenStemAndConsGrade %^CC2CCC: ;
          Diphth (ʹ:|0:ʹ)     ǩ: _ ǩ:    BetweenStemAndConsGrade %^CC2CCC: ;
          Diphth (ʹ:|0:ʹ)     l  _ l     BetweenStemAndConsGrade %^CC2CCC: ;
!€# kuâl0l%^CC2CCC%>a
!€0 kuâlˈl00a
          Diphth (ʹ:|0:ʹ)     m  _ m     BetweenStemAndConsGrade %^CC2CCC: ;
          Diphth (ʹ:|0:ʹ)     n  _ n (j) BetweenStemAndConsGrade %^CC2CCC: ;
          Diphth (ʹ:|0:ʹ)     p  _ p     BetweenStemAndConsGrade %^CC2CCC: ;
          Diphth (ʹ:|0:ʹ)     r  _ r     BetweenStemAndConsGrade %^CC2CCC: ;
          Diphth (ʹ:|0:ʹ)     s  _ s     BetweenStemAndConsGrade %^CC2CCC: ;
          Diphth (ʹ:|0:ʹ)     š  _ š     BetweenStemAndConsGrade %^CC2CCC: ;
          Diphth (ʹ:|0:ʹ) (h) t _ t      BetweenStemAndConsGrade %^CC2CCC: ;
          Diphth (ʹ:|0:ʹ)     v _ v      BetweenStemAndConsGrade %^CC2CCC: ;


!! !Removing X3 length mark
"Removing Consonant X3 length mark after diphthong in vertical line %ˈ LEFT ARROW"
!! __@RULENAME@__ - deriving 
!!b c č ǯ d đ g ǧ k ǩ l m n p r s š t v also htˈt nˈnj
!! {{{BetweenStemAndConsGrade %^C2CC:
 %ˈ:0 <= Diphth (ʹ:|0:ʹ)     b _ b     BetweenStemAndConsGrade %^CCC2CC: ;
         Diphth (ʹ:|0:ʹ)     c _ c     BetweenStemAndConsGrade %^CCC2CC: ;
         Diphth (ʹ:|0:ʹ)     č _ č     BetweenStemAndConsGrade %^CCC2CC: ;
         Diphth (ʹ:|0:ʹ)     ǯ _ ǯ     BetweenStemAndConsGrade %^CCC2CC: ;
         Diphth (ʹ:|0:ʹ)     d _ d     BetweenStemAndConsGrade %^CCC2CC: ;
         Diphth (ʹ:|0:ʹ)     đ _ đ     BetweenStemAndConsGrade %^CCC2CC: ;
         Diphth (ʹ:|0:ʹ)     g _ g     BetweenStemAndConsGrade %^CCC2CC: ;
         Diphth (ʹ:|0:ʹ)     ǧ _ ǧ     BetweenStemAndConsGrade %^CCC2CC: ;
         Diphth (ʹ:|0:ʹ)     k _ k     BetweenStemAndConsGrade %^CCC2CC: ;
         Diphth (ʹ:|0:ʹ)     ǩ _ ǩ     BetweenStemAndConsGrade %^CCC2CC: ;
         Diphth (ʹ:|0:ʹ)     l _ l     BetweenStemAndConsGrade %^CCC2CC: ;
         Diphth (ʹ:|0:ʹ)     m _ m     BetweenStemAndConsGrade %^CCC2CC: ;
         Diphth (ʹ:|0:ʹ)     n _ n (j) BetweenStemAndConsGrade %^CCC2CC: ;
         Diphth (ʹ:|0:ʹ)     p _ p     BetweenStemAndConsGrade %^CCC2CC: ;
         Diphth (ʹ:|0:ʹ)     r _ r     BetweenStemAndConsGrade %^CCC2CC: ;
         Diphth (ʹ:|0:ʹ)     s _ s     BetweenStemAndConsGrade %^CCC2CC: ;
         Diphth (ʹ:|0:ʹ)     š _ š     BetweenStemAndConsGrade %^CCC2CC: ;
         Diphth (ʹ:|0:ʹ) (h) t _ t     BetweenStemAndConsGrade %^CCC2CC: ;
         Diphth (ʹ:|0:ʹ)     v _ v     BetweenStemAndConsGrade %^CCC2CC: ;
!! }}}



