! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or divvun@samediggi.no

!! !!!Skolt Sámi TWOLC file
!
! $Id$
!
!

!! !!!Introduction
!! The rule file for Skolt Saami is divided into 5 main types:
!! 
!! # Definitions
!! # Consonant shift rules (tbw)
!! # Vowel alternation rules
!! # Consonant gradation rules
!! # Rules for cleaning up and composing end result


!! !!!Alphabets, sets and definitions

!! !!Alphabet

Alphabet 
  a b c d e f g h i ï j k l m n o p q r s t u v w x y z !!= * @CODE@
  A B C D E F G H I Ï J K L M N O P Q R S T U V W X Y Z !!= * @CODE@
  æ ä ø ö å á é í					!!= * @CODE@
  Å Ä Ö Ø Æ Á						!!= * @CODE@
  Á É Ó Ú Í À È Ò Ù Ì Ë Ü Ï Â Ê Ô Û Î Ã Ý þ Ñ Ð		!!= * @CODE@
  á é ó ú í à è ò ù ì ë ü ï â ê ô û î ã ý þ ñ ð ß ª ß ç	!!= * @CODE@
  č ǩ ǯ ǧ ž đ ǥ ʒ š ŋ					!!= * @CODE@
  Č Ǩ Ǯ Ǧ Ž Đ Ǥ Ʒ Š Ŋ					!!= * @CODE@
  õ â							!!= * @CODE@
  Õ Â							!!= * @CODE@

 ʹ  !! * MODIFIER LETTER PRIME: U+02B9 suprasegmental palatization
 ʼ  !! * MODIFIER LETTER APOSTROPHE: U+02BC Syllable marker that appears in writing 
 %ˈ !! * MODIFIER LETTER VERTICAL LINE: U+02C8 as overlong consonant marker that does not appear in writing 

!  for consonant lengthening
%{XC%}:m
%{XC%}:v

%{XC%}:n
%{XC%}:r
%{XC%}:l

%{XC%}:k


!! !! Trigger symbols

 %^RmVow:0 !!= * @CODE@ for removing vowels e, â, a in second syllable before consonant  
 %^1VOW:0 %^2VOW:0 %^3VOW:0 %^4VOW:0 %^5VOW:0 !!= * @CODE@ Vow triggers

 %^VOWLower:0 !!= * @CODE@ i>e, õ>â, â>ä, u>o, o>å
 %^VOWRaise:0 !!= * @CODE@ e>i, ä>e 
 %^VOWBack:0	     !!= * @CODE@ Backing e>õ, e>â
 %^VOWFront:0	     !!= * @CODE@ Fronting
 %^PAL:0 %^PALNo:0 %^PALE:0 %^PALÄ:0   !!= * @CODE@ Palatalisation
 %^VC:0         !!= * @CODE@ Voicing s:z š:ž
 %^Â:0		!!= * @CODE@ Indicates placement of â in stem

!!  !! CHARACTERISTIC BREAKDOWN 2015-02-17
%^VV2V:0        !!= * @CODE@ Shortens vowel 
%^V2VV:0 	!!= * @CODE@ Lengthens vowel
%^CC2C:0 	!!= * @CODE@ Shortens consonant
%^C2CC:0 	!!= * @CODE@ Lengthens consonant
%^CC2CCC:0 	!!= * @CODE@ Strong to Extra Strong consonant
%^C2CCC:0 	!!= * @CODE@ Weak to Extra strong consonant
%^XYYY2XYY:0 	!!= * @CODE@ Weakens Extra strong to strong consonant cluster
%^XYYY2XY:0 	!!= * @CODE@ Weakens Extra strong to weak consonant cluster
%^XYY2XY:0 	!!= * @CODE@ Shortens consonant cluster
%^XY2XYY:0 	!!= * @CODE@ Lengthens consonant cluster

!! !! Gradation triggers 2015.01.23
 %^XSt2St:0	!!= * @CODE@ Causes weakening from extra strong to strong
 %^XSt2W:0	!!= * @CODE@ Causes weakening from extra strong to weak
 %^St2W:0	!!= * @CODE@ Causes weakening from strong to weak
 %^W2St:0	!!= * @CODE@ Causes strengthening from weak to strong
 %^W2XSt:0	!!= * @CODE@ Causes strengthening from weak to extra strong
 %^St2XSt:0	!!= * @CODE@ Causes strengthening from strong to extra strong

!! Other vowel length and consonant length will be phased out
 %^VOWSH:0	  !!= * @CODE@
 %^VOWXSH:0	  !!= * @CODE@ Extra shortens vowel in relation to consonant
 %^VOWL:0     !!= * @CODE@ Lengthens vowel in relation to consonant
 %^VOWXL:0    !!= * @CODE@ Extra lengthens vowel in relation to consonant in nouns, for example
 %^CXLong:0 %^CLong:0 %^CShort:0 !!= * @CODE@ Lengthening, shortening

 X3:0  !!= * @CODE@ causes weak grade
 %{VU%}:u         !!= * @CODE@ used with stuuʹl
 %{Â1%}:0 !!= * @CODE@ used with "mainsted" to tell (a story)
 %{A1%}:0 !!= * @CODE@ used with "vuejted" to drive (causative)
 %{E1%}:0 !!= * @CODE@ used with "kåʹmrded" to bow
 %{%ʹ%}:0 !!= * @CODE@ archiform for MODIFIER LETTER ACUTE ACCENT used in stems where the lemma does not contain a MODIFIER LETTER ACUTE ACCENT and yet the paradigm requires one.
 %{EÂ%}:e !!= * @CODE@ archiform for ie:eâ variation
 %^IMPSG2:0	!!= * @CODE@ 
 %^INDPRSSG3:0  !!= * @CODE@ 
 %^FRICG:0	!!= * @CODE@ 
 %^VOWTense:0   !!= * @CODE@ â:e
 %^VOWLax:0     !!= * @CODE@ e:â 

!! Literal quotes and angles must be escaped (cf morpheme boundaries below):

 »7     !! * »
 «7     !! * «
 %[%>%] !! * >
 %[%<%] !! * <

!! Morpheme boundaries:
 «  ! Derivational prefix
 »  ! Derivational suffix
 %< ! Inflectional prefix
 %> ! Inflectional suffix
 #  ! Word boundary for both lexicalised and dynamic compounds
 %^ ! (exceptional) soft hyphenation point

!! End of alphabet definitions
 ;


!! !!Sets
Sets 

! Letter =
 Cns	= b c d f g h j k l m n p q r s t v w x z đ ʒ ǯ č ŋ ǩ ǧ ǥ š ž ; !!= * @CODE@
 CNoV	= b c d f g h j k l m n p q r s t w x z đ ʒ ǯ č ŋ ǩ ǧ ǥ š ž ;   !!= * @CODE@

 CNoJ	= b c d f g h k l m n p q r s t v w x z đ ʒ ǯ č ŋ ǩ ǧ ǥ š ž ;   !!= * @CODE@
 Vow	= a e i o u å ä â õ           	      	      	      	        !!= * @CODE@
          A E I O U Å Ä Â Õ ;	    				        !!= * @CODE@

 CNoJV  = b c d f g h k l m n p q r s t w x z đ ʒ ǯ č ŋ ǩ ǧ ǥ š ž ;     !!= * @CODE@
 CNoDLRZ = b c f g h j k m n p q s t v w x z đ ǯ č ŋ ǩ ǧ ǥ š ž ;        !!= * @CODE@


!! !!Definitions

Definitions 
!! {{{
ShortV2XL = [0:a a|0:ä a:ä] ;  !!= * @CODE@

LongV2SH = [a:0 a|â:0 â|ä:0 ä|e:0 e|i:0 i|å:0 å|o:0 o|u:0 u|õ:0 õ|a:0 a:ä|â:0 â:õ|ä:0 ä:a|ä:0 ä:a|e:0 e:i|i:0 i:e|å:0 å:o|o:0 o:u|u:0 u:o|õ:0 õ:â] ; !!= * @CODE@ 

Diphth = [e â|e:i â:õ|e ä|e:i â:e|e:i ä:â|e:i ä:e|i â|i:e â:ä|i â:e|i e|i:e e:â|i:e e:ä|i e:õ|i õ|i:e õ:â|i:e õ:ä|u â|u â:õ|u e|u e:â|u â:e|u e:õ|u e:ä|u õ|u õ:â|u õ:e|u å|u å:ä|u å:e|u ä|u ä:å|u ä:e] ; !!= * @CODE@ 

DbVow = [a a|a:ä a:ä|i i|i:e i:e|o o|o:å o:å|õ õ|õ:â õ:â|u u|u:o u:o|â â|â:õ â:õ|e e|e e:ä|e ä:e|e:i e:i|å å|å:o å:o|ä ä|ä:a ä:a] ; !!= * @CODE@ 

2DbVow = [0:a a|0:a ä:a|0:i i|0:i e:i|0:o o|0:o u:o|0:o å:o|0:õ õ|0:õ â:õ|0:u u|0:u o:u|0:â â|0:e e|0:å å|0:å o:å|0:ä ä] ; !!= * @CODE@ 

AllDbVow = [DbVow|2DbVow] ; !!= * @CODE@ 

2SgVow = [a:0 a|a:0 a:ä|i:0 i|i:0 i:e|o:0 o|o:0 o:å|õ:0 õ|õ:0 õ:â|u:0 u|u:0 u:o|â:0 â|â:0 â:õ|e:0 e|e:0 e:i|å:0 å|å:0 å:o| ä:0 ä|ä:0 ä:a] ; !!= * @CODE@ 

SurfLongVow = [Diphth|AllDbVow] ; !!= * @CODE@ 

2ShortVow = [Diphth|2SgVow] ; !!= * @CODE@ 


CC = [b b|c c|č č|ǯ ǯ|d d|đ đ|g g|ǧ ǧ|k k|ǩ ǩ|l l|m m|n n|p p|r r|s s|š š|t t|v v|h t t|n n j] ; !!= * @CODE@

LongC = [k: k:|s: s:|š: š:|c: c:|č: č:|m m:|v v:|đ đ:|n n:|l l:|r r:] ; !!= * @CODE@ 
!! }}}

!! preceded by two vowels VxVx, VxVy
!! {{{
DiphthwithCC = [k:ǥ k:ǥ|s:z s:z|š:ž š:ž|c:ʒ c:ʒ|č:j č:j|m m:0|v v:0|đ đ:0|n n:0|l l:0|r r:0] ; !!= * @CODE@
!! }}}

!! preceded by two vowels
!! {{{
CC2CforPrecedingVV = [k:ǥ k:ǥ|ǩ:j ǩ:j|s:z s:z|š:ž š:ž|c:ʒ c:ʒ|č:j č:j|m m:0|v v:0|đ đ:0|n n:0|l l:0|p:v p:0|t:đ t:0|r r:0] ; !!= * @CODE@ 
!! }}}

!! preceded by one vowel Vx=>VxVx påll=>pååll
!|ǧ:j ǧ:0|ǧ:ǥ ǧ:0 
!! {{{
CCforPrecedingV2VV =  [b b|p p|m m|v v| d d|t t|đ đ|n n|l l|r r|s s|š š|c c|č č| ǯ ǯ | k k|ǩ ǩ|g g|ǧ ǧ|ǧ:g ǧ:g|ǧ:ǥ ǧ:ǥ ] ; !!= * @CODE@ 

iCC2iCforPrecedingV2VV = [ i (ʹ:|0:ʹ) [b b:0|ʒ ʒ:0|d d:0|g g:0|k k:0|ǩ ǩ:0|l l:0|m m:0|n n:0|p p:0|r r:0|t t:0|v v:0]] ; !!= * @CODE@ 

uCC2uCforPrecedingV2VV = [ [u|%{VU%}:u] (ʹ:|0:ʹ) [ʒ ʒ:0|d d:0|g g:0|k k:0|ǩ ǩ:0|l l:0|m m:0|n n:0|p p:0|r r:0|t t:0]] ; !!= * @CODE@ 
!! }}}

!! extra long vowel (u or i)
!! {{{
CC2CafterV2VVandUI = [uCC2uCforPrecedingV2VV|iCC2iCforPrecedingV2VV] ;  !!= * @CODE@ 
!! }}}

!! extra long vowel (d or l or r)
!! {{{
CC2CforPrecedingV2VV = [l [b b:0|ǧ:j ǧ:0|ǧ:ǥ ǧ:0|m m:0|t t:0]|m p p:0|p c c:0|r [d d:0|n n:0]|s [k: k:0|ǩ: ǩ:0|t t:0]] ; !!= * @CODE@ 
!! }}}

!! extra long consonant (d or l or r)
!! {{{
C2CCafterVV2V = [l [b 0:b|j:ǧ 0:ǧ|ǥ:ǧ 0:ǧ|m 0:m|t 0:t]|m p 0:p|p c 0:c|r d 0:d|s [:k 0:k|:ǩ 0:ǩ|t 0:t]] ; !!= * @CODE@ 
!! }}}

!! Long consonant cluster
LongCluster = [l [g g|ǧ ǧ|m m|t t]|m p p|p c c|r d d |s [k k|ǩ ǩ|t t]] ;
!! Short consonant cluster


!! Onset consonant or word boundary
OnSetC = [Cns|#] ;


!! following morpheme or word boundary
!! {{{
RBound = [#|%>] ; !!= * @CODE@ 
!! }}}

!! relative lengthening of vowel right context following vowel:vowel
!! 0:õ <=> OnSetC _ :õ RelVowLengthening
!! {{{
RelVowLengthening = [(ʹ:|0:ʹ) [CCforPrecedingV2VV  %^V2VV: (%^CC2C:)|CC2CforPrecedingV2VV (a: i:) %^V2VV: %^CC2C:|CC2CafterV2VVandUI %^V2VV: %^CC2C:|CC2CforPrecedingVV %^CC2C:|[k|m|n] %^VOWL:] ] ; !!= * @CODE@
!! }}}

!! relative lengthening of vowel right context following vowel:vowel
!! 0:i <=> OnSetC _ :i RelVowLengtheningNOTi
!! {{{
RelVowLengtheningNOTi = [(ʹ:|0:ʹ) [CCforPrecedingV2VV  %^V2VV:|[CC2CforPrecedingV2VV|uCC2uCforPrecedingV2VV] %^V2VV: %^CC2C:|CC2CforPrecedingVV %^CC2C: ]] ; !!= * @CODE@
!! }}}





!! relative lengthening of vowel right context following vowel:vowel
!! 0:u <=> OnSetC _ :u RelVowLengtheningNOTu
!! {{{
RelVowLengtheningNOTu = [(ʹ:|0:ʹ) [CCforPrecedingV2VV  %^VOWL: %^CShort:|[iCC2iCforPrecedingV2VV|CC2CforPrecedingV2VV] %^V2VV: %^CC2C:|CC2CforPrecedingVV %^CC2C:]] ; !!= * @CODE@
!! }}}


!! possible triggers before VOWLower and VOWRaise
!! {{{
PrecedeVOWLowerRaise = [([%^VOWSH:|%^VOWXSH:] %^CLong:|[%^VOWL:|%^VOWXL:] %^CShort:)] ; !!= * @CODE@ 
!! }}}

!! possible triggers before PALNo and PAL
PrecedePALNo = [( %^VV2V: )] ;
! [([%^St2XSt:|%^St2W:|%^XSt2St:|%^XSt2W:|%^W2St:|%^W2XSt:]|[%^VOWSH:|%^VOWXSH:] (%^CLong:)|[%^VOWL:|%^VOWXL:] (%^FRICG:) (%^CShort:)) (%^VOWLower:|%^VOWRaise:) (%^VOWFront:|%^VOWBack:) (%^VOWTense:|%^VOWLax:) (X3:)] ;

!! possible triggers before PALE PALÄ
PrecedePALAllo = [( %^VV2V: )] ;
![([%^St2XSt:|%^St2W:|%^XSt2St:|%^XSt2W:|%^W2St:|%^W2XSt:]|[%^VOWSH:|%^VOWXSH:] (%^CLong:)|[%^VOWL:|%^VOWXL:] (%^FRICG:) (%^CShort:)) ] ; 

!! possible triggers between VowHeight and word end
!! {{{
BetweenVowHeightAndMorph = [(%^VOWFront:|%^VOWBack:) (%^VOWTense:|%^VOWLax:) (X3:) ((%^PALE:|%^PALÄ:) %^PAL:|%^PALNo:)] ; !!= * @CODE@ 
!! }}}

!! neutral to vowel height and backness
!! {{{
NeutrHighBack = [(%^VOWLower:|%^VOWRaise:) (%^VOWFront:|%^VOWBack:)] ; !!= * @CODE@ 
!! }}}

!! neutral to vowel and consonant length
!! {{{
NeutrGrade = [([%^VOWL:|%^VOWXL:] (%^FRICG:0) (%^CShort:)|[%^VOWSH: |%^VOWXSH:] (%^CLong:))] ; !!= * @CODE@ 
!! }}}



!! * X3 = C Vx Cx Cx     C Vx Vy Cx ' Cx
!! * X2 = C Vx Vx Cx Cx  C Vx Vy Cx Cx
!! * X1 = C Vx Vx Cx     C Vx Vy Cx


!! !!!Rules
Rules 

!! !!Vowel shortening rules 
!! (tbw)



"Even syllabic verbs I, vowel shortening â:0"
!! __@RULENAME@__ - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
!! {{{
 â:0 <=> OnSetC _ â: (ʹ:|0:ʹ) [LongC|Cns:+] [%^VOWSH: %^CLong:|%^VOWXSH: (%^CLong:)] ; !!= @CODE@
         [Cns:+|:Cns+] _ [Cns|i:|n j|l m|s ǩ:|s t|š ǩ:|š m] (%^VOWSH: (%^CLong:)|%^VC:) %^RmVow: ; !!= @CODE@
!! }}}

"Even syllabic verbs I, vowel shortening e:0"
!! __@RULENAME@__ - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
!! {{{
 e:0 <=> OnSetC _ e: (ʹ:|0:ʹ) [LongC|Cns: :Cns*] [%^VOWSH: %^CLong:|%^VOWXSH: (%^CLong:)] ; !!= @CODE@
         [Cns:+|:Cns+] _ [Cns|i:|n j|l m|s ǩ:|s t|š ǩ:|š m] (%^VOWSH: (%^CLong:)|%^VC:) %^RmVow: ; !!= @CODE@
         OnSetC _ e: (ʹ:|0:ʹ) Cns:+ Vow: Cns %^VOWSH: ; !!= @CODE@
!! deriving cieʹǩǩes+N+Sg+Ill: ceäkˈksa
  OnSetC :Vow+ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] _  s: [%^VOWSH: %^CLong:| %^VOWXSH: %^CLong:]  %^VOWLower: (%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:) %^PAL:)  %^RmVow: ; !!= @CODE@
!! }}}

"Even syllabic verbs I, vowel shortening å:0"
!! __@RULENAME@__ - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
!! {{{
 å:0 <=> OnSetC _ å: (ʹ:|0:ʹ) [LongC|Cns: :Cns*] [%^VOWSH: %^CLong:|%^VOWXSH: (%^CLong:)] ; !!= @CODE@
!! }}}

"Even syllabic verbs I, vowel shortening õ:0"
!! __@RULENAME@__ - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
!! {{{
 õ:0 <=> OnSetC _ õ: (ʹ:|0:ʹ) [LongC|Cns: :Cns*] [%^VOWSH: %^CLong:|%^VOWXSH: (%^CLong:)] ; !!= @CODE@
         OnSetC _ õ: (ʹ:|0:ʹ) Cns:+ Vow: [Cns|n j|l m|s ǩ:|s t|š ǩ:|š m]  %^VOWSH: ; !!= @CODE@
!! }}}

"Even syllabic verbs I, vowel shortening u:0"
!! __@RULENAME@__ - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
!! {{{
 u:0 <=> OnSetC _ u: (ʹ:|0:ʹ) [C2CCafterVV2V|Cns: :Cns+] (i:) [%^VOWSH: %^CLong:|%^VOWXSH: (%^CLong:)] ; !!= @CODE@
         OnSetC _ u: (ʹ:|0:ʹ) Cns:+ Vow: [Cns|n j|l m|s ǩ:|s t|š ǩ:|š m] [%^VOWSH:|%^RmVow:] ; !!= @CODE@
!! }}}

"Even syllabic verbs I, vowel shortening i:0"
!! __@RULENAME@__ - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
!! {{{
 i:0 <=> OnSetC _ i: (%ʹ:|0:%ʹ) [LongC|Cns: :Cns*] [%^VOWSH: %^CLong:|%^VOWXSH: (%^CLong:)] ; !!= @CODE@
         Cns _ %> ǥ a ; !!= @CODE@ ! +Sg+Ill N_HÕʹPPI
         [Cns:+|:Cns+] _ [Cns|n j|l m|s ǩ:|s t|š ǩ:|š m] (%^VOWSH: (%^CLong:)|%^VC:) %^RmVow: ; !!= @CODE@
         OnSetC _ i: (ʹ:|0:ʹ) Cns:+ Vow: [Cns|n j|l m|s ǩ:|s t|š ǩ:|š m]  %^VOWSH: ; !!= @CODE@ ! iilâsǩ:ilska
!! }}}

"Even syllabic verbs I, vowel shortening o:0"
!! __@RULENAME@__ - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
 o:0 <=> OnSetC _ o: (ʹ:|0:ʹ) [u m:+ |LongC|Cns: :Cns*] [%^VOWSH: %^CLong:|%^VOWXSH: (%^CLong:)] ;
!€# ooum0%^VOWSH%^CLongu
!€0 0oumm00u
    OnSetC _ o: (ʹ:|0:ʹ) [LongC|Cns: :Cns*] %^VV2V: ;
!€# šoomm%^VV2V%^VOWRaise%>u
!€0 š0omm000u

         [Cns:+|:Cns+] _ [Cns|i:|n j|l m|s ǩ:|s t|š ǩ:|š m] (%^VOWSH: (%^CLong:)|%^VC:) %^RmVow: ; 


"Even syllabic verbs I, vowel shortening a:0"
!! __@RULENAME@__ - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
 a:0 <=> OnSetC _ a: (ʹ:|0:ʹ) [LongC|Cns: :Cns*] %^VV2V: ;
!€# saa0kk%^VV2V%^PAL%>e
!€0 s0aʹǩǩ000e
 OnSetC _ a: (ʹ:|0:ʹ) [LongC|Cns: :Cns*] [%^VOWSH: %^CLong:|%^VOWXSH: (%^CLong:)] ;
         [Cns:+|:Cns+] _ [Cns|i:|n j|l m|s ǩ:|s t|š ǩ:|š m] (%^VOWSH: (%^CLong:)|%^VC:) %^RmVow: ; 
         OnSetC _ a: (%ʹ:|0:%ʹ) Cns:+ Vow: [Cns|n j|l m|s ǩ:|s t|š ǩ:|š m]  %^VOWSH: ; 
   OnSetC :Vow+ (%ʹ:|0:%ʹ) [LongC|Cns:+ - [Cns:* [l|n]]] _ i: %> Vow: ; 


"vowel to syllable mark a:ʼ"
!! __@RULENAME@__ - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
!! {{{
 a:ʼ <=> OnSetC :Vow+ (%ʹ:|0:%ʹ)  Cns:* [l|n]  _ i: %> Vow: ; !!= @CODE@
!! }}}


"ZERO to syllable mark 0:ʼ"
!! __@RULENAME@__ - used in zeeʹtt+N+Sg+Abe: zeeʹttʼtää
 0:ʼ <=> OnSetC :Vow+ (%ʹ:|0:%ʹ)  Cns:* Cx Cx %>  _ Cx ; 
         where Cx in (s t) ;
!€# ijss%>0sa
!€# ijss0ʼsa



"Even syllabic verbs I, vowel shortening ä:0"
!! __@RULENAME@__ - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
 ä:0 <=> OnSetC _ ä: Cns: %{XC%}: %^VV2V: %^C2CC: ;
!€# mään%{XC%}%^VV2V: %^C2CC%^VOWRaise%>u
!€0 m0ann000u
   OnSetC _ ä: (ʹ:|0:ʹ) [LongC|Cns: :Cns*|Cns:+] [%^VOWSH: %^CLong:|%^VOWXSH: (%^CLong:)] ;
         OnSetC _ ä: (ʹ:|0:ʹ) Cns:+ Vow: [Cns|i:|n j|l m|s ǩ:|s t|š ǩ:|š m] %^VOWSH: %^CLong: ;





!! !!Vowel alternation rules

!! !VOWEL SHIFT

!! !RAISING

!! deriving +Ind+Prt+Sg1, +Ind+Prt+Sg2 +Ind+Prt+Pl3 in teevvad:tivvu

"Even syllabic verbs I, vowel raising o:u"
!! __@RULENAME@__ - Adding +Prt+Pl3 for ed verbs, Removing u: and i:  second element due to njeiddad njeiddu
 o:u <=>  OnSetC  o: _ (%ʹ:|0:%ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  (%^VOWSH: %^CLong:) %^VOWRaise: %> [e|u] ;
       OnSetC  :0 _ [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  (%^VV2V:) %^VOWRaise: RBound ;
!€# šoomm%^VV2V%^VOWRaise%>u
!€0 š0omm000u

          OnSetC o:0 _ (%ʹ:|0:%ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  (%^VOWSH: %^CLong:) %^VOWRaise: ((%^PALE:|%^PALÄ:) %^PAL:) %> [e|u] ;
          OnSetC 0:u _ (%ʹ:|0:%ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  (%^VOWL: %^CShort:) %^VOWRaise: (%^PALE:|%^PALÄ:) %^PAL: %> [e|u] ;
  OnSetC _ (u:) (%ʹ:|0:%ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  (%^VOWSH: %^CLong:|%^VOWL:) %^VOWRaise: ((%^PALE:|%^PALÄ:) %^PAL:) %> [e|u] ;


"Even syllabic verbs I, vowel raising å:o"
!! __@RULENAME@__ - Adding +Prt+Pl3 for ed verbs, Removing u: and i:  second element due to njeiddad njeiddu
 å:o <=>  OnSetC _ å:o (%ʹ:|0:%ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  (%^VOWL: %^CShort:|%^VOWXL: (%^CShort:)) %^VOWRaise: ; 
          OnSetC [å:o|0:o] _ (%ʹ:|0:%ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] NeutrGrade  %^VOWRaise: ;
          OnSetC 0: _ (ʹ:|0:ʹ) CCforPrecedingV2VV %^V2VV: %^VOWRaise: ;
!€# k0åʹll%^V2VV%^VOWRaise%>in
!€0 kooʹll0000in
          OnSetC _ (u:) (%ʹ:|0:%ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  (%^VOWSH: %^CLong:|%^VOWL:) %^VOWRaise: ((%^PALE:|%^PALÄ:) %^PAL:) %> [e|u] ;



"Even syllabic verbs I, vowel raising e:i"
!! __@RULENAME@__ - Adding +Prt+Pl3 for ed verbs, Removing u: and i:  second element due to njeiddad njeiddu
 e:i <=>
!!
  OnSetC _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] %^VOWRaise: ;
!€# pess%^VOWRaise%>u
!€0 piss00u
  OnSetC [0:i] _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] %^V2VV: %^VOWRaise: ;
!€# p0ess%^V2VV%^VOWRaise%>už
!€0 piiss000už
  OnSetC  [e:|e:0|0:i] _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  PrecedeVOWLowerRaise %^VOWRaise: ;
  OnSetC _ (u:) (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] PrecedeVOWLowerRaise %^VOWRaise: ;
! diphthong raising with e:i at onset - deriving +Ind+Prt+Pl3 in reäkkad > riâkku, veârrad > viõrru
  _ [ä:|â: ] (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  PrecedeVOWLowerRaise  %^VOWRaise: ;



"Even syllabic verbs I, vowel raising â:õ" 
!! __@RULENAME@__ - Adding +Prt+Pl3 for ed verbs, Removing u: and i:  second element due to njeiddad njeiddu
 â:õ <=>
          OnSetC u   _      (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+|Cns:+ :%ˈ Cns:+] %^VOWRaise:  ;
!€# ruâk0k%^VOWRaise%^CC2CCC%>u
!€0 ruõkˈk000u
  OnSetC â:  _      (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  (%^VOWSH: %^CLong:) %^VOWRaise: ;
          OnSetC â:0 _      (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  (%^VOWSH: %^CLong:) %^VOWRaise: (%^PAL:) ;
                 0:õ _      (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]           NeutrGrade %^VOWRaise: ;
          OnSetC     _ (u:) (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  (%^VOWSH: %^CLong:|%^VOWL:) %^VOWRaise: (%^PAL:) ;
          OnSetC u   _      (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+|Cns:+ :%ˈ Cns:+] ([%^VOWSH:|%^VOWXSH:] %^CLong:) %^VOWRaise: ;
          OnSetC e:  _      (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+|Cns:+ 0:%ˈ Cns:+]  (%^VOWSH: %^CLong:) %^VOWRaise: ;



"Even syllabic verbs I, diphthong raising beginning with u ä:å"
!! __@RULENAME@__ - deriving +Ind+Prt+Pl3 in kuâs'sad > kuõs'su, kuärŋŋad kuårŋŋu
!! {{{
 ä:å <=> OnSetC u  _ (%ʹ:|0:%ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^VOWSH: %^CLong:) %^VOWRaise: %> u ; !!= @CODE@
!! }}}


"Even syllabic verbs I, diphthong raising beginning with e:i ä:â"
!! __@RULENAME@__ - deriving +Ind+Prt+Pl3 in reäkkad > riâkku, veârrad > viõrru
!! {{{
 ä:â <=> OnSetC e:  _ (%ʹ:|0:%ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+|Cns:+ 0:%ˈ Cns:+]  PrecedeVOWLowerRaise %^VOWRaise: %> u ; !!= @CODE@
!! }}}



"Even syllabic verbs I, diphthong raising beginning with e:i ä:e"
!! __@RULENAME@__ 
 ä:e <=> OnSetC [u|e:i]  _ :ʹ [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+|Cns:+ 0:%ˈ Cns:+]  PrecedeVOWLowerRaise %^VOWRaise: %^PALE: %^PAL: ;
!€# čuä0ckk%^CONSH%^PALE%^PAL%>es
!€# čueʹcǩ00000es

"Even syllabic verbs I, diphthong raising beginning with ä:a u"
!! __@RULENAME@__ - deriving läullad > laullum
 ä:a <=> [ ä: | OnSetC (0:) ] _ Cns: [%{XC%}: |Cns:*] (%^VV2V: | %^V2VV:) %^VOWRaise: ;
!€# mään%{XC%}%^VV2V%^VOWRaise%^C2CC%>u
!€0 m0ann000u
!€# äldd%^VOWRaise%>u
!€0 aldd00u
!€# 0äldd%^V2VV%^VOWRaise%>u
!€0 aald0000u


  OnSetC (0:a)  _ u  (%ʹ:|0:%ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+|Cns:+ 0:%ˈ Cns:+] (%^VOWSH: %^CLong:) %^VOWRaise: %>  u ;
         OnSetC  ä:    _    (%ʹ:|0:%ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+|Cns:+ 0:%ˈ Cns:+] %^VOWSH: %^CLong:  %>  a ; 
         OnSetC  ä:    _    (%ʹ:|0:%ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+|Cns:+ 0:%ˈ Cns:+] (%^VOWXSH: (%^CLong:)|%^VOWL: (%^CShort:)) %^VOWRaise: ;
         OnSetC        _ ä: (%ʹ:|0:%ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+|Cns:+ 0:%ˈ Cns:+] (%^VOWL:   (%^CShort:)) %^VOWRaise: ; 



!! !LOWERING 

! Where: VIGGAD +Ind+Prs+Sg3, +Ind+Prs+Pl3
!        TIETTED 
! long and single vowels



"Even syllabic verbs I, vowel lowering o:å"
!! __@RULENAME@__ - deriving +Ind+Prs+Sg3, +Ind+Prs+Pl3 in poorrâd
!! {{{
 o:å <=> OnSetC    _ o:å (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^VOWL: %^CShort:)                   %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:) %^PAL:) RBound ; !!= @CODE@
         OnSetC o: _     (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^VOWSH: %^CLong:|%^VOWL: %^CShort:) %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:) %^PAL:) RBound ; !!= @CODE@
         OnSetC    _     (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^VOWSH: %^CLong:)                   %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:) %^PAL:) RBound ; !!= @CODE@
! juʹrdded+V+Cond+Sg1: joordčem
         OnSetC 0:å _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^VOWL: %^CShort:)  %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:) %^PAL:) RBound ; !!= @CODE@
! juʹrdded > joordam
         OnSetC    _ (u:|i:) (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^VOWL: %^CShort:) %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:) %^PAL:) RBound ; !!= @CODE@
         OnSetC    _ ((ʹ:) (v:u|j:i)) (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^VOWSH: %^CLong:) %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:) %^PAL:) RBound ; !!= @CODE@
!! }}}


"Even syllabic verbs I, vowel lowering i:e"
!! __@RULENAME@__ - deriving +Ind+Prs+Sg3, +Ind+Prs+Pl3 in viǯǯâd
 i:e <=> OnSetC _ i:e (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^VOWL: %^CShort:)  %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:) %^PAL:) RBound ; 
         OnSetC _ [õ:â|õ:ä|â:ä|e:ä|e:â] (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (e: s:) (%^VOWL: %^CShort:|%^VOWSH: %^CLong:|%^VOWXSH: %^CLong:)  %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:) %^PAL:) (RBound|%^RmVow:|%^VC:) ;
           OnSetC i: _   (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^VOWSH: %^CLong:| %^VOWL: %^CShort:)                      %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:) %^PAL:) RBound ;
           OnSetC    _   (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^VOWSH: %^CLong:)                      %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:) %^PAL:) RBound ;
! juʹrdded+V+Cond+Sg1: joordčem
!           OnSetC 0:e _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^VOWL: %^CShort:)  %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:) %^PAL:) RBound ;
           OnSetC 0: _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^VOWL: %^CShort:)  %^VOWLower: ;
!€# v0iǯǯ%^VOWL%^CShort%^VOWLower%>as
!€0 veeǯǯ0000as

! juʹrdded > joordam
           OnSetC _ (u:|i:) (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^VOWL: %^CShort:) %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:) %^PAL:) RBound ;
           OnSetC _ ((ʹ:) (v:u|j:i)) (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^VOWSH: %^CLong:) %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:) %^PAL:) RBound ;



"Even syllabic verbs I, vowel lowering a:ä"
!! __@RULENAME@__ - deriving +Ind+Prs+Sg3, +Ind+Prs+Pl3 in viǯǯâd
!! {{{
 a:ä <=> OnSetC    _ a:ä (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^VOWL: %^CShort:)  %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:) %^PAL:) RBound ; !!= @CODE@
         OnSetC a: _     (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^VOWSH: %^CLong:|%^VOWL: %^CShort:)                      %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:) %^PAL:) RBound ; !!= @CODE@
         OnSetC    _     (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^VOWSH: %^CLong:)                      %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:) %^PAL:) RBound ; !!= @CODE@
! juʹrdded+V+Cond+Sg1: joordčem
         OnSetC 0:ä _    (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^VOWL: %^CShort:)  %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:) %^PAL:) RBound ; !!= @CODE@
! juʹrdded > joordam
         OnSetC     _ (u:|i:) (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^VOWL: %^CShort:) %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:) %^PAL:) RBound ; !!= @CODE@
         OnSetC     _ ((ʹ:) (v:u|j:i)) (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^VOWSH: %^CLong:) %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:) %^PAL:) RBound ; !!= @CODE@
!! }}}


!"Even syllabic verbs I, vowel raising a:ä"
!! __@RULENAME@__ - Adding +Prt+Pl3 for ed verbs, Removing u: and i:  second element due to njeiddad njeiddu
! a:ä <=> OnSetC a: _ (%ʹ:|0:%ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  (%^VOWSH: %^CLong:) %^VOWRaise: %> [e|u] ; 
!         OnSetC a:0 _ (%ʹ:|0:%ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  (%^VOWSH: %^CLong:) %^VOWRaise: ((%^PALE:|%^PALÄ:) %^PAL:) %> [e|u] ;
!         OnSetC 0:ä _ (%ʹ:|0:%ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  (%^VOWL: %^CShort:) %^VOWRaise: (%^PALE:|%^PALÄ:) %^PAL: %> [e|u] ;
!  OnSetC _ (u:) (%ʹ:|0:%ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  (%^VOWSH: %^CLong:|%^VOWL:) %^VOWRaise: ((%^PALE:|%^PALÄ:) %^PAL:) %> [e|u] ;



"Even syllabic verbs I, vowel lowering u:o"
!! __@RULENAME@__ - deriving +Ind+Prs+Sg3, +Ind+Prs+Pl3 in uʹvdded
!! {{{
 u:o <=>  OnSetC _ :o (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^VOWL: %^CShort:)  %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:) %^PAL:) RBound ; !!= @CODE@
        OnSetC u: _   (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+|r d d:] NeutrGrade   %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:) %^PAL:) RBound ; !!= @CODE@
       OnSetC (0:o) _  (ʹ:| 0:ʹ) [CNoV: Cns:*|Cns:+ (%ˈ:|:%ˈ) Cns:+] NeutrGrade %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:) %^PAL:) RBound ; !!= @CODE@
! uʹvdded+V+Ind+Pl3: ouʹdde 
 OnSetC (0:) _ [(ʹ:) v: (0:ʹ)|%{VU%}:u (ʹ:|0:ʹ)] [d:+|l:+|r:+|ʒ:+] (%^VOWL: %^CShort:|%^VOWXL: (%^CShort:))  %^VOWLower: ; !!= @CODE@
! OnSetC (0:o) _ (ʹ:)  v:u (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^VOWL: %^CShort:|%^VOWXL: (%^CShort:)) %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:) %^PAL:) RBound ;
!! vuʹvll+N+Sg+Ill: voulla
 OnSetC  _ (ʹ:)  v:u (0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^VOWL: %^CShort:) %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:) %^PAL:) RBound ; !!= @CODE@
 OnSetC (0:) _ (ʹ:) [CNoV: Cns:*|Cns:+ (%ˈ:|:%ˈ) Cns:+] NeutrGrade %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:) %^PAL:) RBound ; !!= @CODE@
!! stuuʹl+N+Sg+Ill: stooula
 OnSetC 0: _ [ %{VU%}:u (ʹ:|0:ʹ) [d|l|r|ʒ]]  %^VOWLower: ; !!= @CODE@
! juʹrdded+V+Cond+Sg1: joordčem
 OnSetC 0: _ (ʹ:|0:ʹ) [CNoV: Cns:*|Cns:+ (%ˈ:|:%ˈ) Cns:+]  %^VOWL:  %^CShort:    %^VOWLower: ; !!= @CODE@
! juʹrdded > joordam
!! }}}



"Even syllabic verbs I, vowel lowering õ:â "
!! __@RULENAME@__ - deriving +Ind+Prs+Sg3, +Ind+Prs+Pl3 in viǯǯâd
!! {{{
 õ:â <=>  _ õ: (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^VOWL: %^CShort:)  %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:) %^PAL:) RBound ; !!= @CODE@
       õ: _    (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^VOWSH: %^CLong:|%^VOWL: %^CShort:)                      %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:) %^PAL:) RBound ; !!= @CODE@
   OnSetC _    (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^VOWSH: %^CLong:)                      %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:) %^PAL:) RBound ; !!= @CODE@
! juʹrdded+V+Cond+Sg1: joordčem
   OnSetC _ 0:â (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^VOWL: %^CShort:)  %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:) %^PAL:) RBound ; !!= @CODE@
! juʹrdded > joordam
   OnSetC _ (u:|i:) (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^VOWL: %^CShort:) %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:) %^PAL:) RBound ; !!= @CODE@
   OnSetC _ ((ʹ:) (v:u|j:i)) (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^VOWSH: %^CLong:) %^VOWLower: (%^VOWFront:|%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:) %^PAL:) RBound ; !!= @CODE@
! diphthong opening after u
! - deriving +Ind+Prs+Sg3, in kuõskkâd kueʹsǩǩe
        u _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^VOWL: (%^FRICG:0) %^CShort:)  %^VOWLower: (X3:) (%^PALNo:) RBound ; !!= @CODE@
! diphthong opening õ:â after i:e
! - deriving +Ind+Prs+Sg3, in čiõkkâd
       i: _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^VOWL: %^CShort:)  %^VOWLower: (X3:)  %^PALNo: RBound ; !!= @CODE@
!! }}}

! diphthongs

"Even syllabic verbs I, diphthong opening after u å:ä"
!! __@RULENAME@__ - deriving +Ind+Prs+Sg3, in kuåccâd kuäʹcce
!! = a>ä lowering clockwise
!! {{{
 å:ä <=> u _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^VOWL: (%^FRICG:0) %^CShort:)  %^VOWLower: (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:) %^PAL:) RBound ; !!= @CODE@
!! }}}

"Even syllabic verbs I, diphthong opening after i:e e:ä"
!! __@RULENAME@__ - deriving +Ind+Prs+Sg3, in čiõkkâd
!! {{{
 e:ä <=> [i:|u] _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (e: s:) (%^VOWL: %^CShort:|%^VOWSH: %^CLong:|%^VOWXSH: %^CLong:) %^VOWLower: (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:) %^PAL:) [RBound|%^RmVow:|%^VC:] ; !!= @CODE@
! vueʹlǧǧed+V+Ind+Prs+Pl3: vuäʹlǧǧa
!! }}}        

"Vowel in second syllable e:a"
!! __@RULENAME@__ - deriving cieʹǩǩes+N+Sg+Gen: ceäkˈkaz
!! {{{
 e:a <=> OnSetC :Vow+ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] _ s: [%^VOWSH: %^CLong:|%^VOWXSH: %^CLong:] %^VOWLower: (%^VOWBack:) (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:) %^PAL:) %^VC: ; !!= @CODE@
!! }}}        

"Even syllabic verbs I, diphthong opening i:e â:ä"
!! __@RULENAME@__ - deriving +Ind+Prs+Sg3, in čiõkkâd
!! {{{
 â:ä <=> i: _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^VOWL: %^CShort:) %^VOWLower: (X3:) (%^PALNo:|(%^PALE:|%^PALÄ:) %^PAL:) RBound ; !!= @CODE@
!! }}}


"Even syllabic verbs I, diphthong opening after i:e e:â"
!! __@RULENAME@__ - deriving +Ind+Prs+Sg3, in pi%{EÂ%}ʹǩǩ:peâkka
!! {{{
 %{EÂ%}:â <=> i: _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^VOWL: %^CShort:) %^VOWLower: (X3:) %^PALNo:  RBound ; !!= @CODE@
!! }}}

"Even syllabic verbs I, diphthong opening after u e:â"
!! __@RULENAME@__ - deriving vueʹlǧǧed+V+Ind+Prs+Pl3: vuâʹlǧǧe
!! {{{
 e:â <=> [u|i:] _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (e: s:) (%^VOWL: (%^FRICG:) %^CShort:|%^VOWSH: (%^CLong:)) %^VOWLower: %^VOWBack: ; !!= @CODE@
!! }}}

!! !Vowel backing

"Even syllabic verbs I, diphthong opening after u e:õ"
!! __@RULENAME@__ - deriving vueʹlǧǧed+V+Ind+Prt+Sg1: vuõʹlǧǧem
!! {{{
 e:õ <=> u _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^VOWL: %^CShort:|%^VOWSH: (%^CLong:)) %^VOWBack: ; !!= @CODE@
!! }}}

!! !Vowel Palatalization
"diphthong allophonic realization in palatalization u å:e"
!! __@RULENAME@__ - deriving 
 å:e <=> u _ :ʹ  [Cns:+|Cns:+ 0:%ˈ Cns:+] PrecedePALAllo %^PALE: %^PAL: ;
!€# puå0v0v%^St2XSt%^PALE%^PAL%>e
!€# pueʹvˈv0000e

!! !Vowel Lowering and Fronting

"Even syllabic verbs I, diphthong opening õ:ä after i:e"
!! __@RULENAME@__ - deriving +Ind+Prs+Sg3, in čiõkkâd
!! {{{
 õ:ä <=>
 i:e _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  (%^VOWSH: (%^FRICG:0) %^CLong:|%^VOWL: %^CShort:) %^VOWLower: (%^PALE:|%^PALÄ:) %^PAL: ; !!= @CODE@
 u _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]  (%^VOWSH: (%^FRICG:0) %^CLong:|%^VOWL: %^CShort:) %^VOWLower: %^VOWFront: ; !!= @CODE@
!! }}}



!! !SECONDARY FRONTING


 
"Even syllabic verbs I, secondary vowel fronting with PAL u õ:e"
!! __@RULENAME@__ - deriving +Ind+Prs+Pl3 in VIQQAD: kuõskkâd >kueʹsǩǩe
!! {{{
 õ:e <=> u _ (%ʹ:|0:%ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^VOWSH: %^CLong:|%^VOWL: %^CShort:)  %^VOWLower: (X3:) (%^PALE:|%^PALÄ:) %^PAL: RBound ; !!= @CODE@ ! deriving +V+Inf in vueʹlǧǧed
! u _ (%ʹ:|0:%ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (%^VOWSH: %^CLong:|%^VOWL: %^CShort:)  %^VOWFront: (X3:) (%^PALE:|%^PALÄ:) %^PAL: RBound ; !!= @CODE@ ! deriving +V+Inf in vueʹlǧǧed
!         i _ (%ʹ:|0:%ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]                                        %^VOWFront:       (%^PALE:|%^PALÄ:) %^PAL: %> ; !!= @CODE@
!! }}} 



! "Even syllabic verbs I, secondary u > v PAL"
! __@RULENAME@__ - deriving +V+Inf in TIETTED: uudd >uʹvdded
! u:v <=> u _ (%ʹ:|0:%ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] %^VOWFront: (%^PALE:|%^PALÄ:) %^PAL: %> ;


!! !RELATIVE VOWEL LENGTHENING

!! vowel lengthening and consonant shortening, %^VOWL and %^CShort 


"Even syllabic verbs I, relative vowel lengthening 0:â"
!! __@RULENAME@__ - deriving +V+Inf in TIETTED: uudd > uʹvdded
!! {{{
 0:â <=> OnSetC _ :â RelVowLengthening ; !!= @CODE@
! Cns: _ â [(u:|i:) (%ʹ:|0:%ʹ) [u l l:|Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]]  %^VOWL: (%^CShort:) [%>|#|(%^PALE:|%^PALÄ:) %^PAL:] ;
!Distinguishing mainstam and maainstam
      Cns: _ â (u:|i:) (ʹ:|0:ʹ)  Cns: %{Â1%}: Cns:+  %^VOWL: ; !!= @CODE@
!      OnSetC _ â [(ʹ:|0:ʹ) [u l l:|Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+]]  %^VOWXL: ;
! Even syllabic verbs I, relative vowel lengthening and lowering 0:â
!simultaneous lengthening and lowering: juʹrdded > joordam juʹrdded+V+Ind+Prs+Sg1
!   OnSetC _ õ:â (ʹ:|0:ʹ) Cns:+ %^VOWL: %^CShort: %^VOWLower: ;
!! }}}


"Even syllabic verbs I, relative vowel lengthening 0:e"
!! __@RULENAME@__ - deriving +V+Inf in TIETTED: uudd > uʹvdded
 0:e <=> OnSetC _ e RelVowLengthening ;
!€# čââʹlmteʹm%^VOWL
!€0 čââʹlmte0m0
        OnSetC _ e (ʹ:|0:ʹ) Cns:+ %^V2VV: ;
!€# p0ess%^V2VV
!€0 peess0
         OnSetC _ i:e RelVowLengthening %^VOWLower: ;
!€# v0iǯǯ%^VOWL%^CShort%^VOWLower%>as
!€0 veeǯǯ0000as
!Distinguishing mainstam and maainstam
      Cns: _ e (u:|i:) (ʹ:|0:ʹ)  Cns:+ %{Â1%}: Cns:+  %^VOWL: ; 
      [ n | š ] _ e (ʹ:|0:ʹ) ǩ ǩ ;
!€# kaađn0e0ǩǩ%^VOWL%^PAL
!€0 kaađneeʹǩǩ00

"Even syllabic verbs I, relative vowel lengthening 0:å"
!! __@RULENAME@__ - deriving
!! {{{
 0:å <=>
!! - deriving påll+N+Sg+Gen: pååll
   OnSetC _ :å RelVowLengthening RBound ; !!= @CODE@
!Distinguishing mainstam and maainstam
!Vowel lengthening in INDPRSSG3 and IMPSG2 0:å kååʹmmerd, maainâst
      Cns: _ å (u:|i:) (ʹ:|0:ʹ)  Cns:+ [%{Â1%}:|%{A1%}:|%{E1%}:] Cns:+  [%^VOWL:|%^IMPSG2:|%^INDPRSSG3:] ; !!= @CODE@
!! }}}

"Even syllabic verbs I, relative vowel lengthening 0:õ"
!! __@RULENAME@__ - deriving +V+Inf in TIETTED: uudd > uʹvdded
 0:õ <=>
!! âʹlǧǧ+N+Pl+Gen: õõʹlji
     OnSetC _ :õ RelVowLengthening ;
!Distinguishing mainstam and maainstam
      Cns: _ õ (u:|i:) (ʹ:|0:ʹ)  Cns: %{Â1%}: Cns:+  %^VOWL: ; 

   OnSetC _ õ Cns:+ [%^V2VV: %^CC2C:|%^VOWXL: %^CShort:] ; 
!€# v0õrr%^V2VV%^CC2C
!€0 võõr000
!€# s0õll%^V2VV%^CC2C
!€0 sõõl00


"Even syllabic verbs I, relative vowel lengthening 0:ä"
!! __@RULENAME@__ - deriving +V+Inf in TIETTED: uudd > uʹvdded
 0:ä <=> OnSetC _ ä (ʹ:|0:ʹ) Cns:+ %^V2VV: (%^CC2C:) RBound ;
!€# 0äldd%^V2VV%^CC2C
!€0 ääld000
 OnSetC _ :ä RelVowLengthening ; 
!Distinguishing mainstam and maainstam
!- kååʹmmerd, maainâst Vowel lengthening in INDPRSSG3 and IMPSG2
     _ ä (i) (ʹ:|0:ʹ) Cns:+ [%{E1%}:|%{A1%}:|%{Â1%}:] Cns:+ [%^VOWL:|%^IMPSG2:|%^INDPRSSG3:] ;




"Even syllabic verbs I, relative vowel lengthening 0:a"
!! __@RULENAME@__ - deriving +V+Inf in MAINSTED: maainstam, mainstam
 0:a <=>
 OnSetC _ :a [RelVowLengthening] ;
!€# 0äldd%^V2VV%^VOWRaise%>už
!€0 aald0000už

!Distinguishing mainstam and maainstam
  OnSetC _ a  i  (ʹ:|0:ʹ) Cns: %{Â1%}: Cns:+  %^VOWL: ;
!€# m0ain%{Â1%}st%^VOWL%^IMPSG2
!€0 maainâst0
!   [ b | ʒ | (ǯ) ǯ | (g) g | i b | i n |(k) k |l|m |(n) n | n v|r g|r| r t | u d|u l| u n | u ŋ | u r] _ s t (%^VOWL:) %^IMPSG2: ;
!   [ ʹ đ đ | ʹ ǩ |ʹ l|ʹ l ǩ |ʹ m m |ʹ p] _ s t %^IMPSG2: ;

   Cns: _ a  i  (ʹ:|0:ʹ) Cns: %{Â1%}: Cns:+  %^VOWL: ; 
   Cns: _ a  i  Cns: %{Â1%}: Cns:+  %^VOWL: ;
   Cns: _ a (u:|i:) (ʹ:|0:ʹ)  Cns:+ [%{Â1%}:|%{A1%}:|%{E1%}:] Cns:+  [%^VOWL:|%^IMPSG2:|%^INDPRSSG3:] ; 




"Even syllabic verbs I, relative vowel lengthening 0:o"
!! __@RULENAME@__ -  simultaneous lengthening and lowering: juʹrdded > joordam juʹrdded+V+Ind+Prs+Sg1
 0:o <=>
!!
   OnSetC _ [:o  RelVowLengthening | u:o [%{VU%}:u|(ʹ:) v:] (ʹ:|0:ʹ) [d|l|r|ʒ] (%^VOWL: %^CShort:|%^VOWXL: (%^CShort:)) %^VOWLower:|u:o  (ʹ:) v:  (0:ʹ) [d d:|l l:|r r:|ʒ ʒ:] (%^VOWXL: (%^CShort:)) %^VOWLower:];
  OnSetC _ å:  (ʹ:|0:ʹ) CCforPrecedingV2VV %^V2VV: %^VOWRaise: ;
!€# k0åʹll%^V2VV%^VOWRaise%>in
!€0 kooʹll0000in
   OnSetC _ o Cns:+ [ %^V2VV: %^CC2C: |%^VOWXL: %^CShort:] ; 
!! - deriving toll+N+Sg+Gen: tool
!€# t0oll%^V2VV%^CC2C
!€0 tool000

!2013-09-13!      OnSetC _ o (u:|i:) (ʹ:|0:ʹ) [[Cns:+|Cns:+ ((%ˈ:|:%ˈ)|:%ˈ) Cns:+]|Cns: %{Â1%}: Cns:+] [%^VOWL:|%^VOWXL:] ;


"Even syllabic nouns I, relative vowel lengthening i"
!! __@RULENAME@__ - ǩiđđ:ǩiiđ
 0:i <=> OnSetC _ :i RelVowLengthening  ;
         OnSetC _ i  (ʹ:)  CNoJ:  %{Â1%}:  Cns:+  [%^VOWL:|%^VOWXL:] ;
   OnSetC _ e:i [Cns:+|Cns:+ ((%ˈ:|:%ˈ)|:%ˈ) Cns:+] %^V2VV: %^VOWRaise: ;
!€# p0ess%^V2VV%^VOWRaise%>už
!€0 piiss000už
   # Cns:* _ i Cns:+ [%^VOWXL: %^CShort:|%^V2VV: %^CC2C:] ;
!€# ǩ0iđđ%^V2VV%^CC2C
!€0 ǩiiđ00



"Even syllabic nouns, relative vowel extra lengthening u not followed by v"
!! __@RULENAME@__ - declension of nouns kunn > kuun
!! {{{
 0:u <=> OnSetC _ [o:u|u] Cns: Cns:* %^V2VV: %^CC2C: ;
 OnSetC _ [o:u|u] RelVowLengtheningNOTu ; !!= @CODE@
! (ʹ:|0:ʹ) [[s: s:|n n:] %^VOWXL:|CNoV: Cns:+ %^VOWL: %^CShort:] RBound ;
! relative lengthening of u
!  Cns: _ u: [(%ʹ:|0:%ʹ) [CNoV:+|CNoV:+ (%ˈ:|:%ˈ) Cns:+]] [%^VOWL:|%^VOWXL:] (%^CShort:) [%>|#|(%^VOWRaise:) (%^PALE:|%^PALÄ:) %^PAL:] ;
           Cns: _  :u (%ʹ:|0:%ʹ)  CNoV: %{Â1%}: Cns:+   %^VOWL: [%>|#|(%^PALE:|%^PALÄ:) %^PAL:] ; !!= @CODE@
!OnSetC _ o:u (%ʹ:|0:%ʹ) Cns:+ %^VOWL: %^CShort: %^VOWRaise: (%^PALE:|%^PALÄ:) %^PAL: %> e ;
!OnSetC _ o:u (%ʹ:|0:%ʹ) Cns:+ %^VOWL: %^CShort: %^VOWRaise: ;
!! }}}


!! simultaneous lengthening and raising, hmm: xfst ordering might be easier



!! !VOWEL DUMMY LOSS


!! !SEMI VOWELS

 
"Even syllabic nouns, for j>i"
!! __@RULENAME@__ - sijdd > siid This will need a special extra-lengthening rule
 j:i <=> OnSetC i: (%{ʹ%}:) _ (ʹ:) [ d d:|s s:] [%^VOWL: %^CShort:|%^VOWXL:|%^VOWLower:|%^VOWSH: %^CLong: %^VOWLower: (%^PALE:|%^PALÄ:) %^PAL:] ;
!€# ijss%^VOWXL%^CShort%>âs
!€0 iis0000âs


"Even syllabic verbs,  for v>u"
!! __@RULENAME@__ - uvdd > ouʹdde
!! {{{
 v:u <=> OnSetC (0:o) u: (ʹ:) _ (0:ʹ) [ʒ ʒ:|d d:|l l:|r r:] [%^VOWL: %^CShort:|%^VOWXL: %^CShort:] ; !!= @CODE@
!! }}}

 "Even syllabic nouns, for h>u"
!! __@RULENAME@__ - luhtt  s s: ...
!! {{{
 h:u <=> V: (%{ʹ%}:) _ (ʹ:|0:ʹ) [s s:|š š:|t t:|c c:|č č:] [%^VOWL: %^CShort:|%^VOWXL:|%^VOWSH: %^CLong:  %^VOWLower: (%^PALE:|%^PALÄ:) %^PAL:] ; !!= @CODE@
!! }}}

"trisyllabic verbs and doer derivations, i>j"
!! __@RULENAME@__ -
!! {{{
 i:j <=> _ %>: Vow ; !!= @CODE@
!! }}}

!! !VOWELS TENSE vs LAX 2012-11-28


"Even-syllabic nouns, for â:e"
!! __@RULENAME@__ -  ǩiâpp:ǩieʹpˈpe
 â:e <=> i _ :ʹ [Cns:+|Cns:+ 0:%ˈ Cns:+] PrecedePALAllo %^PALE: %^PAL: ;
!! +N+Sg+Ill
!€# miâ0rr%^PALE%^PAL%>e
!€0 mieʹrr000e
!€# piâ0ss%^PALE%^PAL
!€0 pieʹss00
      u _ :ʹ [Cns:+|Cns:+ 0:%ˈ Cns:+] PrecedePALAllo %^PALE: %^PAL: ;
!€# kuâ0ll%^PALE%^PAL
!€0 kueʹll00

!! !VOWEL and ZERO ALTERNATION


"Realization for %{E1%} in e "
!! __@RULENAME@__ - kååʹmmerd
!! {{{
 %{E1%}:e <=> _ (ʹ:|0:ʹ) Cns:+ (%^VOWL:) %^IMPSG2: ; !!= @CODE@
!! }}}

"Realization for %{A1%} in a " 
!! __@RULENAME@__ - kåʹmrded, vuejat, maainâst
!! {{{
 %{A1%}:a <=> _ (ʹ:|0:ʹ) Cns:+ (%^VOWL:) %^IMPSG2: ; !!= @CODE@
              _ (ʹ:|0:ʹ|ʼ:) [i|Cns:+] (%^VOWL:) %^INDPRSSG3: ; !!= @CODE@
!! }}}

"Realization for %{Â1%} in â " 
!! __@RULENAME@__ - maainâst
 %{Â1%}:â <=> Cns _ Cns:+ (%^VOWL:) %^IMPSG2: ;
!€# m0ain%{Â1%}st%^VOWL%^IMPSG2
!€0 maainâst00



"Realization for %{E1%} in a" 
!! __@RULENAME@__ - 
!! {{{
 %{E1%}:a <=> _ (ʹ:|0:ʹ) [i|Cns:+] (%^VOWL:) %^INDPRSSG3: ; !!= @CODE@
!! }}}

"Realization for %{Â1%} in a" 
!! __@RULENAME@__ - 
 %{Â1%}:a <=> _ (ʹ:|0:ʹ|ʼ:) [i|Cns:+] (%^VOWL:) %^INDPRSSG3: ;
!€# m0ain%{Â1%}st%^VOWL%^INDPRSSG3
!€0 maainast00
         
!! !THE NON-ORTHOGRAPHIC SYLLABLE 


"Loss of ʼ when preceded by vowel"
!! __@RULENAME@__ - This is a temporary solution to  "ʼ" in võʹllʼjed
!! {{{
 ʼ:0 <=> :Vow _ ; !!= @CODE@ 
!! }}}

"%{A1%}:ʼ when subseqent syllable has vowel v"
!! __@RULENAME@__ - This is a temporary solution to  "ʼ" in võʹllʼjed 2013-08-29
!! {{{
%{A1%}:ʼ <=> [l l|n n] _ i: %> Vow: ; !!= @CODE@ 
!! }}}

!! !PALATALIZATION


!2013-09-11!"Even-syllabic verbs I, Palatalization mark between V and C"
!2013-09-11!!! __@RULENAME@__ - used +Ind+Prs+Pl3, +Ind+Prt+Pl3
!2013-09-11! %{ʹ%}:ʹ <=> OnSetC (Vow:0|:Vow) Vow:Vow _ [Cns:Cns Cns:*|Cns:Cns+ (%ˈ:|:%ˈ) Cns:+|Cns:Cns+ 0:%ˈ Cns:+] (%^VOWSH: (%^CLong:)|%^VOWTense:|%^VOWL: %^CShort:) (%^VOWLower:|%^VOWRaise:) (%^VOWFront:|%^VOWTense:0) (X3:) (%^PALE:|%^PALÄ:) %^PAL: RBound ;



"0:ʹ as transfer from left of v:u"
!! __@RULENAME@__ - used +Ind+Prs+Pl3, uʹvdded+V+Ind+Prs+Pl3: ouʹdde
0:ʹ <=>      v:u _ [ʒ ʒ:|d d:|l l:|r r:]       PrecedePALNo  (%^PALE:|%^PALÄ:) %^PAL: ; 
         ʹ:0 v:u _ [ʒ ʒ:|d d:|l l:|r r:]       PrecedePALNo  ((%^PALE:|%^PALÄ:) %^PAL:) RBound ;
              h:u _ [c c:|č č:|s s:|š š:|t t:]  PrecedePALNo  (%^PALE:|%^PALÄ:) %^PAL: ;
             Vow: _ [Cns:+|Cns:+ (%ˈ:|0:%ˈ) Cns:+] (i:) PrecedePALNo  (%^PALE:|%^PALÄ:) %^PAL: ;
!€# saa0kk%^VV2V%^PAL%>e
!€0 s0aʹǩǩ000e


!! The left context allows for:
!! Vowel shortening, and j/v > Vow 



 
"Even-syllabic verbs I, Palatalization of g:ǧ and k:ǩ"
!! __@RULENAME@__ -
 Cx:Cy <=> Vow: (ʹ:|0:ʹ) (Cns:)     _ Cx: (%^VV2V:|%^VOWSH: (%^CLong:)) NeutrHighBack (X3:) (%^PALE:|%^PALÄ:) %^PAL: RBound ;
           Vow: (ʹ:|0:ʹ) (Cns:) Cx: _     (%^VV2V:|%^VOWSH: (%^CLong:)) NeutrHighBack (X3:) (%^PALE:|%^PALÄ:) %^PAL: RBound ;
!with insertion of palatal before
           Vow: (ʹ:|0:ʹ) (Cns:)     _ Cx: (%^VV2V:|%^VOWSH: (%^CLong:)) NeutrHighBack (X3:) (%^PALE:|%^PALÄ:) %^PAL: RBound ;
           Vow: (ʹ:|0:ʹ) (Cns:) Cx: _     (%^VV2V:|%^VOWSH: (%^CLong:)) NeutrHighBack (X3:) (%^PALE:|%^PALÄ:) %^PAL: RBound ;

                where Cx in (g k)
                      Cy in (ǧ ǩ)
                matched ;
!€# jo0kk%^PAL%>e
!€0 joʹǩǩ00e
!€# saa0kk%^VV2V%^PAL%>e
!€0 s0aʹǩǩ000e

"Even-syllabic nouns I, Depalatalization of ǧ and weakening of grade to ǥ"
!! __@RULENAME@__ - vueʹlǧǧed+V+Ind+Prs+Sg1: vuâlǥam
!! {{{
 ǧ:ǥ <=> l    _ ǧ: [%^VOWL:|%^VOWXL:] (%^FRICG:) %^CShort: NeutrHighBack %^PALNo: ; !!= @CODE@

!! }}}
!! derive vueʹlǧǧed+V+Imp+Pl3: vuâlǥǥaz

"Even-syllabic nouns I,  ǧ and weakening of grade to j"
!! __@RULENAME@__ - vueʹlǧǧed+V+Ind+Prs+4: vueʹljet  âʹlǧǧ+N+Sg+Gen: ââʹlj
!! {{{
 ǧ:j <=> l _ ǧ: [%^VOWL: %^CShort:|%^VOWXL: (%^CShort:)] NeutrHighBack ((%^PALE:|%^PALÄ:) %^PAL:) RBound ; !!= @CODE@
!! }}}

"Even-syllabic nouns I, Depalatalization of ǧǧ"
!! __@RULENAME@__ - vueʹlǧǧed+V+Ind+Prs+Sg3: vuâlgg
!! {{{
 ǧ:g <=> Vow: (ʹ:|0:ʹ) (Cns:)          _ (%ˈ:) ǧ: (%^VOWSH: (%^CLong:)) NeutrHighBack       %^PALNo: ; !!= @CODE@
         Vow: (ʹ:|0:ʹ) (Cns:) ǧ: (%ˈ:) _          (%^VOWSH: (%^CLong:)) NeutrHighBack (X3:) %^PALNo: ; !!= @CODE@
!! }}}


"Even-syllabic nouns I, Depalatalization of ǩ:k"
!! __@RULENAME@__ -
!! {{{
 ǩ:k <=>																												    !!= * @CODE@
  Vow: (%ʹ:|0:%ʹ) (l)              _ (%ˈ:|:%ˈ) ǩ: (e: s:) ([%^VOWSH:|%^VOWXSH:] (%^CLong:)) NeutrHighBack       %^PALNo: ;  !!= * @CODE@
  Vow: (%ʹ:|0:%ʹ) (l) ǩ: (%ˈ:|:%ˈ) _              (e: s:) ([%^VOWSH:|%^VOWXSH:] (%^CLong:)) NeutrHighBack (X3:) %^PALNo: ;  !!= * @CODE@
  Vow:0           (s|š)            _                       (%^VOWSH: %^CLong:)  %^RmVow: %> a ;						    !!= * @CODE@
!! }}}

"Even-syllabic nouns, removing palatalization in -est +Loc nouns"
!! __@RULENAME@__ - removing palatalization in +Sg+Ill,  pieʹss:peässa
   ʹ:0 <=>  [Vow:|:Vow] _ [:Cns :Cns+|Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] (e: s:) PrecedePALNo %^PALNo: [RBound|%^RmVow:|%^VC:] ;
!€# čââʹlmteʹm%{XC%}%^CLong%^PALNo%>a
!€0 čââʹlmte0mm000a

!jurdded
                    Vow: _ CNoV: Cns:+ PrecedePALNo %^PALNo: ; 
                         _ v:u ; 



!! !!Consonant QUANTITY CHANGE gradation rules

!! !Weakening Consonant Cluster

!! dealing with relative length changes
!! mõõnnâd : mõʹnne : mõõn 

!"Even syllabic verbs I, cg in relation to Vowel length"
!! !Vow Cx Cx:0 

"Even syllabic verbs I, cg for b"
!! __@RULENAME@__ - used with  neiʹbb+N+Sg+Gen: neeiʹb
!! {{{
 b:0 <=> SurfLongVow  [i (ʹ:|0:ʹ)|(ʹ:|0:ʹ) l] b _ (%^VOWL:|%^VOWXL:) %^CShort: ; !!= @CODE@
!! }}}

"Even syllabic verbs I, cg m:0"
!! __@RULENAME@__ - used with  +Imp+Sg2, +Ind+Prs+ConNeg, 
 m:0 <=> SurfLongVow (ʹ:|0:ʹ)  (s|r|l|p|d:) m: _ (%^VOWL:|%^VOWXL:) %^CShort: ;
         SurfLongVow (ʹ:|0:ʹ)  m _ %^CC2C: ;
!€# sääʹmm%^CC2C
!€0 sääʹm000
         SurfLongVow (ʹ:) h: (ʹ:|0:ʹ)      m: _ (%^VOWL:|%^VOWXL:) %^CShort: ; 
          
"Even syllabic verbs I, cg v:0"
!! __@RULENAME@__ - used with  +Imp+Sg2, +Ind+Prs+ConNeg, teevvad > teev
!! {{{
 v:0 <=> SurfLongVow (i) (ʹ:|0:ʹ) v: _ (%^VOWL:|%^VOWXL:) %^CShort: ; !!= @CODE@
!! }}}

"Even syllabic nouns I, with extra lengthening of vowel ij>ii/uv>uu and dd>d"
!! __@RULENAME@__ - used with  +N+Sg+Nom > +N+Sg+Gen, sijdd > siid

 d:0 <=> d _ (%^V2VV:) %^CC2C: ;
!€# 0äldd%^V2VV%^CC2C
!€0 ääld000
 [(0:o) u: (ʹ:) v:|i: (ʹ:) j:] (0:ʹ) d _  %^VOWXL:  %^CShort: ;
 ! juʹrdded > joordam
          SurfLongVow (ʹ:|0:ʹ) CNoJV  d _ [%^VOWL: %^CShort:|%^VOWXL: (%^CShort:)] ;
                    u: (ʹ:) v:u (0:ʹ) d _  %^VOWXL: (%^CShort:) ; 
!! uʹvdded+V+Ind+Prs+4: uuʹdet
                     u (ʹ:) v:u (0:ʹ) d _  %^VOWL:  %^CShort: ; 
! njeiddad > njeeid
! 2012-06-12 v:u allows for uʹvdded
!! - idd:id, +Imp+Sg2, +Ind+Prs+ConNeg double consonants following vowel and "i" at coda that become single consonants in gradation
        2SgVow (ʹ:|0:ʹ) d _ Vow: [Cns|n j|l m|s ǩ:|s t|š ǩ:|š m]  %^VOWSH: %^CLong: %^RmVow: ; 

"Even syllabic verbs I, cg n:0"
!! __@RULENAME@__ - used with  +Imp+Sg2, +Ind+Prs+ConNeg, jiõnn:jiõn
!! {{{
 n:0 <=> SurfLongVow  (ʹ:|0:ʹ)  (s|r|l|p|d:) n:  _ (a: i:) (%^VOWL:|%^VOWXL:) %^CShort: ; !!= @CODE@
         SurfLongVow  (ʹ:) h: (ʹ:|0:ʹ) n: _ (%^VOWXL:) %^CShort: ; !!= @CODE@
!! }}}
          
"Even syllabic verbs I, cg for ʒ"
!! __@RULENAME@__ - used with  pääʹʒʒelm+N+Sg+Ill: päʹʒlma
!! {{{
 ʒ:0 <=>
!2013-09-19    SurfLongVow (ʹ:|0:ʹ) ʒ _ %^VOWXL: (%^CShort:) ;
      u: (ʹ:) v:u (0:ʹ) ʒ _  %^VOWXL: (%^CShort:) ; !!= @CODE@
        2SgVow (ʹ:|0:ʹ) ʒ _ Vow: [Cns|n j|l m|s ǩ:|s t|š ǩ:|š m]  %^VOWSH:  %^CLong: %^RmVow: ; !!= @CODE@
!! }}}

"Even syllabic verbs I, cg for s"
!! __@RULENAME@__ 
 s:0 <=>  i: j:i (ʹ:|0:ʹ) s _ %^VOWXL: %^CShort: ;
!€# ijss%^VOWXL%^CShort%>âs
!€0 iis0000âs

"Even syllabic verbs I, cg for đ" 
!! __@RULENAME@__ - used with  ǩiđđ:ǩiiđ
 đ:0 <=> đ: _ %^V2VV: %^CC2C: ;
!  Vow: (ʹ:|0:ʹ) đ: _ [%^V2VV: %^CC2C:|%^VOWXL: %^CShort:] ;
!€# ǩ0iđđ%^V2VV%^CC2C
!€0 ǩiiđ00
     	  [DbVow|Diphth] (ʹ:|0:ʹ) đ: _ %^VOWL: %^CShort: ;


"Even syllabic verbs I, cg for r" 
!! __@RULENAME@__ - used with  võrr:võõr
 r:0 <=> r: _ (%^V2VV:) %^CC2C: ;
!  [SurfLongVow (ʹ:|0:ʹ)|u: (ʹ:) v:u (0:ʹ)] r: _  [%^V2VV: %^CC2C:|%^VOWXL: %^CShort:] ;
!€# v0õrr%^V2VV%^CC2C
!€0 võõr000
!€# paarr%^CC2C
!€0 paar00
          [DbVow|Diphth] (ʹ:|0:ʹ) r: _  [%^St2W:|%^VOWL: %^CShort:] ; 


"Even syllabic verbs I, cg for l"
!! __@RULENAME@__ - used with  vuʹvll+N+Sg+Gen: vuuʹl
 l:0 <=>
!! deriving 
        l _ %^V2VV: %^CC2C: ;
!           SurfLongVow (ʹ:|0:ʹ) l: _  [%^V2VV: %^CC2C:|%^VOWXL: %^CShort:] ;
!€# t0oll%^V2VV%^CC2C
!€0 tool00
   u: (ʹ:) v:u (0:ʹ) l: _  %^V2VV: %^CC2C: ;
!- used with  vuʹvll+N+Sg+Gen: vuuʹl
!€# vuʹv0ll%^V2VV%^CC2C
!€0 vu0uʹl00

!! deriving påll+N+Sg+Gen: pååll
         [[DbVow|Diphth]|SurfLongVow u] (ʹ:|0:ʹ) l: _  [%^St2W:|%^VOWL: %^CShort:] ; 
!€# kuâ0ll%^VOWL%^CShort%^PAL
!€0 kueʹl0000

"Even syllabic verbs I, cg for g"
!! __@RULENAME@__ - used with  cõõggâlm+N+Sg+Ill: cõglma
!! {{{
 g:0 <=>  2SgVow (ʹ:|0:ʹ) g _ Vow: [Cns|n j|l m|s ǩ:|s t|š ǩ:|š m]  %^VOWSH: %^CLong:  %^RmVow: ; !!= @CODE@
          SurfLongVow  (ʹ:|0:ʹ)  [r|l|d:] g:  _ (%^VOWL:|%^VOWXL:) %^CShort: ; !!= @CODE@
!! }}}

"Even syllabic verbs I, cg for ǧ"
!! __@RULENAME@__ - used with  vueʹlǧǧed+V+Ind+Prs+Sg1: vuâlǥam
!! {{{
 ǧ:0 <=> SurfLongVow (ʹ:|0:ʹ) l [ǧ:j|ǧ:] _ [%^VOWL: %^CShort:|%^VOWXL: (%^CShort:)] NeutrHighBack (%^PALNo:|(%^PALE:|%^PALÄ:) %^PAL:) RBound ; !!= @CODE@
!! }}}


! "Even syllabic verbs I, cg vv:v"
! v:0 <=> :Vow (ʹ:|0:ʹ) v _ (%^VOWL:) %^CShort: ; 

"Even syllabic nouns I, with extra lengthening of vowel V>VV and KK>ǤǤ"
!! __@RULENAME@__ 
!! used with  +N+Sg+Nom > +N+Sg+Gen for cases like
!! {{lookki > looǥǥi}}.
 k:ǥ <=>                                                          
  OnSetC [DbVow|Diphth] (ʹ:) k: _ (i:) (%^VOWRaise:) [%^CC2C:|%^VOWL:  %^CShort:] ;
  OnSetC [DbVow|Diphth] (ʹ:) _  k: (i:) (%^VOWRaise:) [%^CC2C:|%^VOWL:  %^CShort:] ;
  OnSetC SurfLongVow    (ʹ:) k: _    (i:) %^VOWXL: (%^CShort:) ;
  OnSetC SurfLongVow    (ʹ:)    _ k: (i:) %^VOWXL: (%^CShort:) ;
  :Vow (ʹ:) k: _ (%^V2VV:) %^CC2C: ;
  :Vow (ʹ:) _ k: (%^V2VV:) %^CC2C:: ;
!€# j0okk%^V2VV%^CC2C
!€0 jooǥǥ00

"Even syllabic nouns I, with extra lengthening of vowel V>VV and ǩ:j"
!! __@RULENAME@__ 
!! used with
!! {{ > }}.
 ǩ:j <=>   
  OnSetC [DbVow|Diphth] (ʹ:|0:ʹ) ǩ:   _  [%^St2W:|%^VOWL: %^CShort:] ;
  OnSetC [DbVow|Diphth] (ʹ:|0:ʹ)  _  ǩ: [%^St2W:|%^VOWL:   %^CShort:] ; 

  OnSetC  SurfLongVow   (ʹ:|0:ʹ) ǩ: (%ˈ:) _          %^VOWXL: (%^CShort:) ; 
  OnSetC  SurfLongVow   (ʹ:|0:ʹ)          _ (%ˈ:) ǩ: %^VOWXL: (%^CShort:) ; 




"Even syllabic verbs I, Voicing š:ž" 
!! __@RULENAME@__ - ss:zz, +Imp+Sg2, +Ind+Prs+ConNeg, double consonants at coda become voiced in gradation
 š:ž <=> 
 OnSetC [Diphth|DbVow] (ʹ:|0:ʹ) š:       _          %^VOWL:   %^CShort: ;
         OnSetC [Diphth|DbVow] (ʹ:|0:ʹ)          _       š: %^VOWL:   %^CShort: ; 
         OnSetC   Diphth       (ʹ:|0:ʹ) š:  %ˈ:  _          %^VOWXL: (%^CShort:) ;
         OnSetC   Diphth       (ʹ:|0:ʹ)          _ %ˈ:   š: %^VOWXL: (%^CShort:) ;
         OnSetC   AllDbVow     (ʹ:|0:ʹ) š: (%ˈ:) _          %^VOWXL: (%^CShort:) ;
         OnSetC   AllDbVow     (ʹ:|0:ʹ)          _ (%ˈ:) š: %^VOWXL: (%^CShort:) ;
!€# kõõččm0õšš%^VOWXL%^CShort
!€0 kõõččmõõžž00
!         _ %^VC: ;

"Even syllabic verbs I, Voicing c:ʒ"
!! __@RULENAME@__ - cc:zz, +Imp+Sg2, +Ind+Prs+ConNeg, double consonants at coda become voiced in gradation
 c:ʒ <=> SurfLongVow (ʹ:|0:ʹ) c: _    (%^VOWL:|%^VOWXL:) %^CShort: ;
         SurfLongVow (ʹ:|0:ʹ)    _ c: (%^VOWL:|%^VOWXL:) %^CShort: ;
!€# čääʹcc%^VOWL%^CShort
!€0 čääʹʒʒ00
                                   _ %^VC: ; 

"Even syllabic verbs I, Voicing č:j"
!! __@RULENAME@__ - ss:zz, +Imp+Sg2, +Ind+Prs+ConNeg, double consonants at coda become voiced in gradation
!! {{{
 č:j <=> OnSetC SurfLongVow (ʹ:|0:ʹ) č: _    (%^VOWL:|%^VOWXL:) %^CShort: ; !!= @CODE@
         OnSetC SurfLongVow (ʹ:|0:ʹ)    _ č: (%^VOWL:|%^VOWXL:) %^CShort: ; !!= @CODE@
                                             _ %^VC: ; !!= @CODE@
!! }}}

"Even syllabic verbs I, Voicing after long vowel or diphthong s:z s:z" 
!! __@RULENAME@__ - ss:zz, +Imp+Sg2, +Ind+Prs+ConNeg, double consonants at coda become voiced in gradation
!! {{{
 s:z <=> OnSetC [Diphth|DbVow] (ʹ:|0:ʹ) s:       _          %^VOWL:   %^CShort: ; !!= @CODE@ 
         OnSetC [Diphth|DbVow] (ʹ:|0:ʹ)          _       s: %^VOWL:   %^CShort: ; !!= @CODE@ 
         OnSetC   Diphth       (ʹ:|0:ʹ) s:  %ˈ:  _          %^VOWXL: (%^CShort:) ; !!= @CODE@ 
         OnSetC   Diphth       (ʹ:|0:ʹ)          _ %ˈ:   s: %^VOWXL: (%^CShort:) ; !!= @CODE@ 
         OnSetC   AllDbVow     (ʹ:|0:ʹ) s: (%ˈ:) _          %^VOWXL: (%^CShort:) ; !!= @CODE@ 
         OnSetC   AllDbVow     (ʹ:|0:ʹ)          _ (%ˈ:) s: %^VOWXL: (%^CShort:) ; !!= @CODE@ 
!! deriving cieʹǩǩes+N+Sg+Gen: ceäkˈkaz
  OnSetC :Vow+ (ʹ:|0:ʹ) [Cns:+|Cns:+ (%ˈ:|:%ˈ) Cns:+] e: _ PrecedePALNo (%^PALNo:|(%^PALE:|%^PALÄ:) %^PAL:) %^VC: ; !!= @CODE@ 
!! }}}

         


"Even syllabic verbs I, third consonant loss" 
!! __@RULENAME@__ - lgg:lǥ, Vow Ca Cx Cx:0, +Imp+Sg2, +Ind+Prs+ConNeg, double consonants following consonant at coda that become single consonants in gradation
!! {{{
 Cx:0 <=> SurfLongVow  (ʹ:|0:ʹ)  [s|r|l|p|d:] Cx: _ (%^VOWL:|%^VOWXL:) %^CShort: ; !!= @CODE@
          SurfLongVow  (%{ʹ%}:) h: (ʹ:|0:ʹ) Cx:  _ (%^VOWL:|%^VOWXL:) %^CShort: ; !!= @CODE@
                where Cx in (ŋ č c s) ; !!= @CODE@
!! }}}

"Even syllabic verbs I, t>đ" 
!! __@RULENAME@__ - tt:đ
!! {{{

 t:đ  <=> SurfLongVow (ʹ:|0:ʹ) _ t:0 (%^VOWL:|%^VOWXL:) %^CShort: ; !!= @CODE@
!! }}}


"Even syllabic nouns I, p>v" 
!! __@RULENAME@__ - pp:v
 p:v  <=> SurfLongVow (ʹ:|0:ʹ) _ p:0  %^CC2C: ;
!€# ǩiâpp%^CC2C
!€0 ǩiâv00



"Even syllabic verbs I, second consonant loss t:0"
!! __@RULENAME@__ -
!! {{{
 t:0 <=> SurfLongVow              (ʹ:|0:ʹ) ([s|r|l]) t: _ (%^VOWL:|%^VOWXL:) %^CShort: ; !!= @CODE@
         SurfLongVow (%{ʹ%}:) h: (ʹ:|0:ʹ)           t: _ (%^VOWL:|%^VOWXL:) %^CShort: ; !!= @CODE@
!! }}}

"Even syllabic verbs I, second consonant loss p:0"
!! __@RULENAME@__ -
 p:0 <=> SurfLongVow (ʹ:|0:ʹ) (Cns:) p: _ %^CC2C: ;
!€# ǩiâpp%^CC2C
!€0 ǩiâv00
          2SgVow (ʹ:|0:ʹ)            p _ Vow: [Cns|n j|l m|s ǩ:|s t|š ǩ:|š m]  %^VOWSH:  %^RmVow: ;  

"Even syllabic verbs I, second consonant loss k:0"
!! __@RULENAME@__ -
!! {{{
 k:0 <=> SurfLongVow  (ʹ:|0:ʹ)  [s|r|l|p] k:  _ (%^VOWL:|%^VOWXL:) %^CShort: ; !!= @CODE@
!       SurfLongVow  (%{ʹ%}:) h: (ʹ:|0:ʹ) k: _ (%^VOWL:|%^VOWXL:) %^CShort: ;
!! }}}

"Even syllabic verbs I, second consonant loss ǩ:0"
!! __@RULENAME@__ -
!! {{{
 ǩ:0 <=>
  SurfLongVow  (ʹ:|0:ʹ)  [s|r|l|p] ǩ:  _ (%^VOWL:|%^VOWXL:) %^CShort: ; !!= @CODE@
!          SurfLongVow  (%{ʹ%}:) h: (ʹ:|0:ʹ) ǩ: _ (%^VOWL:|%^VOWXL:) %^CShort: ;
!! }}}


"Even syllabic verbs I, lgg>lǥ" 
!! __@RULENAME@__ - vueʹlǧǧed+V+Ind+Prs+Sg2 => vuâlǥak
!! {{{
 g:ǥ  <=> SurfLongVow (ʹ:0|%{ʹ%}:0) [l|r|d:]    _ g: (%^VOWL:|%^VOWXL:) %^FRICG:0  %^CShort: ; !!= @CODE@
          SurfLongVow (ʹ:0|%{ʹ%}:0) [l|r|d:] g: _    (%^VOWL:|%^VOWXL:) %^FRICG:0 %^CShort: ; !!= @CODE@
!! }}}

!vueʹlǧǧed+V+Imp+Pl3: vuâlǥǥaz
!"Even syllabic verbs I, lgg>lǥǥ" ! lgg:lǥǥ

"Odd syllabic nouns I, lǥ>lg"
!! __@RULENAME@__ - ǩeâlǥal+N+Sg+Gen: ǩeâlggal
!! {{{
ǥ:g <=> 2ShortVow   l _ 0: Vow: Cns [%^VOWSH: %^CLong:|%^VOWXSH: (%^CLong:)]  ; !!= @CODE@
!! }}}
!€# ǩeâlǥ0al
!€0 ǩeâlggal

"even syllabic verbs I, 'lǧǧ>'lj" 
!! __@RULENAME@__ - vueʹlǧǧed+V+Ind+Prt+Pl1 => vuõʹljim
!! {{{
 g:j  <=> SurfLongVow   :ʹ [l|r|d:]  _ g:0 (%^VOWL:|%^VOWXL:) %^CShort: NeutrHighBack (X3:) (%^PALE:|%^PALÄ:) %^PAL: ; !!= @CODE@
!! }}}








!! !!Rules for cleaning up and composing end result

!! * Strengthening Consonant Cluster
!! * V shortening and C lengthening 
!! * %^CLong is generally used in conjunction with a preceding %^VOWSH
!! * In verbs this will be observed in finite forms
!! * In derivational morphology, e.g. -škuetted, this is not the case

"Orthographic Consonant lengthening Weak to strong or extra strong %{XC%}:m"
!! __@RULENAME@__
 %{XC%}:m <=>  Vow: (ʹ:|0:ʹ) m _ [%^W2XSt:|%^CLong:] %^PALNo: ;
!€# čââʹlmteʹm%{XC%}%^CLong%^PALNo%>a
!€0 čââʹlmte0mm000a

"Orthographic Consonant lengthening Weak to strong or extra strong %{XC%}:v"
!! __@RULENAME@__
 %{XC%}:v <=>  Vow: (ʹ:|0:ʹ) v _ %^W2XSt: ;


"Orthographic Consonant lengthening Weak to strong or extra strong %{XC%}:n"
!! __@RULENAME@__
 %{XC%}:n <=>  Vow: (ʹ:|0:ʹ) n _ %^W2XSt: ;
!€# mään%{XC%}%^W2XSt%^VOWRaise%>u
!€0 m0ann000u

"Orthographic Consonant lengthening Weak to strong or extra strong %{XC%}:r"
!! __@RULENAME@__
 %{XC%}:r <=>  Vow: (ʹ:|0:ʹ) r _ %^W2XSt: ;
!€# puär0%{XC%}%^W2XSt%^VOWRaise%>u
!€0 puårˈr000u

"Orthographic Consonant lengthening Weak to strong or extra strong %{XC%}:l"
!! __@RULENAME@__
 %{XC%}:l <=>  Vow: (ʹ:|0:ʹ) l _ %^W2XSt: ;
!€# suâl0%{XC%}%^W2XSt%^VOWRaise%>u
!€0 suõlˈl000u

"Orthographic Consonant lengthening Weak to strong or extra strong %{XC%}:k"
!! __@RULENAME@__
 %{XC%}:k <=>  Vow: (ʹ:|0:ʹ) k _ %^W2XSt: ;




"Orthographic Consonant lengthening in INDPRSSG3 and IMPSG2 0:m"
!! __@RULENAME@__ - kåʹmrded+V+Imp+Sg2: kååʹmmerd 
 0:m <=> SurfLongVow (ʹ:|0:ʹ)  m _ [%{E1%}:] Cns:+ [%^IMPSG2:|%^INDPRSSG3:] ; !!= @CODE@
        m _ %^VOWSH: %^CLong: ;

!€# ooum0%^VOWSH%^CLongu
!€0 0oumm00u


"Consonant lengthening 0:b"
!! __@RULENAME@__ - 
!! {{{
 0:b <=>  2ShortVow  (ʹ:|0:ʹ) [l] b _ Vow: Cns [%^VOWSH: %^CLong:|%^VOWXSH: (%^CLong:)] (%^PALNo:) ; !!= @CODE@
!! }}}

"Consonant lengthening 0:p"
!! __@RULENAME@__ - 
!! {{{
 0:p <=>  2ShortVow  (ʹ:|0:ʹ) [m|l] p _ Vow: Cns [%^VOWSH: %^CLong:|%^VOWXSH: (%^CLong:)] (%^PALNo:) ; !!= @CODE@
!! }}}

"Orthographic Consonant lengthening in INDPRSSG3 and IMPSG2 0:n"
!! __@RULENAME@__
 0:n <=>  Vow: (ʹ:|0:ʹ) n _ %^W2XSt: ;
!€# mään0%^W2XSt%^VOWRaise%>u
!€0 m0ann000u
 OnSetC  2ShortVow (ʹ:|0:ʹ) n _ (j â: l) [%^VOWSH: %^CLong:|%^VOWXSH: (%^CLong:)] ; 


"Consonant lengthening 0:d"
!! __@RULENAME@__ - juurd+N+Sg+Gen: jurddi
!! {{{
 0:d <=> 2ShortVow (ʹ:) (r) d _ (i:)  [%^VOWSH: %^CLong:|%^VOWXSH: (%^CLong:)] ; !!= @CODE@
!! }}}

"Consonant lengthening 0:t"
!! __@RULENAME@__ - 
!! {{{
 0:t <=> 2ShortVow (ʹ:) (s) :t _ [%^VOWSH: %^CLong:|%^VOWXSH: (%^CLong:)] ; !!= @CODE@
!! }}}

"Consonant lengthening 0:c"
!! __@RULENAME@__ - 
!! {{{
 0:c <=>  2ShortVow  (ʹ:|:ʹ) p c _ [%^VOWSH: %^CLong:|%^VOWXSH: (%^CLong:)] ; !!= @CODE@
!! }}}

"Consonant lengthening 0:đ"
!! __@RULENAME@__ - čââʹđ+N+Sg+Gen: čâđđam
!! {{{
 0:đ <=>  2ShortVow  (ʹ:)  đ _ [%^VOWSH: %^CLong:|%^VOWXSH: (%^CLong:)] ; !!= @CODE@
!! }}}

"Consonant lengthening 0:l"
!! __@RULENAME@__ - 
!! {{{
 0:l <=>  2ShortVow  (ʹ:)  l (0:%ˈ) _ (e: s:) [%^VOWSH: %^CLong:|%^VOWXSH: (%^CLong:)] ; !!= @CODE@
!! }}}

"Consonant lengthening 0:g"
!! __@RULENAME@__ - ǩeâlǥal+N+Sg+Gen: ǩeâlggal
!! {{{
 0:g <=>  2ShortVow   l ǥ: _ Vow: Cns [%^VOWSH: %^CLong:|%^VOWXSH: (%^CLong:)] ; !!= @CODE@
!! }}}

"Consonant lengthening 0:ǧ"
!! __@RULENAME@__ - 
!! {{{
 0:ǧ <=>  2ShortVow  :ʹ  l ǥ: _ Vow: Cns [%^VOWSH: %^CLong:|%^VOWXSH: (%^CLong:)] ((%^PALE:|%^PALÄ:) %^PAL:) ; !!= @CODE@
!! }}}

"Consonant lengthening 0:ǩ"
!! __@RULENAME@__ - 
!! {{{
 0:ǩ <=>  2ShortVow  :ʹ  l ǩ _ Vow: Cns [%^VOWSH: %^CLong:|%^VOWXSH: (%^CLong:)] ((%^PALE:|%^PALÄ:) %^PAL:) ; !!= @CODE@
!! }}}

"Consonant lengthening 0:k"
!! __@RULENAME@__ - 
!! {{{
 0:k <=>  2ShortVow  (ʹ:) [l|s|š] :k _ Vow: Cns [%^VOWSH: %^CLong:|%^VOWXSH: (%^CLong:)] (%^PALNo:) ; !!= @CODE@
!! }}}

!! !!CONSONANT QUALITY CHANGE

"Consonant quality change j:ǧ"
!! __@RULENAME@__ - 
 j:ǧ <=>  2ShortVow  :ʹ  l _ 0: Vow: Cns [%^VOWSH: %^CLong:|%^VOWXSH: (%^CLong:)] ((%^PALE:|%^PALÄ:) %^PAL:) ;

"Consonant quality change ǥ:ǧ"
!! __@RULENAME@__ 
 ǥ:ǧ <=>  2ShortVow  :ʹ  l _ 0: Vow: Cns [%^VOWSH: %^CLong:|%^VOWXSH: (%^CLong:)] (%^PALE:|%^PALÄ:) %^PAL: ;
!Dummy

"Devoicing ž:š"
!! __@RULENAME@__
 ž:š <=> Cns %{Â1%}:0 _ ;
!€# taarb%{Â1%}ž%>am
!€0 taarb0š0am

!! !Pedagogical X3 length mark after diphthongs in vertical line %ˈ 

!! !Adding X3 length mark
"Consonant X3 lengthening after diphthong in vertical line %ˈ "
!! __@RULENAME@__ - used in +N+Sg+Ill __jeäll:jiâlˈlu, __
!!b c č ǯ d đ g ǧ k ǩ l m n p r s š t v also htˈt nˈnj
 0:%ˈ <=> Diphth (ʹ:|0:ʹ)     b  _ b      (e: s:) [[%^VOWSH:|%^VOWXSH:] %^CLong:|(%^VOWRaise:) ((%^PALE:|%^PALÄ:) %^PAL:) %^CC2CCC:] ;
          Diphth (ʹ:|0:ʹ)     c  _ c      (e: s:) [[%^VOWSH:|%^VOWXSH:] %^CLong:|(%^VOWRaise:) ((%^PALE:|%^PALÄ:) %^PAL:) %^CC2CCC:] ;
          Diphth (ʹ:|0:ʹ)     č  _ č      (e: s:) [[%^VOWSH:|%^VOWXSH:] %^CLong:|(%^VOWRaise:) ((%^PALE:|%^PALÄ:) %^PAL:) %^CC2CCC:] ;
          Diphth (ʹ:|0:ʹ)     ǯ  _ ǯ      (e: s:) [[%^VOWSH:|%^VOWXSH:] %^CLong:|(%^VOWRaise:) ((%^PALE:|%^PALÄ:) %^PAL:) %^CC2CCC:] ;
          Diphth (ʹ:|0:ʹ)     d  _ d      (e: s:) [[%^VOWSH:|%^VOWXSH:] %^CLong:|(%^VOWRaise:) ((%^PALE:|%^PALÄ:) %^PAL:) %^CC2CCC:] ;
          Diphth (ʹ:|0:ʹ)     đ  _ đ      (e: s:) [[%^VOWSH:|%^VOWXSH:] %^CLong:|(%^VOWRaise:) ((%^PALE:|%^PALÄ:) %^PAL:) %^CC2CCC:] ;
          Diphth (ʹ:|0:ʹ)     g: _ g:     (e: s:) [[%^VOWSH:|%^VOWXSH:] %^CLong:|(%^VOWRaise:) ((%^PALE:|%^PALÄ:) %^PAL:) %^CC2CCC:] ;
          Diphth (ʹ:|0:ʹ)     ǧ: _ ǧ:     (e: s:) [[%^VOWSH:|%^VOWXSH:] %^CLong:|(%^VOWRaise:) ((%^PALE:|%^PALÄ:) %^PAL:) %^CC2CCC:] ;
          Diphth (ʹ:|0:ʹ)     k: _ k:     (e: s:) [[%^VOWSH:|%^VOWXSH:] %^CLong:|(%^VOWRaise:) ((%^PALE:|%^PALÄ:) %^PAL:) %^CC2CCC:] ;
          Diphth (ʹ:|0:ʹ)     ǩ: _ ǩ:     (e: s:) [[%^VOWSH:|%^VOWXSH:] %^CLong:|(%^VOWRaise:) ((%^PALE:|%^PALÄ:) %^PAL:) %^CC2CCC:] ;
          Diphth (ʹ:|0:ʹ)     l  _ l      (e: s:) [[%^VOWSH:|%^VOWXSH:] %^CLong:|(%^VOWRaise:) ((%^PALE:|%^PALÄ:) %^PAL:) %^CC2CCC:] ;
!€# kuâl0l%^CC2CCC%>a
!€0 kuâlˈl00a
          Diphth (ʹ:|0:ʹ)     m  _ m      (e: s:) [[%^VOWSH:|%^VOWXSH:] %^CLong:|(%^VOWRaise:) ((%^PALE:|%^PALÄ:) %^PAL:) %^CC2CCC:] ;
          Diphth (ʹ:|0:ʹ)     n  _ n  (j) (e: s:) [[%^VOWSH:|%^VOWXSH:] %^CLong:|(%^VOWRaise:) ((%^PALE:|%^PALÄ:) %^PAL:) %^CC2CCC:] ;
          Diphth (ʹ:|0:ʹ)     p  _ p      (e: s:) [[%^VOWSH:|%^VOWXSH:] %^CLong:|(%^VOWRaise:) ((%^PALE:|%^PALÄ:) %^PAL:) %^CC2CCC:] ;
          Diphth (ʹ:|0:ʹ)     r  _ r      (e: s:) [[%^VOWSH:|%^VOWXSH:] %^CLong:|(%^VOWRaise:) ((%^PALE:|%^PALÄ:) %^PAL:) %^CC2CCC:] ;
          Diphth (ʹ:|0:ʹ)     s  _ s      (e: s:) [[%^VOWSH:|%^VOWXSH:] %^CLong:|(%^VOWRaise:) ((%^PALE:|%^PALÄ:) %^PAL:) %^CC2CCC:] ;
          Diphth (ʹ:|0:ʹ)     š  _ š      (e: s:) [[%^VOWSH:|%^VOWXSH:] %^CLong:|(%^VOWRaise:) ((%^PALE:|%^PALÄ:) %^PAL:) %^CC2CCC:] ;
          Diphth (ʹ:|0:ʹ) (h) t _ t       (e: s:) [[%^VOWSH:|%^VOWXSH:] %^CLong:|(%^VOWRaise:) ((%^PALE:|%^PALÄ:) %^PAL:) %^CC2CCC:] ;
          Diphth (ʹ:|0:ʹ)     v _ v       (e: s:) [[%^VOWSH:|%^VOWXSH:] %^CLong:|(%^VOWRaise:) ((%^PALE:|%^PALÄ:) %^PAL:) %^CC2CCC:] ;


!! !Removing X3 length mark
"Removing Consonant X3 length mark after diphthong in vertical line %ˈ LEFT ARROW"
!! __@RULENAME@__ - deriving 
!!b c č ǯ d đ g ǧ k ǩ l m n p r s š t v also htˈt nˈnj
!! {{{
 %ˈ:0 <= Diphth (ʹ:|0:ʹ)     b _ b     [[%^VOWL:|%^VOWXL:] %^CShort:] ; !!= @CODE@
         Diphth (ʹ:|0:ʹ)     c _ c     [[%^VOWL:|%^VOWXL:] %^CShort:] ; !!= @CODE@
         Diphth (ʹ:|0:ʹ)     č _ č     [[%^VOWL:|%^VOWXL:] %^CShort:] ; !!= @CODE@
         Diphth (ʹ:|0:ʹ)     ǯ _ ǯ     [[%^VOWL:|%^VOWXL:] %^CShort:] ; !!= @CODE@
         Diphth (ʹ:|0:ʹ)     d _ d     [[%^VOWL:|%^VOWXL:] %^CShort:] ; !!= @CODE@
         Diphth (ʹ:|0:ʹ)     đ _ đ     [[%^VOWL:|%^VOWXL:] %^CShort:] ; !!= @CODE@
         Diphth (ʹ:|0:ʹ)     g _ g     [[%^VOWL:|%^VOWXL:] %^CShort:] ; !!= @CODE@
         Diphth (ʹ:|0:ʹ)     ǧ _ ǧ     [[%^VOWL:|%^VOWXL:] %^CShort:] ; !!= @CODE@
         Diphth (ʹ:|0:ʹ)     k _ k     [[%^VOWL:|%^VOWXL:] %^CShort:] ; !!= @CODE@
         Diphth (ʹ:|0:ʹ)     ǩ _ ǩ     [[%^VOWL:|%^VOWXL:] %^CShort:] ; !!= @CODE@
         Diphth (ʹ:|0:ʹ)     l _ l     [[%^VOWL:|%^VOWXL:] %^CShort:] ; !!= @CODE@
         Diphth (ʹ:|0:ʹ)     m _ m     [[%^VOWL:|%^VOWXL:] %^CShort:] ; !!= @CODE@
         Diphth (ʹ:|0:ʹ)     n _ n (j) [[%^VOWL:|%^VOWXL:] %^CShort:] ; !!= @CODE@
         Diphth (ʹ:|0:ʹ)     p _ p     [[%^VOWL:|%^VOWXL:] %^CShort:] ; !!= @CODE@
         Diphth (ʹ:|0:ʹ)     r _ r     [[%^VOWL:|%^VOWXL:] %^CShort:] ; !!= @CODE@
         Diphth (ʹ:|0:ʹ)     s _ s     [[%^VOWL:|%^VOWXL:] %^CShort:] ; !!= @CODE@
         Diphth (ʹ:|0:ʹ)     š _ š     [[%^VOWL:|%^VOWXL:] %^CShort:] ; !!= @CODE@
         Diphth (ʹ:|0:ʹ) (h) t _ t     [[%^VOWL:|%^VOWXL:] %^CShort:] ; !!= @CODE@
         Diphth (ʹ:|0:ʹ)     v _ v     [[%^VOWL:|%^VOWXL:] %^CShort:] ; !!= @CODE@
!! }}}

"Removing Consonant X3 length mark after diphthong in vertical line %ˈ RIGHT ARROW"
!! __@RULENAME@__ - deriving 
!!b c č ǯ d đ g ǧ k ǩ l m n p r s š t v also htˈt nˈnj
!! {{{
 %ˈ:0 => Diphth (ʹ:|0:ʹ)     b _ b     [[%^VOWL:|%^VOWXL:] %^CShort:] ; !!= @CODE@
         Diphth (ʹ:|0:ʹ)     c _ c     [[%^VOWL:|%^VOWXL:] %^CShort:] ; !!= @CODE@
         Diphth (ʹ:|0:ʹ)     č _ č     [[%^VOWL:|%^VOWXL:] %^CShort:] ; !!= @CODE@
         Diphth (ʹ:|0:ʹ)     ǯ _ ǯ     [[%^VOWL:|%^VOWXL:] %^CShort:] ; !!= @CODE@
         Diphth (ʹ:|0:ʹ)     d _ d     [[%^VOWL:|%^VOWXL:] %^CShort:] ; !!= @CODE@
         Diphth (ʹ:|0:ʹ)     đ _ đ     [[%^VOWL:|%^VOWXL:] %^CShort:] ; !!= @CODE@
         Diphth (ʹ:|0:ʹ)     g _ g     [[%^VOWL:|%^VOWXL:] %^CShort:] ; !!= @CODE@
         Diphth (ʹ:|0:ʹ)     ǧ _ ǧ     [[%^VOWL:|%^VOWXL:] %^CShort:] ; !!= @CODE@
         Diphth (ʹ:|0:ʹ)     k _ k     [[%^VOWL:|%^VOWXL:] %^CShort:] ; !!= @CODE@
         Diphth (ʹ:|0:ʹ)     ǩ _ ǩ     [[%^VOWL:|%^VOWXL:] %^CShort:] ; !!= @CODE@
         Diphth (ʹ:|0:ʹ)     l _ l     [[%^VOWL:|%^VOWXL:] %^CShort:] ; !!= @CODE@
         Diphth (ʹ:|0:ʹ)     m _ m     [[%^VOWL:|%^VOWXL:] %^CShort:] ; !!= @CODE@
         Diphth (ʹ:|0:ʹ)     n _ n (j) [[%^VOWL:|%^VOWXL:] %^CShort:] ; !!= @CODE@
         Diphth (ʹ:|0:ʹ)     p _ p     [[%^VOWL:|%^VOWXL:] %^CShort:] ; !!= @CODE@
         Diphth (ʹ:|0:ʹ)     r _ r     [[%^VOWL:|%^VOWXL:] %^CShort:] ; !!= @CODE@
         Diphth (ʹ:|0:ʹ)     s _ s     [[%^VOWL:|%^VOWXL:] %^CShort:] ; !!= @CODE@
         Diphth (ʹ:|0:ʹ)     š _ š     [[%^VOWL:|%^VOWXL:] %^CShort:] ; !!= @CODE@
         Diphth (ʹ:|0:ʹ) (h) t _ t     [[%^VOWL:|%^VOWXL:] %^CShort:] ; !!= @CODE@
         Diphth (ʹ:|0:ʹ)     v _ v     [[%^VOWL:|%^VOWXL:] %^CShort:] ; !!= @CODE@
!! 2013-09-13 for looser voikko X3 marker vertical can disappear everywhere
          _ ; !!= @CODE@
!! }}}


