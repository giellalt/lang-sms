! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or feedback@divvun.no

!! !!!Skolt Sámi TWOLC file
!
! $Id$
!
!

!! !!!Introduction
!! The rule file for Skolt Saami is divided into 5 main types:
!! 
!! # Definitions
!! # Consonant shift rules (tbw)
!! # Vowel alternation rules
!! # Consonant gradation rules
!! # Rules for cleaning up and composing end result


!! !!!Alphabets, sets and definitions

!! !!Alphabet

Alphabet 
  a b c d e f g h i j k l m n o p q r s t u v w x y z !!= * @CODE@
  A B C D E F G H I J K L M N O P Q R S T U V W X Y Z !!= * @CODE@
  ä å â õ      !!= * @CODE@ 
  Ä Å Â Õ       !!= * @CODE@
  č ǩ ǯ ǧ ž đ ǥ ʒ š ŋ					!!= * @CODE@
  Č Ǩ Ǯ Ǧ Ž Đ Ǥ Ʒ Š Ŋ					!!= * @CODE@
  ẹ   	      	   !!= * @CODE@ Pedagogical purposes
  Ẹ		   !!= * @CODE@ Pedagogical purposes

  æ ø ö á é í					!!= * @CODE@
  Æ Ø Ö Á						!!= * @CODE@
  É Ó Ú Í À È Ò Ù Ì Ë Ü Ï Ê Ô Û Î Ã Ý þ Ñ Ð		!!= * @CODE@
  é ó ú í à è ò ù ì ë ü ï ê ô û î ã ý þ ñ ð ß ª ß ç	!!= * @CODE@


 ʹ  !! * MODIFIER LETTER PRIME: U+02B9 suprasegmental palatization
 ʼ  !! * MODIFIER LETTER APOSTROPHE: U+02BC Syllable marker that appears in writing 
 ˈ !! * MODIFIER LETTER VERTICAL LINE: U+02C8 as overlong consonant marker that does not appear in writing except for pedagogical purposes

!  for consonant lengthening
!%{XC%}:m
!%{XC%}:v

!%{XC%}:n
!%{XC%}:d
!%{XC%}:r
!%{XC%}:l

!%{XC%}:k


!! !! Trigger symbols
%^Pen:0 !!= * @CODE@ for combinations 2015-09-28 Miikka

 %^RmVow:0 !!= * @CODE@ for removing vowels e, â, a before final consonant in stem
 %^UltRmVow:0 !!= * @CODE@ for removing vowels e, â, a after final consonant in stem
! %^PenRmVow:0 != * @CODE@ for removing penultimate vowels e, â, a, i.e. vowel befor final stem consonant/consonants
 %^RmCns:0 !!= * @CODE@ for removing stem final consonant, e.g. final z in ǩeeʹstes:ǩeâsttez ǩeâstta 
%^1VOW:0 %^2VOW:0 %^3VOW:0 %^4VOW:0 %^5VOW:0 !!= * @CODE@ Vow triggers

 %^VOWLower:0 !!= * @CODE@ i>e, õ>â, â>ä, u>o, o>å
 %^VOWRaise:0 !!= * @CODE@ e>i, ä>e 
! %^PenVOWRaise:0 
! e>i, ä>e
! %^PenVOWLower:0
 ! i>e, õ>â, â>ä, u>o, o>å
 %^LowerI2E:0	 !!= * @CODE@ this is for diphthong in mieʹlǩǩ:meâlkka
! %^PenLowerI2E:0
 ! this is for diphthong in mieʹlǩǩ:meâlkka
 %^U2Õ:0	 !!= * @CODE@ lowers u>õ	 
! %^PenU2Õ:0
! lowers u>õ	 
 %^VOWBack:0	     !!= * @CODE@ Backing e>õ, e>â vuäinn BUT vuõiʹni
! %^PenVOWBack:0
    ! Backing e>õ, e>â vuäinn BUT vuõiʹni

 %^VOWFront:0	     !!= * @CODE@ Fronting
 %^PAL:0 %^PALNo:0 %^PALE:0 %^PALÄ:0 %^PALẸ:0 %^PALÂ:0 %^PALÕ:0   !!= * @CODE@ Palatalisation
! %^PenPAL:0 %^PenPALNo:0 %^PenPALE:0 %^PenPALÄ:0 %^PenPALẸ:0 %^PenPALÂ:0  
!! Penultimate Palatalisation
!%^PenPALÕ:0 %^PenPALÂ:0
%^PALK:0     !!= * @CODE@ causes k:ǩ and g:ǧ

 %^VC:0         !!= * @CODE@ Voicing s:z š:ž
 %^DeVC:0         !!= * @CODE@ devoicing z:s ž:š
 %^Â:0		!!= * @CODE@ Indicates placement of â in stem

!!  !! CHARACTERISTIC BREAKDOWN 2015-02-17
%^VV2V:0        !!= * @CODE@ Shortens vowel 
%^V2VV:0 	!!= * @CODE@ Lengthens vowel
%^CC2C:0 	!!= * @CODE@ Shortens consonant
%^C2CC:0 	!!= * @CODE@ Lengthens consonant
%^CC2CCC:0 	!!= * @CODE@ Strong to Extra Strong consonant
%^CCC2C:0 	!!= * @CODE@ Extra Strong to Weak consonant
%^CCC2CC:0 	!!= * @CODE@ Extra Strong to Strong consonant
%^XYY2XY:0 	!!= * @CODE@ Shortens consonant cluster
%^XY2XYY:0 	!!= * @CODE@ Lengthens consonant cluster
%^XYY2VY:0	!!= * @CODE@ Consonant cluster to vowel and consonant sijdd:siid
%^XYY2VYY:0	!!= * @CODE@ Consonant cluster to vowel and geminate uvdd:ouʹdd
%^XYY2IY:0	!!= * @CODE@ Consonant cluster to i  and consonant kueʹhtt:kueiʹt
%^VY2XYY:0	!!= * @CODE@ Consonant cluster i/u to j/v
%^VYY2XYY:0	!!= * @CODE@ Consonant cluster i/u to j/v

%^KKK2GG:0	!!= * @CODE@ Geminate weakening 
%^KK2GG:0	!!= * @CODE@ Geminate weakening jokk:jooǥǥ
%^KK2G:0		!!= * @CODE@ Geminate weakening	 in allegro pååss:påz
%^K2GG:0	!!= * @CODE@ Geminate weakening čårrõs:čårrõõzz
%^K2G:0		!!= * @CODE@ Geminate weakening	 in allegro čâustõk


!%^PenVV2V:0     
!! Shortens vowel in penultimate syllable
!%^PenV2VV:0     
!! Lengthens vowel in penultimate syllable
!%^PenCC2C:0     
!! Shortens consonant in penultimate syllable
!%^PenCCC2C:0        
!! Extra strong to weak consonant in penultimate syllable
!%^PenCCC2CC:0        
!! Extra strong to strong consonant in penultimate syllable
!%^PenCC2CCC:0     
!! Extra Lengthens consonant in penultimate syllable
!%^PenC2CC:0     
!! Lengthens consonant in penultimate syllable
!%^PenC2CCC:0 	
!! Lengthens consonant in penultimate syllable
!%^PenXYY2VY:0	
!! Vhss:VVus in penultimate syllable
!%^PenXYY2VYY:0	
!! consonant cluster to Vowel and Geminate
!%^PenVY2XYY:0	
!! VVus:Vhss in penultimate syllable
!%^PenXY2XYY:0	
!! 
!%^PenXYY2XY:0	
!! 
!%^PenXYYY2XYY:0	
!! Extra strong to strong consonant cluster
!%^PenXYYY2XY:0	
!! Extra strong to weak consonant cluster
!%^PenXYY2XYYY:0	
!! Extra Lengthens consonant cluster
!%^PenKKK2ZERO:0	
!! Geminate weakening jeeʹel
 %^KKK2ZERO:0   !! FIXME
!%^PenKKK2GG:0      
!! Geminate weakening
!%^PenKK2GG:0       
!! Geminate weakening jokk:jooǥǥ
!%^PenGG2KK:0       
!! Geminate strengthening

! %^PenAllegro:0	    
!! This introduces vertical bar after diphth before consonants
 %^Allegro:0	    !!= * @CODE@ This introduces vertical bar after diphth before consonants

%^J2I:0		!!= * @CODE@ in tuâjj:tuâi
%^I2J:0		!!= * @CODE@ in agent names
%^I2Zero:0		!!= * @CODE@ in agent names

!! !! Gradation triggers 2015.01.23
 %^XSt2St:0	!!= * @CODE@ Causes weakening from extra strong to strong
 %^XSt2W:0	!!= * @CODE@ Causes weakening from extra strong to weak
 %^St2W:0	!!= * @CODE@ Causes weakening from strong to weak
 %^W2St:0	!!= * @CODE@ Causes strengthening from weak to strong
 %^W2XSt:0	!!= * @CODE@ Causes strengthening from weak to extra strong
 %^St2XSt:0	!!= * @CODE@ Causes strengthening from strong to extra strong

!! Other vowel length and consonant length will be phased out
 %^VOWSH:0	  !!= * @CODE@
 %^VOWXSH:0	  !!= * @CODE@ Extra shortens vowel in relation to consonant
 %^VOWXL:0    !!= * @CODE@ Extra lengthens vowel in relation to consonant in nouns, for example
 %^CXLong:0 %^CLong:0 %^CShort:0 !!= * @CODE@ Lengthening, shortening

 X3:0  !!= * @CODE@ causes weak grade
 %{VU%}:u         !!= * @CODE@ used with stuuʹl
 %{Â1%}:0 !!= * @CODE@ used with "mainsted" to tell (a story)
 %{A1%}:0 !!= * @CODE@ used with "vuejted" to drive (causative)
 %{E1%}:0 !!= * @CODE@ used with "kåʹmrded" to bow
 %{%ʹ%}:0 !!= * @CODE@ archiform for MODIFIER LETTER ACUTE ACCENT used in stems where the lemma does not contain a MODIFIER LETTER ACUTE ACCENT and yet the paradigm requires one.
 %{EÂ%}:e !!= * @CODE@ archiform for ie:eâ variation
 %^IMPSG2:0	!!= * @CODE@ 
 %^INDPRSSG3:0  !!= * @CODE@ 
 %^PenVow2a:0	!!= * @CODE@ Changes penultimate vowel to a; e:a â:a
 %^FRICG:0	!!= * @CODE@ 
 %^VOWTense:0   !!= * @CODE@ â:e
 %^VOWLax:0     !!= * @CODE@ e:â 

!! Literal quotes and angles must be escaped (cf morpheme boundaries below):

 »7     !! * »
 «7     !! * «
 %[%>%] !! * >
 %[%<%] !! * <

!! Morpheme boundaries:
 «  ! Derivational prefix
 »  ! Derivational suffix
 %< ! Inflectional prefix
 %> ! Inflectional suffix
 #  ! Word boundary for both lexicalised and dynamic compounds
 %^ ! (exceptional) soft hyphenation point
 %- ! Hyphen for question particle

!! End of alphabet definitions
 ;


!! !!Sets
Sets 

! Letter =
 Cns	= b c d f g h j k l m n p q r s t v w x z đ ʒ ǯ č ŋ ǩ ǧ ǥ š ž 
          B C D F G H J K L M N P Q R S T V W X Z Đ Ʒ Ǯ Č Ŋ Ǩ Ǧ Ǥ Š Ž ; !!= * @CODE@
 CNoV	= b c d f g h j k l m n p q r s t w x z đ ʒ ǯ č ŋ ǩ ǧ ǥ š ž 
          B C D F G H J K L M N P Q R S T W X Z Đ Ʒ Ǯ Č Ŋ Ǩ Ǧ Ǥ Š Ž ;   !!= * @CODE@

 CNoJ	= b c d f g h k l m n p q r s t v w x z đ ʒ ǯ č ŋ ǩ ǧ ǥ š ž 
          B C D F G H K L M N P Q R S T V W X Z Đ Ʒ Ǯ Č Ŋ Ǩ Ǧ Ǥ Š Ž ;   !!= * @CODE@

 Vow	= a e ẹ i o u å ä â õ           	      	      	      	        !!= * @CODE@
          A E Ẹ I O U Å Ä Â Õ ;	    				        !!= * @CODE@

 CNoJV  = b c d f g h k l m n p q r s t w x z đ ʒ ǯ č ŋ ǩ ǧ ǥ š ž 
 	  B C D F G H K L M N P Q R S T W X Z Đ Ʒ Ǯ Č Ŋ Ǩ Ǧ Ǥ Š Ž ;     !!= * @CODE@

 CNoDLRZ = b c f g h j k m n p q s t v w x z đ ǯ č ŋ ǩ ǧ ǥ š ž 
           B C F G H J K M N P Q S T V W X Z Đ Ǯ Č Ŋ Ǩ Ǧ Ǥ Š Ž ;        !!= * @CODE@


!! !!Definitions

Definitions
 DummiesBeforeConsLength = [%^V2VV: |%^VV2V: |%^VOWRaise:| %^VOWLower:| %^PAL: | %^PALNo: |%^PALÂ: |%^PALÕ:| %^PALE: | %^PALÄ: | %^PALẸ: ] ;
 DummiesBeforeVowelHeight = [( %^V2VV: |%^VV2V: )] ; 

!! preceded by two vowels
!! {{{
CC2CforPrecedingVV = [k:ǥ k:ǥ|k:j k:j|ǩ:j ǩ:j|ǩ:ǥ ǩ:ǥ|s:z s:z|š:ž š:ž|c:ʒ c:ʒ|č:j č:j|m m:0|v v:0|đ đ:0|n n:0|l l:0|p:v p:0|t:đ t:0|r r:0] ; !!= * @CODE@ 
!! }}}

!! preceded by one vowel Vx=>VxVx påll=>pååll
!|ǧ:j ǧ:0|ǧ:ǥ ǧ:0 
!! {{{
CCforPrecedingV2VV =  [b b|p p|m m|v v| d d|t t|đ đ|n n|l l|r r|s s|š š|c c|č č| ǯ ǯ | k k|ǩ ǩ|g g|ǧ ǧ|ǧ:g ǧ:g|ǧ:ǥ ǧ:ǥ ] ; !!= * @CODE@ 

iCC2iCforPrecedingV2VV = [ i (ʹ:|0:ʹ) [b b:0|ʒ ʒ:0|d d:0|g g:0|k k:0|ǩ ǩ:0|l l:0|m m:0|n n:0|p p:0|r r:0|t t:0|v v:0]] ; !!= * @CODE@ 

uCC2uCforPrecedingV2VV = [ [u|%{VU%}:u] (ʹ:|0:ʹ) [ʒ ʒ:0|d d:0|g g:0|k k:0|ǩ ǩ:0|l l:0|m m:0|n n:0|p p:0|r r:0|t t:0]] ; !!= * @CODE@ 
!! }}}

!! extra long vowel (u or i)
!! {{{
CC2CafterV2VVandUI = [uCC2uCforPrecedingV2VV|iCC2iCforPrecedingV2VV] ;  !!= * @CODE@ 
!! }}}

!! extra long vowel (d or l or r)
!! {{{
CC2CforPrecedingV2VV = [l [b b:0|ǧ:j ǧ:0|ǧ:ǥ ǧ:0|m m:0|t t:0]|m p p:0|p c c:0|r [d d:0|n n:0]|s [k: k:0|ǩ: ǩ:0|t t:0]] ; !!= * @CODE@ 
!! }}}

!! extra long consonant (d or l or r)
!! {{{
C2CCafterVV2V = [l [b 0:b|j:ǧ 0:ǧ|ǥ:ǧ 0:ǧ|m 0:m|t 0:t]|m p 0:p|p c 0:c|r d 0:d|s [:k 0:k|:ǩ 0:ǩ|t 0:t]] ; !!= * @CODE@ 
!! }}}

!! Long consonant cluster
LongCluster = [l [d d |g g|ǧ ǧ|m m|t t]|m p p|p c c|r d d |s [k k|ǩ ǩ|t t]] ;

!! Shortening consonant cluster
XYY2XYCluster = [l [m m:|b b:|p p:| d d:|t t:|g g:|ǧ: ǧ:|k: k:|ǩ: ǩ:]|m [p p:| j j: ]|p c c:|r [d d:|g: g:|ǧ: ǧ:|k: k:|ǩ: ǩ:|č: č:|t t: | j j: ] |s [k k:|ǩ ǩ:|t t:]] ;



!! Short consonant cluster


!! Onset consonant or word boundary
OnSetC = [:Cns (Cns:|:Cns) |#|%>] ;


!! following morpheme or word boundary
!! {{{
RBound = [#|%>|»] ; !!= * @CODE@ 
!! }}}



!! possible triggers before VOWLower and VOWRaise

BetweenStemAndHeight = [( %^VV2V: |%^V2VV:)] ;

!! possible triggers before PALE PALÄ
BetweenStemAndPALAllo = [ BetweenStemAndHeight (%^VOWLower:|%^VOWRaise:|%^LowerI2E:)] ;

!! possible triggers between stem and PALNo and PAL
BetweenStemAndPALNo = [ BetweenStemAndHeight [(%^VOWLower:|%^VOWRaise:|%^LowerI2E:) (%^PALÄ:|%^PALE:|%^PALẸ:|%^PALÕ:|%^PALÂ:)| %^VOWBack: ] (%^Allegro:) ] ;

!! possible triggers between word end and consonant grade
BetweenStemAndConsGrade = [ BetweenStemAndHeight ((%^VOWRaise:|%^VOWLower:|%^LowerI2E:) ((%^PALÂ:|%^PALÕ:) (%^Allegro:) %^PALNo:|%^VOWLower: %^PALÄ: |(%^PALÕ:|%^PALE:|%^PALÄ:|%^PALẸ:) (%^Allegro:) %^PAL:)| %^VOWBack: (%^Allegro:) (%^PALNo:|%^PAL:) ) ] ;

!! possible triggers between vowel length and Palatalization
BetweenVowLengthAndPALNo = [(%^VOWLower:|%^VOWRaise:|%^LowerI2E:) (%^PALÄ:|%^PALE:|%^PALẸ:|%^PALÕ:|%^PALÂ:)| %^VOWBack: ] ;

BetweenVowHeightAndConsGrade = [((%^PALE:|%^PALÄ:|%^PALẸ:|%^PALÕ:) (%^Allegro:) %^PAL:|(%^PALÂ:|%^PALÕ:) (%^Allegro:) %^PALNo:)] ;

!! possible triggers between VowHeight and word end
!! {{{
BetweenVowHeightAndMorph = [(%^VOWFront:|%^VOWBack:) (%^VOWTense:|%^VOWLax:) ((%^PALE:|%^PALÄ:|%^PALẸ:|%^PALÕ:) (%^Allegro:) %^PAL:|(%^PALÂ:|%^PALÕ:) (%^Allegro:) %^PALNo:)] ; !!= * @CODE@ 
!! }}}


BetweenPALNoAndMorphRightArrow = [(%^CC2C:|%^C2CC:|%^XY2XYY:|%^XYY2XY:|%^KK2GG:|%^CC2CCC:|%^CCC2C:|%^CCC2CC:|%^XYY2VY:|%^XYY2VYY:|%^KKK2GG:) ] ;

BetweenPALNoAndMorph = [(%^CC2C:|%^C2CC:|%^XY2XYY:|%^XYY2XY:|%^KK2GG:|%^CC2CCC:|%^CCC2C:|%^CCC2CC:|%^XYY2VY:|%^XYY2VYY:|%^KKK2GG:) RBound ] ;

PenBetweenStemAndPALNo = [(%^Pen: %^V2VV:|%^Pen: %^VV2V:) (%^Pen: %^VOWRaise:|%^Pen: %^VOWLower:|%^Pen: %^VOWBack:) (%^Pen: %^Allegro:) ] ;

PenBetweenStemAndConsGrade = [(%^Pen: %^V2VV:|%^Pen: %^VV2V:) ((%^Pen: %^VOWRaise:|%^Pen: %^VOWLower:) ((%^Pen: %^PALÂ:|%^Pen: %^PALÕ:) (%^Pen: %^Allegro:) %^Pen: %^PALNo:|(%^Pen: %^PALÕ:|%^Pen: %^PALE:|%^Pen: %^PALÄ:|%^Pen: %^PALẸ:) (%^Pen: %^Allegro:) %^Pen: %^PAL:)| %^Pen: %^VOWBack: (%^Pen: %^Allegro:) (%^Pen: %^PALNo:|%^Pen: %^PAL:) ) ] ;

PenBetweenStemAndVowelLoss = [(%^Pen: %^V2VV:|%^Pen: %^VV2V:) [(%^Pen: %^VOWRaise:|%^Pen: %^VOWLower:) ((%^Pen: %^PALÂ:|%^Pen: %^PALÕ:) (%^Pen: %^Allegro:) %^Pen: %^PALNo:|(%^Pen: %^PALE:|%^Pen: %^PALÄ:|%^Pen: %^PALẸ:|%^Pen: %^PALÕ:) (%^Pen: %^Allegro:) %^Pen: %^PAL:)| %^Pen: %^VOWBack: (%^Pen: %^Allegro:) (%^Pen: %^PALNo:|%^Pen: %^PAL:)] ([%^Pen: %^C2CC:|%^Pen: %^XY2XYY:]|%^Pen: %^CC2C:|%^Pen: %^XYY2XY:|%^Pen: %^CC2CCC:)] ;


PenBetweenStemAndStemFinalVoicing = [(%^Pen: %^V2VV:|%^Pen: %^VV2V:) [(%^Pen: %^VOWRaise:|%^Pen: %^VOWLower:) ((%^Pen: %^PALÂ:|%^Pen: %^PALÕ:) (%^Pen: %^Allegro:) %^Pen: %^PALNo:|(%^Pen: %^PALÕ:|%^Pen: %^PALE:|%^Pen: %^PALÄ:|%^Pen: %^PALẸ:) (%^Pen: %^Allegro:) %^Pen: %^PAL:)| %^Pen: %^VOWBack: (%^Pen: %^Allegro:) (%^Pen: %^PALNo:|%^Pen: %^PAL:) ] (%^Pen: %^C2CC:|%^Pen: %^XY2XYY:|%^Pen: %^XYY2VY:|%^Pen: %^CC2C:|%^Pen: %^XYY2XY:|%^Pen: %^CCC2CC:|%^Pen: %^CC2CCC:|%^Pen: %^CCC2C:) (%^RmVow:)] ;

PenBetweenPALNoAndMorph = [(%^Pen: %^CC2C:|%^Pen: %^C2CC:|%^Pen: %^XYY2XY:|%^Pen: %^KK2GG:|%^Pen: %^CC2CCC:|%^Pen: %^CCC2C:|%^Pen: %^CCC2CC:|%^Pen: %^XY2XYY:|%^Pen: %^XYY2VY:|%^Pen: %^XYY2VYY:|%^Pen: %^KKK2GG:) RBound ] ;


!! neutral to vowel height and backness
!! {{{
NeutrHighBack = [(%^VOWLower:|%^VOWRaise:) (%^VOWFront:|%^VOWBack:)] ; !!= * @CODE@ 
!! }}}

!! neutral to vowel length
VNeutrGrade = [(%^VV2V: |%^V2VV: )];
!! neutral to consonant length
!! {{{
CNeutrGrade = [(%^CC2C: |[%^C2CC:|%^XY2XYY:] |%^CC2CCC: |%^KK2GG: |%^KKK2GG: |%^XYY2VY: |%^XYY2VYY:|%^CCC2CC: )] ;
!! }}}
!! neutral to vowel and consonant length
!! {{{
NeutrGrade = [VNeutrGrade |CNeutrGrade] ; !!= * @CODE@ 
!! }}}



!! * X3 = C Vx Cx Cx     C Vx Vy Cx ˈ Cx
!! * X2 = C Vx Vx Cx Cx  C Vx Vy Cx Cx
!! * X1 = C Vx Vx Cx     C Vx Vy Cx


!! !!!Rules
Rules 

!! !!Vowel shortening rules 



"Vowel shortening â:0"
!! __@RULENAME@__ - used in
 â:0 <=>
          OnSetC _ â: (u) (ʹ:|0:ʹ) Cns: [ Cns:*|:Cns*]  %^VV2V:  ;
!! čââʹđ+N+Sg+Ill: __heart/sydän__
!!€ čââʹđ0%^VV2V%^PALNo%^C2CC%>ma
!!€ č0â0đđ000%>ma
    OnSetC   _ (:ʼ) [Cns:+|i:|n j|l m|s ǩ:|s t|š ǩ:|š m] PenBetweenStemAndVowelLoss %^RmVow: ;
!         [Cns:+|:Cns+] _ [Cns|i:|n j|l m|s ǩ:|s t|š ǩ:|š m] PenBetweenStemAndVowelLoss %^RmVow: ;

      Cns: (:Cns*)  _ (:ʼ) Cns:+ PenBetweenStemAndVowelLoss %^RmVow: ;


"Even syllabic verbs I, vowel shortening ẹ:0"
!! __@RULENAME@__ - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
 ẹ:0 <=>
   OnSetC _ ẹ: (ʹ:|0:ʹ) [Cns: :Cns*] %^VV2V:  ;
 OnSetC _ ẹ: (u|i) (ʹ:|0:ʹ) Cns: [ Cns:*|:Cns*]  %^VV2V:  ;
 OnSetC _ ẹ: (ʹ:|0:ʹ)  Cns: [ Cns:*|:Cns*]   Vow: Cns:+ %^Pen: %^VV2V: ;


"Even syllabic verbs I, vowel shortening e:0"
!! __@RULENAME@__ - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
 e:0 <=>
   OnSetC _ [e: |:e] (ʹ:|0:ʹ) [Cns: :Cns*] %^VV2V:  ;
!! teevvad+V+Prt+4:
!!€ teevv%^VV2V%^VOWRaise%>uš
!!€ t0ivv00%>uš
   OnSetC  _ (0:ʹ) [Cns:+|i:|n j|l m|s ǩ:|s t|š ǩ:|š m] PenBetweenStemAndVowelLoss %^RmVow: ; 
!! Ââvel+N+Prop+Sg+Loc  __Ivalo__
!!€ Ââvel%^RmVow%>est
!!€ Ââv0l0%>est

 OnSetC _ e: (u) (ʹ:|0:ʹ) Cns: [ Cns:*|:Cns*]  %^VV2V:  ;

 OnSetC _ e: (ʹ:|0:ʹ)  Cns: [ Cns:*|:Cns*]   Vow: Cns:+ %^Pen: %^VV2V: ;
!! eʹǩrded+V+Inf
!!€ eeʹǩǩerd%^Pen: %^VV2V%^Pen: %^CC2C%^RmVow%>ed
!!€ 0eʹǩ00rd000%>ed
!! deriving cieʹǩǩes+N+Sg+Ill: ceäkˈksa


"Even syllabic verbs I, vowel shortening å:0"
!! __@RULENAME@__ - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
 å:0 <=>
 OnSetC _ å: (u) (ʹ:|0:ʹ) Cns: [ Cns:*|:Cns*]  %^VV2V:  ;
         OnSetC _ å: (ʹ:|0:ʹ) Cns: (:Cns+|Cns:+) Vow: [Cns:+ |n j|l m|s ǩ:|s t|š ǩ:|š m]  %^Pen: %^VV2V: ;
!! sååbbar+N+Sg+Gen __meeting__
!!€ sååbbar%^Pen: %^VV2V
!!€ s0åbbar0
      Cns: (:Cns*)  _ Cns:+ PenBetweenStemAndVowelLoss %^RmVow: ;



"Even syllabic verbs I, vowel shortening õ:0"
!! __@RULENAME@__ - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
 õ:0 <=>
 OnSetC _ õ: (ʹ:|0:ʹ) [Cns: :Cns*] %^VV2V: ;
!!€ mõõ0nn%^VV2V%^PAL%>e
!!€ m0õʹnn00%>e
!!€ mõõ0nn%^VV2V%^VOWLower%^PAL%>e
!!€ m0âʹnn000%>e
         OnSetC _ õ: (ʹ:|0:ʹ) Cns: (:Cns+|Cns:+) Vow: [Cns:+|n j|l m|s ǩ:|s t|š ǩ:|š m]  %^Pen: %^VV2V: ;
!! kõõnjâl+N+Sg+Gen __tear__
!!€ kõõn0jâl%^Pen%^VV2V%^Pen%^C2CC
!!€ k0õnnjâl00

 OnSetC _ õ: (u) (ʹ:|0:ʹ) Cns: [ Cns:*|:Cns*]  %^VV2V:  ;

      Cns: (:Cns*)  _ Cns:+ PenBetweenStemAndVowelLoss %^RmVow: ;


"Even syllabic verbs I, vowel shortening u:0"
!! __@RULENAME@__ - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
 u:0 <=> OnSetC _ u: (ʹ:|0:ʹ) [C2CCafterVV2V|Cns: :Cns+] (i:) %^VV2V: ;
!! juurd+N+Ess __thought__
!!€ juurd0%^VV2V%^C2CC%>j%>en
!!€ j0urdd00%>j%>en
      OnSetC  _ u: (ʹ:|0:ʹ) Cns:+ Vow: [Cns:+|n j|l m|s ǩ:|s t|š ǩ:|š m] %^Pen: %^VV2V: ;
      Cns: (:Cns*)  _ Cns:+ PenBetweenStemAndVowelLoss %^RmVow: ;
!! mainstummuš+N+Sg+Ill:
!!€ mainstum0uš%^Pen%^C2CC%^RmVow%>a
!!€ mainstumm0š00%>a


"Even syllabic verbs I, vowel shortening i:0"
!! __@RULENAME@__ - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
 i:0 <=>
         OnSetC _ i: (ʹ:|0:ʹ) [Cns: :Cns*] %^VV2V: ;
!! viikkâd+V+Ind+Prs+Pl3
!!€ vii0kk%^VV2V%^VOWLower%^PAL%>e
!!€ v0eʹǩǩ000%>e
         Cns _ %^UltRmVow: %> ǥ a ; ! +Sg+Ill N_HÕʹPPI
        OnSetC  _ [Cns:+|n j|l m|s ǩ:|s t|š ǩ:|š m] PenBetweenStemAndVowelLoss %^RmVow: ;
!         [Cns:+|:Cns+] _ [Cns:+|n j|l m|s ǩ:|s t|š ǩ:|š m] PenBetweenStemAndVowelLoss %^RmVow: ;
!! prääʹzniǩ+N+Sg+Ill: __celebration/juhla__
!!€ prääʹzniǩ%^Pen%^PALNo%^RmVow%^PALNo%>a
!!€ prää0zn0k000%>a

         OnSetC _ i: (ʹ:|0:ʹ) Cns:+ Vow: [Cns:+|n j|l m|s ǩ:|s t|š ǩ:|š m]  %^Pen: %^VV2V: ; ! iilâsǩ:ilska

 OnSetC _ i: (u) (ʹ:|0:ʹ) Cns: [ Cns:*|:Cns*]  %^VV2V:  ;

      Cns: (:Cns*)  _ Cns:+ PenBetweenStemAndVowelLoss %^RmVow: ;
      [Cns:|Vow:] _ (%^RmVow:) %^I2Zero: ;
!!€ žẹẹvai%^RmVow%^I2Zero%>ǥa
!!€ žẹẹv0000%>ǥa

"Even syllabic verbs I, vowel shortening o:0"
!! __@RULENAME@__ - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
 o:0 <=> 
    OnSetC _ o: (ʹ:|0:ʹ) [Cns: :Cns*] %^VV2V: ;
!!€ šoomm%^VV2V%^VOWRaise%>u
!!€ š0umm00%>u
    OnSetC _ o: (u) (ʹ:|0:ʹ) [Cns: :Cns*] Vow: Cns: [Cns:*|:Cns*] %^Pen: %^VV2V: ;
!!€ poonjâst%^Pen%^VV2V%^RmVow%>ed
!!€ p0onj0st00%>ed

  OnSetC _ o: (u) (ʹ:|0:ʹ) Cns: [ Cns:*|:Cns*]  %^VV2V:  ;
!! ooumaž+N+Sg+Gen
!!€ ooum0%^VV2V%^XY2XYYu
!!€ 0oumm00u

         [Cns:+|:Cns+] _ [Cns:+|i:|n j|l m|s ǩ:|s t|š ǩ:|š m]  %^RmVow: ; 

      Cns: (:Cns*)  _ Cns:+ PenBetweenStemAndVowelLoss %^RmVow: ;


"Even syllabic verbs I, vowel shortening a:0 "
!! __@RULENAME@__ - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
 a:0 <=> OnSetC _ a: (i|u) (ʹ:|0:ʹ) [Cns: :Cns*] %^VV2V: ;
!!€ saa0kk%^VV2V%^PAL%>e
!!€ s0aʹǩǩ00%>e
 OnSetC _ a: (i|u) (ʹ:|0:ʹ) [Cns: :Cns*] Vow: Cns: [Cns:*|:Cns*] %^Pen: %^VV2V: ;
       OnSetC   _ (:ʼ) [Cns:+|i:|n j|l m|s ǩ:|s t|š ǩ:|š m] PenBetweenStemAndVowelLoss %^RmVow: ; 
!! Aanar+N+Prop+Sg+Ill: __Inari/Enare__
!!€ Aanar%^RmVow%>a
!!€ Aan0r0%>a
         OnSetC _ a: (i) (ʹ:|0:ʹ) Cns:+ Vow: [Cns:+|n j|l m|s ǩ:|s t|š ǩ:|š m]  %^Pen: %^VV2V: ; 
    ?:Vow+ (ʹ:|0:ʹ) [Cns:+ - [Cns:* [l|n]]] _ i: %^RmVow:  ; 

 OnSetC _ a: (u) (ʹ:|0:ʹ) Cns: [ Cns:*|:Cns*]  %^VV2V:  ;
      Cns: (:Cns*)  _ (:ʼ) Cns:+ PenBetweenStemAndVowelLoss %^RmVow: ;

  OnSetC _ (:ʼ) Cns:+ %^RmVow:  ;
!! mättʼted+V+Inf: __teach/opettaa__
!!€ mätta0t%^RmVow%>ed
!!€ mätt0ʼt0%>ed




"vowel to syllable mark a:ʼ"
!! __@RULENAME@__ - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
!! {{{
 a:ʼ <=> ?:Vow+ (ʹ:|0:ʹ)  Cns:* [l|n]  _ i: %> Vow: ;
!! }}}



"ZERO to syllable mark 0:ʼ here"
!! __@RULENAME@__ - used in zeeʹtt+N+Sg+Abe: zeeʹttʼtää
 0:ʼ <=>
     [ :n | :l ] ([%^C2CC:|%^CCC2CC:|%^XY2XYY:]) %> _ j ;
     [ :n | :l ] ([%^C2CC:|%^CCC2CC:|%^XY2XYY:]) %> _ (Vow:0) i:j ;
!!€ valmštõõll%>0i%^I2J%>i
!!€ valmštõõll%>ʼj0%>i
!! piânˈnai+N+Ess: __dog/koira__
!!€ piânˈn%^CCC2CC%>0j%>en
!!€ piân0n0%>ʼj%>en
     [ :n | :l ] _ i:j PenBetweenStemAndVowelLoss (%^RmVow:) %^I2J: ;
!!€ jieʹll0i%^I2J%>e%>st
!!€ jieʹllʼj0%>e%>st
     n Vow: _ n %^RmVow: ;
     s Vow: _ :s %^RmVow: ;
     t Vow: _ t %^RmVow: ;
!! mättʼted+V+Inf: __teach/opettaa__
!!€ mätta0t%^RmVow%>ed
!!€ mätt0ʼt0%>ed
     j Vow: _ j %^RmVow: ;
!!jåʹttʼted+V+Inf
        m (ˈ:) m BetweenStemAndConsGrade (%^CCC2CC:) %>  _ m ; 
        v (ˈ:) v BetweenStemAndConsGrade (%^CCC2CC:) %>  _ v ; 
        s (ˈ:) s BetweenStemAndConsGrade (%^CCC2CC:) %>  _ s ; 
        t (ˈ:) t: BetweenStemAndConsGrade (%^CCC2CC:|%^XYY2XY:) %>  _ t ; 
        k (ˈ:) k BetweenStemAndConsGrade (%^CCC2CC:) %>  _ k ; 
        ?:ǩ (ˈ:) :ǩ BetweenStemAndConsGrade BetweenPALNoAndMorphRightArrow %> _ ǩ ; 
        ?:ž (ˈ:) :ž BetweenStemAndConsGrade BetweenPALNoAndMorphRightArrow %>  _ ž ; 
        ?:č (ˈ:) :č BetweenStemAndConsGrade BetweenPALNoAndMorphRightArrow %>  _ č ; 
!!€ ijss%>0sa
!!€ ijss%>ʼsa
!!€ kåčč%>0čem
!!€ kåčč%>ʼčem
!!€ 0autt%^V2VV%^XYY2XY%>0taa
!!€ aaut000%>ʼtaa


"ZERO to hyphen minus 0:- in this rule"
!! __@RULENAME@__ - used in Kääzzkõsraajõstuâjj-joouk
 0:%- <=>
        Cx  _ #  Cx ;
	where Cx in (b c d f g h j k l m n p q r s t v w x z đ ʒ ǯ č ŋ ǩ ǧ ǥ š ž) ; 





"Even syllabic verbs I, vowel shortening ä:0"
!! __@RULENAME@__ - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
 ä:0 <=> OnSetC _ ä: Cns: :Cns* %^VV2V:  ;
   OnSetC _ ä: (i) (ʹ:|0:ʹ) Cns: (:Cns+|Cns:+) Vow: [Cns:+|n j|l m|s ǩ:|s t|š ǩ:|š m]  %^Pen: %^VV2V: ;

 OnSetC _ ä: (u) (ʹ:|0:ʹ) Cns: [ Cns:*|:Cns*]  %^VV2V:  ;




!! !!Vowel alternation rules

!! !VOWEL SHIFT

!2015-05-20 What is this?(tbw)

!! !RAISING

!! deriving +Ind+Prt+Sg1, +Ind+Prt+Sg2 +Ind+Prt+Pl3 in teevvad:tivvu

"Even syllabic verbs I, vowel raising o:u"
!! __@RULENAME@__ - Adding +Prt+Pl3 for ed verbs, Removing u: and i:  second element due to njeiddad njeiddu
 o:u <=>  OnSetC  o: _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+]  BetweenStemAndHeight %^VOWRaise:  ;
       OnSetC  (Vow:0) _ [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+]  (%^VV2V:) %^VOWRaise: RBound ;
!!€ šoomm%^VV2V%^VOWRaise%>u
!!€ š0umm00%>u

       OnSetC o:0 _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+]  BetweenStemAndHeight %^VOWRaise: ((%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) %> [e|u] ;
       OnSetC 0:u _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+]  (%^V2VV: %^CShort:) %^VOWRaise: (%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL: %> [e|u] ;
           OnSetC _ (u:) (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+]  (%^VOWSH: %^CLong:|%^V2VV:) %^VOWRaise: ((%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) %> [e|u] ;


"Even syllabic verbs I, vowel raising å:o"
!! __@RULENAME@__ - Adding +Prt+Pl3 for ed verbs, Removing u: and i:  second element due to njeiddad njeiddu
 å:o <=>  OnSetC _ å: (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+]   %^VOWRaise: ; 
          OnSetC [å:|0:o] _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+] BetweenStemAndHeight  %^VOWRaise: ;
           [å:o|0:|å:0] _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+] BetweenStemAndHeight %^VOWRaise: ;
!!€ kååʹpp%^VOWRaise%^CC2C%>in
!!€ kooʹv0000%>in
!!€ k0åʹll%^V2VV%^VOWRaise%>in
!!€ kooʹll00%>in
          OnSetC _ (u:) (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+]  %^VOWRaise:  ;
!          OnSetC _ (u:) (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+]  %^VOWRaise: ((%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) %> [e|u] ;
!!€ kåbll%^VOWRaise


"Even syllabic verbs I, vowel raising e:i"
!! __@RULENAME@__ - Adding +Prt+Pl3 for ed verbs, Removing u: and i:  second element due to njeiddad njeiddu
 e:i <=>
  OnSetC _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+] %^VOWRaise: ;
!!€ pess%^VOWRaise%>u
!!€ piss0%>u
  OnSetC [0:i] _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+] %^V2VV: %^VOWRaise: ;
!!€ p0ess%^V2VV%^VOWRaise%>už
!!€ piiss00%>už
  OnSetC Vow:0 _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+] %^VV2V: %^VOWRaise: ;
!!€ teevv%^VV2V%^VOWRaise%>uš
!!€ t0ivv00%>uš
  OnSetC  [e:|e:0|0:i] _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+]  BetweenStemAndHeight %^VOWRaise: ;
  OnSetC _ (u:) (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+] BetweenStemAndHeight %^VOWRaise: ;
! diphthong raising with e:i at onset - deriving +Ind+Prt+Pl3 in reäkkad > riâkku, veârrad > viõrru
 OnSetC  _ [ä:|â: ] (u) (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+]  %^VOWRaise: ;
!!reâugg+N+Sg+Ill
!!€ reâugg%^VOWRaise%>u
!!€ riõugg0%>u
!!€ jeä0gˈg%^VOWRaise%^PALẸ%^PAL%^CCC2CC
!!€ jiẹʹǧ0ǧ0000


"Even syllabic verbs I, vowel raising ẹ:i"
!! __@RULENAME@__ - used in
 ẹ:i <=>
  OnSetC _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+] %^VOWRaise: ;
!!€ pẹss%^VOWRaise%>u
!!€ piss0%>u
  OnSetC [0:] _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+] %^V2VV: %^VOWRaise: ;
!!€ p0ẹss%^V2VV%^VOWRaise%>už
!!€ piiss00%>už
  OnSetC Vow:0 _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+] %^VV2V: %^VOWRaise: ;
!!€ tẹẹvv%^VV2V%^VOWRaise%>uš
!!€ t0ivv00%>uš
  OnSetC  [ẹ:|ẹ:0|0:i] _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+]  BetweenStemAndHeight %^VOWRaise: ;
  OnSetC _ (u:) (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+] BetweenStemAndHeight %^VOWRaise: ;


"Even syllabic verbs I, vowel raising â:õ" 
!! __@RULENAME@__ - Adding +Prt+Pl3 for ed verbs, Removing u: and i:  second element due to njeiddad njeiddu
 â:õ <=>
          OnSetC u   _    (i)  (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+|Cns:+ :ˈ Cns:+| Cns:+ (ˈ:|:ˈ) :Cns]  %^VOWRaise:  (%^PALNo:|%^PAL:) BetweenPALNoAndMorph ;
!!€ ruâk0k%^VOWRaise%^CC2CCC%>u
!!€ ruõkˈk00%>u
!!€ čuâkˈk%^VOWRaise%>u
!!€ čuõkˈk0%>u
  OnSetC â:  _      (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+]  BetweenStemAndHeight %^VOWRaise: ;
  OnSetC _   â:   (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+]  %^VOWRaise: ;
!! mââʹnn+N+Pl+Acc: __egg/muna__
!!€ mââʹnn%^VOWRaise%^CC2C%>id
!!€ mõõʹn000%>id
          OnSetC â:0 _      (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+]  %^VV2V: %^VOWRaise: (%^PAL:) BetweenPALNoAndMorph ;
          OnSetC 0: _      (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+]  %^V2VV: %^VOWRaise: (%^PAL:) BetweenPALNoAndMorph ;
!! čâʹlmm+N+Pl+Acc: __eye/silmä__
!!€ č0â0lmm%^V2VV%^VOWRaise%^PAL%^XYY2XY%>id
!!€ čõõʹlm00000%>id

          OnSetC     _ (u:) (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+]   %^VOWRaise: (%^PAL:) BetweenPALNoAndMorph ;
          OnSetC u   _   (i)   (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+|Cns:+ :ˈ Cns:+] %^VOWRaise: (%^PALNo:|%^PAL:) BetweenPALNoAndMorph  ;
          OnSetC [ e: | i]  _   (u)   (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+|Cns:+ :ˈ Cns:+] %^VOWRaise: (%^PALNo:|%^PAL:) BetweenPALNoAndMorph ;
!!reâugg+N+Sg+Ill
!!€ reâugg%^VOWRaise%>u
!!€ riõugg0%>u



"Even syllabic verbs I, diphthong raising beginning with u ä:å"
!! __@RULENAME@__ - deriving +Ind+Prt+Pl3 in kuâsˈsad > kuõsˈsu, kuärŋŋad kuårŋŋu
!! {{{
 ä:å <=> OnSetC u  _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+] BetweenStemAndHeight %^VOWRaise: BetweenPALNoAndMorph ;
!! }}}


"Even syllabic verbs I, diphthong raising beginning with e:i ä:â"
!! __@RULENAME@__ - deriving +Ind+Prt+Pl3 in reäkkad > riâkku, veârrad > viõrru
!! {{{
 ä:â <=> OnSetC e:  _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+|Cns:+ :ˈ Cns:+]  BetweenStemAndHeight %^VOWRaise: BetweenPALNoAndMorph ;
!! }}}



"Even syllabic verbs I, diphthong raising beginning with e:i ä:e"
!! __@RULENAME@__ - used in
 ä:e <=> OnSetC [u|e:i]  _ (i|u) :ʹ [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+|Cns:+ :ˈ Cns:+]  BetweenStemAndHeight %^VOWRaise: %^PALE: %^PAL: ;
  e  _ (i|u) 0: [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+|Cns:+ :ˈ Cns:+] Vow: Cns:*   %^Pen: %^PALE: %^Pen: %^PAL: ;
!!€ jeä0kˈkel%^Pen: %^PALE%^Pen: %^PAL%^Pen: %^KKK2ZERO
!!€ jeeʹ000el000



"Even syllabic verbs I, diphthong raising beginning with e:i ä:ẹ"
!! __@RULENAME@__ - used in
 ä:ẹ <=> OnSetC [u|e:i]  _ (i|u) :ʹ [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+|Cns:+ :ˈ Cns:+]  BetweenStemAndHeight (%^VOWRaise:) %^PALẸ: %^PAL: ;
!!€ čuä0ckk%^PALẸ%^PAL%^XYY2XY%>es
!!€ čuẹʹcǩ0000%>es
!!€ suä0kk%^PALẸ%^PAL%^KK2GG
!!€ suẹʹjj000


"diphthong backing beginning with u ä:õ"
!! __@RULENAME@__ - used in
 ä:õ <=>  u _ (i|u) (:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+|Cns:+ :ˈ Cns:+]  BetweenStemAndHeight %^VOWBack: ;
          [u|e:] _ (i|u) (:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+|Cns:+ :ˈ Cns:+]  BetweenStemAndHeight %^VOWRaise: %^PALÕ:  ;
!!€ jeä0l0l%^VOWRaise%^PALÕ%^PAL%^CC2CCC%>e
!!€ jiõʹlˈl0000%>e

"Even syllabic verbs I, diphthong raising beginning with ä:a u"
!! __@RULENAME@__ - deriving läullad > laullum
 ä:a <=> [ ä: | OnSetC (0:) ] _ Cns: [ Cns:* | :Cns* ] BetweenStemAndHeight %^VOWRaise: ;
!!€ äldd%^VOWRaise%>u
!!€ aldd0%>u
!!€ 0äldd%^V2VV%^VOWRaise%^CC2C%>u
!!€ aald0000%>u
 [ ä: | OnSetC (0:) ] _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+|Cns:+ :ˈ Cns:+] BetweenStemAndHeight %^VOWRaise: ;
 OnSetC _ ä: (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+|Cns:+ :ˈ Cns:+] %^VOWRaise: ;
!! čääʹcc+N+Pl+Acc __water__
!!€ čääʹcc%^VOWRaise%^KK2GG%>id
!!€ čaaʹʒʒ00%>id
  OnSetC (0:a)  _ (u|i)  (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+|Cns:+ :ˈ Cns:+] BetweenStemAndHeight %^VOWRaise:  ;
         OnSetC  ä:    _    (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+|Cns:+ :ˈ Cns:+] BetweenStemAndHeight %^VOWRaise: [%^C2CC:|%^XY2XYY:]  %>  a ; 
         OnSetC  ä:    _    (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+|Cns:+ :ˈ Cns:+] BetweenStemAndHeight %^VOWRaise: ;




!! !LOWERING 

! Where: VIGGAD +Ind+Prs+Sg3, +Ind+Prs+Pl3
!        TIETTED 
! long and single vowels



"Even syllabic verbs I, vowel lowering o:å"
!! __@RULENAME@__ - deriving +Ind+Prs+Sg3, +Ind+Prs+Pl3 in poorrâd
 o:å <=> OnSetC    _ o:å (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+]  %^VOWLower:  ;
         OnSetC o: _     (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+] (%^VV2V:) %^VOWLower:  ;
!!€ poo0rr%^V2VV%^VOWLower%^PAL%>e
!!€ p0åʹrr000%>e
         OnSetC    _     (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+]   %^VOWLower: ;
! juʹrdded+V+Cond+Sg1: joordčem
         OnSetC 0:å _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+] %^V2VV:   %^VOWLower: ;
! juʹrdded > joordam
         OnSetC    _ (u:|i:) (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+] %^VOWLower: ;
         OnSetC    _ ((ʹ:) (v:u|j:i)) (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+] %^VOWLower: ;



"Even syllabic verbs I, vowel lowering i:e"
!! __@RULENAME@__ - deriving +Ind+Prs+Sg3, +Ind+Prs+Pl3 in viǯǯâd
 i:e <=>
   OnSetC _ i:e (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+] (%^V2VV:)  %^VOWLower: ;
!!€ v0iǯǯ%^V2VV%^VOWLower%>as 
!!€ veeǯǯ00%>as
         OnSetC _ [õ:â|õ:ä|â:ä|e:ä|e:â] (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+] (e: s:)  %^VOWLower: ;
           OnSetC i: _   (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+] (%^VV2V:) %^VOWLower: ;
           OnSetC  _   (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+]  %^VOWLower: ;
! juʹrdded+V+Cond+Sg1: joordčem
!           OnSetC 0:e _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+] V2VV:  %^VOWLower: (%^VOWFront:|%^VOWBack:) (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) RBound ;
           OnSetC 0: _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+] %^V2VV:  %^VOWLower: ;
!!€ v0iǯǯ%^V2VV%^VOWLower%>as
!!€ veeǯǯ00%>as

! juʹrdded > joordam
           OnSetC _ (u:|i:) (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+] %^VOWLower: ;
           OnSetC _ ((ʹ:) (v:u|j:i)) (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+] %^VOWLower: ;
  OnSetC _ â: (ʹ:|0:ʹ) Cns: [Cns:* |:Cns*] %^LowerI2E: ;
!! mieʹlǩǩ+N+Sg+Ill: __milk/maito__
!!€ miâlkk%^LowerI2E%>a
!!€ meâlkk0%>a

"Even syllabic verbs I, vowel lowering a:ä"
!! __@RULENAME@__ - deriving +Ind+Prs+Sg3, +Ind+Prs+Pl3 in viǯǯâd
!! {{{
 a:ä <=> OnSetC    _ a:ä (u|i) (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+]  %^VOWLower: (%^VOWFront:|%^VOWBack:) (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) RBound ;
         OnSetC a: _     (u|i) (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+] BetweenStemAndHeight %^VOWLower: (%^VOWFront:|%^VOWBack:) (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) RBound ;
         OnSetC    _     (u|i) (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+] BetweenStemAndHeight %^VOWLower: (%^VOWFront:|%^VOWBack:) (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) RBound ;
! juʹrdded+V+Cond+Sg1: joordčem
         OnSetC 0: _    (u|i) (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+] (%^V2VV: )  %^VOWLower: (%^VOWFront:|%^VOWBack:) (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) BetweenPALNoAndMorph ;
! juʹrdded > joordam
!         OnSetC     _ (u:|i:) (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+] %^VOWLower: (%^VOWFront:|%^VOWBack:) (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) RBound ;
         OnSetC     _ ((ʹ:) (v:u|j:i)) (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+] BetweenStemAndHeight %^VOWLower: (%^VOWFront:|%^VOWBack:) (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) RBound ;
!! }}}


!"Even syllabic verbs I, vowel raising a:ä"
! __@RULENAME@__ - Adding +Prt+Pl3 for ed verbs, Removing u: and i:  second element due to njeiddad njeiddu
! a:ä <=> OnSetC a: _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+]  BetweenStemAndHeight %^VOWRaise: %> [e|u] ; 
!         OnSetC a:0 _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+]  BetweenStemAndHeight %^VOWRaise: ((%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) %> [e|u] ;
!         OnSetC 0:ä _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+]  (%^Pen: %^V2VV: %^CShort:) %^VOWRaise: (%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL: %> [e|u] ;
!  OnSetC _ (u:) (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+]  (%^VOWSH: %^CLong:|%^Pen: %^V2VV:) %^VOWRaise: ((%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) %> [e|u] ;



"Even syllabic verbs I, vowel lowering u:o"
!! __@RULENAME@__ - deriving +Ind+Prs+Sg3, +Ind+Prs+Pl3 in uʹvdded
 u:o <=>
        OnSetC (0:) _ (ʹ:) v: (0:ʹ) [n:+ |d:+|l:+|r:+|ʒ:+|ǯ:+|j:+] (%^V2VV:) %^VOWLower:  ;

!! uʹvdded+V+Cond+Pl3 __to give__
!!€ 0uvdd%^V2VV%^VOWLower%^XYY2VY%>če
!!€ ooud0000%>če
!!€ uvdd%^VOWLower%^XYY2VYY
!!€ oudd00
!! vuʹvll+N+Sg+Ill: voulla
!!€ vuʹvll%^VOWLower%^PALNo%^XYY2VYY%>a
!!€ vo0ull000%>a
  OnSetC _ [o|u:o] (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+]  %^VOWLower: (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) (%^KK2GG:|%^CC2C:|%^XYY2XY:) RBound ;
        OnSetC u: _   (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+|r d d:] VNeutrGrade   %^VOWLower: (%^VOWFront:|%^VOWBack:) (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) (%^KK2GG:|%^CC2C:|%^XYY2XY:) RBound ;
!! suukkâd+V+Imprt+Sg3: __row/soutaa__
!!€ suukk%^VOWLower%^KK2GG%>as
!!€ sooǥǥ00%>as
       OnSetC (0:o) _ (ʹ:| 0:ʹ) [CNoV: Cns:*|Cns:+ (ˈ:|:ˈ) Cns:+] BetweenStemAndHeight %^VOWLower: ; 
!       OnSetC (0:o) _  (ʹ:| 0:ʹ) [CNoV: Cns:*|Cns:+ (ˈ:|:ˈ) Cns:+] BetweenStemAndHeight %^VOWLower: (%^VOWFront:|%^VOWBack:) (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) RBound ; 
!! juʹrdded+V+Ind+Prs+Sg1 __think__
!!€ j0urdd%^V2VV%^VOWLower%^XYY2XY%>am
!!€ joord0000%>am
! uʹvdded+V+Ind+Pl3: ouʹdde 

 OnSetC  _ (ʹ:)  v:u (0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+] %^VOWLower: (%^VOWFront:|%^VOWBack:) (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) RBound ;
! OnSetC (0:) _ (ʹ:) [CNoV: Cns:*|Cns:+ (ˈ:|:ˈ) Cns:+] VNeutrGrade %^VOWLower: (%^VOWFront:|%^VOWBack:) (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) RBound ;
 OnSetC (0:) _ %{VU%}: (ʹ:|0:ʹ) Cns:+ BetweenStemAndHeight %^VOWLower: ;
!! stuuʹl+N+Sg+Ill: __chair/tuoli__
!!€ st0u%{VU%}ʹl%^V2VV%^VOWLower%^PALNo%>a
!!€ stoou0l000%>a
! OnSetC 0: _ [ %{VU%}:u (ʹ:|0:ʹ) [d|l|r|ʒ]]  %^VOWLower: ;
! juʹrdded+V+Cond+Sg1: joordčem
! OnSetC 0: _ (ʹ:|0:ʹ) [CNoV: Cns:*|Cns:+ (ˈ:|:ˈ) Cns:+]  %^Pen: %^V2VV:  %^CShort:    %^VOWLower: ; 
! juʹrdded > joordam

"Even syllabic verbs I, vowel lowering u:õ "
!! __@RULENAME@__ - deriving
 u:õ <=>
       [u:|OnsetC (0:)] _  (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+] BetweenStemAndHeight %^U2Õ: ;
!!€ č0ukk%^V2VV%^U2Õ%>až
!!€ čõõkk00%>až
    OnSetC  _ u: (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+]  %^U2Õ: ;



"Even syllabic verbs I, vowel lowering õ:â "
!! __@RULENAME@__ - deriving +Ind+Prs+Sg3, +Ind+Prs+Pl3 in viǯǯâd
 õ:â <=>
       i: _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+] %^VOWLower: [ BetweenPALNoAndMorph |%^PAL:|%^PALNo:] ;
!! riõkkâd+V+Ind+Prs+Sg3 __to whip__
!!€ riõkˈk%^VOWLower#
!!€ reâkˈk0#
       õ: _  (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+] (%^VV2V:) %^VOWLower: ;
!!€ mõõ0nn%^VV2V%^VOWLower%^PAL%>e
!!€ m0âʹnn000%>e
    OnSetC  _ õ: (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+]  %^VOWLower: ;
!!€ mõõnn%^VOWLower%^CC2C%>as
!!€ mâân000%>as

! juʹrdded > joordam
   OnSetC _ (u:|i:) (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+] %^VOWLower: (%^VOWFront:|%^VOWBack:) (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) RBound ; 
   OnSetC _ ((ʹ:) (v:u|j:i)) (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+] BetweenStemAndHeight %^VOWLower: (%^VOWFront:|%^VOWBack:) (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) RBound ;
! diphthong opening after u
        u _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+] %^VOWLower: [ BetweenPALNoAndMorph |%^PAL:|%^PALNo:] ;
!! kuõskkâd+V+Ind+Prs+Sg3
!!€ kuõskk%^VOWLower
!!€ kuâskk0


!! !diphthongs

"Even syllabic verbs I, diphthong opening after u å:ä"
!! __@RULENAME@__ - deriving +Ind+Prs+Sg3, in kuåccâd kuäʹcce
!! = a>ä lowering clockwise
 å:ä <=> u _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+] BetweenStemAndHeight %^VOWLower: (%^PALNo:|%^PAL:) BetweenPALNoAndMorph ;
!!€ kuå0cc%^VOWLower%^PAL%^CC2CCC
!!€ kuäʹcˈce


"Even syllabic verbs I, diphthong opening after i:e e:ä"
!! __@RULENAME@__ - deriving +Ind+Prs+Sg3, in čiõkkâd
!! {{{
 e:ä <=> [i:|u] _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+] (e: s:) %^VOWLower: (%^PALNo:|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) [RBound|%^RmVow:|%^VC:] ;
! vueʹlǧǧed+V+Ind+Prs+Pl3: vuäʹlǧǧa
!! }}}

"Vowel in second syllable e:a"
!! __@RULENAME@__ - deriving cieʹǩǩes+N+Sg+Gen: ceäkˈkaz
 e:a <=> 
  OnSetC _ (ʹ:|0:ʹ) [Cns:*] PenBetweenStemAndVowelLoss %^PenVow2a: ;
!! cieʹǩǩes+N+Sg+Gen
!!€ ciâk0kes%^Pen%^VOWLower%^Pen%^CC2CCC%^PenVow2a%^VC
!!€ ceäkˈkaz0000



"Even syllabic verbs I, diphthong opening i:e â:ä for â:ä "
!! __@RULENAME@__ - deriving +Ind+Prs+Sg3, in čiõkkâd
 â:ä <=>
 u _ i (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+] %^VOWLower: ;
!!vueiʹnned+V+Ind+Prs+Sg3
!!€ vuâinn%^VOWLower
!!€ vuäinn0
 i: _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+] %^VOWLower: (%^PALNo:| %^PAL:)  ;
 i: _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+] Vow: Cns:+ %^Pen: %^VOWLower: (%^Pen: %^PALNo:| %^Pen: %^PAL:)  ;
 u _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+] %^PALÄ:  ;
!! vueʹlǧǧed+Use/NG+V+Ind+Prs+Pl3 __leave/lähteä__
!!€ vuâ0lgg%^PALÄ%^PAL%>e
!!€ vuäʹlǧǧ00%>e

"Even syllabic verbs I, diphthong opening after i:e e:â"
!! __@RULENAME@__ - deriving +Ind+Prs+Sg3, in pi%{EÂ%}ʹǩǩ:peâkka
!! {{{
 %{EÂ%}:â <=> i: _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+] %^VOWLower: %^PALNo:  RBound ;
!! }}}

"Even syllabic verbs I, diphthong opening after u e:â"
!! __@RULENAME@__ - deriving vueʹlǧǧed+V+Ind+Prs+Pl3: vuâʹlǧǧe
!! {{{
 e:â <=> [u|i:] _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+] (e: s:) %^VOWLower: %^VOWBack: ;
!! }}}

!! !Vowel backing

"Even syllabic verbs I, diphthong opening after u e:õ"
!! __@RULENAME@__ - deriving vueʹlǧǧed+V+Ind+Prt+Sg1: vuõʹlǧǧem
!! {{{
 e:õ <=> u _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+] %^VOWBack: ;
!! }}}

" u å:õ"
!! __@RULENAME@__ - used in
!! = a>ä lowering clockwise
 å:õ <=> u _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+] %^VOWRaise: ;
!!€ puått%^VOWRaise%>u
!!€ puõtt0%>u



!! !Vowel Palatalization
"diphthong allophonic realization in palatalization u å:e"
!! __@RULENAME@__ - deriving 
 å:e <=> u _ :ʹ  [Cns:+|Cns:+ :ˈ Cns:+]  %^PALE: %^PAL: ;
!!€ puå0v0v%^PALE%^PAL%^CC2CCC%>e
!!€ pueʹvˈv000%>e


"diphthong allophonic realization in palatalization u å:ẹ"
!! __@RULENAME@__ - deriving 
 å:ẹ <=> u _ :ʹ  [Cns:+|Cns:+ :ˈ Cns:+] %^PALẸ: %^PAL: ;

!! !Vowel Lowering and Fronting

"Even syllabic verbs I, diphthong opening õ:ä after i:e"
!! __@RULENAME@__ - deriving +Ind+Prs+Sg3, in čiõkkâd
 õ:ä <=>
 i:e _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+]  BetweenStemAndHeight %^VOWLower: %^PALÄ: ;
 u _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+]  BetweenStemAndHeight %^VOWLower: %^PALÄ: ;




!! !SECONDARY FRONTING


 
"Even syllabic verbs I, secondary vowel fronting with PAL u õ:e"
!! __@RULENAME@__ - deriving +Ind+Prs+Pl3 in VIQQAD: kuõskkâd >kueʹsǩǩe
 õ:e <=>
  u _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+] %^VOWLower: %^PALE: ; ! deriving +V+Inf in vueʹlǧǧed

         i _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+] %^VOWLower: %^PALE: ; 
! - deriving +Ind+Prs+Sg3, in kuõskkâd kueʹsǩǩe CHECK THIS



"Even syllabic verbs I, secondary u > v "
!! __@RULENAME@__ 
 u:v <=> u _ (ʹ:|0:ʹ) [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+] %^VY2XYY: ;


!! !RELATIVE VOWEL LENGTHENING

!! vowel lengthening and consonant shortening, %^Pen: %^V2VV and %^CShort 

"Even syllabic verbs I, relative vowel lengthening 0:â"
!! __@RULENAME@__ - deriving +V+Inf in TIETTED: uudd > uʹvdded
 0:â <=> OnSetC _ Vow:â (u:|i:|%{VU%}:) (ʹ:|0:ʹ)  Cns: [Cns:*|:Cns*] %^V2VV: ;
   OnSetC _ [â|õ:â] (u:|i:|%{VU%}:) (ʹ:|0:ʹ) Cns:+ %^V2VV: ( %^VOWLower: ) ;
!! šõddâd+V+Imprt+Sg3: 
!!€ š0õdd%^V2VV%^VOWLower%>as
!!€ šââdd00%>as

!Distinguishing mainstam and maainstam
      OnSetC _ â (u:|i:) (ʹ:|0:ʹ)  Cns: [Cns:*|:Cns*]  Vow: [Cns:+|:Cns]  %^Pen: %^V2VV: ;
      e _ Cns: Cns:+ %^PALÂ: %^PALNo: ;

!"Relative vowel lengthening 0:Vx"
! __@RULENAME@__ - used in
! 0:Vx <=> OnSetC _ ?:Vx (u:|i:|%{VU%}:|:u) (ʹ:|0:ʹ)  Cns: [Cns:*|:Cns*] %^V2VV: ;
!  OnSetC _ ?:Vx (u:|i:|%{VU%}:) (ʹ:|0:ʹ)  Cns: [Cns:*|:Cns*] Vow: Cns:+  %^Pen: %^V2VV: ;
!                where Vx in (a å) ;

"0:ẹ relative vowel lengthening"
!! __@RULENAME@__ - 
 0:ẹ <=>         OnSetC _ ẹ (i|u) Cns: (Cns:*|:Cns*) %^V2VV: ;
!!€ p0ẹss%^V2VV
!!€ pẹẹss0


"Even syllabic verbs I, relative vowel lengthening 0:e"
!! __@RULENAME@__ - deriving +V+Inf in TIETTED: uudd > uʹvdded
 0:e <=> OnSetC _ e  (ʹ:|0:ʹ) Cns:+ %^V2VV: BetweenVowHeightAndConsGrade ;
!!€ čââʹlmt0eʹm%^V2VV
!!€ čââʹlmtee0m0
        OnSetC _ Vow:e (i|u) (ʹ:|0:ʹ) Cns: (Cns:*|:Cns*) %^V2VV: ;
!!€ p0ẹi0vv%^V2VV%^PAL%^XYY2XY
!!€ peeiʹv0000
         OnSetC _ i:e  (ʹ:|0:ʹ) Cns:+ %^V2VV: %^VOWLower: ;
         OnSetC _ i:e  (ʹ:|0:ʹ) Cns:+ %^V2VV: %^VOWLower: ;
!!€ v0iǯǯ%^V2VV%^VOWLower%>as 
!!€ veeǯǯ00%>as

!Distinguishing mainstam and maainstam
      Cns: _ e (u:|i:) (ʹ:|0:ʹ)  Cns:+ (Vow:) Cns:+  %^V2VV: ; 
!!€ n0eiʹbb%^V2VV%^CC2C
!!€ neeiʹb000
      [ n | š ] _ e (ʹ:|0:ʹ) ǩ ǩ %^V2VV: ;
!!€ kaađn0e0ǩǩ%^V2VV%^PAL
!!€ kaađneeʹǩǩ00


"Even syllabic verbs I, relative vowel lengthening 0:å"
!! __@RULENAME@__ - deriving
 0:å <=>
   OnSetC _ [å|o:å] (u:|i:) (ʹ:|0:ʹ) Cns:+ %^V2VV: ( %^VOWLower: ) ;
   OnSetC _ å (ʹ:|0:ʹ) Cns:+ %^V2VV: ;
   OnSetC _ å (ʹ:|0:ʹ) Cns:+ Vow: [Cns:*|:Cns*] %^Pen: %^V2VV: ;


!Distinguishing mainstam and maainstam
!Vowel lengthening in INDPRSSG3 and IMPSG2 0:å kååʹmmerd, maainâst
!      Cns: _ å (u:|i:) (ʹ:|0:ʹ)  Cns: [ Cns:* | :Cns* ] [e:|a:|â:] Cns:+  %^Pen: %^V2VV: ;


"Even syllabic verbs I, relative vowel lengthening 0:õ"
!! __@RULENAME@__ - deriving +V+Inf in TIETTED: uudd > uʹvdded
 0:õ <=>
     OnSetC _ Vow:õ (u:|i:) (ʹ:|0:ʹ)  Cns: [Cns:* |:Cns ] %^V2VV: ;
!! âʹlǧǧ+N+Pl+Gen: __boy/poika__
!!€ 0âʹlǧǧ%^V2VV%^VOWRaise%^PAL%^XYY2XY%>i
!!€ õõʹlj000%>i
!! čâʹlmm+N+Pl+Acc: __eye/silmä__
!!€ č0â0lmm%^V2VV%^VOWRaise%^PAL%^XYY2XY%>id
!!€ čõõʹlm00000%>id
!Distinguishing mainstam and maainstam
      Cns: _ õ (u:|i:) (ʹ:|0:ʹ)  Cns: Cns:* Vow: [Cns:+|:Cns]  %^Pen: %^V2VV: ; 

   OnSetC _ õ (ʹ:|0:ʹ) Cns: [Cns:* |:Cns ] %^V2VV: ; 
!!€ v0õrr%^V2VV%^CC2C
!!€ võõr000
!!€ s0õll%^V2VV%^CC2C
!!€ sõõl000
!!€ mättʼt0õss%^V2VV%^KK2GG
!!€ mättʼtõõzz00
!!€ s0õhss%^V2VV%^XYY2VY
!!€ sõõus000

"Even syllabic verbs I, relative vowel lengthening 0:ä"
!! __@RULENAME@__ - deriving +V+Inf in TIETTED: uudd > uʹvdded
 0:ä <=> OnSetC _ ä (i|:u) (ʹ:|0:ʹ) Cns:+ %^V2VV: ;
 OnSetC _ ä  (ʹ:|0:ʹ) Cns:+ %^V2VV: ([%^CC2C:|%^XYY2XY:]) RBound ;
!!€ 0äldd%^V2VV%^CC2C
!!€ ääld000
!!€ j0äuʹrr%^V2VV%^CC2C
!!€ jääuʹr000
   OnSetC _ ä h: (ʹ:|0:ʹ) Cns:+ %^V2VV: BetweenVowHeightAndConsGrade %^XYY2VY: ;
!! mäʹhssed+V+Ind+Prs+Sg1: __pay/maksaa__
!!€ m0ähss%^V2VV%^XYY2VY%>am
!!€ määus000%>am
! €s0ähss%^V2VV%^XYY2VY%>am
! €sääus000%>am

 OnSetC _ [å:ä|ä|a:ä] (h:u|u|i) (ʹ:|0:ʹ) Cns:+ %^V2VV: ( %^VOWLower: )  ; 
!Distinguishing mainstam and maainstam
!- kååʹmmerd, maainâst Vowel lengthening in INDPRSSG3 and IMPSG2
    OnSetC _ ä (h:u|u|i) (ʹ:|0:ʹ) Cns:+ Vow: [Cns:+|:Cns] [%^Pen: %^V2VV:|%^IMPSG2:|%^INDPRSSG3:] ;
    OnSetC _ ä (i|h:u|u) (ʹ:|0:ʹ) Cns:+ Vow: Cns:* %^Pen: %^V2VV: ;



"Even syllabic verbs I, relative vowel lengthening 0:a"
!! __@RULENAME@__ - deriving +V+Inf in MAINSTED: maainstam, mainstam
 0:a <=>
  OnSetC _ Vow:a (i|u) (ʹ:|0:ʹ) Cns:+ %^V2VV: ;
!!€ 0äldd%^V2VV%^VOWRaise%^CC2C%>už
!!€ aald0000%>už
!!€ dur0ak%^V2VV
!!€ duraak0

!Distinguishing mainstam and maainstam
  OnSetC _ a  (i|u)  (ʹ:|0:ʹ) Cns:+ Vow: [Cns:+|:Cns]  %^Pen: %^V2VV: ;
!!€ m0ainâst%^Pen: %^V2VV
!!€ maainâst0
   [ b | ʒ | (ǯ) ǯ | (g) g | i b | i n |(k) k |l|m |(n) n | n v|r g|r| r t | u d|u l| u n | u ŋ | u r] _ s t (%^Pen: %^V2VV:) %^IMPSG2: ;
   [ ʹ đ đ | ʹ ǩ |ʹ l|ʹ l ǩ |ʹ m m |ʹ p] _ s t %^IMPSG2: ;
   Cns: _ a  i  (ʹ:|0:ʹ) Cns: Vow: [Cns:+|:Cns]  %^Pen: %^V2VV: ; 
   Cns: _ a  i  Cns: Vow: [Cns:+|:Cns]  %^Pen: %^V2VV: ;
   Cns: _ a (u:|i:) (ʹ:|0:ʹ)  Cns:+ Vow: [Cns:+|:Cns]  [%^Pen: %^V2VV:|%^IMPSG2:|%^INDPRSSG3:] ; 




"Even syllabic verbs I, relative vowel lengthening 0:o"
!! __@RULENAME@__ -  simultaneous lengthening and lowering: juʹrdded > joordam juʹrdded+V+Ind+Prs+Sg1
 0:o <=>
   OnSetC _ [u:o  [Cns: - v: ] [Cns:* |:Cns*] | u:o [%{VU%}:u|(ʹ:) v:] (ʹ:|0:ʹ) [d|l|r|ʒ] | u:o  (ʹ:) [ v:|h:]  (0:ʹ) [d d:|l l:|r r:|ʒ ʒ:|s s:|t t:| č č: |c c: ] ] %^V2VV:  %^VOWLower: ;
!!€ 0uvdd%^V2VV%^VOWLower%^XYY2VY%>če
!!€ ooud0000%>če
!! juʹrdded+V+Ind+Prs+Sg1 __think__
!!€ j0urdd%^V2VV%^VOWLower%^XYY2XY%>am
!!€ joord0000%>am

  OnSetC _ å:o  (ʹ:|0:ʹ) Cns:+  %^V2VV: %^VOWRaise: ;
!!€ k0åʹll%^V2VV%^VOWRaise%>in
!!€ kooʹll00%>in
   OnSetC _ o (i|u) (ʹ:|0:ʹ) Cns:+  %^V2VV: ; 
! - deriving toll+N+Sg+Gen: tool
!!€ t0oll%^V2VV%^CC2C
!!€ tool000
!!€ t0o0ll%^V2VV%^PAL%^CC2C%>e
!!€ tooʹl0000%>e

OnSetC _ o (i|u) (ʹ:|0:ʹ) Cns:+ Vow: [Cns:+|:Cns] %^Pen: %^V2VV: ;
!!€ k0onttâr%^Pen: %^V2VV%^Pen: %^XYY2XY
!!€ koont0âr00



"Even syllabic nouns I, relative vowel lengthening i"
!! __@RULENAME@__ - ǩiđđ:ǩiiđ
 0:i <=> OnSetC _ [ e:i| ẹ:i ]  [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+] %^V2VV: %^VOWRaise:  ;
OnSetC _ i [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+] %^V2VV: BetweenVowHeightAndMorph   ;
  OnSetC _ i  [(ʹ:|0:ʹ) CNoJ:|[h:|u] (ʹ:|0:ʹ) Cns:+] Vow: [Cns:+|:Cns]  %^Pen: %^V2VV: ;
!! pirsted+V+Ind+Prs+ConNeg
!!€ p0irâst%^Pen%^V2VV
!!€ piirâst0
   OnSetC _ [e:i|ẹ:i] [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+] %^V2VV: %^VOWRaise: ;
!!€ p0ẹss%^V2VV%^VOWRaise%>už
!!€ piiss00%>už

   OnSetC _ i (ʹ:|0:ʹ) CNoJ: Cns:*  %^V2VV:  ;
!   OnSetC _ i (ʹ:|0:ʹ) Cns:+ [ %^V2VV: (%^PAL:|%^PALNo:) [%^CC2C:|%^XYY2XY:]] ;
!!€ ǩ0iđđ%^V2VV%^CC2C
!!€ ǩiiđ000



"Even syllabic nouns, relative vowel extra lengthening u not followed by v"
!! __@RULENAME@__ - declension of nouns kunn > kuun
 0:u <=> OnSetC _ [o:u|u] (ʹ:|0:ʹ) Cns: Cns:* (%^Pen: %^CC2C:) %^V2VV: ((%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:|%^PALNo:) BetweenPALNoAndMorph ;
 OnSetC _ o:u (i) (ʹ:|0:ʹ) Cns: Cns:*  %^V2VV:  %^VOWRaise:  ;
 OnSetC _ u (i) (ʹ:|0:ʹ) Cns: Cns:*  %^V2VV: BetweenVowHeightAndMorph ;
           Cns: _  u (ʹ:|0:ʹ)  CNoV: Vow: [Cns:+|:Cns]   %^Pen: %^V2VV: [%>|#|(%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:] ;
 
!! mainstummuš+N+Err/Orth+Sg+Gen
!!€ mainstumm0ušš%^Pen%^CC2C%^V2VV%^KK2GG
!!€ mainstum0uužž000


!! simultaneous lengthening and raising, hmm: xfst ordering might be easier



!! !VOWEL DUMMY LOSS


!! !SEMI VOWELS

 
"Even syllabic nouns, for j>i"
!! __@RULENAME@__ - sijdd > siid This will need a special extra-lengthening rule
 j:i <=> OnSetC i: (%{ʹ%}:) _ (ʹ:) [ d d:|s s:] BetweenStemAndConsGrade %^XYY2VY: ;
!!€ ijss%^XYY2VY%>âs
!!€ iis00%>âs
  Vow: (ʹ:|:ʹ) _ j: BetweenStemAndConsGrade %^J2I: [%^CC2C:|%^XYY2XY:] ;

!!€ sââʹjj%^PALNo%^J2I%^CC2C
!!€ sââ0i00
!! tuâjj+N+Sg+Acc __work__
!!€ tuâjj%^J2I%^CC2C
!!€ tuâi000

"Even syllabic verbs,  for v>u"
!! __@RULENAME@__ - uvdd > ouʹdde
 v:u <=>  (0:o) u: (ʹ:) _ (0:ʹ) [ǯ ǯ: |ʒ ʒ:|d d:|l l:|r r:] BetweenStemAndConsGrade [%^XYY2VY: |%^CCC2CC:|%^XYY2VYY:] ;
!!€ uv0dd%^PAL%^XYY2VY
!!€ uuʹd000
!!€ uvdd%^VOWLower%^XYY2VYY
!!€ oudd00

 "Even syllabic nouns, for h>u"
!! __@RULENAME@__ - luhtt  s s: ...
 h:u <=>
 Vow: (%{ʹ%}:) _ (:ʹ) [s s:|š š:|t t:|c c:|č č:] BetweenStemAndConsGrade %^XYY2VY: ;
!! uhss+N+Der/Dim+N+Sg+Gen __door__
!!€ uh0ss%^PAL%^XYY2VY%>e
!!€ uuʹs000%>e

!!€ s0ähss%^V2VV%^XYY2VY%>am
!!€ sääus000%>am

 Vow: (%{ʹ%}:) _ (:ʹ) [s s:|š š:|t t:|c c:|č č:] Vow: Cns:* PenBetweenStemAndConsGrade %^Pen: %^XYY2VY: ;
!! piiutâs+N+Sg+Nom __clothing/vaate__
!!€ p0ihttâz%^Pen%^V2VV%^Pen%^XYY2VY%^DeVC
!!€ piiut0âs000


 "Even syllabic nouns, for h>i"
!! __@RULENAME@__ - kueʹhtt 
h:i <=> Vow: (:ʹ) _ t t: BetweenStemAndConsGrade %^XYY2IY: ;
!! kueiʹt+Num+Sg+Gen __two/kaksi__
!!€ kuâh0tt%^PALE%^PAL%^XYY2IY
!!€ kueiʹt0000

"trisyllabic verbs and doer derivations, i>j"
!! __@RULENAME@__ - used in
 i:j <=> [Vow: |Cns:] (0:ʼ) _ PenBetweenStemAndVowelLoss (%^RmVow:) %^I2J:  ;
   Vow:i (0:ʹ) _ [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+] BetweenStemAndHeight %^VOWRaise:  %^VYY2XYY: ;
!! peigg
!!€ peigg%>u
!!€ pijgg%>u

!! !VOWELS TENSE vs LAX 2012-11-28


"Even-syllabic nouns, for â:e"
!! __@RULENAME@__ -  ǩiâpp:ǩieʹpˈpe
 â:e <=> i _ :ʹ [Cns:+|Cns:+ (:ˈ|ˈ:)  Cns:+]  %^PALE: %^PAL: ;
!! +N+Sg+Ill
!!€ miâ0rr%^PALE%^PAL%>e
!!€ mieʹrr00%>e
!!€ piâ0ss%^PALE%^PAL
!!€ pieʹss00
      u _ (:ʹ) [Cns:+|Cns:+ (:ˈ|ˈ:) Cns:+] %^PALE: %^PAL: ;
!!€ kuâ0ll%^PALE%^PAL
!!€ kueʹll00
  [u|:i]  _ (i|u) :ʹ [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+|Cns:+ :ˈ Cns:+]  BetweenStemAndHeight (%^VOWRaise:) %^PALE: %^PAL: ;
!! tieʹtted+V+Imprt+Sg2: __know/tietää__
!!€ teâ0tt%^VOWRaise%^PALE%^PAL%^CC2C
!!€ tieʹđ00000
  e:  _ (i|u) :ʹ [Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+|Cns:+ :ˈ Cns:+]  %^PALE:  ;
!!€ ǩeâ0sttez%^Pen: %^PALE%^Pen: %^PAL%^Pen: %^XYY2XY%^DeVC
!!€ ǩeeʹst0es0000

"Even-syllabic nouns, for â:ẹ"
!! __@RULENAME@__ - used in
 â:ẹ <=> i _ :ʹ [Cns:+|Cns:+ :ˈ Cns:+]  %^PALẸ: %^PAL: ;
!! pieʹll+N+Sg+Nom: __half__
!!€ piâ0ll%^PALẸ%^PAL
!!€ piẹʹll00


"Palatalization for ẹ:e"
!! __@RULENAME@__ - used in
 ẹ:e <=> OnSetC (ẹ:|0:) _ (i|u) :ʹ [Cns:+|Cns:+ (:ˈ|ˈ:0) Cns:+] BetweenStemAndHeight %^PAL:  ;
!!€ p0ẹi0vv%^V2VV%^PAL%^CC2C
!!€ peeiʹv0000
         OnSetC _ ẹ: (i|u) :ʹ [Cns:+|Cns:+ (:ˈ|ˈ:0) Cns:+]  %^PAL:  ;

!! !VOWEL and ZERO ALTERNATION


!"Realization for %{E1%} in e "
!!! __@RULENAME@__ - kååʹmmerd
!!! {{{
! %{E1%}:e <=> _ (ʹ:|0:ʹ) Cns:+ (%^Pen: %^V2VV:) %^IMPSG2: ;
!!! }}}

!"Realization for %{A1%} in a " 
!!! __@RULENAME@__ - kåʹmrded, vuejat, maainâst
!!! {{{
! %{A1%}:a <=> _ (ʹ:|0:ʹ) Cns:+ (%^Pen: %^V2VV:) %^IMPSG2: ;
!              _ (ʹ:|0:ʹ|ʼ:) [i|Cns:+] (%^Pen: %^V2VV:) %^INDPRSSG3: ;
!!! }}}

"Realization for â in a"
!! __@RULENAME@__ - used in
 â:a <=> OnSetC _ (ʹ:|0:ʹ) [Cns:+] (%^Pen: %^V2VV:) %^PenVow2a: ;
!!ǩiõrggned+V+Ind+Prs+Sg3
!!€ ǩiõrggân%^PenVow2a
!!€ ǩiõrggan0

!! !THE NON-ORTHOGRAPHIC SYLLABLE 


"Loss of ʼ when preceded by vowel"
!! __@RULENAME@__ - This is a temporary solution to  "ʼ" in võʹllʼjed
!! {{{
 ʼ:0 <=> Vow:Vow _ Cns: ; 
!! }}}

"%{A1%}:ʼ when subseqent syllable has vowel v"
!! __@RULENAME@__ - This is a temporary solution to  "ʼ" in võʹllʼjed 2013-08-29
!! {{{
%{A1%}:ʼ <=> [l l|n n] _ i: %> Vow: ; 
!! }}}

!! !PALATALIZATION


!2013-09-11"Even-syllabic verbs I, Palatalization mark between V and C"
!2013-09-11 __@RULENAME@__ - used +Ind+Prs+Pl3, +Ind+Prt+Pl3
!2013-09-11 %{ʹ%}:ʹ <=> OnSetC (Vow:0|:Vow) Vow:Vow _ [Cns:Cns Cns:*|Cns:Cns+ (ˈ:|:ˈ) Cns:+|Cns:Cns+ :ˈ Cns:+] (%^VOWSH: (%^CLong:)|%^VOWTense:|%^Pen: %^V2VV: %^CShort:) (%^VOWLower:|%^VOWRaise:) (%^VOWFront:|%^VOWTense:0) (%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL: RBound ;



"0:ʹ as transfer from left of v:u"
!! __@RULENAME@__ - used +Ind+Prs+Pl3, uʹvdded+V+Ind+Prs+Pl3: ouʹdde
0:ʹ <=> v: (:ˈ) _ [ʒ ʒ:|d d:|l l:|r r:]       BetweenStemAndPALNo  %^PAL: [%^XYY2VY:] ;
!      v:u (:ˈ) _ [ʒ ʒ:|d d:|l l:|r r:]       BetweenStemAndPALNo  %^PAL: [%^XYY2VY:|%^XYY2VYY:|%^XYY2XY:] ;
!!€ uv0dd%^PAL%^XYY2VY
!!€ uuʹd000
 
         ʹ:0 v:u (:ˈ) _ [ʒ ʒ:|d d:|l l:|r r:]       BetweenStemAndPALNo  ( %^PAL:) RBound ;
              h: (:ˈ) _ [c c:|č č:|s s:|š š:|t t:]  BetweenStemAndPALNo  %^PAL: [ %^XYY2VY: |%^XYY2IY: ] ;
!!€ t0äh0tt%^V2VV%^PAL%^XYY2VY
!!€ tääuʹt0000
!! kueiʹt+Num+Sg+Gen __two/kaksi__
!!€ kuâh0tt%^PALE%^PAL%^XYY2IY
!!€ kueiʹt0000
             [Vow:|:Vow] (i|u) (:ˈ) _ Cns: [Cns:*|:Cns*|Cns:* (ˈ:|:ˈ) Cns:*] (i:) BetweenStemAndPALNo  %^PAL: ;
!!€ saa0kk%^VV2V%^PAL%>e
!!€ s0aʹǩǩ00%>e
!!€ mõõ0nn%^VV2V%^VOWLower%^PAL%>e
!!€ m0âʹnn000%>e
!!€ kuâ0ll%^PALE%^PAL
!!€ kueʹll00
!!€ stu%{VU%}0l%^PAL
!!€ stuuʹl0
             [Vow:|:Vow] (i|u) (:ˈ)  _ Cns: [Cns:*|:Cns*|Cns:* (ˈ:|:ˈ) Cns:*] Vow: Cns:* PenBetweenStemAndPALNo (%^Pen: %^PALE:)  %^Pen: %^PAL: ;
!!€ k0ä0llez%^Pen: %^V2VV%^Pen: %^PAL%^DeVC
!!€ kääʹlles000
!!€ jeä0kˈkel%^Pen: %^PALE%^Pen: %^PAL%^Pen: %^KKK2ZERO
!!€ jeeʹ000el000

!! The left context allows for:
!! Vowel shortening, and j/v > Vow 

"d:đ in weak grade"
!! __@RULENAME@__ - used in
 d:đ <=> Vow: (:ʹ) _ [g: (ˈ:|:ˈ)  g:] BetweenStemAndConsGrade [%^XYY2XY:] ;
!!€ ǩeädgg%^VOWRaise%^PALẸ%^PAL%^XYY2XY
!!€ ǩiẹđj00000
         Vow: (:ʹ) _ [g: (ˈ:|:ˈ) g:] Vow: Cns* PenBetweenStemAndConsGrade [%^Pen: %^XYY2XY:] ;

 
"Even-syllabic verbs I, Palatalization of g:ǧ"
!! __@RULENAME@__ - used in
 g:ǧ <=> Vow: (ʹ:|0:ʹ) (Cns:)     _ (ˈ:|:ˈ) g: BetweenStemAndPALNo (%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL: (%^CCC2CC:) RBound ;
           Vow: (ʹ:|0:ʹ) (Cns:) g: (ˈ:|:ˈ) _   BetweenStemAndPALNo (%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL: (%^CCC2CC:) RBound ;

    Vow: 0: _ BetweenStemAndPALNo %^PAL: ;
!! bioloog+N+Sg+Ill __biologist__
!!€ bioloo0g%^PAL%>e
!!€ biolooʹǧ0%>e


"Even-syllabic verbs I, Palatalization of k:ǩ here"
!! __@RULENAME@__ - used in
 k:ǩ <=>
 Vow: (ʹ:|0:ʹ) (Cns:)   _ (0:) k: [BetweenStemAndPALNo |Vow: Cns:+ PenBetweenStemAndPALNo %^Pen: ] (%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL: ([%^CC2C:|%^XYY2XY:]) (%^DeVC:) RBound ;
 Vow: (ʹ:|0:ʹ) Cns:   _ (k:) [BetweenStemAndPALNo |Vow: Cns:+ PenBetweenStemAndPALNo %^Pen: ] (%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL: ([%^CC2C:|%^XYY2XY:]) (%^DeVC:) RBound ;
!! hääʹsǩ Perhaps the stem should simply be __häskk__
!!€ hää0sk%^PAL
!!€ hääʹsk0
 Vow: (ʹ:|0:ʹ) (Cns:)   _ (0:) k: Vow: Cns:+ PenBetweenStemAndPALNo (%^Pen: [%^PALE:|%^PALÄ:|%^PALẸ:]) %^Pen: %^PAL: (%^Pen: [%^CC2C:|%^XYY2XY:]) (%^DeVC:) RBound ;
!! mieʹlǩǩ+N+Sg+Acc: __milk/maito__
!!€ miâ0lkk%^PALE%^PAL%^CC2C
!!€ mieʹlǩ0000
 Vow: (ʹ:|0:ʹ)        _ (ˈ:|:ˈ) k: BetweenStemAndPALNo (%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL: (%^CC2CCC:) RBound ;
! Vow: (ʹ:|0:ʹ) (Cns:)     _ k: (%^VV2V:|%^VOWSH: (%^CLong:)) NeutrHighBack (%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL: RBound ;
  Vow: (ʹ:|0:ʹ) (Cns:) k: (ˈ:|:ˈ) _   BetweenStemAndPALNo (%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL: (%^CC2CCC:) RBound ;

!!€ jo0kk%^PAL%>e
!!€ joʹǩǩ0%>e
!!€ saa0kk%^VV2V%^PAL%>e
!!€ s0aʹǩǩ00%>e
  Vow: _ (%^RmVow:) %^PALK: ;
!! kåålvak+N+Sg+Loc
!!€ kåålvak%^RmVow%^PALK%>est
!!€ kåålv0ǩ00%>est

"Even-syllabic nouns I, Depalatalization of ǧ and weakening of grade to ǥ"
!! __@RULENAME@__ - vueʹlǧǧed+V+Ind+Prs+Sg1: vuâlǥam
 ǧ:ǥ <=> Vow: (ʹ:0) (Cns:)    _ ǧ: BetweenStemAndPALNo %^PALNo: [%^CC2C:|%^XYY2XY:]  ;

!! derive vueʹlǧǧed+V+Imp+Pl3: vuâlǥǥaz

"Even-syllabic nouns I,  ǧ and weakening of grade to j"
!! __@RULENAME@__ - vueʹlǧǧed+V+Ind+Prs+4: vueʹljet  âʹlǧǧ+N+Sg+Gen: ââʹlj
 ǧ:j <=> [ ʹ | :ʹ] Cns: _ ǧ: BetweenStemAndPALNo ((%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) [%^CC2C:|%^XYY2XY:] ;
        [i|u] [ ʹ | :ʹ] _ ǧ: BetweenStemAndPALNo ((%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:) [%^CC2C:|%^XYY2XY:] ;
!! äiʹǧǧ+N+Sg+Gen: __time/aika__
!!€ 0äiʹǧǧ%^V2VV%^CC2C
!!€ ääiʹj000

"Even-syllabic nouns I, Depalatalization of ǧǧ"
!! __@RULENAME@__ - vueʹlǧǧed+V+Ind+Prs+Sg3: vuâlgg
 ǧ:g <=> Vow: (ʹ:) (Cns:)          _ (ˈ:) ǧ: BetweenStemAndPALNo  %^PALNo: RBound ;
         Vow: (ʹ:) (Cns:) ǧ: (ˈ:) _        BetweenStemAndPALNo %^PALNo: RBound ;
!!€ vueʹlǧǧ%^PALNo
!Maybe this is not needed

"Even-syllabic nouns I, Depalatalization of ǩ:k"
!! __@RULENAME@__ - used in
 ǩ:k <=> 
  Vow: (ʹ:) (Cns)  _ (ˈ:|:ˈ) ǩ: BetweenStemAndPALNo    %^PALNo: RBound ;
  Vow: (ʹ:) (Cns) ǩ: (ˈ:|:ˈ) _  BetweenStemAndPALNo %^PALNo: RBound ;
!!mäʹtǩǩ+N+Sg+Ill
!!€ mäʹtǩǩ%^PALNo%>a
!!€ mä0tkk0%>a
   Vow: _ 0: %^PALNo: ;
!! kaađneǩ+N+Sg+Ill:
!!€ kaađneǩ0%^PALNo%^C2CC%>a
!!€ kaađnekk00%>a

!  Vow: (ʹ:) (Cns)  _ (ˈ:|:ˈ) ǩ: (e: s:) BetweenStemAndPALNo    %^PALNo: RBound ;
!  Vow: (ʹ:) (Cns) ǩ: (ˈ:|:ˈ) _    (e: s:) BetweenStemAndPALNo %^PALNo: RBound ;
 Vow: (ˈ:|:ˈ) _ (0:) PenBetweenStemAndVowelLoss (%^RmVow:) %^PALNo: ;
!! prääʹzniǩ+N+Sg+Ill: __celebration/juhla__
!!€ prääʹzniǩ%^Pen: %^PALNo%^RmVow%^PALNo%>a
!!€ prää0zn0k000%>a
 Cns: (ˈ:|:ˈ) _ Vow: Cns:* PenBetweenStemAndVowelLoss (%^RmVow:) %^PALNo: (%^DeVC:) RBound ;


"Even-syllabic nouns, removing palatalization in -est +Loc nouns"
!! __@RULENAME@__ - removing palatalization in +Sg+Ill,  pieʹss:peässa
   ʹ:0 <=>  [Vow:|:Vow] _ [:Cns :Cns+|Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+] BetweenStemAndPALNo %^PALNo: [RBound|%^UltRmVow:|%^VC:] ;
!!€ čââʹlmteʹm0%^PALNo%^C2CC%>a
!!€ čââʹlmte0mm00%>a
   Vow:  _ [:Cns :Cns+|Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+] BetweenStemAndPALNo %^PALNo: ;
!!€ čââʹlmteʹm0%^PALNo%^C2CC%>a
!!€ čââʹlmte0mm00%>a
   [Vow:|:Vow] _ [:Cns :Cns+|Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+] %^RmVow: %^PALNo: [RBound|%^UltRmVow:|%^VC:] ;
!! stroitteʹl+N+Sg+Ill: __plant/laitos__
!!€ stroitteʹl%^RmVow%^PALNo%>a
!!€ stroitt00l00%>a
  [Vow:|:Vow] _ [:Cns :Cns+|Cns:+|Cns:+ (ˈ:|:ˈ) Cns:+] Vow: Cns:* PenBetweenStemAndPALNo %^Pen: %^PALNo:  ;
!! prääʹzniǩ+N+Sg+Ill: __celebration/juhla__
!!€ prääʹzniǩ%^Pen: %^PALNo%^RmVow%^PALNo%>a
!!€ prää0zn0k000%>a

!jurdded
                    Vow: _ CNoV: Cns:+ BetweenStemAndPALNo %^PALNo: ; 
                    Vow: _ v:u ; 



!! !!Consonant QUANTITY CHANGE gradation rules

!! !Weakening Consonant Cluster

!! dealing with relative length changes
!! mõõnnâd : mõʹnne : mõõn 

!"Even syllabic verbs I, cg in relation to Vowel length"
! !Vow Cx Cx:0 

"Even syllabic verbs I, cg m:0"
!! __@RULENAME@__ - used with  +Imp+Sg2, +Ind+Prs+ConNeg, 
 m:0 <=>
 Vow: (ʹ:|0:ʹ) (s|r|l|p|d:)  m: (ˈ:) _ BetweenStemAndConsGrade [%^CC2C:|%^CCC2C:|%^XYY2XY:] ;
!!€ sääʹmm%^CC2C
!!€ sääʹm00
         Vow: (ʹ:) h: (ʹ:|0:ʹ)      m: (ˈ:) _ BetweenStemAndConsGrade [%^CC2C: |%^CCC2C:|%^XYY2XY:] ;
         m: (ˈ:) _ Vow: Cns:+ PenBetweenStemAndConsGrade [%^Pen: %^CC2C:|%^Pen: %^CCC2C:|%^Pen: %^XYY2XY:] ;
!! kåʹmrded+V+Ind+Prs+Sg3 __bow__
!!€ k0åʹmmerd%^Pen: %^V2VV%^PenVow2a
!!€ kååʹmmard00

"Even syllabic verbs I, cg for b"
!! __@RULENAME@__ - used
 b:0 <=> Vow:  [i (ʹ:|0:ʹ)|(ʹ:|0:ʹ) Cns: ] b _   BetweenStemAndConsGrade [%^CC2C:|%^XYY2XY:] ;
!! neiʹbb+N+Sg+Gen: __knife/veitsi__
!!€ n0eiʹbb%^V2VV%^CC2C
!!€ neeiʹb000
!! äʹrbb+N+Sg+Gen: __heritage/perintö__
!!€ 0ä0rbb%^V2VV%^PAL%^CC2C
!!€ ääʹrb0000
 Vow:  [i (ʹ:|0:ʹ)|(ʹ:|0:ʹ) Cns: ] b _ Vow: Cns:*   PenBetweenStemAndConsGrade %^Pen: [%^CC2C:|%^XYY2XY:] ;
!! joorbâs+A+Sg+Nom
!!€ j0orbbâz%^Pen%^V2VV%^Pen%^XYY2XY%^DeVC
!!€ joorb0âs00000

"Even syllabic verbs I, second consonant loss p:0"
!! __@RULENAME@__ - used in
 p:0 <=> Vow: (ʹ:|0:ʹ) (Cns:) p: _ BetweenStemAndConsGrade [%^CC2C:|%^XYY2XY:] ;
!!€ ǩiâpp%^CC2C
!!€ ǩiâv00
          Vow: (ʹ:|0:ʹ)            p _ Vow: [Cns:+|n j|l m|s ǩ:|s t|š ǩ:|š m] PenBetweenStemAndConsGrade  [%^Pen: %^CC2C:|%^Pen: %^XYY2XY:] ;  


"Even syllabic verbs I, cg v:0"
!! __@RULENAME@__ - used with  +Imp+Sg2, +Ind+Prs+ConNeg, teevvad > teev

 v:0 <=>
  v: _    BetweenStemAndConsGrade [%^CC2C:|%^XYY2XY:] ;
!!peiʹvv+N+Sg+Gen+Indef
!!€ p0ẹi0vv%^V2VV%^PAL%^CC2C
!!€ peeiʹv000
 v: _  Vow: Cns: [Cns:* |:Cns*]  PenBetweenStemAndConsGrade [%^Pen: %^CC2C:|%^Pen: %^XYY2XY:] ;
!! peiʹvv+N+Sg+Nom: __day/päivä__
!!€ tuõvvâz%^Pen: %^CC2C%^DeVC
!!€ tuõv0âs00



"f:0"
!! __@RULENAME@__ - used in

 f:0 <=>
      f: _    BetweenStemAndConsGrade [%^CC2C:|%^XYY2XY:] ;
!! kaaʹff+N+Sg+Gen __coffee__
!!€ kaaʹff%^CC2C
!!€ kaaʹf0


"Even syllabic verbs I, cg n:0"
!! __@RULENAME@__ - used with  +Imp+Sg2, +Ind+Prs+ConNeg, jiõnn:jiõn
 n:0 <=> 
      n: _  (j)  BetweenStemAndConsGrade [%^CC2C:|%^XYY2XY:] ;
!!päʹrnn
         Vow:  (ʹ:) h: (ʹ:|0:ʹ) n: _ (j) BetweenStemAndConsGrade [%^CC2C:|%^XYY2XY:] ; 
      n: _ (j) Vow: Cns:*  PenBetweenStemAndConsGrade [%^Pen: %^CC2C:|%^Pen: %^XYY2XY:] ;
          

"Even syllabic nouns I, with extra lengthening of vowel ij>ii/uv>uu and dd>d"
!! __@RULENAME@__ - used with  +N+Sg+Nom > +N+Sg+Gen, sijdd > siid

 d:0 <=> d _ BetweenStemAndConsGrade [%^CC2C:|%^XYY2XY:] ;
!!€ 0äldd%^V2VV%^CC2C
!!€ ääld000
!!€ 0äldd%^V2VV%^VOWRaise%^CC2C%>už
!!€ aald0000%>už
 [v:| j:] (0:ʹ) d _  BetweenStemAndConsGrade  [%^XYY2VY:] ;
!!€ uv0dd%^PAL%^XYY2VY
!!€ uuʹd000
!! juʹrdded+V+Ind+Prs+Sg1 __think__
!!€ j0urdd%^V2VV%^VOWLower%^XYY2XY%>am
!!€ joord0000%>am

!! uʹvdded+V+Ind+Prs+4: uuʹdet

! njeiddad > njeeid
! 2012-06-12 v:u allows for uʹvdded
!! - idd:id, +Imp+Sg2, +Ind+Prs+ConNeg double consonants following vowel and "i" at coda that become single consonants in gradation
         d _ Vow: [Cns:+|n j|l m|s ǩ:|s t|š ǩ:|š m]  PenBetweenStemAndConsGrade [%^Pen: %^CC2C:|%^Pen: %^XYY2XY:] ; 





"Even syllabic verbs I, cg for ʒ"
!! __@RULENAME@__ - used with  pääʹʒʒelm+N+Sg+Ill: päʹʒlma
 ʒ:0 <=>
      u: (ʹ:|0:ʹ) v:u (0:ʹ) ʒ _  BetweenStemAndConsGrade %^XYY2VY: ;
      [Vow - u] (i|u) (0:ʹ) ʒ _  BetweenStemAndConsGrade [%^XYY2XY:|%^CC2C:] ;
!! sauʒʒ+N+Pl+Nom __sheep/lammas__
!!€ s0auʒʒ%^V2VV%^%^XYY2VY
!!€ saauʒ000

        Vow: (ʹ:|0:ʹ) ʒ _ Vow: [Cns:+|n j|l m|s ǩ:|s t|š ǩ:|š m]  PenBetweenStemAndConsGrade  [%^Pen: %^CC2C:|%^Pen: %^XYY2XY:] ;


"ǯ:0"
!! __@RULENAME@__ - used in
 ǯ:0 <=>
      Vow: (ʹ:|:ʹ) [ v: (0:ʹ)|Cns:] ǯ _  BetweenStemAndConsGrade [%^CC2C:|%^XYY2XY:| %^XYY2VY: ] ;
!!kuʹvǯǯ+N+Sg+Gen
!!€ kuv0ǯǯ%^PAL%^XYY2VY
!!€ kuuʹǯ000
        Vow: (ʹ:|0:ʹ) (Cns:) ǯ _ Vow: [Cns:+|n j|l m|s ǩ:|s t|š ǩ:|š m] PenBetweenStemAndConsGrade  [%^Pen: %^CC2C:|%^Pen: %^XYY2XY:|%^Pen: %^XYY2VY:] ;


"č:0"
!! __@RULENAME@__ - used in
 č:0 <=>
        Vow: (ʹ:|0:ʹ) Cns: č _ Vow: [Cns:+|n j|l m|s ǩ:|s t|š ǩ:|š m] PenBetweenStemAndConsGrade  [%^Pen: %^CC2C:|%^Pen: %^XYY2XY:] ;

       Vow: [ (ʹ:|0:ʹ)  Cns: | [u|i] (ʹ:|0:ʹ) ] č: _ BetweenStemAndConsGrade [%^CC2C:|%^XYY2XY:] ;
       Vow:  (ʹ:|0:ʹ) [h:|j:] (ʹ:|0:ʹ) č:  _ BetweenStemAndConsGrade %^XYY2VY: ;
       Vow: [ (ʹ:|0:ʹ)  Cns: | [u|i] (ʹ:|0:ʹ) ] č: _ Vow: Cns:+ PenBetweenStemAndConsGrade [%^Pen: %^CC2C:|%^Pen: %^XYY2XY:] ;
       Vow:  (ʹ:|0:ʹ) [h:|j:] (ʹ:|0:ʹ) č:  _ Vow: Cns:+ PenBetweenStemAndConsGrade %^Pen: %^XYY2VY: ;

"c:0"
!! __@RULENAME@__ - used in
 c:0 <=>
        (ʹ:|0:ʹ) Cns: c _ Vow: [Cns:+|n j|l m|s ǩ:|s t|š ǩ:|š m] PenBetweenStemAndConsGrade  [%^Pen: %^CC2C:|%^Pen: %^XYY2XY:] ;
       Vow: [ (ʹ:|0:ʹ)  Cns: | [u|i] (ʹ:|0:ʹ) ] c: _ BetweenStemAndConsGrade [%^CC2C:|%^XYY2XY:] ;
       Vow:  (ʹ:|0:ʹ) [h:|j:] (ʹ:|0:ʹ) c:  _ BetweenStemAndConsGrade %^XYY2VY: ;
       Vow: [ (ʹ:|0:ʹ)  Cns: | [u|i] (ʹ:|0:ʹ) ] c: _ Vow: Cns:+ PenBetweenStemAndConsGrade [%^Pen: %^CC2C:|%^Pen: %^XYY2XY:] ;
       Vow:  (ʹ:|0:ʹ) [h:|j:] (ʹ:|0:ʹ) c:  _ Vow: Cns:+ PenBetweenStemAndConsGrade %^Pen: %^XYY2VY: ;

"ž:0"
!! __@RULENAME@__ - used in
 ž:0 <=>
      Vow: (ʹ:|0:ʹ) Cns: ž _  BetweenStemAndConsGrade [%^CC2C:|%^XYY2XY:] ;
         (ʹ:|0:ʹ) Cns: ž _ Vow: [Cns:+|n j|l m|s ǩ:|s t|š ǩ:|š m] PenBetweenStemAndConsGrade  [%^Pen: %^CC2C:|%^Pen: %^XYY2XY:] ;



"z:0"
!! __@RULENAME@__ - used in
 z:0 <=>
      Vow: (ʹ:|0:ʹ) Cns: z _  BetweenStemAndConsGrade [%^CC2C:|%^XYY2XY:] ;
        (ʹ:|0:ʹ) Cns: z _ Vow: [Cns:+|n j|l m|s ǩ:|s t|š ǩ:|š m] PenBetweenStemAndConsGrade  [%^Pen: %^CC2C:|%^Pen: %^XYY2XY:] ;
      Vow: _ PenBetweenStemAndVowelLoss (%^PenVow2a:) %^RmCns: ;
!!€ ǩeâsttez%^PenVow2a%^RmCns
!!€ ǩeâstta000

"Even syllabic verbs I, cg for đ" 
!! __@RULENAME@__ - used with  ǩiđđ:ǩiiđ
 đ:0 <=>
 Vow: (ʹ:|0:ʹ) đ: _    BetweenStemAndConsGrade %^CC2C: ;
!!€ ǩ0iđđ%^V2VV%^CC2C
!!€ ǩiiđ00
     	 đ: _ Vow: Cns:  PenBetweenStemAndConsGrade [%^Pen: %^CC2C:|%^Pen: %^XYY2XY:] ;
!!€ 0ođđâz%^Pen: %^V2VV%^Pen: %^CC2C%^DeVC
!!€ oođ0âs000

"Even syllabic verbs I, cg for r" 
!! __@RULENAME@__ - used with  võrr:võõr
 r:0 <=>
 Vow: (i|u) (ʹ:|0:ʹ) (Cns:)  r: (ˈ:) _    BetweenStemAndConsGrade [%^CC2C:|%^CCC2C:|%^XYY2XY:] ;
!!€ v0õrr%^V2VV%^CC2C
!!€ võõr000
!!€ paarr%^CC2C
!!€ paar00
!!€ j0äu0rr%^V2VV%^PAL%^CC2C
!!€ jääuʹr0000
 Vow: (i|u) (ʹ:|0:ʹ) (Cns:) r: (ˈ:) _  Vow: Cns:  PenBetweenStemAndConsGrade [%^Pen: %^CC2C:|%^Pen: %^CCC2C:|%^Pen: %^XYY2XY:] ;


"Even syllabic verbs I, cg for l"
!! __@RULENAME@__ - used with  vuʹvll+N+Sg+Gen: vuuʹl
 l:0 <=>
 Vow: (ʹ:|0:ʹ) (Cns:) l: (ˈ:) _ (j)   BetweenStemAndConsGrade [%^CC2C:|%^CCC2C:|%^XYY2XY:] ;
!! deriving 
!!€ t0oll%^V2VV%^CC2C
!!€ tool00
!!€ st0ä0llj%^V2VV%^PAL%^CC2C
!!€ stääʹl0j000
   u: (ʹ:) v:u (0:ʹ) l: _  %^V2VV: ((%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL:|%^PALNo:) [%^CC2C:|%^CCC2C:|%^XYY2XY:] ;
!- used with  vuʹvll+N+Sg+Gen: vuuʹl
!!€ vuʹv0ll%^V2VV%^CC2C
!!€ vu0uʹl00

!! deriving kueʹll+N+Sg+Gen: kueʹl
!!€ kuâ0ll%^V2VV%^PALE%^PAL%^CC2C
!!€ kueʹl00000

"j:0 "
!! __@RULENAME@__ - used in
 j:0 <=>
     j: _    BetweenStemAndConsGrade (%^J2I:) [%^CC2C:|%^XYY2XY:|%^XYY2VY:] ;
!! ǩeʹrjj+N+Pl+Nom __book__
!!€ ǩ0eʹrjj%^V2VV%^CC2C
!!€ ǩeeʹrj000
!! tuâjj+N+Sg+Acc __work__
!!€ tuâjj%^J2I%^CC2C
!!€ tuâi000
    j: _ Vow: Cns:+ PenBetweenStemAndConsGrade [%^Pen: %^CC2C:|%^Pen: %^XYY2XY:|%^Pen: %^XYY2VY:] ;
!!€ p0ojjâz%^Pen: %^V2VV%^Pen: %^CC2C%^DeVC
!!€ pooj0âs000

"Even syllabic verbs I, cg for g"
!! __@RULENAME@__ - used in
 g:0 <=>  Vow: (ʹ:|0:ʹ) (Cns:+) g: _ Vow: [Cns:+|n j|l m|s ǩ:|s t|š ǩ:|š m] PenBetweenStemAndConsGrade [%^Pen: %^CC2C:|%^Pen: %^XYY2XY:] ;
!! cõõggâlm+N+Sg+Ill
!!€ cõõggâlm%^Pen%^VV2V%^Pen%^CC2C%^RmVow%>a
!!€ c0õg00lm000%>a
!!€ ǩeâlggal%^Pen%^XYY2XY
!!€ ǩeâlǥ0al0

          Vow:  [i (0:ʹ)|u (0:ʹ) |(0:ʹ)  [ŋ|r|l|d:]] g: _    BetweenStemAndConsGrade [%^CC2C:|%^XYY2XY:] ;

"Even syllabic verbs I, cg for ǧ"
!! __@RULENAME@__ - used with  vueʹlǧǧed+V+Ind+Prs+Sg1: vuâlǥam
 ǧ:0 <=> Vow: [ [ :ʹ| ʹ: ] Cns: | [u:|i:] [ :ʹ| ʹ: ] ] [ǧ:j|ǧ:] _ BetweenStemAndConsGrade [%^CC2C:|%^XYY2XY:] ;
!! äiʹǧǧ+N+Sg+Gen: __time/aika__
!!€ 0äiʹǧǧ%^V2VV%^CC2C
!!€ ääiʹj000
  Vow: [ [ :ʹ| ʹ: ] Cns: | [u:|i:] [ :ʹ| ʹ: ] ] [ǧ:j|ǧ:] _ BetweenStemAndConsGrade [%^Pen: %^CC2C:|%^Pen: %^XYY2XY:] ;

!! GEMINATE TO WEAK QUALITY GEMINATE
"Even syllabic nouns I, with extra lengthening of vowel V>VV and KK>ǤǤ"
!! __@RULENAME@__  - used in
!! used with  +N+Sg+Nom > +N+Sg+Gen for cases like
!! lookki > looǥǥi.
 k:ǥ <=>
 Vow:  k: _   BetweenStemAndPALNo %^KK2GG: ;
         Vow:   _ [k: | 0:] BetweenStemAndPALNo [%^KK2GG: | %^K2GG:] ;

!! čâustõk+N+Sg+Gen
!!€ čâust0õk0%^V2VV%^K2GG
!!€ čâustõõǥǥ0
       Vow:   _ BetweenStemAndPALNo %^K2G: ;
!! with allegro
!! čâustõk+N+Sg+Ela+PxSg3
!!€ čâustõk%^K2G%>stes
!!€ čâustõǥ0%>stes

  Vow:  k: _ (i:) BetweenStemAndPALNo %^KK2GG: ;
  Vow:  _  k: (i:) BetweenStemAndPALNo %^KK2GG: ;
    k: _ (%^V2VV:) %^KK2GG: ;
    _ k: (%^V2VV:) %^KK2GG: ;
!!€ j0okk%^V2VV%^KK2GG
!!€ jooǥǥ00
    k: ˈ: _ (%^V2VV:) %^KKK2GG: ;
  Vow:Vow  _ ˈ: k: (%^V2VV:) %^KKK2GG: ;
!! čuâǥǥas+N+Sg+Nom  __road__
!!€ čuâkˈk%^KK2GG%>as
!!€ čuâǥ0ǥ00%>as

  Vow:Vow  _ k: BetweenStemAndPALNo %^KK2G: ;
!! loǥškueʹtted __begin to read__
!!€ lookk%^VV2V%^KK2G»škueʹtted
!!€ l0oǥ000»škueʹtted

  Vow:Vow  k: (ˈ:) _ Vow: Cns:* PenBetweenStemAndPALNo %^Pen: [%^KKK2GG: | %^KK2GG: ] ;
  Vow:Vow  _ (ˈ:) k: Vow: Cns:* PenBetweenStemAndPALNo %^Pen: [%^KKK2GG: | %^KK2GG: ] ;
!! čõõǥǥâs
!!€ č0õkkâz
!!€ čõõǥǥâs

   

"Even syllabic nouns I, with extra lengthening of vowel V>VV and k:j"
!! __@RULENAME@__ - used in
!! used with
!!  > .
 k:j <=>   
 Vow: :ʹ k: _  BetweenStemAndPALAllo (%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL: %^KK2GG: ;
         Vow: (:ˈ) :ʹ    _ [ k:|0:] BetweenStemAndPALAllo (%^PALE:|%^PALÄ:|%^PALẸ:) [%^PAL: %^KK2GG:| %^Allegro: %^PAL: %^KK2G:| %^PAL: %^K2GG: ] ;
       Vow: (:ˈ) :ʹ  _  BetweenStemAndPALNo  %^PAL: %^K2G: ;
!! with allegro

!!2015-10-05
  Vow: :ʹ k: (ˈ:)  _ BetweenStemAndPALAllo (%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL: [ %^KK2GG:| %^KKK2GG:] ;
  Vow: :ʹ  _ (ˈ:) k: BetweenStemAndPALAllo (%^PALE:|%^PALÄ:|%^PALẸ:) %^PAL: [%^KK2GG: | %^KKK2GG:] ; 
!!€ suä0kk%^PALẸ%^PAL%^KK2GG
!!€ suẹʹjj000

  Vow :ʹ k:  (ˈ:)   _ Vow: Cns:* PenBetweenStemAndPALAllo (%^Pen: %^PALE:|%^Pen: %^PALÄ:|%^Pen: %^PALẸ:) %^Pen: %^PAL: %^Pen: %^KKK2GG: ;
   Vow: :ʹ  _  (ˈ:)  k: Vow: Cns:* PenBetweenStemAndPALAllo (%^Pen: %^PALE:|%^Pen: %^PALÄ:|%^Pen: %^PALẸ:) %^Pen: %^PAL: %^Pen: %^KKK2GG: ; 
!!€ jeä0kˈkel%^Pen: %^PALE%^Pen: %^PAL%^Pen: %^KKK2GG%^RmVow%>až
!!€ jeeʹj0j0l0000%>až


"Even syllabic nouns I, with extra lengthening of vowel V>VV and ǩ:j"
!! __@RULENAME@__ - used in
!! used with
!!  > .
 ǩ:j <=>   
 Vow: (ʹ:|0:ʹ) ǩ: _    BetweenStemAndPALAllo ((%^PALÄ:|%^PALẸ:|%^PALE:) %^PAL:) %^KK2GG: ;
         Vow: (ʹ:|0:ʹ)    _ ǩ: BetweenStemAndPALAllo ((%^PALÄ:|%^PALẸ:|%^PALE:) %^PAL:) %^KK2GG: ;

  Vow: (ʹ:|0:ʹ) ǩ:   _ BetweenStemAndPALAllo ((%^PALÄ:|%^PALẸ:|%^PALE:) %^PAL:) %^KK2GG: ;
  Vow: (ʹ:|0:ʹ)  _  ǩ: BetweenStemAndPALAllo ((%^PALÄ:|%^PALẸ:|%^PALE:) %^PAL:) %^KK2GG: ; 

  Vow:   (ʹ:|0:ʹ) ǩ: (ˈ:) _ PALAllo ((%^PALÄ:|%^PALẸ:|%^PALE:) %^PAL:)  %^KKK2GG: ; 
  Vow:   (ʹ:|0:ʹ)          _ (ˈ:) ǩ: PALAllo ((%^PALÄ:|%^PALẸ:|%^PALE:) %^PAL:) %^KKK2GG: ; 


"Even syllabic nouns I, with extra lengthening of vowel V>VV and ǩ:ǥ here"
!! __@RULENAME@__ - used in
!! used with
!!  > .
 ǩ:ǥ <=>   
 Vow: (ʹ:) ǩ: (ˈ:) _    BetweenStemAndPALAllo %^PALNo: [%^KK2GG:|%^KKK2GG:] ;
 Vow: (ʹ:) _ (ˈ:) ǩ: BetweenStemAndPALAllo %^PALNo: [%^KK2GG:|%^KKK2GG:] ;



"Even syllabic verbs I, Voicing š:ž" 
!! __@RULENAME@__ - ss:zz, +Imp+Sg2, +Ind+Prs+ConNeg, double consonants at coda become voiced in gradation
 š:ž <=> 
 Vow: (ʹ:|0:ʹ) š: _    BetweenStemAndConsGrade %^KK2GG: ;
         Vow: (ʹ:|0:ʹ)    _ [š: |0:] BetweenStemAndConsGrade [ %^KK2GG: | %^K2GG: ] ;
!! lookkmõš+N+Sg+Gen
!!€ lookkm0õš0%^V2VV%^K2GG
!!€ lookkm0õžž00
       Vow: 0:*  _  BetweenStemAndConsGrade %^K2G: ;
!! with allegro


          Vow:      (ʹ:|0:ʹ) š:  ˈ:  _   BetweenStemAndConsGrade %^KKK2GG: ;
          Vow:      (ʹ:|0:ʹ) _   ˈ:  š:  BetweenStemAndConsGrade %^KKK2GG: ;
!!€ kõõččm0õšš%^V2VV%^KK2GG
!!€ kõõččmõõžž00
       Vow:  _ š: (%^VV2V: ) %^KK2G: ;
!!€ pååss%^VV2V%^KK2G»škuätt
!!€ p0åz0000škuätt

      Vow:   _ PenBetweenStemAndStemFinalVoicing %^VC: ;
!!mainstummuš+N+Sg+Gen:
!!€ mainstum0uš%^Pen%^C2CC%^VC
!!€ mainstummuž00

"Even syllabic verbs I, Voicing c:ʒ"
!! __@RULENAME@__ - cc:zz, +Imp+Sg2, +Ind+Prs+ConNeg, double consonants at coda become voiced in gradation
 c:ʒ <=> Vow: (ʹ:|0:ʹ) c: _    BetweenStemAndConsGrade %^KK2GG: ;
         Vow: (ʹ:|0:ʹ)    _ c: BetweenStemAndConsGrade %^KK2GG: ;
!!€ čääʹcc%^KK2GG
!!€ čääʹʒʒ00
         Vow: (ʹ:|0:ʹ) c: ˈ: _    BetweenStemAndConsGrade %^KKK2GG: ;
         Vow: (ʹ:|0:ʹ)    _ ˈ: c: BetweenStemAndConsGrade %^KKK2GG: ;
!!€ puõcˈc%^VOWLower%^PALÄ%^KKK2GG
!!€ puäʒ0ʒ000
                                   _ %^VC: ; 
       Vow:  _ c: (%^VV2V: ) %^KK2G: ;
!!€ pååss%^VV2V%^KK2G»škuätt
!!€ p0åz0000škuätt


"Even syllabic verbs I, Voicing č:j"
!! __@RULENAME@__ - ss:zz, +Imp+Sg2, +Ind+Prs+ConNeg, double consonants at coda become voiced in gradation
 č:j <=> Vow: (ʹ:|0:ʹ) č: _    BetweenStemAndConsGrade %^KK2GG: ;
         Vow: (ʹ:|0:ʹ)    _ č: BetweenStemAndConsGrade [ %^KK2GG: | %^CC2C: ] ;
                                             _ %^VC: ;

       Vow:  _ č: (%^VV2V: ) %^KK2G: ;
!!€ pååss%^VV2V%^KK2G»škuätt
!!€ p0åz0000škuätt
     Vow: (ʹ:|0:ʹ) č: ˈ: _    BetweenStemAndConsGrade %^KKK2GG: ;
     Vow: (ʹ:|0:ʹ)    ˈ: _ č: BetweenStemAndConsGrade %^KKK2GG: ;

"Even syllabic verbs I, Voicing after long vowel or diphthong s:z s:z" 
!! __@RULENAME@__ - ss:zz, +Imp+Sg2, +Ind+Prs+ConNeg, double consonants at coda become voiced in gradation
 s:z <=> Vow: (ʹ:|0:ʹ) s:  _  BetweenStemAndConsGrade  %^KK2GG: ; 
         Vow: (:ˈ) (ʹ:|0:ʹ) _   [s: |0:] BetweenStemAndConsGrade  [%^KK2GG: |%^K2GG: ] ; 
!!€ mättʼt0õss%^V2VV%^KK2GG
!!€ mättʼtõõzz00
       Vow: 0:*  _  BetweenStemAndConsGrade %^K2G: ;
!! with allegro

         Vow:  (ʹ:|0:ʹ) s:  ˈ:  _  BetweenStemAndConsGrade %^KKK2GG: ; 
         Vow:  (ʹ:|0:ʹ)  _ ˈ:   s: BetweenStemAndConsGrade %^KKK2GG: ; 
!! deriving cieʹǩǩes+N+Sg+Gen: ceäkˈkaz
    [Cns: :Cns*|Cns:+ (ˈ:|:ˈ) Cns:+] Vow: _ PenBetweenStemAndStemFinalVoicing %^VC: ; 
!! cieʹǩǩes+N+Sg+Gen
!!€ ceäkˈkez%^PenVow2a
!!€ ceäkˈkaz0
       Vow:  _ s: (%^VV2V: ) %^KK2G: ;
!!€ pååss%^VV2V%^KK2G»škuätt
!!€ p0åz0000škuätt

   Vow (ʹ:) _ 0: BetweenStemAndConsGrade %^K2GG: ;

!! čårrõs+N+Sg+Gen
!!€ čårr0õs0%^V2VV%^K2GG
!!€ čårr0õzz0


"Devoicing z:s"
!! __@RULENAME@__ - used in
 z:s <=>  Cns: Vow: _ PenBetweenStemAndStemFinalVoicing %^DeVC: ;
!! aaunâs+N+Sg+Nom: __material/aine__
!!€ 0aunnâz%^Pen%^V2VV%^Pen%^XYY2XY%^DeVC
!!€ aaun0âs00000
!! piiutâs+N+Sg+Nom __clothing/vaate__
!!€ p0ihttâz%^Pen: %^V2VV%^Pen: %^XYY2VY%^DeVC
!!€ piiut0âs000

"Devoicing ž:š"
!! __@RULENAME@__ - used in
 ž:š <=> Cns: Vow: _ PenBetweenStemAndStemFinalVoicing %^DeVC:;
!!€ p0õttâž%^Pen%^V2VV%^Pen%^CC2C%^DeVC
!!€ põõđ0âš00000
!!€ heäpˈpaž%^Pen%^CCC2C%^DeVC
!!€ heäv00aš000



"Even syllabic verbs I, second consonant loss t:0"
!! __@RULENAME@__ - used in
 t:0 <=> Vow:  (i|u)   (ʹ:|0:ʹ) ((Cns: )[b|p|v|f|n|s|c|r|l|š|č|j|k]) t: _ BetweenStemAndConsGrade  [%^CC2C:|%^XYY2XY:] ;
!! autt+N+Pl+Nom __car/auto__
!!€ 0autt%^V2VV%^CC2C
!!€ aaut000
         Vow: (%{ʹ%}:) h: (ʹ:|0:ʹ)           t: _ BetweenStemAndConsGrade  %^XYY2VY: ;

 t _ Vow: Cns:+ PenBetweenStemAndConsGrade [%^Pen: %^XYY2XY:|%^Pen: %^XYY2VY:] ;
!!€ k0onttâr%^Pen%^V2VV%^Pen%^XYY2XY
!!€ koont0âr0000
!! piiutâs+N+Sg+Nom __clothing/vaate__
!!€ p0ihttâz%^Pen: %^V2VV%^Pen: %^XYY2VY%^DeVC
!!€ piiut0âs000


"Even syllabic verbs I, third consonant loss" 
!! __@RULENAME@__ - lgg:lǥ, Vow Ca Cx Cx:0, +Imp+Sg2, +Ind+Prs+ConNeg, double consonants following consonant at coda that become single consonants in gradation
 Cx:0 <=>
       Vow: [ (ʹ:|0:ʹ)  Cns: | [u|i] (ʹ:|0:ʹ) ] Cx: _ BetweenStemAndConsGrade [%^CC2C:|%^XYY2XY:] ;
       Vow:  (ʹ:|0:ʹ) [h:|j:] (ʹ:|0:ʹ) Cx:  _ BetweenStemAndConsGrade %^XYY2VY: ;
       Vow: [ (ʹ:|0:ʹ)  Cns: | [u|i] (ʹ:|0:ʹ) ] Cx: _ Vow: Cns:+ PenBetweenStemAndConsGrade [%^Pen: %^CC2C:|%^Pen: %^XYY2XY:] ;
       Vow:  (ʹ:|0:ʹ) [h:|j:] (ʹ:|0:ʹ) Cx:  _ Vow: Cns:+ PenBetweenStemAndConsGrade %^Pen: %^XYY2VY: ;
       Vow: Cx:  _ (%^VV2V: ) %^KK2G: ;
                where Cx in (s š) ;
!!€ s0ähss%^V2VV%^XYY2VY%>am
!!€ sääus000%>am


!"Even syllabic verbs I, cg for s"
! __@RULENAME@__ - used in
! s:0 <=>  i: (%{ʹ%}:) j: (ʹ:|0:ʹ) s _ BetweenStemAndConsGrade %^XYY2VY: ;
!!€ ijss%^VOWXL%^CShort%>âs
!!€ iis000%>âs
!      Vow:  (ʹ:|0:ʹ)  [r|l|p] s: _ BetweenStemAndConsGrade [%^CC2C:|%^XYY2XY:] ;
!      Vow:  (%{ʹ%}:) h: (ʹ:|0:ʹ) s:  _ BetweenStemAndConsGrade [%^CC2C:|%^XYY2XY:] ;
!      Vow: (%{ʹ%}:) h: (ʹ:|0:ʹ) s:  _ BetweenStemAndConsGrade %^XYY2VY: ;
!! uhss+N+Sg+Gen __door__
!!€ uhss%^XYY2VY
!!€ uus00
!"Even syllabic verbs I, cg for š"
! __@RULENAME@__
! š:0 <=>
!      Vow:  (ʹ:|0:ʹ)  [r|l|p] š: _ BetweenStemAndConsGrade [%^CC2C:|%^XYY2XY:] ;
!      Vow:  (%{ʹ%}:) h: (ʹ:|0:ʹ) š:  _ BetweenStemAndConsGrade [%^CC2C:|%^XYY2XY:] ;
!      Vow: (%{ʹ%}:) h: (ʹ:|0:ʹ) š:  _ BetweenStemAndConsGrade %^XYY2VY: ;
! šapšš+N+Sg+Gen __white fish/siika__
!!€ š0apšš%^V2VV%^CC2C
!!€ šaapš000






"Consonant loss ŋ:0"
!! __@RULENAME@__ - used in
 ŋ:0 <=>
 Vow: (ʹ:|0:ʹ) ([Cns - ŋ]) ŋ: _    BetweenStemAndConsGrade [%^CC2C:|%^XYY2XY:] ;
 Vow: (ʹ:|0:ʹ) ([Cns - ŋ]) ŋ: _  Vow: Cns:*  PenBetweenStemAndConsGrade [%^Pen: %^CC2C:|%^Pen: %^XYY2XY:] ;



"Even syllabic verbs I, t>đ" 
!! __@RULENAME@__ - tt:đ
!! {{{

 t:đ  <=> Vow: (:ˈ) (ʹ:|0:ʹ) _ t:0 (i:) BetweenStemAndConsGrade [%^CC2C:|%^CCC2C:] ;
!! }}}
!!€ ǩiõ0tt%^Allegro%^CC2C%>stan
!!€ ǩiõˈđ000%>stan


"Even syllabic nouns I, p>v" 
!! __@RULENAME@__ - pp:v
 p:v  <=> Vow: (:ˈ) (ʹ:|0:ʹ) _ (ˈ:) p:0 BetweenStemAndConsGrade  [%^CC2C: | %^CCC2C: ] ;
!!€ ǩiâpp%^CC2C
!!€ ǩiâv00
 Vow: (:ˈ) (ʹ:|0:ʹ) _ (ˈ:) p:0 PenBetweenStemAndConsGrade [%^Pen: %^CC2C:|%^Pen: %^CCC2C:] ;
!!€ heäpˈpaž%^Pen%^CCC2C%^DeVC
!!€ heäv00aš000

"Consonant quality change ǥ:j"
!! __@RULENAME@__ - used in
 ǥ:j <=>
  Vow:  i (0:ʹ)  _ %^PAL: ;
!!€ lääi0ǥ%^PAL
!!€ lääiʹj0

"Even syllabic verbs I, second consonant loss k:0"
!! __@RULENAME@__ - used in
 k:0 <=>   k:  _ BetweenStemAndConsGrade  [ %^CC2C: | %^XYY2XY: ] ;
!! loǥ<škueʹtted __begin to read__
!!€ lookk%^VV2V%^CC2C»škueʹtted
!!€ l0oǥ000»škueʹtted
!! mieʹlǩǩ+N+Sg+Acc: __milk/maito__
!!€ miâ0lkk%^PALE%^PAL%^CC2C
!!€ mieʹlǩ0000
       Vow: k:  _ (%^VV2V: ) %^KK2G: ;
!!€ pååss%^VV2V%^KK2G»škuätt
!!€ p0åz0000škuätt
           k:  _ Vow: Cns:+ PenBetweenStemAndConsGrade  [ %^Pen: %^CC2C: | %^Pen: %^XYY2XY: ] ;

 k: (ˈ:) _ Vow: Cns:+ PenBetweenStemAndConsGrade  %^Pen: %^KKK2ZERO: ;
 Vow: (0:) _ (ˈ:) k: Vow: Cns:+ PenBetweenStemAndConsGrade  %^Pen: %^KKK2ZERO: ;
!!€ jeä0kˈkel%^Pen: %^PALE%^Pen: %^PAL%^Pen: %^KKK2ZERO
!!€ jeeʹ000el000

"Even syllabic verbs I, second consonant loss ǩ:0"
!! __@RULENAME@__ - used in
 ǩ:0 <=>
  Vow:  (ʹ:|0:ʹ)  [[i|u] (ʹ:|0:ʹ) |c|ŋ|s|r|l|p] ǩ:  _ BetweenStemAndConsGrade  [%^CC2C:|%^XYY2XY:] ;

    ǩ:  _ Vow: Cns:+ PenBetweenStemAndConsGrade  [%^Pen: %^CC2C:|%^Pen: %^XYY2XY:] ;
!! eʹǩrded+V+Inf
!!€ eeʹǩǩerd%^Pen%^VV2V%^Pen%^CC2C%^RmVow%>ed
!!€ 0eʹǩ00rd00000%>ed

"Even syllabic verbs I, second consonant loss ǥ:0"
!! __@RULENAME@__ - used in
 ǥ:0 <=>
  Vow:  (ʹ:|0:ʹ)  [i|u] (ʹ:|0:ʹ)  ǥ:  _ BetweenStemAndConsGrade  %^CC2C: ;

    ǥ:  _ Vow: Cns:+ PenBetweenStemAndConsGrade  %^Pen: %^CC2C: ;
!! påǥsted+V+Inf
!!€ pååǥǥast%^Pen%^VV2V%^Pen%^CC2C%^RmVow%>ed
!!€ p0åǥ00st00000%>ed


"Even syllabic verbs I, lgg>lǥ" 
!! __@RULENAME@__ - vueʹlǧǧed+V+Ind+Prs+Sg2 => vuâlǥak
 g:ǥ  <=> Vow: [i|u | (ʹ:0|%{ʹ%}:0) [l|r|d:] ]    _ g: BetweenStemAndConsGrade [%^CC2C:|%^XYY2XY:] ;
 Vow: (ʹ:0) (Cns:+)  _ g: Vow: Cns:+ PenBetweenStemAndConsGrade [%^Pen: %^XYY2XY:] ;
! Vow: (ʹ:0) (Cns:+)  _ g: Vow: Cns:+ PenBetweenStemAndConsGrade [%^Pen: %^CC2C:|%^Pen: %^XYY2XY:] ;
!!€ ǩeâlggal%^Pen: %^XYY2XY
!!€ ǩeâlǥ0al0
!!€ aaggâst%^Pen%^VV2V%^Pen%^CC2C%^RmVow%>ed
!!€ 0ag00st00000%>ed
!!$ 0aǥ00st00000%>ed


!vueʹlǧǧed+V+Imp+Pl3: vuâlǥǥaz
!"Even syllabic verbs I, lgg>lǥǥ" 
! lgg:lǥǥ

"Odd syllabic nouns I, lǥ>lg"
!! __@RULENAME@__ - ǩeâlǥal+N+Sg+Gen: ǩeâlggal
ǥ:g <=>
 Vow: (ʹ:0)  l _ 0:  BetweenStemAndConsGrade %^XY2XYY:  ;

 Vow: (ʹ:0)  l _ 0: Vow: Cns:* PenBetweenStemAndConsGrade %^Pen: %^XY2XYY:  ;

!!€ ǩeâlǥ0al%^Pen%^XY2XYY
!!€ ǩeâlggal00

"even syllabic verbs I, 'lǧǧ>'lj" 
!! __@RULENAME@__ - used in
 g:j  <=> Vow:  [ i :ʹ|:ʹ [l|r|d:]]  _ g:0 BetweenStemAndPALNo %^PAL: %^XYY2XY: ;
!vueʹlǧǧed+V+Ind+Prt+Pl1 => vuõʹljim
!!€ vuâ0lgg%^VOWRaise%^PAL%^CC2C%>im
!!€ vuõʹlj0000%>im
!!€ 0äi0gg%^V2VV%^PAL%^XYY2XY
!!€ ääiʹj0000





!! !!Rules for cleaning up and composing end result

!! * Strengthening Consonant Cluster
!! * V shortening and C lengthening 
!! * %^C2CC is generally used in conjunction with a preceding %^VV2V
!! * In verbs this will be observed in finite forms
!! * In derivational morphology, e.g. -škuetted, this is not the case

!"Orthographic Consonant lengthening Weak to strong or extra strong %{XC%}:m"
!!! __@RULENAME@__ - used in
! %{XC%}:m <=>  Vow: (ʹ:|0:ʹ) m _ BetweenStemAndConsGrade [%^C2CC:|%^XY2XYY:] ;
!!!€ čââʹlmteʹm%{XC%}%^PALNo%^C2CC%>a
!!!€ čââʹlmte0mm000%>a

!"Orthographic Consonant lengthening Weak to strong or extra strong %{XC%}:v"
!!! __@RULENAME@__ - used in
! %{XC%}:v <=>  Vow: (ʹ:|0:ʹ) v _ BetweenStemAndConsGrade [%^C2CC:|%^XY2XYY:] ;


!"Orthographic Consonant lengthening Weak to strong or extra strong %{XC%}:n"
!!! __@RULENAME@__ - used in
! %{XC%}:n <=>  Vow: (ʹ:|0:ʹ) n _ BetweenStemAndConsGrade [%^C2CC:|%^XY2XYY:] ;
!!€ mään%{XC%}%^VV2V%^VOWRaise%^C2CC%>u
!!€ m0ann0000u

!"Orthographic Consonant lengthening Weak to strong or extra strong %{XC%}:d"
!!! __@RULENAME@__ - used in
! %{XC%}:d <=>  Vow: (ʹ:|0:ʹ) d _ BetweenStemAndConsGrade [%^C2CC:|%^XY2XYY:] ;

!"Orthographic Consonant lengthening Weak to strong or extra strong %{XC%}:r"
!!! __@RULENAME@__ - used in
! %{XC%}:r <=>  Vow: (ʹ:|0:ʹ) r _ BetweenStemAndConsGrade [%^C2CC:|%^XY2XYY:] ;
!!€ puär0%{XC%}%^VOWRaise%>u
!!€ puårˈr000u

!"Orthographic Consonant lengthening Weak to strong or extra strong %{XC%}:l"
!!! __@RULENAME@__ - used in
! %{XC%}:l <=>  Vow: (ʹ:|0:ʹ) l (:ˈ) _ BetweenStemAndConsGrade [ %^C2CC:|%^XY2XYY:] ;
!! suâl+N+Pl+Nom __island__
!!€ suâl0%{XC%}%^VOWRaise%>u
!!€ suõlˈl000u

!"Orthographic Consonant lengthening Weak to strong or extra strong %{XC%}:k"
!!! __@RULENAME@__ - used in
! %{XC%}:k <=>  Vow: (ʹ:|0:ʹ) k _ %^W2XSt: ;




"Orthographic Consonant lengthening in INDPRSSG3 and IMPSG2 0:m"
!! __@RULENAME@__ - kåʹmrded+V+Imp+Sg2: kååʹmmerd 
 0:m <=> Vow:Vow (ʹ:|0:ʹ)  m _ Vow: Cns:+ PenBetweenStemAndConsGrade [%^Pen: %^C2CC:|%^Pen: %^XY2XYY:]  ;
! Vow:Vow (ʹ:|0:ʹ)  m _ Vow: Cns:+ PenBetweenStemAndConsGrade [%^Pen: %^C2CC:|%^Pen: %^XY2XYY:] [%^IMPSG2:|%^INDPRSSG3:] ;
        m _ BetweenStemAndConsGrade [%^C2CC:|%^XY2XYY:] ;

!!€ ooum0%^VV2V%^XY2XYYu
!!€ 0oumm00u
!!€ čââʹlmteʹm0%^PALNo%^C2CC%>a
!!€ čââʹlmte0mm000a

"Consonant lengthening 0:b"
!! __@RULENAME@__ - used in
 0:b <=>  Vow:  (ʹ:|0:ʹ) [l] b _  BetweenStemAndConsGrade [%^C2CC:|%^XY2XYY:] ;
  Vow:  (ʹ:|0:ʹ) [l] b _ Vow: Cns PenBetweenStemAndConsGrade [%^Pen: %^C2CC:|%^Pen: %^XY2XYY:] ;


"Consonant lengthening 0:p"
!! __@RULENAME@__ - used in
 0:p <=>  Vow:  (ʹ:|0:ʹ) [m|l] p _ BetweenStemAndConsGrade [%^C2CC:|%^XY2XYY:] ;
           Vow:  (ʹ:|0:ʹ) [m|l] p _ Vow: Cns PenBetweenStemAndConsGrade [%^Pen: %^C2CC:|%^Pen: %^XY2XYY:] ;	       

"Consonant lengthening 0:v"
!! __@RULENAME@__ - used in
 0:v <=>
  Vow:  ( i:|u:) (ʹ:|0:ʹ) (Cns:) v _ BetweenStemAndConsGrade [%^C2CC:|%^XY2XYY:] ;
  Vow:  ( i:|u:) (ʹ:|0:ʹ) (Cns:) v _ Vow: Cns:* PenBetweenStemAndConsGrade [%^Pen: %^C2CC:|%^Pen: %^XY2XYY:] ;
!           where Cx in (b p v) ;
!! kååvas+N+Sg+Gen: __kota__
!!€ kååv0as%^Pen: %^VV2V%^Pen: %^C2CC%^VC
!!€ k0åvvaz000

"Orthographic Consonant lengthening in INDPRSSG3 and IMPSG2 0:n"
!! __@RULENAME@__ - used in
 0:n <=>  Vow: (ʹ:|0:ʹ) n _ (j)  BetweenStemAndConsGrade [%^C2CC:|%^XY2XYY:] ;
!!€ mään0%^VV2V%^VOWRaise%^C2CC%>u
!!€ m0ann0000u
 Vow: (ʹ:|0:ʹ) n _ (j) Vow: Cns PenBetweenStemAndConsGrade [%^Pen: %^C2CC:|%^Pen: %^XY2XYY:] ;

!! kõõnjâl+N+Sg+Gen __tear__
!!€ kõõn0jâl%^Pen: %^VV2V%^Pen: %^C2CC
!!€ k0õnnjâl00


"Consonant lengthening 0:d"
!! __@RULENAME@__ - juurd+N+Sg+Gen: jurddi
 0:d <=> Vow: (ʹ:|:ʹ) (r) d _ (i:)  BetweenStemAndConsGrade [%^C2CC:|%^XY2XYY:] ;
    Vow: (ʹ:|:ʹ) (r) d _ Vow: Cns:*  PenBetweenStemAndConsGrade [%^Pen: %^C2CC:|%^Pen: %^XY2XYY:] ;

"Consonant lengthening 0:t"
!! __@RULENAME@__ - used in
 0:t <=> Vow: (ʹ:) (s) :t _ BetweenStemAndConsGrade [%^C2CC:|%^XY2XYY:] ;
      Vow: (ʹ:) (s) :t _ Vow: Cns:*  PenBetweenStemAndConsGrade [%^Pen: %^C2CC:|%^Pen: %^XY2XYY:] ;


"Consonant lengthening 0:ǯ"
!! __@RULENAME@__ - used in
 0:ǯ <=>  Vow:  (ʹ:|:ʹ) Cns ǯ _ BetweenStemAndConsGrade [%^C2CC:|%^XY2XYY:] ;
         Vow:  (ʹ:|:ʹ) Cns ǯ _ Vow: Cns:*  PenBetweenStemAndConsGrade [%^Pen: %^C2CC:|%^Pen: %^XY2XYY:] ;

"Consonant lengthening 0:c"
!! __@RULENAME@__ - used in
 0:c <=>  Vow:  (ʹ:|:ʹ) Cns c _ BetweenStemAndConsGrade [%^C2CC:|%^XY2XYY:] ;
         Vow:  (ʹ:|:ʹ) Cns c _ Vow: Cns:*  PenBetweenStemAndConsGrade [%^Pen: %^C2CC:|%^Pen: %^XY2XYY:] ;

"Consonant lengthening 0:đ"
!! __@RULENAME@__ - čââʹđ+N+Sg+Gen: čâđđam
 0:đ <=> Vow:  (ʹ:)  đ _ BetweenStemAndConsGrade [%^C2CC:|%^XY2XYY:] ;
!! čââʹđ+N+Sg+Ill: __heart/sydän__
!!€ čââʹđ0%^VV2V%^PALNo%^C2CC%>ma
!!€ č0â0đđ000%>ma
         Vow:  (ʹ:)  đ _ Vow: Cns:*  PenBetweenStemAndConsGrade [%^Pen: %^C2CC:|%^Pen: %^XY2XYY:] ;


"Consonant lengthening 0:l"
!! __@RULENAME@__ - used in
 0:l <=>
  (ʹ:|:ʹ) l (:ˈ) _ BetweenStemAndConsGrade [ %^C2CC:|%^XY2XYY:] ;
  (ʹ:|:ʹ) l (:ˈ) _ Vow: Cns:*  PenBetweenStemAndConsGrade [ %^Pen: %^C2CC:|%^Pen: %^XY2XYY:] ;
!! suâl+N+Par __island/saari__
!!€ suâl0%^VOWRaise%^C2CC%>ud 
!!€ suõll00%>ud


"Consonant lengthening 0:ŋ"
!! __@RULENAME@__ - used in
 0:ŋ <=>   ŋ _ BetweenStemAndConsGrade [ %^C2CC:|%^XY2XYY:] ;
           ŋ _ Vow: Cns:+ PenBetweenStemAndConsGrade [%^Pen: %^C2CC:|%^Pen: %^XY2XYY:] ;


"Consonant lengthening 0:g"
!! __@RULENAME@__  - used in
 0:g <=>  Vow:   l ǥ: _ BetweenStemAndConsGrade [ %^C2CC:|%^XY2XYY:] ;
  Vow:   l ǥ: _ Vow: Cns PenBetweenStemAndConsGrade [ %^Pen: %^C2CC:|%^Pen: %^XY2XYY:] ;
!! ǩeâlǥal+N+Sg+Gen __kilkura__
!!€ ǩeâlǥ0al%^Pen: %^C2CC
!!€ ǩeâlggal0

"Consonant lengthening 0:ǧ"
!! __@RULENAME@__ - used in
!! {{{
 0:ǧ <=>  Vow:  :ʹ  l ǥ: _ Vow: Cns:+ PenBetweenStemAndPALNo (%^Pen: %^PALE:|%^Pen: %^PALÄ:|%^Pen: %^PALẸ:) %^Pen: %^PAL: %^Pen: %^XY2XYY: ;
!! }}}

"Consonant lengthening 0:ǩ "
!! __@RULENAME@__ - used in
!! {{{

 0:ǩ <=>
  ǩ _ BetweenStemAndConsGrade %^C2CC: ;
   :ʹ  l ǩ _ Vow: Cns:+ PenBetweenStemAndPALNo (%^Pen: %^PALE:|%^Pen: %^PALÄ:|%^Pen: %^PALẸ:) %^Pen: %^PAL: %^Pen: %^XY2XYY: ;
!! }}}

"Consonant lengthening 0:k"
!! __@RULENAME@__ - used in
 0:k <=>
 Vow (ʹ:) [k| ǩ:k] _ BetweenStemAndConsGrade %^C2CC: ;
 Vow (ʹ:) [l|n|ŋ|r|s|š|t] [k| ǩ:k] _ BetweenStemAndConsGrade %^XY2XYY: ;
!!€ durak0%^C2CC%>a
!!€ durakk00a
!! kaađneǩ+N+Sg+Ill:
!!€ kaađneǩ0%^PALNo%^C2CC%>a
!!€ kaađnekk00%>a
   Vow (ʹ:) [k| ǩ:k] _ Vow: Cns:+ PenBetweenStemAndConsGrade %^Pen: %^C2CC: ;
   Vow (ʹ:) [l|n|ŋ|r|s|š|t] [k| ǩ:k] _ Vow: Cns:+ PenBetweenStemAndConsGrade %^Pen: %^XY2XYY: ;

"0:s"
!! __@RULENAME@__ - used in
 0:s <=> Vow (:ʹ) s: _ BetweenStemAndConsGrade %^C2CC: ;

!! čårrõs+N+Sg+Gen
!!€ čårr0õs0%^V2VV%^K2GG
!!€ čårr0õzz0

"0:z"
!! __@RULENAME@__ - used in
 0:z <=> Vow (:ʹ) s: _ BetweenStemAndConsGrade %^K2GG: ;

!! čårrõs+N+Sg+Gen
!!€ čårr0õs0%^V2VV%^K2GG
!!€ čårr0õzz0

"0:ž"
!! __@RULENAME@__ - used in
 0:ž <=> Vow (:ʹ) š: _ BetweenStemAndConsGrade %^K2GG: ;

!! lookkmõš+N+Sg+Gen
!!€ lookkm0õš0%^V2VV%^K2GG
!!€ lookkm0õžž00

"0:ǥ"
!! __@RULENAME@__ - used in
 0:ǥ <=> Vow (ʹ:) k: _ BetweenStemAndPALAllo %^K2GG: ;  

!! čâustõk+N+Sg+Gen
!!€ čâust0õk0%^V2VV%^K2GG
!!€ čâustõõǥǥ0

"0:j"
!! __@RULENAME@__ - used in
 0:j <=> Vow (:ʹ) k: _ BetweenStemAndPALAllo %^PAL: %^K2GG: ;  

!! čâustõk+N+Sg+Gen
!!€ čâust0õ0k0%^V2VV%^PAL%^K2GG
!!€ čâustõõʹjj000

!! !!CONSONANT QUALITY CHANGE

"Consonant quality change j:ǧ"
!! __@RULENAME@__ - used in
 j:ǧ <=>   :ʹ  Cns: _ 0: Vow: Cns:+ PenBetweenStemAndPALNo ((%^Pen: %^PALE:|%^Pen: %^PALÄ:|%^Pen: %^PALẸ:) %^Pen: %^PAL:) %^Pen: %^XY2XYY: ;

"Consonant quality change ǥ:ǧ"
!! __@RULENAME@__ - used in
 ǥ:ǧ <=>  :ʹ  Cns: _ 0: Vow: Cns:+ PenBetweenStemAndPALNo (%^Pen: %^PALE:|%^Pen: %^PALÄ:|%^Pen: %^PALẸ:) %^Pen: %^PAL: %^Pen: %^XY2XYY: ;
!Dummy



!! !Pedagogical X3 length mark after diphthongs in vertical line ˈ 

!2015-05-20 What is this?(tbw)


!! !Adding X3 length mark
"Consonant X3 lengthening after diphthong in vertical line ˈ character"
!! __@RULENAME@__ - used in +N+Sg+Ill
!! __jeäll:jiâlˈlu, __
!! b c č ǯ d đ g ǧ k ǩ l m n p r s š t v also htˈt nˈnj
 0:ˈ <=> (ʹ:|0:ʹ)     b  _ b    [ BetweenStemAndConsGrade %^CC2CCC:| Vow: Cns:+ PenBetweenStemAndConsGrade %^Pen: %^CC2CCC:] ;
          (ʹ:|0:ʹ)     c  _ c    [ BetweenStemAndConsGrade %^CC2CCC:| Vow: Cns:+ PenBetweenStemAndConsGrade %^Pen: %^CC2CCC:] ;
          (ʹ:|0:ʹ)     č  _ č    [ BetweenStemAndConsGrade %^CC2CCC:| Vow: Cns:+ PenBetweenStemAndConsGrade %^Pen: %^CC2CCC:] ;
          (ʹ:|0:ʹ)     ǯ  _ ǯ    [ BetweenStemAndConsGrade %^CC2CCC:| Vow: Cns:+ PenBetweenStemAndConsGrade %^Pen: %^CC2CCC:] ;
          (ʹ:|0:ʹ)     d  _ d    [ BetweenStemAndConsGrade %^CC2CCC:| Vow: Cns:+ PenBetweenStemAndConsGrade %^Pen: %^CC2CCC:] ;
          (ʹ:|0:ʹ)     đ  _ đ    [ BetweenStemAndConsGrade %^CC2CCC:| Vow: Cns:+ PenBetweenStemAndConsGrade %^Pen: %^CC2CCC:] ;
          (ʹ:|0:ʹ)     g: _ g:   [ BetweenStemAndConsGrade %^CC2CCC:| Vow: Cns:+ PenBetweenStemAndConsGrade %^Pen: %^CC2CCC:] ;
          (ʹ:|0:ʹ)     ǥ: _ ǥ:   [ BetweenStemAndConsGrade %^CC2CCC:| Vow: Cns:+ PenBetweenStemAndConsGrade %^Pen: %^CC2CCC:] ;
          (ʹ:|0:ʹ)     ǧ: _ ǧ:   [ BetweenStemAndConsGrade %^CC2CCC:| Vow: Cns:+ PenBetweenStemAndConsGrade %^Pen: %^CC2CCC:] ;
          (ʹ:|0:ʹ)     j _ j:   [ BetweenStemAndConsGrade %^CC2CCC:| Vow: Cns:+ PenBetweenStemAndConsGrade %^Pen: %^CC2CCC:] ;
          (ʹ:|0:ʹ)     k: _ k:   [ BetweenStemAndConsGrade %^CC2CCC:| Vow: Cns:+ PenBetweenStemAndConsGrade %^Pen: %^CC2CCC:] ;
          (ʹ:|0:ʹ)     ǩ: _ ǩ:   [ BetweenStemAndConsGrade %^CC2CCC:| Vow: Cns:+ PenBetweenStemAndConsGrade %^Pen: %^CC2CCC:] ;
          (ʹ:|0:ʹ)     l  _ :l    [ BetweenStemAndConsGrade %^CC2CCC:| Vow: Cns:+ PenBetweenStemAndConsGrade %^Pen: %^CC2CCC:] ;
!!€ kuâl0l%^CC2CCC%>a
!!€ kuâlˈl00a
          (ʹ:|0:ʹ)     m  _ :m    [ BetweenStemAndConsGrade %^CC2CCC:| Vow: Cns:+ PenBetweenStemAndConsGrade %^Pen: %^CC2CCC:] ;
          (ʹ:|0:ʹ)     n  _ :n (j)[ BetweenStemAndConsGrade %^CC2CCC:| Vow: Cns:+ PenBetweenStemAndConsGrade %^Pen: %^CC2CCC:] ;
          (ʹ:|0:ʹ)     p  _ p    [ BetweenStemAndConsGrade %^CC2CCC:| Vow: Cns:+ PenBetweenStemAndConsGrade %^Pen: %^CC2CCC:] ;
          (ʹ:|0:ʹ)     r  _ :r    [ BetweenStemAndConsGrade %^CC2CCC:| Vow: Cns:+ PenBetweenStemAndConsGrade %^Pen: %^CC2CCC:] ;
          (ʹ:|0:ʹ)     s  _ s    [ BetweenStemAndConsGrade %^CC2CCC:| Vow: Cns:+ PenBetweenStemAndConsGrade %^Pen: %^CC2CCC:] ;
          (ʹ:|0:ʹ)     š  _ š    [ BetweenStemAndConsGrade %^CC2CCC:| Vow: Cns:+ PenBetweenStemAndConsGrade %^Pen: %^CC2CCC:] ;
          (ʹ:|0:ʹ) (h) t _ t     [ BetweenStemAndConsGrade %^CC2CCC:| Vow: Cns:+ PenBetweenStemAndConsGrade %^Pen: %^CC2CCC:] ;
          (ʹ:|0:ʹ)     v _ v     [ BetweenStemAndConsGrade %^CC2CCC:| Vow: Cns:+ PenBetweenStemAndConsGrade %^Pen: %^CC2CCC:] ;







!! !Removing X3 length mark
"Removing Consonant X3 length mark after diphthong in vertical line ˈ LEFT ARROW"
!! __@RULENAME@__ - deriving 
!! b c č ǯ d đ g ǧ k ǩ l m n p r s š t v also htˈt nˈnj
! BetweenStemAndConsGrade [%^C2CC:|%^XY2XYY:]
!! {{{ 
 ˈ:0 <=> (ʹ:|0:ʹ)     b _ b:     [BetweenStemAndConsGrade | Vow: Cns:*  PenBetweenStemAndConsGrade %^Pen: ][%^CCC2CC:|%^CCC2C:] ;
         (ʹ:|0:ʹ)     c _ c:     [BetweenStemAndConsGrade | Vow: Cns:*  PenBetweenStemAndConsGrade %^Pen: ][%^CCC2CC:|%^CCC2C:] ;
         (ʹ:|0:ʹ)     č _ č:     [BetweenStemAndConsGrade | Vow: Cns:*  PenBetweenStemAndConsGrade %^Pen: ][%^CCC2CC:|%^CCC2C:] ;
         (ʹ:|0:ʹ)     ʒ _ ʒ:     [BetweenStemAndConsGrade | Vow: Cns:*  PenBetweenStemAndConsGrade %^Pen: ][%^CCC2CC:|%^CCC2C:] ;
         (ʹ:|0:ʹ)     ǯ _ ǯ:     [BetweenStemAndConsGrade | Vow: Cns:*  PenBetweenStemAndConsGrade %^Pen: ][%^CCC2CC:|%^CCC2C:] ;
         (ʹ:|0:ʹ)     d _ d:     [BetweenStemAndConsGrade | Vow: Cns:*  PenBetweenStemAndConsGrade %^Pen: ][%^CCC2CC:|%^CCC2C:] ;
         (ʹ:|0:ʹ)     đ _ đ:     [BetweenStemAndConsGrade | Vow: Cns:*  PenBetweenStemAndConsGrade %^Pen: ][%^CCC2CC:|%^CCC2C:] ;
         (ʹ:|0:ʹ)     g _ g:     [BetweenStemAndConsGrade | Vow: Cns:*  PenBetweenStemAndConsGrade %^Pen: ][%^CCC2CC:|%^CCC2C:] ;
         (ʹ:|0:ʹ)     :ǧ _ [:ǧ|ǧ:0|g:0]     [BetweenStemAndConsGrade | Vow: Cns:*  PenBetweenStemAndConsGrade %^Pen: ][%^CCC2CC:|%^CCC2C:] ;
         (ʹ:|0:ʹ)     k: _ k:     [BetweenStemAndConsGrade | Vow: Cns:+ PenBetweenStemAndConsGrade] [%^CCC2CC:|%^CCC2C:|%^Pen: %^KKK2ZERO: ] ;
         (ʹ:|0:ʹ)     :ǩ _ [:ǩ|ǩ:0|k:0]     [BetweenStemAndConsGrade | Vow: Cns:*  PenBetweenStemAndConsGrade %^Pen: ][%^CCC2CC:|%^CCC2C:] ;
         (ʹ:|0:ʹ)     l _ l:     [BetweenStemAndConsGrade | Vow: Cns:*  PenBetweenStemAndConsGrade %^Pen: ][%^CCC2CC:|%^CCC2C:] ;
         (ʹ:|0:ʹ)     m _ m:     [BetweenStemAndConsGrade | Vow: Cns:*  PenBetweenStemAndConsGrade %^Pen: ][%^CCC2CC:|%^CCC2C:] ;
         (ʹ:|0:ʹ)     n _ n: (j) [BetweenStemAndConsGrade | Vow: Cns:*  PenBetweenStemAndConsGrade %^Pen: ][%^CCC2CC:|%^CCC2C:] ;
         (ʹ:|0:ʹ)     p: _ p:     [BetweenStemAndConsGrade | Vow: Cns:*  PenBetweenStemAndConsGrade %^Pen: ][%^CCC2CC:|%^CCC2C:] ;
         (ʹ:|0:ʹ)     r _ r:     [BetweenStemAndConsGrade | Vow: Cns:*  PenBetweenStemAndConsGrade %^Pen: ][%^CCC2CC:|%^CCC2C:] ;
         (ʹ:|0:ʹ)     s _ s:     [BetweenStemAndConsGrade | Vow: Cns:*  PenBetweenStemAndConsGrade %^Pen: ][%^CCC2CC:|%^CCC2C:] ;
         (ʹ:|0:ʹ)     š _ š:     [BetweenStemAndConsGrade | Vow: Cns:*  PenBetweenStemAndConsGrade %^Pen: ][%^CCC2CC:|%^CCC2C:] ;
         (ʹ:|0:ʹ) (h) t: _ t:     [BetweenStemAndConsGrade | Vow: Cns:*  PenBetweenStemAndConsGrade %^Pen: ][%^CCC2CC:|%^CCC2C:] ;
         (ʹ:|0:ʹ)     v _ v:     [BetweenStemAndConsGrade | Vow: Cns:*  PenBetweenStemAndConsGrade %^Pen: ][%^CCC2CC:|%^CCC2C:] ;
!! }}}



! OUT-DATED
!"Realization for %{Â1%} in â " 
!!! __@RULENAME@__ - maainâst
! %{Â1%}:â <=> Cns _ Cns:+ (%^Pen: %^V2VV:) %^IMPSG2: ;
!!€ m0ain%{Â1%}st%^Pen%^V2VV%^IMPSG2
!!€ maainâst00
!


!"Realization for %{E1%} in a" 
!!! __@RULENAME@__ - 
!!! {{{
! %{E1%}:a <=> _ (ʹ:|0:ʹ) [i|Cns:+] (%^Pen: %^V2VV:) %^INDPRSSG3: ;
!!! }}}

!"Realization for %{Â1%} in a" 
!!! __@RULENAME@__ - 
! %{Â1%}:a <=> _ (ʹ:|0:ʹ|ʼ:) [i|Cns:+] (%^Pen: %^V2VV:) %^INDPRSSG3: ;
!!€ m0ain%{Â1%}st%^Pen%^V2VV%^INDPRSSG3
!!€ maainast00
         

"Limitations for ZERO:x 1"
 0: => [: - 0:| .#.] _ [: - 0:| .#.] ;