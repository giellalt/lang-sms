! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or divvun@samediggi.no

!! !!!Skolt Sámi TWOLC file
!
! $Id$
!
!

!! !!!Introduction
!! The rule file for Skolt Saami is divided into 5 main types:
!! 
!! # Definitions
!! # Consonant shift rules (tbw)
!! # Vowel alternation rules
!! # Consonant gradation rules
!! # Rules for cleaning up and composing end result


!! !!! Alphabets, sets and definitions

!! !! Alphabet

Alphabet 
  a b c d e f g h i ï j k l m n o p q r s t u v w x y z !!= * @CODE@
  A B C D E F G H I Ï J K L M N O P Q R S T U V W X Y Z !!= * @CODE@
  æ ä ø ö å á é í										!!= * @CODE@
  Å Ä Ö Ø Æ Á											!!= * @CODE@
  Á É Ó Ú Í À È Ò Ù Ì Ë Ü Ï Â Ê Ô Û Î Ã Ý þ Ñ Ð			!!= * @CODE@
  á é ó ú í à è ò ù ì ë ü ï â ê ô û î ã ý þ ñ ð ß ª ß ç	!!= * @CODE@
  č ǩ ǯ ǧ ž đ ǥ ʒ š ŋ									!!= * @CODE@
  Č Ǩ Ǯ Ǧ Ž Đ Ǥ Ʒ Š Ŋ									!!= * @CODE@
  õ â													!!= * @CODE@
  Õ Â													!!= * @CODE@

 %ˈ !! * Vertical line: as overlong consonant marker that does not appear in writing 
 ʼ    !! * Upper right single quotation mark: Syllable marker that appears in writing 
 ˊ  !! * MODIFIER LETTER ACUTE ACCENT: suprasegmental palatalization

!! !! Trigger symbols

 %^RmVow:0 !!= * @CODE@ for removing vowels e, â, a in second syllable before consonant  
 %^1VOW:0 %^2VOW:0 %^3VOW:0 %^4VOW:0 %^5VOW:0 !!= * @CODE@ Vow triggers

 %^VOWLower:0 !!= * @CODE@ i>e, õ>â, â>ä, u>o, o>å
 %^VOWRaise:0 !!= * @CODE@ e>i, ä>e 
 %^VOWSH:0	  !!= * @CODE@
 %^VOWXSH:0	  !!= * @CODE@ Extra shortens vowel in relation to consonant
 %^VOWL:0     !!= * @CODE@ Lengthens vowel in relation to consonant
 %^VOWXL:0    !!= * @CODE@ Extra lengthens vowel in relation to consonant in nouns, for example
 %^CXLong:0 %^CLong:0 %^CShort:0 !!= * @CODE@ Lengthening, shortening
 %^VOWBack:0	     !!= * @CODE@ Backing e>õ, e>â
 %^VOWFront:0	     !!= * @CODE@ Fronting
 %^PAL:0 %^PALNo:0   !!= * @CODE@ Palatalisation
 %^VC:0         !!= * @CODE@ Voicing s:z š:ž
 X3:0  !!= * @CODE@ causes weak grade
 %{Â1%}:0 !!= * @CODE@ used with "mainsted" to tell (a story)
 %{A1%}:0 !!= * @CODE@ used with "vuejted" to drive (causative)
 %{E1%}:0 !!= * @CODE@ used with "kåˊmrded" to bow
 %{%ˊ%}:0 !!= * @CODE@ archiform for MODIFIER LETTER ACUTE ACCENT used in stems where the lemma does not contain a MODIFIER LETTER ACUTE ACCENT and yet the paradigm requires one.
 %{EÂ%}:e !!= * @CODE@ archiform for ie:eâ variation
 %^IMPSG2:0	    !!= * @CODE@ 
 %^INDPRSSG3:0  !!= * @CODE@ 
 %^FRICG:0	    !!= * @CODE@ 
 %^VOWTense:0   !!= * @CODE@ â:e
 %^VOWLax:0   !!= * @CODE@ e:â 

!! Literal quotes and angles must be escaped (cf morpheme boundaries below):

 »7     !! * »
 «7     !! * «
 %[%>%] !! * >
 %[%<%] !! * <

! Morpheme boundaries:
 «  ! Derivational prefix
 »  ! Derivational suffix
 %< ! Inflectional prefix
 %> ! Inflectional suffix
 #  ! Word boundary for both lexicalised and dynamic compounds
 %^ ! (exceptional) soft hyphenation point

! End of alphabet definitions
  ;


!! !!Sets
Sets 

! Letter =
 Cns	= b c d f g h j k l m n p q r s t v w x z đ ʒ ǯ č ŋ ǩ ǧ ǥ š ž ; !!= * @CODE@
 CNoV	= b c d f g h j k l m n p q r s t w x z đ ʒ ǯ č ŋ ǩ ǧ ǥ š ž ; !!= * @CODE@

 CNoJ	= b c d f g h k l m n p q r s t v w x z đ ʒ ǯ č ŋ ǩ ǧ ǥ š ž ; !!= * @CODE@
 Vow	= a e i o u å ä â õ
          A E I O U Å Ä Â Õ ;	    !!= * @CODE@

 CNoJV  = b c d f g h k l m n p q r s t w x z đ ʒ ǯ č ŋ ǩ ǧ ǥ š ž ;    !!= * @CODE@


!! !! Definitions

Definitions 
!! {{{
ShortV2XL = [ 0:a a | 0:a a:ä ] ;
!! }}}

!! {{{
LongV2SH = [ a:0 a | â:0 â | ä:0 ä | e:0 e | i:0 i | å:0 å | o:0 o | u:0 u | õ:0 õ | a:0 a:ä | â:0 â:õ | ä:0 ä:a | ä:0 ä:a | e:0 e:i | i:0 i:e | å:0 å:o | o:0 o:u | u:0 u:o | õ:0 õ:â ] ;
!! }}}

!! {{{
Diphth = [ e â | e:i â:õ | e ä | e:i ä:â | e:i ä:e | i â | i:e â:ä | i â:e | i e | i:e e:â | i:e e:ä | i e:õ | i õ | i:e õ:â | i:e õ:ä | u â | u â:õ | u e | u e:â | u e:õ | u e:ä | u õ | u õ:â | u õ:e | u å | u å:ä | u å:e | u ä | u ä:å | u ä:e ] ;
!! }}}

!! {{{
2DbVow = [ a a | a:ä a:ä | 0:a a | i i | i:e i:e | 0:i i | o o | o:å o:å | 0:o o | õ õ | õ:â õ:â | 0:õ õ | u u | u:o u:o | 0:u u | â â | 0:â â | 0:õ â:õ | â:õ â:õ | e e | 0:e e | 0:i e:i | e:i e:i | å å | 0:å å | 0:o å:o | å:o å:o | ä ä | 0:ä ä | 0:a ä:a | ä:a ä:a | e e:ä | e e:â ] ;
!! }}}

!! {{{
2SgVow = [ a:0 a | a:0 a:ä | i:0 i | i:0 i:e | o:0 o | o:0 o:å | õ:0 õ | õ:0 õ:â | u:0 u | u:0 u:o | â:0 â | â:0 â:õ | e:0 e | e:0 e:i | å:0 å | å:0 å:o |  ä:0 ä | ä:0 ä:a ] ;
!! }}}

!! {{{
2LongVow = [ Diphth | 2DbVow ] ;
!! }}}

!! {{{
2ShortVow = [ Diphth | 2SgVow ] ;
!! }}}

!! {{{
LongC = [ k: k: | s: s: | š: š: | c: c: | č: č: | m m: | v v: | đ đ: | n n: | l l: | r r: ] ;
!! }}}


!! preceded by two vowels
!! {{{
LongC2SH = [ k:ǥ k:ǥ | s:z s:z | š:ž š:ž | c:ʒ c:ʒ | č:j č:j | m m:0 | v v:0 | đ đ:0 | n n:0 | l l:0 | r r:0 ] ;
!! }}}

!! Onset consonant or word boundary
!! {{{
OnSetC = [ Cns | # ] ;
!! }}}

!! following morpheme or word boundary
!! {{{
RBound = [ # | %> ] ;
!! }}}

!! possible triggers befor VOWLower and VOWRaise
!! {{{
PrecedeVOWLowerRaise = [( [ %^VOWSH: | %^VOWXSH: ] %^CLong: | [ %^VOWL: | %^VOWXL: ] %^CShort: )] ;
!! }}}

!! possible triggers before PALNo and PAL
!! {{{
PrecedePALNo = [( [ %^VOWSH: | %^VOWXSH: ] %^CLong: | [ %^VOWL: | %^VOWXL: ] ( %^FRICG: )  %^CShort: ) ( %^VOWLower: | %^VOWRaise: ) ( %^VOWFront: | %^VOWBack: ) (X3: ) ] ;
!! }}}

!! neutral to vowel height and backness
!! {{{
NeutrHighBack = [( %^VOWLower: | %^VOWRaise: ) ( %^VOWFront: | %^VOWBack: )] ;
!! }}}

Rules !!= !!! @CODE@

!! !! Consonant shift rules 
!! (tbw)



"Even syllabic verbs I, vowel shortening â:0"
!! __@RULENAME@__ - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
!! {{{
 â:0 <=> OnSetC _ â: ( %ˊ: | 0:%ˊ ) [ LongC | Cns:+ ] [ %^VOWSH: | %^VOWXSH: ] ( %^CLong: ) ;
         [ Cns:+ | :Cns+ ] _ [ Cns | i: | n j | l m | s ǩ: | s t | š ǩ: | š m ] ( %^VOWSH: ( %^CLong: ) | %^VC: ) %^RmVow: ;
!! }}}

"Even syllabic verbs I, vowel shortening e:0"
!! __@RULENAME@__ - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
!! {{{
 e:0 <=> OnSetC _ e: (%ˊ: | 0:%ˊ ) [ LongC | Cns: :Cns* ] [ %^VOWSH: | %^VOWXSH: ] ;
         [ Cns:+ | :Cns+ ] _ [ Cns | i: | n j | l m | s ǩ: | s t | š ǩ: | š m ] ( %^VOWSH: ( %^CLong: ) | %^VC: ) %^RmVow: ;
         OnSetC _ e: (%ˊ: | 0:%ˊ ) Cns:+ Vow: Cns %^VOWSH:  ;
!! }}}

"Even syllabic verbs I, vowel shortening å:0"
!! __@RULENAME@__ - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
!! {{{
 å:0 <=> OnSetC _ å: (%ˊ: | 0:%ˊ ) [ LongC | Cns: :Cns* ] [ %^VOWSH: | %^VOWXSH: ] ;
!! }}}

"Even syllabic verbs I, vowel shortening õ:0"
!! __@RULENAME@__ - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
!! {{{
 õ:0 <=> OnSetC _ õ: (%ˊ: | 0:%ˊ ) [ LongC | Cns: :Cns* ] [ %^VOWSH: | %^VOWXSH: ]  ;
         OnSetC _ õ: (%ˊ: | 0:%ˊ ) Cns:+ Vow: [ Cns | n j | l m | s ǩ: | s t | š ǩ: | š m ]  %^VOWSH:  ;
!! }}}

"Even syllabic verbs I, vowel shortening u:0"
!! __@RULENAME@__ - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
!! {{{
 u:0 <=> OnSetC _ u: (%ˊ: | 0:%ˊ ) [ LongC | Cns: :Cns* ] [ %^VOWSH: | %^VOWXSH: ] ;
         OnSetC _ u: (%ˊ: | 0:%ˊ ) Cns:+ Vow: Cns [ %^VOWSH: | %^RmVow: ] ;
!! }}}

"Even syllabic verbs I, vowel shortening i:0"
!! __@RULENAME@__ - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
!! {{{
 i:0 <=> OnSetC _ i: (%ˊ: | 0:%ˊ ) [ LongC | Cns: :Cns* ] [ %^VOWSH: | %^VOWXSH: ] ;
         Cns _ %>: ǥ a ; ! +Sg+Ill N_HÕˊPPI
         [ Cns:+ | :Cns+ ] _ [ Cns | n j | l m | s ǩ: | s t | š ǩ: | š m ] ( %^VOWSH: ( %^CLong: ) | %^VC: ) %^RmVow: ;
         OnSetC _ i: (%ˊ: | 0:%ˊ ) Cns:+ Vow: [ Cns | n j | l m | s ǩ: | s t | š ǩ: | š m ]  %^VOWSH:  ; ! iilâsǩ:ilska
!! }}}

"Even syllabic verbs I, vowel shortening o:0"
!! __@RULENAME@__ - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
!! {{{
 o:0 <=> OnSetC _ o: (%ˊ: | 0:%ˊ ) [ LongC | Cns: :Cns* ] [ %^VOWSH: | %^VOWXSH: ] ;
         [ Cns:+ | :Cns+ ] _ [ Cns | i: | n j | l m | s ǩ: | s t | š ǩ: | š m ] ( %^VOWSH: ( %^CLong: ) | %^VC: ) %^RmVow: ;
!! }}}

"Even syllabic verbs I, vowel shortening a:0"
!! __@RULENAME@__ - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
!! {{{
 a:0 <=> OnSetC _ a: (%ˊ: | 0:%ˊ ) [ LongC | Cns: :Cns* ] [ %^VOWSH: | %^VOWXSH: ] ;
         [ Cns:+ | :Cns+ ] _ [ Cns | i: | n j | l m | s ǩ: | s t | š ǩ: | š m ] ( %^VOWSH: ( %^CLong: ) | %^VC: ) %^RmVow: ;
         OnSetC _ a: (%ˊ: | 0:%ˊ ) Cns:+ Vow: [ Cns | n j | l m | s ǩ: | s t | š ǩ: | š m ]  %^VOWSH:  ; ! 
!! }}}

"Even syllabic verbs I, vowel shortening ä:0"
!! __@RULENAME@__ - used in +Ind+Prs+Pl3, +Ind+Prt+Pl3
!! {{{
 ä:0 <=> OnSetC _ ä: ( %ˊ: | 0:%ˊ ) [ LongC | Cns: :Cns* | Cns:+] [ %^VOWSH: | %^VOWXSH: ] ;
         OnSetC _ ä: (%ˊ: | 0:%ˊ ) Cns:+ Vow: Cns %^VOWSH:  ;
!! }}}

! 2013-08-29
"Even syllabic verbs I, diphthong shortening consonant lengthening"
!! __@RULENAME@__ - used in +N+Sg+Ill __jeäll:jiâlˈlu, __
!! {{{
 0:%ˈ <=> [ e: ä: | e: â: | i: â: | i: e: | u: â: | u: e: ] (%ˊ: | 0:%ˊ ) Cx: _ Cx: ( j ) [ %^VOWSH: %^CLong: | %^VOWTense: %^CLong: ] NeutrHighBack (X3: ) ( %^PAL: |%^PALNo: ) RBound  ;
                where Cx in (c đ k ǩ l m n p r s t v) ;
!! }}}




!! !!Vowel alternation rules

!! !VOWEL SHIFT

!! !RAISING

!! deriving +Ind+Prt+Sg1, +Ind+Prt+Sg2 +Ind+Prt+Pl3 in teevvad:tivvu

"Even syllabic verbs I, vowel raising o:u"
!! __@RULENAME@__ - Adding +Prt+Pl3 for ed verbs, Removing u: and i:  second element due to njeiddad njeiddu
!! {{{
 o:u <=>  OnSetC  o: _ (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ]  ( %^VOWSH: %^CLong: ) %^VOWRaise: %>: [ e | u ]  ; 
          OnSetC o:0 _ (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ]  ( %^VOWSH: %^CLong: ) %^VOWRaise: ( %^PAL: ) %>: [ e | u ] ;
          OnSetC 0:u _ (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ]  ( %^VOWL: %^CShort: ) %^VOWRaise: %^PAL: %>: [ e | u ] ;
  OnSetC _ ( u: ) (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ]  ( %^VOWSH: %^CLong: | %^VOWL: ) %^VOWRaise: ( %^PAL: ) %>: [ e | u ]  ;
!! }}}

"Even syllabic verbs I, vowel raising å:o"
!! __@RULENAME@__ - Adding +Prt+Pl3 for ed verbs, Removing u: and i:  second element due to njeiddad njeiddu
!! {{{
 å:o <=>  OnSetC å: _ (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ]  ( %^VOWSH: %^CLong: ) %^VOWRaise: %>: [ e | u ]  ; 
          OnSetC å:0 _ (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ]  ( %^VOWSH: %^CLong: ) %^VOWRaise: ( %^PAL: ) %>: [ e | u ] ;
          OnSetC 0:o _ (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ]  ( %^VOWL: %^CShort: ) %^VOWRaise: %^PAL: %>: [ e | u ] ;
          OnSetC _ ( u: ) (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ]  ( %^VOWSH: %^CLong: | %^VOWL: ) %^VOWRaise: ( %^PAL: ) %>: [ e | u ]  ;
!! }}}

"Even syllabic verbs I, vowel raising e:i"
!! __@RULENAME@__ - Adding +Prt+Pl3 for ed verbs, Removing u: and i:  second element due to njeiddad njeiddu
!! {{{
 e:i <=>   [ e: | e:0 | 0:i ] _ (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ]  PrecedeVOWLowerRaise %^VOWRaise: ;
  OnSetC _ ( u: ) (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ] PrecedeVOWLowerRaise %^VOWRaise: ;
! diphthong raising with e:i at onset - deriving +Ind+Prt+Pl3 in reäkkad > riâkku, veârrad > viõrru
  _ [ ä:â | â:õ  ] (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ | Cns:+ 0:%ˈ Cns:+ ]  PrecedeVOWLowerRaise  %^VOWRaise:  %>: u ;
!! }}}


"Even syllabic verbs I, vowel raising â:õ" 
!! __@RULENAME@__ - Adding +Prt+Pl3 for ed verbs, Removing u: and i:  second element due to njeiddad njeiddu
!! {{{
 â:õ <=>   â: _ (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ]  ( %^VOWSH: %^CLong: ) %^VOWRaise:   ; 
           â:0 _ (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ]  ( %^VOWSH: %^CLong: ) %^VOWRaise: ( %^PAL: ) ;
           0:õ _ (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ]  [ %^VOWL: | %^VOWXL: ] (%^CShort: ) %^VOWRaise: ;
  OnSetC _ ( u: ) (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ]  ( %^VOWSH: %^CLong: | %^VOWL: ) %^VOWRaise: ( %^PAL: ) ;
           u  _ (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ | Cns:+ :%ˈ Cns:+ ] ( %^VOWSH: %^CLong: ) %^VOWRaise: ;
           e:  _ (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ | Cns:+ 0:%ˈ Cns:+ ]  ( %^VOWSH: %^CLong: ) %^VOWRaise:  ;
!! }}}


"Even syllabic verbs I, diphthong raising beginning with u ä:å"
!! __@RULENAME@__ - deriving +Ind+Prt+Pl3 in kuâs'sad > kuõs'su, kuärŋŋad kuårŋŋu
!! {{{
 ä:å <=> u  _ (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ] ( %^VOWSH: %^CLong: ) %^VOWBack: ;
!! }}}

"Even syllabic verbs I, diphthong raising beginning with e:i ä:â"
!! __@RULENAME@__ - deriving +Ind+Prt+Pl3 in reäkkad > riâkku, veârrad > viõrru
!! {{{
 ä:â <=> e:  _ (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ | Cns:+ 0:%ˈ Cns:+ ]  PrecedeVOWLowerRaise %^VOWBack: %>: u ;
!! }}}


"Even syllabic verbs I, diphthong raising beginning with e:i ä:e"
!! __@RULENAME@__ - deriving 
!! {{{
 ä:e <=> [ u | e:i ]  _ (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ | Cns:+ 0:%ˈ Cns:+ ]  PrecedeVOWLowerRaise %^VOWRaise: ;
!! }}}

"Even syllabic verbs I, diphthong raising beginning with ä:a u"
!! __@RULENAME@__ - deriving läullad > laullum
!! {{{
 ä:a <=> OnSetC (0:a)  _ u (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ | Cns:+ 0:%ˈ Cns:+ ]  ( %^VOWSH: %^CLong: ) %^VOWRaise: %>:  u ;
         OnSetC ä: _ (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ | Cns:+ 0:%ˈ Cns:+ ] ( %^VOWXSH: ( %^CLong: ) | %^VOWL: ( %^CShort: )) %^VOWBack: ;
         OnSetC _ ä: (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ | Cns:+ 0:%ˈ Cns:+ ] ( %^VOWL: ( %^CShort: )) %^VOWBack: ;
!! }}}


!! !LOWERING 

! Where: VIGGAD +Ind+Prs+Sg3, +Ind+Prs+Pl3
!        TIETTED 
! long and single vowels



"Even syllabic verbs I, vowel lowering o:å"
!! __@RULENAME@__ - deriving +Ind+Prs+Sg3, +Ind+Prs+Pl3 in poorrâd
!! {{{
 o:å <=> OnSetC _ o:å (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ] ( %^VOWL: %^CShort: )  %^VOWLower: ( %^VOWFront: | %^VOWBack: ) (X3: ) ( %^PALNo: | %^PAL: ) RBound ;
           OnSetC o: _   (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ] ( %^VOWSH: %^CLong: | %^VOWL: %^CShort: )                      %^VOWLower: ( %^VOWFront: | %^VOWBack: ) (X3: ) ( %^PALNo: | %^PAL: ) RBound ;
           OnSetC _   (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ] ( %^VOWSH: %^CLong: )                      %^VOWLower: ( %^VOWFront: | %^VOWBack: ) (X3: ) ( %^PALNo: | %^PAL: ) RBound ;
! juˊrdded+V+Cond+Sg1: joordčem
           OnSetC 0:å _ (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ] ( %^VOWL: %^CShort: )  %^VOWLower: ( %^VOWFront: | %^VOWBack: ) (X3: ) ( %^PALNo: | %^PAL: ) RBound ;
! juˊrdded > joordam
           OnSetC _ ( u: | i: ) (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ] ( %^VOWL: %^CShort: ) %^VOWLower: ( %^VOWFront: | %^VOWBack: ) (X3: ) ( %^PALNo: | %^PAL: ) RBound ;
           OnSetC _ ((%ˊ: ) ( v:u | j:i )) (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ] ( %^VOWSH: %^CLong: ) %^VOWLower: ( %^VOWFront: | %^VOWBack: ) (X3: ) ( %^PALNo: | %^PAL: ) RBound ;
!! }}}

"Even syllabic verbs I, vowel lowering i:e"
!! __@RULENAME@__ - deriving +Ind+Prs+Sg3, +Ind+Prs+Pl3 in viǯǯâd
!! {{{
 i:e <=> OnSetC _ i:e (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ] ( %^VOWL: %^CShort: )  %^VOWLower: ( %^VOWFront: | %^VOWBack: ) (X3: ) ( %^PALNo: | %^PAL: ) RBound ;
     OnSetC _ e: (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ] ( %^VOWL: %^CShort: )  %^VOWLower: ( %^VOWFront: | %^VOWBack: ) (X3: ) ( %^PALNo: | %^PAL: ) RBound ;
           OnSetC i: _   (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ] ( %^VOWSH: %^CLong: |  %^VOWL: %^CShort: )                      %^VOWLower: ( %^VOWFront: | %^VOWBack: ) (X3: ) ( %^PALNo: | %^PAL: ) RBound ;
           OnSetC _   (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ] ( %^VOWSH: %^CLong: )                      %^VOWLower: ( %^VOWFront: | %^VOWBack: ) (X3: ) ( %^PALNo: | %^PAL: ) RBound ;
! juˊrdded+V+Cond+Sg1: joordčem
           OnSetC 0:e _ (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ] ( %^VOWL: %^CShort: )  %^VOWLower: ( %^VOWFront: | %^VOWBack: ) (X3: ) ( %^PALNo: | %^PAL: ) RBound ;
! juˊrdded > joordam
           OnSetC _ ( u: | i: ) (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ] ( %^VOWL: %^CShort: ) %^VOWLower: ( %^VOWFront: | %^VOWBack: ) (X3: ) ( %^PALNo: | %^PAL: ) RBound ;
           OnSetC _ ((%ˊ: ) ( v:u | j:i )) (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ] ( %^VOWSH: %^CLong: ) %^VOWLower: ( %^VOWFront: | %^VOWBack: ) (X3: ) ( %^PALNo: | %^PAL: ) RBound ;
            _ [õ:â | õ:ä | â:ä | e:ä | e:â ] (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ] ( %^VOWL: %^CShort: )  %^VOWLower: (X3: ) ( %^PALNo: | %^PAL: ) RBound ;
!! }}}

"Even syllabic verbs I, vowel lowering a:ä"
!! __@RULENAME@__ - deriving +Ind+Prs+Sg3, +Ind+Prs+Pl3 in viǯǯâd
!! {{{
 a:ä <=> OnSetC _ a:ä (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ] ( %^VOWL: %^CShort: )  %^VOWLower: ( %^VOWFront: | %^VOWBack: ) (X3: ) ( %^PALNo: | %^PAL: ) RBound ;
           OnSetC a: _   (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ] ( %^VOWSH: %^CLong: | %^VOWL: %^CShort: )                      %^VOWLower: ( %^VOWFront: | %^VOWBack: ) (X3: ) ( %^PALNo: | %^PAL: ) RBound ;
           OnSetC _   (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ] ( %^VOWSH: %^CLong: )                      %^VOWLower: ( %^VOWFront: | %^VOWBack: ) (X3: ) ( %^PALNo: | %^PAL: ) RBound ;
! juˊrdded+V+Cond+Sg1: joordčem
           OnSetC 0:ä _ (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ] ( %^VOWL: %^CShort: )  %^VOWLower: ( %^VOWFront: | %^VOWBack: ) (X3: ) ( %^PALNo: | %^PAL: ) RBound ;
! juˊrdded > joordam
           OnSetC _ ( u: | i: ) (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ] ( %^VOWL: %^CShort: ) %^VOWLower: ( %^VOWFront: | %^VOWBack: ) (X3: ) ( %^PALNo: | %^PAL: ) RBound ;
           OnSetC _ ((%ˊ: ) ( v:u | j:i )) (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ] ( %^VOWSH: %^CLong: ) %^VOWLower: ( %^VOWFront: | %^VOWBack: ) (X3: ) ( %^PALNo: | %^PAL: ) RBound ;
!! }}}

!"Even syllabic verbs I, vowel raising a:ä"
!! __@RULENAME@__ - Adding +Prt+Pl3 for ed verbs, Removing u: and i:  second element due to njeiddad njeiddu
! a:ä <=> OnSetC a: _ (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ]  ( %^VOWSH: %^CLong: ) %^VOWRaise: %>: [ e | u ]  ; 
!         OnSetC a:0 _ (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ]  ( %^VOWSH: %^CLong: ) %^VOWRaise: ( %^PAL: ) %>: [ e | u ] ;
!         OnSetC 0:ä _ (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ]  ( %^VOWL: %^CShort: ) %^VOWRaise: %^PAL: %>: [ e | u ] ;
!  OnSetC _ ( u: ) (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ]  ( %^VOWSH: %^CLong: | %^VOWL: ) %^VOWRaise: ( %^PAL: ) %>: [ e | u ]  ;



"Even syllabic verbs I, vowel lowering u:o"
!! __@RULENAME@__ - deriving +Ind+Prs+Sg3, +Ind+Prs+Pl3 in uˊvdded
!! {{{
 u:o <=>  _ u: (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ] ( %^VOWL: %^CShort: )  %^VOWLower: ( %^VOWFront: | %^VOWBack: ) (X3: ) ( %^PALNo: | %^PAL: ) RBound ;
        u: _     (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ] ( %^VOWSH: %^CLong: | %^VOWL: %^CShort: )                      %^VOWLower: ( %^VOWFront: | %^VOWBack: ) (X3: ) ( %^PALNo: | %^PAL: ) RBound ;
       OnSetC _  ( %ˊ: |  0:%ˊ ) [ CNoV: Cns:* | Cns:+ %ˈ: Cns:+ ] ( %^VOWSH: %^CLong: )                      %^VOWLower: ( %^VOWFront: | %^VOWBack: ) (X3: ) ( %^PALNo: | %^PAL: ) RBound ;
! uˊvdded+V+Ind+Pl3: ouˊdde 
 OnSetC (0:o) _  (%ˊ: | 0:%ˊ ) v: ( 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ] [ %^VOWL: | %^VOWXL: ] (%^CShort: )  %^VOWLower: ;

! juˊrdded+V+Cond+Sg1: joordčem
 OnSetC 0:o _ ( %ˊ: | 0:%ˊ ) [ CNoV: Cns:* | Cns:+ %ˈ: Cns:+ ]  %^VOWL: ( %^CShort: )   %^VOWLower: ;
! juˊrdded > joordam
!2013-09-08 OnSetC _ ( u: | i: ) (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ] ( %^VOWL: %^CShort: ) %^VOWLower: ( %^VOWFront: | %^VOWBack: ) (X3: ) ( %^PALNo: | %^PAL: ) RBound ;
 OnSetC ( 0:o ) _ (%ˊ: ) [ CNoV: Cns:* | Cns:+ %ˈ: Cns:+ ] ( [ %^VOWL: | %^VOWXL: ] ( %^CShort: )) %^VOWLower: ( %^VOWFront: | %^VOWBack: ) (X3: ) ( %^PALNo: | %^PAL: ) RBound ;
 OnSetC ( 0:o ) _ (%ˊ: )  v:u (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ] ( %^VOWL: %^CShort: | %^VOWXL: %^CShort: ) %^VOWLower: ( %^VOWFront: | %^VOWBack: ) (X3: ) ( %^PALNo: | %^PAL: ) RBound ;
!! }}}


"Even syllabic verbs I, vowel lowering õ:â "
!! __@RULENAME@__ - deriving +Ind+Prs+Sg3, +Ind+Prs+Pl3 in viǯǯâd
!! {{{
 õ:â <=>  _ õ: (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ] ( %^VOWL: %^CShort: )  %^VOWLower: ( %^VOWFront: | %^VOWBack: ) (X3: ) ( %^PALNo: | %^PAL: ) RBound ;
        õ: _     (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ] ( %^VOWSH: %^CLong: | %^VOWL: %^CShort: )                      %^VOWLower: ( %^VOWFront: | %^VOWBack: ) (X3: ) ( %^PALNo: | %^PAL: ) RBound ;
 OnSetC _     (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ] ( %^VOWSH: %^CLong: )                      %^VOWLower: ( %^VOWFront: | %^VOWBack: ) (X3: ) ( %^PALNo: | %^PAL: ) RBound ;
! juˊrdded+V+Cond+Sg1: joordčem
 OnSetC _ 0:â (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ] ( %^VOWL: %^CShort: )  %^VOWLower: ( %^VOWFront: | %^VOWBack: ) (X3: ) ( %^PALNo: | %^PAL: ) RBound ;
! juˊrdded > joordam
 OnSetC _ ( u: | i: ) (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ] ( %^VOWL: %^CShort: ) %^VOWLower: ( %^VOWFront: | %^VOWBack: ) (X3: ) ( %^PALNo: | %^PAL: ) RBound ;
 OnSetC _ ((%ˊ: ) ( v:u | j:i )) (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ] ( %^VOWSH: %^CLong: ) %^VOWLower: ( %^VOWFront: | %^VOWBack: ) (X3: ) ( %^PALNo: | %^PAL: ) RBound ;
! diphthong opening after u
! - deriving +Ind+Prs+Sg3, in kuõskkâd kueˊsǩǩe
 u _ (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ] ( %^VOWL: ( %^FRICG:0 ) %^CShort: )  %^VOWLower: (X3: ) ( %^PALNo: | %^PAL: ) RBound ;
! diphthong opening õ:â after i:e
! - deriving +Ind+Prs+Sg3, in čiõkkâd
i: _ (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ] ( %^VOWL: %^CShort: )  %^VOWLower: (X3: )  %^PALNo: RBound ;
!! }}}

! diphthongs

"Even syllabic verbs I, diphthong opening after u"
!! __@RULENAME@__ - deriving +Ind+Prs+Sg3, in kuåccâd kuäˊcce
!! {{{
 å:ä <=> u _ (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ] ( %^VOWL: ( %^FRICG:0 ) %^CShort: )  %^VOWLower: (X3: ) ( %^PALNo: | %^PAL: ) RBound ;
!! }}}

"Even syllabic verbs I, diphthong opening after i:e _e:ä_"
!! __@RULENAME@__ - deriving +Ind+Prs+Sg3, in čiõkkâd
!! {{{
 e:ä <=> [ i: | u ] _ (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ] ( %^VOWL: %^CShort: )  %^VOWLower: (X3: ) ( %^PALNo: | %^PAL: ) RBound ;
! vueˊlǧǧed+V+Ind+Prs+Pl3: vuäˊlǧǧa
!! }}}        

"Even syllabic verbs I, diphthong opening i:e _â:ä_"
!! __@RULENAME@__ - deriving +Ind+Prs+Sg3, in čiõkkâd
!! {{{
 â:ä <=> i: _ (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ] ( %^VOWL: %^CShort: )  %^VOWLower: (X3: ) ( %^PALNo: | %^PAL: ) RBound ;
!! }}}


"Even syllabic verbs I, diphthong opening after i:e e:â"
!! __@RULENAME@__ - deriving +Ind+Prs+Sg3, in pi%{EÂ%}ˊǩǩ:peâkka
!! {{{
 %{EÂ%}:â <=> i: _ (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ] ( %^VOWL: %^CShort: )  %^VOWLower: (X3: ) %^PALNo:  RBound ;
!! }}}

"Even syllabic verbs I, diphthong opening after u e:â"
!! __@RULENAME@__ - deriving vueˊlǧǧed+V+Ind+Prs+Pl3: vuâˊlǧǧe
!! {{{
 e:â <=> [ u | i: ] _ (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ] ( %^VOWL: ( %^FRICG: ) %^CShort: | %^VOWSH: ( %^CLong: ) )  %^VOWLower: %^VOWBack: ;
!! }}}

"Even syllabic verbs I, diphthong opening after u e:õ"
!! __@RULENAME@__ - deriving vueˊlǧǧed+V+Ind+Prt+Sg1: vuõˊlǧǧem
!! {{{
 e:õ <=> u _ (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ] ( %^VOWL: %^CShort: | %^VOWSH: ( %^CLong: ) ) %^VOWBack: ;
!! }}}


"Even syllabic verbs I, diphthong opening _õ:ä_ after i:e"
!! __@RULENAME@__ - deriving +Ind+Prs+Sg3, in čiõkkâd
!! {{{
 õ:ä <=> [ i:e | u ] _ (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ]  (%^VOWSH: ( %^FRICG:0 ) %^CLong: | %^VOWL: %^CShort: ) %^VOWLower: %^VOWFront: ;
!! }}}



!! !SECONDARY FRONTING


 
"Even syllabic verbs I, secondary vowel fronting with PAL u õ:e"
!! __@RULENAME@__ - deriving +Ind+Prs+Pl3 in VIQQAD: kuõskkâd >kueˊsǩǩe
!! {{{
 õ:e <=> u _ (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ]  ( %^VOWSH: %^CLong: | %^VOWL: %^CShort: )  %^VOWFront: (X3: ) %^PAL: RBound ; ! deriving +V+Inf in vueˊlǧǧed
         i _ (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ] %^VOWFront: %^PAL: %>: ;
!! }}} 



! "Even syllabic verbs I, secondary u > v PAL"
! __@RULENAME@__ - deriving +V+Inf in TIETTED: _uudd_ >uˊvdded
! u:v <=> u _ (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ] %^VOWFront: %^PAL: %>: ;

!! !RELATIVE VOWEL LENGTHENING

!! vowel lengthening and consonant shortening, %^VOWL and %^CShort 


"Even syllabic verbs I, relative vowel lengthening 0:â"
!! __@RULENAME@__ - deriving +V+Inf in TIETTED: uudd > uˊvdded
!! {{{
 0:â <=> Cns: _ â [( u: | i: ) (%ˊ: | 0:%ˊ ) [ u l l: | Cns:+ | Cns:+ %ˈ: Cns:+ ] ]  %^VOWL: ( %^CShort: ) [ %>: | #: | %^PAL: ] ;
!Distinguishing mainstam and maainstam
      Cns: _ â ( u: | i: ) ( %ˊ: | 0:%ˊ )  Cns: %{Â1%}: Cns:+  %^VOWL: ;
      OnSetC _ â [ (%ˊ: | 0:%ˊ ) [ u l l: | Cns:+ | Cns:+ %ˈ: Cns:+ ] ]  %^VOWXL:  ;
! Even syllabic verbs I, relative vowel lengthening and lowering 0:â
!simultaneous lengthening and lowering: juˊrdded > joordam juˊrdded+V+Ind+Prs+Sg1
   OnSetC _ õ:â (%ˊ: | 0:%ˊ ) Cns:+ %^VOWL: %^CShort: %^VOWLower:  ;
!! }}}

"Even syllabic verbs I, relative vowel lengthening 0:e"
!! __@RULENAME@__ - deriving +V+Inf in TIETTED: uudd > uˊvdded
!! {{{
 0:e <=> Cns: _ e [( u: | i: ) (%ˊ: | 0:%ˊ ) [ u l l: | Cns:+ | Cns:+ %ˈ: Cns:+ ] ]  %^VOWL: ( %^CShort: ) [ %>: | #: | %^PAL: ] ;
!Distinguishing mainstam and maainstam
      Cns: _ e ( u: | i: ) ( %ˊ: | 0:%ˊ )  Cns: %{Â1%}: Cns:+  %^VOWL: ;
      OnSetC _ e [ (%ˊ: | 0:%ˊ ) [ u l l: | Cns: :Cns* | Cns:+ %ˈ: Cns:+ ] ]  %^VOWXL:   ;
!Even syllabic verbs I, relative vowel lengthening and lowering 0:e"
!simultaneous lengthening and lowering: juˊrdded > joordam juˊrdded+V+Ind+Prs+Sg1
      OnSetC _ i:e (%ˊ: | 0:%ˊ ) Cns:+ %^VOWL: %^CShort: %^VOWLower:  ;
!! }}}

"Even syllabic verbs I, relative vowel lengthening 0:å"
!! __@RULENAME@__ - deriving +V+Inf in TIETTED: uudd > uˊvdded
!! {{{
 0:å <=> Cns: _ å [( u: | i: ) (%ˊ: | 0:%ˊ ) [ u l l: | Cns:+ | Cns:+ %ˈ: Cns:+ ] ]  %^VOWL: ( %^CShort: ) [ %>: | #: | %^PAL: ] ;
!Distinguishing mainstam and maainstam
!Vowel lengthening in INDPRSSG3 and IMPSG2 0:å kååˊmmerd, maainâst
      Cns: _ å ( u: | i: ) ( %ˊ: | 0:%ˊ )  Cns:+ [ %{Â1%}: | %{A1%}: | %{E1%}: ] Cns:+  [ %^VOWL: | %^IMPSG2: | %^INDPRSSG3: ] ;
      OnSetC _ å [ (%ˊ: | 0:%ˊ ) [ u l l: | Cns: :C* | Cns:+ %ˈ: Cns:+ ] ]  %^VOWXL:  ;

! Even syllabic verbs I, relative vowel lengthening and lowering 0:å
! simultaneous lengthening and lowering: juˊrdded > joordam juˊrdded+V+Ind+Prs+Sg1
   OnSetC _ o:å (%ˊ: | 0:%ˊ ) Cns:+ %^VOWL: %^CShort: %^VOWLower:  ;
!! }}}

"Even syllabic verbs I, relative vowel lengthening 0:õ"
!! __@RULENAME@__ - deriving +V+Inf in TIETTED: uudd > uˊvdded
!! {{{
 0:õ <=> OnSetC _ :õ [( u: | i: ) (%ˊ: | 0:%ˊ ) [ u l l: | Cns:+ | Cns:+ %ˈ: Cns:+ ] ]  [ %^VOWL: | %^VOWXL: ] ( %^CShort: ) ;

!Distinguishing mainstam and maainstam
      Cns: _ õ ( u: | i: ) ( %ˊ: | 0:%ˊ )  Cns: %{Â1%}: Cns:+  %^VOWL: ;
      OnSetC _ õ [ (%ˊ: | 0:%ˊ ) [ u l l: | Cns: :Cns* | Cns:+ %ˈ: Cns:+ ] ]  %^VOWXL:  ;
!! }}}

"Even syllabic verbs I, relative vowel lengthening 0:ä"
!! __@RULENAME@__ - deriving +V+Inf in TIETTED: uudd > uˊvdded
!! {{{
 0:ä <=> Cns _ :ä [( u | i ) ( %ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ] ] [ %^VOWL: | %^VOWXL: ] ;
!Distinguishing mainstam and maainstam
!- kååˊmmerd, maainâst Vowel lengthening in INDPRSSG3 and IMPSG2
     _ ä (i) ( %ˊ: | 0:%ˊ ) Cns:+ [ %{E1%}: | %{A1%}: | %{Â1%}: ] Cns:+ [ %^VOWL: | %^IMPSG2: | %^INDPRSSG3: ] ;
!! }}}



"Even syllabic verbs I, relative vowel lengthening 0:a"
!! __@RULENAME@__ - deriving +V+Inf in MAINSTED: maainstam, mainstam
!! {{{
 0:a <=> Cns: _ a ( u: | i: ) (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ]  %^VOWL: ;
!Distinguishing mainstam and maainstam
         Cns: _ a  i  ( %ˊ: | 0:%ˊ ) Cns: %{Â1%}: Cns:+  %^VOWL: ;
         Cns: _ a  i  Cns: %{Â1%}: Cns:+  %^VOWL: ;
      Cns: _ a ( u: | i: ) ( %ˊ: | 0:%ˊ )  Cns:+ [ %{Â1%}: | %{A1%}: | %{E1%}: ] Cns:+  [ %^VOWL: | %^IMPSG2: | %^INDPRSSG3: ] ;
! - conjugation of ceˊpcced > ciiˊpcež
 OnSetC _ ä:a (%ˊ: | 0:%ˊ ) Cns:+ %^VOWL: %^CShort: %^VOWRaise: %^PAL: %>: e ;
!! }}}



!! 2012-06-05 removing "i" and "u" because viikkâd renders viiiǥǥam


"Even syllabic nouns I, relative vowel lengthening i"
!! __@RULENAME@__ - ǩiđđ:ǩiiđ
!! {{{
 0:i <=> Cns: _ i (%ˊ: | 0:%ˊ ) [ CNoJ:+ | CNoJ:+ %ˈ: Cns:+ ]  [ %^VOWL: | %^VOWXL: ] ;
         OnSetC _ e:i (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ]  [ %^VOWL: | %^VOWXL: ] ( %^CShort: ) %^VOWRaise: ; 
    Cns: _ i  ( %ˊ: )  CNoJ:  %{Â1%}:  Cns:+  [ %^VOWL: | %^VOWXL: ]  ;
!! }}}



"Even syllabic nouns, relative vowel extra lengthening u not followed by v"
!! __@RULENAME@__ - declension of nouns kunn > kuun
!! {{{
 0:u <=> OnSetC _ u: (%ˊ: | 0:%ˊ ) [ [ s: s: | n n: ] %^VOWXL: | CNoV: Cns:+ %^VOWL: %^CShort: ] RBound ;
! relative lengthening of u
          Cns: _ u: [ (%ˊ: | 0:%ˊ ) [ CNoV:+ | CNoV:+ %ˈ: Cns:+ ] ] [ %^VOWL: | %^VOWXL: ] ( %^CShort: ) [ %>: | #: | ( %^VOWRaise: ) %^PAL: ] ;
      Cns: _ u: (%ˊ: | 0:%ˊ )  CNoV: %{Â1%}: Cns:+   %^VOWL: [ %>: | #: | %^PAL: ] ;
    OnSetC _ o:u (%ˊ: | 0:%ˊ ) Cns:+ %^VOWL: %^CShort: %^VOWRaise: %^PAL: %>: e ;
!! }}}


!! simultaneous lengthening and raising, hmm: xfst ordering might be easier


"Even syllabic verbs I, relative vowel lengthening and lowering u:o _0:o_"
!! __@RULENAME@__ -  simultaneous lengthening and lowering: juˊrdded > joordam juˊrdded+V+Ind+Prs+Sg1
!! {{{
 0:o <=> OnSetC _ u:o ( %ˊ: | 0:%ˊ )  CNoV: Cns:* [ %^VOWL: %^CShort: | %^VOWXL: ( %^CShort: ) ] %^VOWLower:  ;
         OnSetC _ u:o (%ˊ: | 0:%ˊ ) v:u (%ˊ: | 0:%ˊ ) Cns:+ %^VOWXL: %^CShort: %^VOWLower:  ;
! Even syllabic verbs I, relative vowel lengthening of o
      OnSetC _ o [( u: | i: ) (%ˊ: | 0:%ˊ ) [ Cns:+ | Cns:+ %ˈ: Cns:+ ] ] [ %^VOWL: | %^VOWXL: ] ( %^CShort: ) [ %>: | #: | %^PAL: ] ;
      OnSetC _ o ( u: | i: ) (%ˊ: | 0:%ˊ )  Cns: %{Â1%}: Cns:+   %^VOWL: [ %>: | #: | %^PAL: ] ;
      OnSetC _ o [ (%ˊ: | 0:%ˊ ) [ u l l: | Cns:+ | Cns:+ %ˈ: Cns:+ ] ]  %^VOWXL:  ;
      OnSetC _ å:o (%ˊ: | 0:%ˊ ) Cns:+ %^VOWL: %^CShort: %^VOWRaise: %^PAL: %>: e ;
!! }}}

!! !VOWEL DUMMY LOSS


!! !SEMI VOWELS

 
"Even syllabic nouns, for j>i"
!! __@RULENAME@__ - sijdd > siid This will need a special extra-lengthening rule
!! {{{
 j:i <=> OnSetC i: ( %{%ˊ%}: ) _ ( %ˊ: ) d d: [ %^VOWL: %^CShort: | %^VOWXL: | %^VOWLower: | %^VOWSH: %^CLong: %^VOWLower: %^PAL: ] ;
!! }}}

"Even syllabic verbs,  for v>u"
!! __@RULENAME@__ - uvdd > ouˊdde
!! {{{
 v:u <=> OnSetC ( 0:o ) u: ( %ˊ: ) _ ( %ˊ: | 0:%ˊ ) [ d d: | l l: ] [ %^VOWL: %^CShort: | %^VOWXL: %^CShort: ]  ;
!! }}}

 "Even syllabic nouns, for h>u"
!! __@RULENAME@__ - luhtt  s s: ...
!! {{{
 h:u <=> V: (%{%ˊ%}: ) _ (%ˊ: | 0:%ˊ ) [ s s: | š š: | t t: | c c: | č č: ] [ %^VOWL: %^CShort: | %^VOWXL: | %^VOWSH: %^CLong:  %^VOWLower: %^PAL: ] ;
!! }}}

"trisyllabic verbs and doer derivations, i>j"
!! __@RULENAME@__ -
 i:j <=> _ %> Vow ;

!! !VOWELS TENSE vs LAX 2012-11-28


"Even-syllabic nouns, for â:e"
!! __@RULENAME@__ - miârr:mieˊrre; ǩiâpp:ǩieˊpˈpe
!! {{{
 â:e <=> i _ ( %ˊ:  | 0:%ˊ ) [ Cns:+ | Cns:+ 0:%ˈ Cns:+ ] %^VOWTense:0 ;
!! }}}  

!! !VOWEL and ZERO ALTERNATION


"Realization for %{E1%} in e "
!! __@RULENAME@__ - kååˊmmerd
 %{E1%}:e <=> _ ( %ˊ: | 0:%ˊ ) Cns:+ %^IMPSG2: ;

"Realization for %{A1%} in a " 
!! __@RULENAME@__ - kåˊmrded, vuejat, maainâst
!! {{{
 %{A1%}:a <=> _ ( %ˊ: | 0:%ˊ ) Cns:+ %^IMPSG2: ;
              _ ( %ˊ: | 0:%ˊ | ʼ: ) [ i | Cns:+ ] %^INDPRSSG3: ;
!! }}}

"Realization for %{Â1%} in â " 
!! __@RULENAME@__ - kåˊmrded, vuejat, maainâst
 %{Â1%}:â <=> _ ( %ˊ: | 0:%ˊ ) Cns:+ %^IMPSG2: ;


"Realization for %{E1%} in a" 
!! __@RULENAME@__ - 
!! {{{
 %{E1%}:a <=> _ ( %ˊ: | 0:%ˊ | ʼ: ) [ i | Cns:+ ] %^INDPRSSG3: ;
!! }}}

"Realization for %{Â1%} in a" 
!! __@RULENAME@__ - 
!! {{{
 %{Â1%}:a <=> _ ( %ˊ: | 0:%ˊ | ʼ: ) [ i | Cns:+ ] %^INDPRSSG3: ;
!! }}}
         
!! !THE NON-ORTHOGRAPHIC SYLLABLE 


"Loss of _ʼ_ when preceded by vowel"
!! __@RULENAME@__ - This is a temporary solution to  "ʼ" in võˊllʼjed
 ʼ:0 <=> :Vow _  ; 

"%{A1%}:ʼ when subseqent syllable has vowel v"
!! __@RULENAME@__ - This is a temporary solution to  "ʼ" in võˊllʼjed 2013-08-29
!! {{{
%{A1%}:ʼ <=> [ l l ] _ i: %>: V: ; 
!! }}}

!! !PALATALIZATION


!2013-09-11!"Even-syllabic verbs I, Palatalization mark between V and C"
!2013-09-11!!! __@RULENAME@__ - used +Ind+Prs+Pl3, +Ind+Prt+Pl3
!2013-09-11! %{%ˊ%}:%ˊ <=> OnSetC ( Vow:0 | :Vow ) Vow:Vow _ [ Cns:Cns Cns:* | Cns:Cns+ %ˈ: Cns:+ | Cns:Cns+ 0:%ˈ Cns:+ ] ( %^VOWSH: ( %^CLong: ) | %^VOWTense: | %^VOWL: %^CShort: ) ( %^VOWLower: | %^VOWRaise: ) ( %^VOWFront: | %^VOWTense:0 ) (X3: ) %^PAL: RBound ;


"0:%ˊ as transfer from left of v:u"
!! __@RULENAME@__ - used +Ind+Prs+Pl3, uˊvdded+V+Ind+Prs+Pl3: ouˊdde
!! {{{
0:%ˊ <=> ( %ˊ: ) [ v:u | h:u ] _ [ d:+ | l:+ ] PrecedePALNo  %^PAL:  ;
  Vow: _ [ Cns:+ | :Cns+ ] PrecedePALNo  %^PAL:  ;
!! }}}

!! The left context allows for:
!! Vowel shortening, and j/v > Vow 



 
"Even-syllabic verbs I, Palatalization of gg and kk"
!! __@RULENAME@__ -
 Cx:Cy <=> Vow: (%ˊ: | 0:%ˊ ) (Cns: )     _ Cx: ( %^VOWSH: ( %^CLong: ) ) NeutrHighBack (X3: ) %^PAL: RBound ;
           Vow: (%ˊ: | 0:%ˊ ) (Cns: ) Cx: _     ( %^VOWSH: ( %^CLong: ) ) NeutrHighBack (X3: ) %^PAL: RBound ;
!with insertion of palatal before
 Vow: (%ˊ: | 0:%ˊ ) (Cns: )     _ Cx: ( %^VOWSH: ( %^CLong: ) ) NeutrHighBack (X3: ) %^PAL: RBound ;
           Vow: (%ˊ: | 0:%ˊ ) (Cns: ) Cx: _     ( %^VOWSH: ( %^CLong: ) ) NeutrHighBack (X3: ) %^PAL: RBound ;

                where Cx in (g k)
                      Cy in (ǧ ǩ)
                matched ;

"Even-syllabic nouns I, Depalatalization of ǧ and weakening of grade to ǥ"
!! __@RULENAME@__ - vueˊlǧǧed+V+Ind+Prs+Sg1: vuâlǥam
 ǧ:ǥ <=> l _ ǧ: ( %^VOWL: | %^VOWXL: ) ( %^FRICG: ) %^CShort: NeutrHighBack %^PALNo: ;
         l ǧ: _ ( %^VOWL: | %^VOWXL: )  %^FRICG:  %^CShort: NeutrHighBack %^PALNo: ;

"Even-syllabic nouns I,  ǧ and weakening of grade to j"
!! __@RULENAME@__ - vueˊlǧǧed+V+Ind+Prs+4: vueˊljet  âˊlǧǧ+N+Sg+Gen: ââˊlj
!! {{{
 ǧ:j <=> l _ ǧ: [ %^VOWL: | %^VOWXL: ] %^CShort: NeutrHighBack ( %^PAL: ) RBound ;
!! }}}

"Even-syllabic nouns I, Depalatalization of ǧǧ"
!! __@RULENAME@__ - vueˊlǧǧed+V+Ind+Prs+Sg3: vuâlgg
!! {{{
 ǧ:g <=> Vow: (%ˊ: | 0:%ˊ ) (Cns: ) _ (%ˈ: ) ǧ: ( %^VOWSH: ( %^CLong: ) ) NeutrHighBack %^PALNo: ;
         Vow: (%ˊ: | 0:%ˊ ) (Cns: ) ǧ: (%ˈ: ) _    ( %^VOWSH: ( %^CLong: ) ) NeutrHighBack (X3: ) %^PALNo: ;
!! }}}

"Even-syllabic nouns I, Depalatalization of ǧǧ and ǩǩ"
!! __@RULENAME@__ -
!! {{{
 ǩ:k <=> Vow: (%ˊ: | 0:%ˊ ) (Cns: )     _ (%ˈ: ) ǩ: PrecedePALNo %^PALNo:  ;
           Vow: (%ˊ: | 0:%ˊ ) (Cns: ) ǩ: (%ˈ: ) _     ( %^VOWSH: ( %^CLong: ) ) NeutrHighBack (X3: ) %^PALNo: RBound ;
         Vow:0 ( s | š ) _ ( %^VOWSH: )  %^RmVow: %>: a ;
!! }}}

"Even-syllabic nouns, removing palatalization in -est +Loc nouns"
!! __@RULENAME@__ - removing palatalization in +Sg+Ill,  pieˊss:peässa
!! {{{
   %ˊ:0 <=>  Vow: _ [ :Cns :Cns+ | Cns:+ | Cns:+ %ˈ: Cns:+ ] PrecedePALNo %^PALNo: RBound ;
!jurdded
       Vow: _ CNoV: Cns:+ PrecedePALNo %^PALNo:  ; 
       _ v:u ;
!         _ v:u ( %ˊ: | 0:%ˊ ) [ d d: | l l: ] [[ %^VOWL: | %^VOWXL: ] ( %^CShort: ) | [ %^VOWSH: ( %^CLong: ) ] ] NeutrHighBack ( %^PAL: | %^PALNo: )  ;
!! }}}


!! !!Consonant gradation rules

!! !Weakening Consonant Cluster

!! dealing with relative length changes
!! mõõnnâd : mõˊnne : mõõn 

!"Even syllabic verbs I, cg in relation to Vowel length"
! Vow Cx Cx:0 


"Even syllabic verbs I, cg"
!! __@RULENAME@__ - used with  +Imp+Sg2, +Ind+Prs+ConNeg, teevvad > teev
 Cx:0 <=> 2LongVow (%ˊ: | 0:%ˊ ) Cx _ ( %^VOWL: ) %^CShort: ;
          2LongVow (%ˊ: | 0:%ˊ ) Cx _ ( %^VOWXL: ) %^CShort: ; 
 		where Cx in (r v) ;

"Even syllabic verbs I, cg for ǧ"
!! __@RULENAME@__ - used with  vueˊlǧǧed+V+Ind+Prs+Sg1: vuâlǥam
 ǧ:0 <=> 2LongVow (%ˊ: | 0:%ˊ ) l ǧ: _ ( %^VOWL: | %^VOWXL: ) %^CShort: ;

"Even syllabic verbs I, cg for b"
!! __@RULENAME@__ - used with  neiˊbb+N+Sg+Gen: neeiˊb
!! {{{
 b:0 <=> 2LongVow  [ i ( %ˊ: | 0:%ˊ ) | ( %ˊ: | 0:%ˊ ) l ] b _ ( %^VOWL: | %^VOWXL: ) %^CShort: ;
!! }}}

"Even syllabic verbs I, cg m:0"
!! __@RULENAME@__ - used with  +Imp+Sg2, +Ind+Prs+ConNeg, 
 m:0 <=> 2LongVow  (%ˊ: | 0:%ˊ )  ( s | r | l | p | d: ) m:  _ ( %^VOWL: | %^VOWXL: ) %^CShort: ;
         2LongVow  (%ˊ: ) h: (%ˊ: | 0:%ˊ ) m: _ ( %^VOWL: | %^VOWXL: ) %^CShort:  ;
          
"Even syllabic nouns I, with extra lengthening of vowel ij>ii/uv>uu and dd>d"
!! __@RULENAME@__ - used with  +N+Sg+Nom > +N+Sg+Gen, sijdd > siid
!! {{{
 d:0 <=> [ ( 0:o ) u: ( %ˊ: ) v: | i: (%ˊ: ) j: ] ( 0:%ˊ ) d _ ( %^VOWXL: | %^VOWL: ) %^CShort:  ;
 ! juˊrdded > joordam
          2LongVow ( %ˊ: | 0:%ˊ ) CNoJV d _ ( %^VOWL: | %^VOWXL: ) %^CShort: ;
! njeiddad > njeeid
! 2012-06-12 _v:u_ allows for uˊvdded
!! - idd:id, +Imp+Sg2, +Ind+Prs+ConNeg double consonants following vowel and "i" at coda that become single consonants in gradation
        2SgVow (%ˊ: | 0:%ˊ ) d _ Vow: [ Cns | n j | l m | s ǩ: | s t | š ǩ: | š m ]  %^VOWSH:  %^RmVow: ;
!! }}}

"Even syllabic verbs I, cg n:0"
!! __@RULENAME@__ - used with  +Imp+Sg2, +Ind+Prs+ConNeg, jiõnn:jiõn
 n:0 <=> 2LongVow  (%ˊ: | 0:%ˊ )  ( s | r | l | p | d: ) n:  _ ( %^VOWL: | %^VOWXL: ) %^CShort: ;
         2LongVow  (%ˊ: ) h: (%ˊ: | 0:%ˊ ) n: _ ( %^VOWXL: ) %^CShort:  ;
          
"Even syllabic verbs I, cg for ʒ"
!! __@RULENAME@__ - used with  pääˊʒʒelm+N+Sg+Ill: päˊʒlma
!! {{{
 ʒ:0 <=>  2SgVow (%ˊ: | 0:%ˊ ) ʒ _ Vow: [ Cns | n j | l m | s ǩ: | s t | š ǩ: | š m ]  %^VOWSH:  %^RmVow: ;
!! }}}

"Even syllabic verbs I, cg for đ" 
!! __@RULENAME@__ - used with  ǩiđđ:ǩiiđ
 đ:0 <=> 2LongVow (%ˊ: | 0:%ˊ ) đ _ ( %^VOWL: | %^VOWXL: ) %^CShort: ;


"Even syllabic verbs I, cg for l"
!! __@RULENAME@__ - used with  vuˊvll+N+Sg+Gen: vuuˊl
!! {{{
 l:0 <=> 2LongVow  [( %ˊ: | 0:%ˊ ) | ( %ˊ: ) v:u  ( :%ˊ ) | u  ( :%ˊ )] l _ ( %^VOWL: | %^VOWXL: ) %^CShort: ;
!! }}}

"Even syllabic verbs I, cg for g"
!! __@RULENAME@__ - used with  cõõggâlm+N+Sg+Ill: cõglma
!! {{{
 g:0 <=>  2SgVow (%ˊ: | 0:%ˊ ) g _ Vow: [ Cns | n j | l m | s ǩ: | s t | š ǩ: | š m ]  %^VOWSH:  %^RmVow: ;
          2LongVow  (%ˊ: | 0:%ˊ )  [ r | l | d: ] g:  _ ( %^VOWL: | %^VOWXL: ) %^CShort: ;
!! }}}


! "Even syllabic verbs I, cg vv:v"
! v:0 <=> :Vow (%ˊ: | 0:%ˊ ) v _ ( %^VOWL: ) %^CShort: ; 

"Even syllabic nouns I, with extra lengthening of vowel V>VV and KK>ǤǤ"
!! __@RULENAME@__ 
!! used with  +N+Sg+Nom > +N+Sg+Gen for cases like
!! {{lookki > looǥǥi}}.
!! {{{
 k:ǥ <=> OnSetC 2LongVow (%ˊ: | 0:%ˊ ) k: _  ( %^VOWL: | %^VOWXL: )  %^CShort: ; !!= @CODE@
         OnSetC 2LongVow (%ˊ: | 0:%ˊ ) _ k:  ( %^VOWL: | %^VOWXL: )  %^CShort: ; !!= @CODE@
!! }}}




"Even syllabic verbs I, Voicing š:ž" 
!! __@RULENAME@__ - ss:zz, +Imp+Sg2, +Ind+Prs+ConNeg, double consonants at coda become voiced in gradation
 š:ž <=> 2LongVow (%ˊ: | 0:%ˊ ) š: _ ( %^VOWL: | %^VOWXL: ) %^CShort: ;
         2LongVow (%ˊ: | 0:%ˊ ) _ š: ( %^VOWL: | %^VOWXL: ) %^CShort: ;
         _ %^VC: ;

"Even syllabic verbs I, Voicing c:ʒ"
!! __@RULENAME@__ - ss:zz, +Imp+Sg2, +Ind+Prs+ConNeg, double consonants at coda become voiced in gradation
!! {{{
 c:ʒ <=> 2LongVow (%ˊ: | 0:%ˊ ) c: _ ( %^VOWL: | %^VOWXL: ) %^CShort: ;
         2LongVow (%ˊ: | 0:%ˊ ) _ c: ( %^VOWL: | %^VOWXL: ) %^CShort: ; 
         _ %^VC: ;
!! }}}

"Even syllabic verbs I, Voicing č:j"
!! __@RULENAME@__ - ss:zz, +Imp+Sg2, +Ind+Prs+ConNeg, double consonants at coda become voiced in gradation
 č:j <=> OnSetC  2LongVow  (%ˊ: | 0:%ˊ ) č: _ ( %^VOWL: | %^VOWXL: ) %^CShort: ; !!= @CODE@
         OnSetC 2LongVow  (%ˊ: | 0:%ˊ ) _ č: ( %^VOWL: | %^VOWXL: ) %^CShort:  ; !!= @CODE@
         _ %^VC: ;

"Even syllabic verbs I, Voicing after long vowel or diphthong s:z s:z" 
!! __@RULENAME@__ - ss:zz, +Imp+Sg2, +Ind+Prs+ConNeg, double consonants at coda become voiced in gradation
 s:z <=> OnSetC 2LongVow  (%ˊ: | 0:%ˊ ) s: _ ( %^VOWL: | %^VOWXL: ) %^CShort: %^VC: ; !!= @CODE@
         OnSetC 2LongVow  (%ˊ: | 0:%ˊ ) _ s: ( %^VOWL: | %^VOWXL: ) %^CShort: %^VC: ; !!= @CODE@
         _ %^VC: ;


"Even syllabic verbs I, third consonant loss" 
!! __@RULENAME@__ - lgg:lǥ, Vow Ca Cx Cx:0, +Imp+Sg2, +Ind+Prs+ConNeg, double consonants following consonant at coda that become single consonants in gradation
!! {{{
 Cx:0 <=> 2LongVow  (%ˊ: | 0:%ˊ )  [ s | r | l | p | d: ] Cx:  _ ( %^VOWL: | %^VOWXL: ) %^CShort: ;
          2LongVow  (%{%ˊ%}: ) h: (%ˊ: | 0:%ˊ ) Cx: _ ( %^VOWL: | %^VOWXL: ) %^CShort: ;
                where Cx in (ŋ č c s) ;
!! }}}

"Even syllabic verbs I, t>đ" 
!! __@RULENAME@__ - tt:đ
 t:đ  <=> 2LongVow (%ˊ: | 0:%ˊ ) _ t:0 ( %^VOWL: | %^VOWXL: ) %^CShort: ;

"Even syllabic nouns I, p>v" 
!! __@RULENAME@__ - pp:v
 p:v  <=> 2LongVow (%ˊ: | 0:%ˊ ) _ p:0 ( %^VOWL: | %^VOWXL: ) %^CShort: ;

"Even syllabic verbs I, second consonant loss t:0"
!! __@RULENAME@__ -
!! {{{
 t:0 <=> 2LongVow (%ˊ: | 0:%ˊ ) t: _ ( %^VOWL: | %^VOWXL: ) %^CShort: ;
         2LongVow  (%ˊ: | 0:%ˊ )  [ s | r | l ] t:  _ ( %^VOWL: | %^VOWXL: ) %^CShort: ;
          2LongVow  (%{%ˊ%}: ) h: (%ˊ: | 0:%ˊ ) t: _ ( %^VOWL: | %^VOWXL: ) %^CShort: ;
!! }}}

"Even syllabic verbs I, second consonant loss p:0"
!! __@RULENAME@__ -
!! {{{
 p:0 <=> 2LongVow (%ˊ: | 0:%ˊ ) ( Cns: ) p: _ ( %^VOWL: | %^VOWXL: ) %^CShort:  ;
          2SgVow (%ˊ: | 0:%ˊ ) p _ Vow: [ Cns | n j | l m | s ǩ: | s t | š ǩ: | š m ]  %^VOWSH:  %^RmVow: ;
!! }}}

"Even syllabic verbs I, second consonant loss k:0"
!! __@RULENAME@__ -
!! {{{
 k:0 <=> 2LongVow  (%ˊ: | 0:%ˊ )  [ s | r | l | p ] k:  _ ( %^VOWL: | %^VOWXL: ) %^CShort: ;
!          2LongVow  (%{%ˊ%}: ) h: (%ˊ: | 0:%ˊ ) k: _ ( %^VOWL: | %^VOWXL: ) %^CShort: ;
!! }}}

"Even syllabic verbs I, second consonant loss ǩ:0"
!! __@RULENAME@__ -
!! {{{
 ǩ:0 <=> 2LongVow (%ˊ: | 0:%ˊ ) ǩ: _ ( %^VOWL: | %^VOWXL: ) %^CShort: ;
         2LongVow  (%ˊ: | 0:%ˊ )  [ s | r | l | p ] ǩ:  _ ( %^VOWL: | %^VOWXL: ) %^CShort: ;
!          2LongVow  (%{%ˊ%}: ) h: (%ˊ: | 0:%ˊ ) ǩ: _ ( %^VOWL: | %^VOWXL: ) %^CShort: ;
!! }}}


"Even syllabic verbs I, lgg>lǥ" 
!! __@RULENAME@__ - vueˊlǧǧed+V+Ind+Prs+Sg2 => vuâlǥak
!! {{{
 g:ǥ  <=> 2LongVow (%ˊ:0 | %{%ˊ%}:0 ) [ l | r | d: ]  _ g: ( %^VOWL: | %^VOWXL: )  %^FRICG:0  %^CShort: ;
          2LongVow (%ˊ:0 | %{%ˊ%}:0 ) [ l | r | d: ]  g: _ ( %^VOWL: | %^VOWXL: ) %^FRICG:0 %^CShort: ;
!! }}}

!vueˊlǧǧed+V+Imp+Pl3: vuâlǥǥaz
!"Even syllabic verbs I, lgg>lǥǥ" ! lgg:lǥǥ

"Odd syllabic nouns I, lǥ>lg"
!! __@RULENAME@__ - ǩeâlǥal+N+Sg+Gen: ǩeâlggal
!! {{{
ǥ:g <=> 2ShortVow  ( %ˊ: )  l _ 0: Vow: Cns ( %^VOWSH: | %^VOWXSH: ) %^CLong: ;
!! }}}

"Even syllabic verbs I, 'lǧǧ>'lj" 
!! __@RULENAME@__ - vueˊlǧǧed+V+Ind+Prt+Pl1 => vuõˊljim
!! {{{
 g:j  <=> 2LongVow   :%ˊ [ l | r | d: ]  _ g:0 ( %^VOWL: | %^VOWXL: ) %^CShort: NeutrHighBack (X3: ) %^PAL: ;
!! }}}








!! !!Rules for cleaning up and composing end result

!! * Strengthening Consonant Cluster
!! * V shortening and C lengthening 
!! * %^CLong is generally used in conjunction with a preceding %^VOWSH
!! * In verbs this will be observed in finite forms
!! * In derivational morphology, e.g. -škuetted, this is not the case



"Orthographic Consonant lengthening in INDPRSSG3 and IMPSG2 0:m"
!! __@RULENAME@__ - kåˊmrded+V+Imp+Sg2: kååˊmmerd 
!! {{{
 0:m <=> 2LongVow ( %ˊ: | 0:%ˊ )  m _ [ %{E1%}: ] Cns:+ [ %^IMPSG2: | %^INDPRSSG3: ] ;
!! }}}

"Orthographic Consonant lengthening in INDPRSSG3 and IMPSG2 0:n"
!! __@RULENAME@__ - mään > mannu
!! {{{
 0:n <=> OnSetC  2ShortVow ( %ˊ: | 0:%ˊ ) n _ ( %^VOWSH: | %^VOWXSH: ) %^CLong: ;
!! }}}

"Consonant lengthening 0:đ"
!! __@RULENAME@__ - čââˊđ+N+Sg+Gen: čâđđam
!! {{{
 0:đ <=>  2ShortVow  ( %ˊ: )  đ _  ( %^VOWSH: | %^VOWXSH: ) %^CLong: ;
!! }}}

"Consonant lengthening 0:g"
!! __@RULENAME@__ - ǩeâlǥal+N+Sg+Gen: ǩeâlggal
!! {{{
 0:g <=>  2ShortVow  ( %ˊ: )  l ǥ: _ Vow: Cns ( %^VOWSH: | %^VOWXSH: ) %^CLong: ;
!! }}}


!! !!Vowel change rules

!! !Illative

