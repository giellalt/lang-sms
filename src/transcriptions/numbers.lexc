! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or divvun@samediggi.no


!! !!!Skolt Saami number <-> letter transducer

LEXICON Root
< [a|b|c|d|e|f|g|h|i|j|k|l|m|n|o|p|q|r|s|t|u|v|w|q|y|z|æ|ø|å|ä|ö|á|č|đ|ŋ|A|B|C|D|E|F|G|H|I|J|K|L|M|N|O|P|Q|R|S|T|U|V|W|X|Y|Z|Æ|Ø|Å|Ä|Ö|Á|Č|Đ|Ŋ|Š|Ŧ|Ž]* > COMMA ; ! This first line is to allow all letter strings.
NUMBERSECTION ;
COMMASECTION ;

LEXICON NUMBERSECTION
HUNDREDSM ;		     ! 200M
čueˊđ:1 HUNDREDM ; ! 100M
TENSM ;		         ! 20-99M
TEENSM ;	         ! 10-19M
1MILJON ;            ! 1M
ONEM ;		         ! 1M
ONESM ;		         ! 2-9M
1GIGA ;            	 ! 1G
ONESG ;		         ! 1-9G
HUNDREDST ;	         ! 200000-999999
čueˊđ:1 HUNDREDT ; ! 100000-100999
TENST ;			     ! 20000-99999,10000-10999
TEENST ;		     ! 11000-19999
ONEST ;			     ! 2000-9999
dohat:1 THOUSAND ; ! 1000-1999

UNDERTHOUSAND ;		 ! 100-999
TENS ;			     ! 20-99
TEENS ;			     ! 10-19
INITIALCARDINAL ;			     ! 1-9
noll:%0 COMMA ;      ! 0

LEXICON ONESG
õhtt% :   1GIGA ;

kueˊhtt:2    GIGA ;
koumm:3  GIGA ;
nellj:4 GIGA ;

vitt:5  GIGA ;
kutt:6  GIGA ;
čiččâm:7  GIGA ;


kääuˊc:8  GIGA ;
ååuˊc:9   GIGA ; 

LEXICON 1GIGA
miljard% :1 OVERMILLIONS ;

LEXICON GIGA
% miljard% : OVERMILLIONS ;

LEXICON HUNDREDSM

kueˊhtt:2    CUODIM ;
koumm:3  CUODIM ;
nellj:4 CUODIM ;

vitt:5  CUODIM ;
kutt:6  CUODIM ;
čiččâm:7  CUODIM ;


kääuˊc:8  CUODIM ;
ååuˊc:9   CUODIM ;

LEXICON CUODIM
čueˊđ: HUNDREDM ; !200, not 100

LEXICON HUNDREDM
      TENSM ;
      TEENSM ;
:%0   ONESM ;
:%0%0 MILJON ;

LEXICON TEENSM
:1 TEENM ;

LEXICON TEENM
õt:1     LOHKAIM ;
kuât:2     LOHKAIM ;
kon:3   LOHKAIM ;
nenj:4  LOHKAIM ;
vitt:5   LOHKAIM ;
kutt:6   LOHKAIM ;
čiččâm:7   LOHKAIM ;
käc:8   LOHKAIM ;
åc:9    LOHKAIM ;

LEXICON LOHKAIM
mlååi: MILJON ;

LEXICON TENSM
lååi:1%0 MILJON ;

kueˊhtt:2    LÅGEVM ;
koumm:3  LÅGEVM ;
nellj:4 LÅGEVM ;

vitt:5  LÅGEVM ;
kutt:6  LÅGEVM ;
čiččâm:7  LÅGEVM ;


kääuˊc:8  LÅGEVM ;
ååuˊc:9   LÅGEVM ;

LEXICON LÅGEVM
lååi:%0 MILJON ;
lo:     ONESM ;
lo%-õhtt% :   1MILJON ;

LEXICON ONEM
õhtt% : 1MILJON ;

LEXICON ONESM
kueˊhtt:2    MILJON ;
koumm:3  MILJON ;
nellj:4 MILJON ;
vitt:5  MILJON ;
kutt:6  MILJON ;
čiččâm:7  MILJON ;
kääuˊc:8  MILJON ;
ååuˊc:9   MILJON ; 

LEXICON 1MILJON
miljon% :1 OVERTHOUSANDS ;
miljon% :1  OVERTHOUSANDS ;

LEXICON MILJON
% miljon% : OVERTHOUSANDS ;

! =================
! Under the million
! =================

LEXICON OVERMILLIONS
                    HUNDREDSM ;
čueˊđ:1           HUNDREDM ;
:%0                 TENSM ;
:%0                 TEENSM ;
:%0%0               1MILJON ;
:%0%0               ONESM ;
:%0%0%0             HUNDREDST ;		        ! x00.000.000
čueˊđ:%0%0%01     HUNDREDT ;    ! 100.000.000
:%0%0%0%0           TENST ;		        !  20.000.000
:%0%0%0%0           TEENST ;		    !  10.000.000
:%0%0%0%0%0         ONEST ;		    !   2.000.000
dohat:%0%0%0%0%01  THOUSAND ;	!   1.000.000
:%0%0%0%0%0%0       UNDERTHOUSAND ;	
:%0%0%0%0%0%0%0     TENS ;
:%0%0%0%0%0%0%0     TEENS ;
:%0%0%0%0%0%0%0%0   ONES ;
:%0%0%0%0%0%0%0%0%0 COMMA ;	     !  x000000

LEXICON OVERTHOUSANDS
HUNDREDST ;		        ! x00.000.000
čueˊđ:1 HUNDREDT ;    ! 100.000.000
:%0 TENST ;		        !  20.000.000
:%0 TEENST ;		    !  10.000.000
:%0%0 ONEST ;		    !   2.000.000
dohat:%0%01 THOUSAND ;	!   1.000.000
:%0%0%0 UNDERTHOUSAND ;	
:%0%0%0%0 TENS ;
:%0%0%0%0 TEENS ;
:%0%0%0%0%0 ONES ;
:%0%0%0%0%0%0 COMMA ;	     !  x000000

LEXICON HUNDREDST

kueˊhtt:2    CUODIT ;
koumm:3  CUODIT ;	! 300000-399999
nellj:4 CUODIT ;	! 400000-499999

vitt:5  CUODIT ;	! 500000-599999
kutt:6  CUODIT ;	! 600000-699999
čiččâm:7  CUODIT ;


kääuˊc:8  CUODIT ;	! 800000-899999
ååuˊc:9   CUODIT ;	! 900000-999999

LEXICON CUODIT
čueˊđ: HUNDREDT ;	! 200000-199999

LEXICON HUNDREDT	     ! X = 1-9, Y = 0-9
TENST ;			     ! X2XYYY, X10YYY
TEENST ;		     ! X1XYYY
õhtt:%01 THOUSANDS ;
:%0 ONEST ;		     ! XX0YYY
:%0%0 THOUSANDS ;	 ! X00YYY

LEXICON TEENST
:1 TEENT ;

LEXICON TEENT
lååi:%0 THOUSANDS ;

õt:1 LOHKAIT ;
kuât:2 LOHKAIT ;
kon:3 LOHKAIT ;
nenj:4 LOHKAIT ;
vitt:5 LOHKAIT ;
kutt:6 LOHKAIT ;
čiččâm:7 LOHKAIT ;
käc:8 LOHKAIT ;
åc:9 LOHKAIT ;

LEXICON LOHKAIT
mlo: THOUSANDS ;

LEXICON TENST

kuâhtt:2 LÅGEVT ;
koumm:3 LÅGEVT ;
nellj:4 LÅGEVT ;
vitt:5 LÅGEVT ;
kutt:6 LÅGEVT ;
čim:7 LÅGEVT ;
käˊhcc:8 LÅGEVT ;
åˊhcc:9 LÅGEVT ;

LEXICON LÅGEVT
lo:%0 THOUSANDS ; !XXX lååi?
lo%-õhttdohat:1 THOUSAND ; !XXX lååi?
lo: ONEST ;

LEXICON ONEST

kueˊhtt:2 THOUSANDS ;
koumm:3 THOUSANDS ;
nellj:4 THOUSANDS ;
vitt:5 THOUSANDS ;
kutt:6 THOUSANDS ;
čiččâm:7 THOUSANDS ;
kääuˊc:8 THOUSANDS ;
ååuˊc:9 THOUSANDS ;

LEXICON THOUSANDS      ! x > 1
dohat: THOUSAND ;


LEXICON THOUSAND
UNDERTHOUSAND ;	   ! 1100-1999
:%0 THOUSANDTENS ;	   ! 1020-1099
:%0 TEENS ;	   ! 1010-1019
:%0%0 INITIALCARDINAL ;	   ! 1001-1009
:%0%0%0 COMMA ;	   ! 1000


LEXICON THOUSANDTENS
lååi:1%0 COMMA ; 
kuâhtt:2 LÅGEV ;
koumm:3 LÅGEV ;
nellj:4 LÅGEV ;
vitt:5 LÅGEV ;
kutt:6 LÅGEV ;
čim:7 LÅGEV ; ! this one different from TENS
käˊhcc:8 LÅGEV ;
åˊhcc:9 LÅGEV ;


!===========================
!Here starts the 999 numbers
!===========================

LEXICON UNDERTHOUSAND
HUNDREDS ;
čueˊtt:1 HUNDRED ;
tjuodát:1 CUODAAT ;

LEXICON HUNDREDS
kueˊhtt:2 CUODI ;
koumm:3 CUODI ;
nellj:4 CUODI ;

vitt:5 CUODI ;
kutt:6 CUODI ;
čiččâm:7 CUODI ;


kääuˊc%-:8 CUODI ;
ååuˊc%-:9 CUODI ;

LEXICON CUODI
čueˊđ: HUNDRED ;

tjuodát: CUODAAT ;

LEXICON HUNDRED
HUNDREDTENS ;
TEENS ;
:%0 INITIALCARDINAL ;
:%0%0 COMMA ;

LEXICON HUNDREDTENS
lååi:1%0 COMMA ; 
kuâhtt:2 LÅGEV ;
koumm:3 LÅGEV ;
nellj:4 LÅGEV ;
vitt:5 LÅGEV ;
kutt:6 LÅGEV ;
čim:7 LÅGEV ; ! this one different from TENS
käˊhcc:8 LÅGEV ;
åˊhcc:9 LÅGEV ;



LEXICON CUODAAT
:%0%0%.    COMMA ;


LEXICON TEENS
:1 TEEN ;
!mlo:1 TEEN2 ; ! Not for sms

LEXICON TEEN
õt:1 LOHKAI ;
kuât:2 LOHKAI ;
kon:3 LOHKAI ;
nenj:4 LOHKAI ;
vitt:5 LOHKAI ;
kutt:6 LOHKAI ;
čin:7 LOHKAI ;
käc:8 LOHKAI ;
åc:9 LOHKAI ;

LEXICON LOHKAI
mlo: COMMA ;
:%.      LOGAAT ;

LEXICON LOGAAT
lågåt: COMMA ;


LEXICON TENS
lååi:1%0 COMMA ; 
kuâhtt:2 LÅGEV ;
koumm:3 LÅGEV ;
nellj:4 LÅGEV ;
vitt:5 LÅGEV ;
kutt:6 LÅGEV ;
čičm:7 LÅGEV ;
käˊhcc:8 LÅGEV ;
åˊhcc:9 LÅGEV ;

LEXICON LÅGEV
!lååi:%0 COMMA ;
lo:%0 COMMA ;
lo: NONINITIALCARDINAL ;


LEXICON ONES
CARDINAL ;

LEXICON INITIALCARDINAL
õhtt:1    COMMA ;
CARDINAL ;
!ORDINAL ;

LEXICON NONINITIALCARDINAL
%-õhtt:1    COMMA ;
CARDINAL ;
!ORDINAL ;

LEXICON CARDINAL

kueˊhtt:2    COMMA ;
koumm:3  COMMA ;
nellj:4 COMMA ;

vitt:5  COMMA ;
kutt:6  COMMA ;
čiččâm:7  COMMA ;


kääuˊc:8  COMMA ;
ååuˊc:9   COMMA ;

LEXICON ORDINAL ! Ordinal is still sme, and not done for sms.
vuosttasj:1%.     COMMA ;
nuppát:2%.   COMMA ;
goalmát:3%.   COMMA ;
nælját:4%. COMMA ;
viđát:5%.   COMMA ;
gudát:6%.   COMMA ;
giehttjit:7%.    COMMA ;
gávtsát:8%.   COMMA ;
avtsát:9%.    COMMA ;

LEXICON COMMA
 ENDLEX ;
! COMMASECTION ; ! debugging, therefore removed.

LEXICON COMMASECTION  ! these are not spellchecked!!!
                  ENDLEX ;
! % komma% :,      Root ;
! % tjuohkkis% :%. Root ;
! % kolon% :%:     Root ;
! % sárggis% :%-   Root ; 
 % la% :%=         Root ; 
 % gráda% :%°      Root ; 
 % paragráfa% :§   Root ; 
! % násti% :%*     Root ; 
 % ja% :&          Root ; 

LEXICON ENDLEX
 # ;

