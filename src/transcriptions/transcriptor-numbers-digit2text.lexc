! Divvun & Giellatekno - open source grammars for Sámi and other languages
! Copyright © 2000-2010 The University of Tromsø & the Norwegian Sámi Parliament
! http://giellatekno.uit.no & http://divvun.no
!
! This program is free software; you can redistribute and/or modify
! this file under the terms of the GNU General Public License as published by
! the Free Software Foundation, either version 3 of the License, or
! (at your option) any later version. The GNU General Public License
! is found at http://www.gnu.org/licenses/gpl.html. It is
! also available in the file $GTHOME/LICENSE.txt.
!
! Other licensing options are available upon request, please contact
! giellatekno@hum.uit.no or divvun@samediggi.no


!! !!!Skolt Saami number <-> letter transducer

Multichar_Symbols

  +Use/NG    ! Do not generate, for isme-ped.fst and apertium
  +String    ! Tag to denote non-numeric strings
  +NumNum    ! Tag to denote real numbers in one form or another

LEXICON Root
< %+String [a|b|c|d|e|f|g|h|i|j|k|l|m|n|o|p|q|r|s|t|u|v|w|q|y|z|æ|ø|å|ä|ö|á|č|đ|ŋ|A|B|C|D|E|F|G|H|I|J|K|L|M|N|O|P|Q|R|S|T|U|V|W|X|Y|Z|Æ|Ø|Å|Ä|Ö|Á|Č|Đ|Ŋ|Š|Ŧ|Ž]* > COMMA ; ! This first line is to allow all letter strings.
NUMBERSECTION ;
COMMASECTION ;

LEXICON NUMBERSECTION
        HUNDREDSM       ; ! 200M
1:čueʹđ HUNDREDM        ; ! 100M
       TENSM           ; ! 20-99M
       TEENSM          ; ! 10-19M
       1MILJON         ; ! 1M
       ONEM            ; ! 1M
       ONESM           ; ! 2-9M
       1GIGA           ; ! 1G
       ONESG           ; ! 1-9G
       HUNDREDST       ; ! 200000-999999
1:čueʹđ HUNDREDT        ; ! 100000-100999
       TENST           ; ! 20000-99999,10000-10999
       TEENST          ; ! 11000-19999
       ONEST           ; ! 2000-9999
1:dohat THOUSAND        ; ! 1000-1999
1%0%0%0%.:dohttad  COMMA        ; ! 1000.
       UNDERTHOUSAND   ; ! 100-999
       TENS            ; ! 20-99
       TEENS           ; ! 10-19
       INITIALCARDINAL ; ! 1-9
%0:noll COMMA           ; ! 0

LEXICON ONESG
:õhtt%   1GIGA ;
2:kueʹhtt GIGA  ;
3:koumm GIGA  ;
4:nellj GIGA  ;
5:vitt GIGA  ;
6:kutt GIGA  ;
7:čiččâm GIGA  ;
8:kääuʹc GIGA  ;
9:ååuʹc GIGA  ; 

LEXICON 1GIGA
1:miljard%  OVERMILLIONS ;

LEXICON GIGA
:% miljard%  OVERMILLIONS ;

LEXICON HUNDREDSM
2:kueʹhtt CUODIM ;
3:koumm CUODIM ;
4:nellj CUODIM ;
5:vitt CUODIM ;
6:kutt CUODIM ;
7:čiččâm CUODIM ;
8:kääuʹc CUODIM ;
9:ååuʹc CUODIM ;

LEXICON CUODIM
:čueʹđ HUNDREDM ; !200, not 100

LEXICON HUNDREDM
  TENSM ;
  TEENSM ;
%0:   ONESM ;
%0%0: MILJON ;

LEXICON TEENSM
1: TEENM ;

LEXICON TEENM
1:õt LOHKAIM ;
2:kuât LOHKAIM ;
3:kon LOHKAIM ;
4:nenj LOHKAIM ;
5:vitt LOHKAIM ;
6:kutt LOHKAIM ;
7:čiččâm LOHKAIM ;
8:käc LOHKAIM ;
9:åc LOHKAIM ;

LEXICON LOHKAIM
:mlååi MILJON ;

LEXICON TENSM
1%0:lååi MILJON ;
2:kueʹhtt   LÅGEVM ;
3:koumm   LÅGEVM ;
4:nellj   LÅGEVM ;
5:vitt   LÅGEVM ;
6:kutt   LÅGEVM ;
7:čiččâm   LÅGEVM ;
8:kääuʹc   LÅGEVM ;
9:ååuʹc   LÅGEVM ;

LEXICON LÅGEVM
%0:lååi MILJON  ;
:lo   ONESM   ;
:lo%-õhtt%    1MILJON ;

LEXICON ONEM
:õhtt%  1MILJON ;

LEXICON ONESM
2:kueʹhtt MILJON ;
3:koumm MILJON ;
4:nellj MILJON ;
5:vitt MILJON ;
6:kutt MILJON ;
7:čiččâm MILJON ;
8:kääuʹc MILJON ;
9:ååuʹc MILJON ; 

LEXICON 1MILJON
1:miljon%  OVERTHOUSANDS ;
1%0%0%0%0%0%0%.:miljoonad  COMMA ;

LEXICON MILJON
:% miljon%  OVERTHOUSANDS ;
%0%0%0%0%0%0%.:% miljoonad  COMMA ;

! =================
! Under the million
! =================

LEXICON OVERMILLIONS
                     HUNDREDSM ;
1:čueʹđ                  HUNDREDM  ;
%0:                 TENSM     ;
%0:                 TEENSM    ;
%0%0:               1MILJON   ;
%0%0:               ONESM ;
%0%0%0:             HUNDREDST ;		        ! x00.000.000
%0%0%01:čueʹđ            HUNDREDT ;    ! 100.000.000
%0%0%0%0:           TENST ;		        !  20.000.000
%0%0%0%0:           TEENST ;		    !  10.000.000
%0%0%0%0%0:         ONEST ;		    !   2.000.000
%0%0%0%0%01:dohat        THOUSAND ;	!   1.000.000
%0%0%0%0%0%0:       UNDERTHOUSAND ;	
%0%0%0%0%0%0%0:     TENS ;
%0%0%0%0%0%0%0:     TEENS ;
%0%0%0%0%0%0%0%0:   ONES ;
%0%0%0%0%0%0%0%0%0: COMMA ;	     !  x000000

LEXICON OVERTHOUSANDS
               HUNDREDST     ; ! x00.000.000
1:čueʹđ            HUNDREDT      ; ! 100.000.000
%0:           TENST         ; !  20.000.000
%0:           TEENST        ; !  10.000.000
%0%0:         ONEST         ; !   2.000.000
%0%01:dohat        THOUSAND      ; !   1.000.000
%0%0%0:       UNDERTHOUSAND ;
%0%0%0%0:     TENS          ;
%0%0%0%0:     TEENS         ;
%0%0%0%0%0:   ONES          ;
%0%0%0%0%0%0: COMMA         ; !  x000000

LEXICON HUNDREDST
2:kueʹhtt CUODIT ;
3:koumm CUODIT ; ! 300000-399999
4:nellj CUODIT ; ! 400000-499999
5:vitt CUODIT ; ! 500000-599999
6:kutt CUODIT ; ! 600000-699999
7:čiččâm CUODIT ;
8:kääuʹc CUODIT ; ! 800000-899999
9:ååuʹc CUODIT ; ! 900000-999999

LEXICON CUODIT
:čueʹđ HUNDREDT ;	! 200000-199999

LEXICON HUNDREDT      ! X = 1-9, Y = 0-9
     TENST     ; ! X2XYYY, X10YYY
     TEENST    ; ! X1XYYY
%01:õhtt  THOUSANDS ;
%0:   ONEST     ; ! XX0YYY
%0%0: THOUSANDS ; ! X00YYY

LEXICON TEENST
1: TEENT ;

LEXICON TEENT
%0:lååi THOUSANDS ;
1:õt   LOHKAIT ;
2:kuât   LOHKAIT ;
3:kon   LOHKAIT ;
4:nenj   LOHKAIT ;
5:vitt   LOHKAIT ;
6:kutt   LOHKAIT ;
7:čiččâm   LOHKAIT ;
8:käc   LOHKAIT ;
9:åc   LOHKAIT ;

LEXICON LOHKAIT
:mlo THOUSANDS ;

LEXICON TENST
2:kuâhtt LÅGEVT ;
3:koumm LÅGEVT ;
4:nellj LÅGEVT ;
5:vitt LÅGEVT ;
6:kutt LÅGEVT ;
7:čim LÅGEVT ;
8:käʹhcc LÅGEVT ;
9:åʹhcc LÅGEVT ;

LEXICON LÅGEVT
%0:lo THOUSANDS ; !XXX lååi?
1:lo%-õhttdohat  THOUSAND  ; !XXX lååi?
:lo   ONEST     ;

LEXICON ONEST
2:kueʹhtt THOUSANDS ;
3:koumm THOUSANDS ;
4:nellj THOUSANDS ;
5:vitt THOUSANDS ;
6:kutt THOUSANDS ;
7:čiččâm THOUSANDS ;
8:kääuʹc THOUSANDS ;
9:ååuʹc THOUSANDS ;

LEXICON THOUSANDS      ! x > 1
:dohat THOUSAND ;
:doohhat+Use/NG THOUSAND ;
%0%0%0%.:dohttad COMMA ;


LEXICON THOUSAND
:%    UNDERTHOUSAND ;   ! 1100-1999
%0:%     THOUSANDTENS ;    ! 1020-1099
%0:%     TEENS ;           ! 1010-1019
%0%0:%   INITIALCARDINAL ; ! 1001-1009
%0%0%0: COMMA ;           ! 1000


LEXICON THOUSANDTENS
1%0:lååi COMMA ; 
2:kuâhtt   LÅGEV ;
3:koumm   LÅGEV ;
4:nellj   LÅGEV ;
5:vitt   LÅGEV ;
6:kutt   LÅGEV ;
7:čim   LÅGEV ; ! this one different from TENS
8:käʹhcc   LÅGEV ;
9:åʹhcc   LÅGEV ;


!===========================
!Here starts the 999 numbers
!===========================

LEXICON UNDERTHOUSAND
HUNDREDS ;
1:čueʹtt HUNDRED ;
1:čuâđad CUODAAT ;

LEXICON HUNDREDS
2:kueʹhtt CUODI ;
3:koumm CUODI ;
4:nellj CUODI ;
5:vitt CUODI ;
6:kutt CUODI ;
7:čiččâm CUODI ;
8:kääuʹc%- CUODI ;
9:ååuʹc%- CUODI ;

LEXICON CUODI
:čueʹđ HUNDRED ;
:čuâđad CUODAAT ;

LEXICON HUNDRED
 HUNDREDTENS     ;
 TEENS           ;
%0:   INITIALCARDINAL ;
%0%0: COMMA           ;

LEXICON HUNDREDTENS
1%0:lååi COMMA ; 
2:kuâhtt LÅGEV ;
3:koumm LÅGEV ;
4:nellj LÅGEV ;
5:vitt LÅGEV ;
6:kutt LÅGEV ;
7:čim LÅGEV ; ! this one different from TENS
8:käʹhcc LÅGEV ;
9:åʹhcc LÅGEV ;



LEXICON CUODAAT
%0%0%.:    COMMA ;


LEXICON TEENS
1: TEEN ;
!1:mlo TEEN2 ; ! Not for sms

LEXICON TEEN
1:õt LOHKAI ;
1:õhttâ+Use/NG LOHKAI ;
2:kuât LOHKAI ;
2:kuâhtt+Use/NG LOHKAI ;
3:kon LOHKAI ;
4:nenj LOHKAI ;
4:nellj+Use/NG LOHKAI ;
5:vit+Use/NG LOHKAI ;
5:vitt LOHKAI ;
6:kut+Use/NG LOHKAI ;
6:kutt LOHKAI ;
7:čin LOHKAI ;
8:käc LOHKAI ;
8:kähcc+Use/NG LOHKAI ;
9:åc LOHKAI ;
9:åhcc+Use/NG LOHKAI ;

LEXICON LOHKAI
:mlo   COMMA  ;
%.:m LOGAAT ;

LEXICON LOGAAT
:lååǥǥad COMMA ;


LEXICON TENS
1%0:lååi COMMA ; 
1%0%.:lååǥǥad COMMA ; 
2:kuâhtt   LÅGEV ;
3:koumm   LÅGEV ;
4:nellj   LÅGEV ;
5:vitt   LÅGEV ;
6:kutt   LÅGEV ;
7:čičm   LÅGEV ;
8:käʹhcc   LÅGEV ;
9:åʹhcc   LÅGEV ;

LEXICON LÅGEV
!%0:lååi COMMA ;
%0:lo COMMA ;
:lo   NONINITIALCARDINAL ;
%0%.:lååǥǥad COMMA ;

LEXICON ONES
CARDINAL ;

LEXICON INITIALCARDINAL
1:õhtt COMMA    ;
   CARDINAL ;
   ORDINAL  ;

LEXICON NONINITIALCARDINAL
1:%-õhtt COMMA    ;
    CARDINAL ;
    ORDINAL  ;

LEXICON CARDINAL

2:kueʹhtt COMMA ;
3:koumm COMMA ;
4:nellj COMMA ;
5:vitt COMMA ;
6:kutt COMMA ;
7:čiččâm COMMA ;
8:kääuʹc COMMA ;
9:ååuʹc COMMA ;

LEXICON ORDINAL ! Ordinals are now sms (from PS/JM dict), please check 11. etc.
1%.:vuõssmõs COMMA ;
1%.:vuõss+Use/NG COMMA ;
2%.:nuʹbb COMMA ;
3%.:kuälmad COMMA ;
4%.:neelljad COMMA ;
5%.:viiđad COMMA ;
6%.:kuuđad COMMA ;
7%.:čiiččad  COMMA ;
8%.:kääucad  COMMA ;
9%.:ååucad COMMA ;

LEXICON COMMA
 ENDLEX ;
! COMMASECTION ; ! debugging, therefore removed.

LEXICON COMMASECTION ! these are not spellchecked!!!
             ENDLEX ;
! % komma% :,      Root ;
! % tjuohkkis% :%. Root ;
! % kolon% :%:     Root ;
! % sárggis% :%-   Root ; 
% la% :%=         Root ; 
% gráda% :%°      Root ; 
% paragráfa% :§   Root ; 
! % násti% :%*     Root ; 
% ja% :&          Root ; 

LEXICON ENDLEX
 # ;
