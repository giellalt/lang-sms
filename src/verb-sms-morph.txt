!!!!!!!!!!!
!! Verbs !!
!!!!!!!!!!!

! Even-syllable stems
! -------------------

! The verb _viǯǯâd_ arrive in form _viǯǯ_
LEXICON VIQQAD
+V+Inf:%>âd # ;
+V+Ind+Prs: VS-I ;
+V+Ind+Prt: VS-I-Prt ;
+V+Imp: VS-I-Imp ;
+V:%^VOWL VSUF-I-POTKOND ; !WG
+V+ActEss:%>men FINAL1 ;
+V+Action:%>âm # ;
+V+PrfPrc:%>âm # ;



! The verb _teevvad_ arrive in form _teevv_
LEXICON TEEVVAD
+V+Ind+Prs:		VSUF-IA-PRS ;
+V+Ind+Prt:		VSUF-IA-PRT ;
+V+Imp:			VSUF-IA-IMP ;
+V:%^VOWL VSUF-I-POTKOND ; !WG
+V+Pot+Sg3:%^VOWL%>až	FINAL1 ; !WG
+V+Inf:%>ad # ;
+V+Ger:%>men # ;
+V+Act:%>am # ;
+V+VAbess:X3kâni # ;		!WG

! The verb _tie´tted_ arrive in form _tiõtt_
LEXICON TIETTED
+V+Ind+Prs:		VSUF-IE-PRS ;
+V+Ind+Prt:		VSUF-IE-PRT ;
+V+Imp:			VSUF-IE-IMP ;
+V:X3				VSUF-I-POTKOND ; !WG
+V+Pot+Sg3:%^VOWL%>ež	FINAL1 ; !WG
+V+Inf:%^VOWFront%^PAL%>ed # ;
+V+Ger:%>men # ;
+V+Act:%>em # ;
+V+VAbess:X3kâni # ;			!WG

! Odd-syllable stems
! ------------------
! Contracted stems
! ----------------


! Intermediate suffix lexica
! --------------------------


! Final suffix lexica
! -------------------

! Suffix series I
! - - - - - - - -

! assuming stem _viǯǯ_
LEXICON VS-I
+Sg1:%^VOWL%>am # ; ! _viǯǯam_
+Sg2:%^VOWL%>ak # ; ! _viǯǯak_
!+Sg3:^P3 # ; ! _veǯǯ_
+Sg3:%^VOWLower # ; ! _veǯǯ_
+Pl1:%>âp # ; ! _viǯǯâp_
+Pl2:%>ve´ted # ; ! _viǯǯve´ted_
+Pl3:%^VOWSH%^VOWLower%^VOWFront%^PAL%>e # ; ! _ve´ǯǯe_
+4:%^VOWL%>ât # ;
+ConNeg:%^VOWL # ;

LEXICON VS-I-Prt
+Sg1:%^VOWSH%^PAL%>em # ; ! _ka´ǧǧem_
+Sg2:%^VOWSH%^PAL%>iǩ # ; ! _ka´ǧǧiǩ_
+Sg3:%^VOWL%>i # ; ! _kaaggi_
+Pl1:%^VOWL%>im # ; ! _kaaggim_
+Pl2:%^VOWL%>id # ; ! _kaaggid_
+Pl3:%^VOWSH%^PAL%>e # ; ! _vi´ǯǯe_ _kuõ´sǩǩe_ _ka'ǧǧe_
+4:%^VOWSH%^PAL%>eš # ;

LEXICON VS-I-Imp
+Sg2:%^VOWL # ; ! _kaagg_
+Sg3:%^VOWL%^VOWLower%>as FINAL1 ;
+Pl1:%>âp FINAL1 ;
+Pl2:%>âd FINAL1 ;
+Pl3:%^VOWSH%^VOWLower%>az FINAL1 ;

! Present tense
! - - - - - - - -

! assuming stem _teevv_
LEXICON VSUF-IA-PRS
 +Sg1:%^VOWL%>am		FINAL1	; !WG !_teevam_ _lääulam_
 +Sg2:%^VOWL%>ak			FINAL1	; !WG _teevak_ _lääulak_
 +Sg3:			FINAL1	; !_teevv_ _läull_
 +Pl1:%>ap			FINAL1	; !_teevvap_ _läullap_
 +Pl2:%>ve´ted		FINAL1	; !_teevvve'ted_ _läullve'ted_
 +Pl3:%^VOWSH%>a		FINAL1	; !_tevva_ _läulla_
 ! epämääräinen pers.			!WG

! assuming stem _tie´tt_
LEXICON VSUF-IE-PRS
 +Sg1:%^VOWL%^VOWLower%>am		FINAL1	; !WG _teâđam_ 
 +Sg2:%^VOWL%^VOWLower%>ak		FINAL1	; !WG ! _teâđak_
 +Sg3:%^VOWLower%^PALNo FINAL1	; ! _teâtt_
 +Pl1:%^VOWFront%^PAL%>ep		FINAL1	; ! _ä´lǧǧep_ _tie´ttep_
 +Pl2:%^VOWFront%^PAL%>ve´ted		FINAL1	; ! _tie´ttve´ted_
 +Pl3:%^VOWSH%^VOWLower%^PAL%>e		FINAL1	; ! _teä´tte_
 +Pl3:%^VOWSH%^VOWLower%^VOWFront%^PAL%>e		FINAL1	; ! _teä´tte_
 ! epämääräinen pers.			!WG
 +4:%^VOWL%^VOWFront%^PAL%>et FINAL1 ; ! vue´ljet
 +ConNeg:%^VOWL%^VOWFront%^PAL # ;

! Past tense
! - - - - - - - -

LEXICON VSUF-IA-PRT
 +Sg1:%^VOWRaise%>um		FINAL1	; !đđ>dd ! _laullum_
 +Sg2:%^VOWRaise%>uk		FINAL1	; !đđ>dd ! _laulluk_
 +Sg3:%^VOWL%>ai			FINAL1	; !WG ! _lääulai_
 +Pl1:%^VOWL%>aim			FINAL1	; !WG ! _lääulaim_
 +Pl2:%^VOWL%>aid			FINAL1	; !WG ! _lääulaid_
 +Pl3:%^VOWSH%^VOWRaise%>u		FINAL1	; ! _laullu_
 ! epämääräinen pers.

LEXICON VSUF-IE-PRT
 +Sg1:%^PAL%>em		FINAL1	; ! _tiõ´ttem_
 +Sg2:%^PAL%>iǩ		FINAL1	; ! _tiõ´ttiǩ_
 +Sg3:%^VOWL%^PAL%>i		FINAL1	; !WG ! _tiõ´đi_
 +Pl1:%^VOWL%^PAL%>im		FINAL1	; !WG ! _tiõ´đim_
 +Pl2:%^VOWL%^PAL%>id		FINAL1	; !WG ! _tiõ´đid_
 +Pl3:%^VOWSH%^PAL%>e		FINAL1	; ! _tie´tte, tiõ´tte_
 +4:%^VOWL%^VOWFront%^PAL%>eš FINAL1 ; ! vue´lješ
 ! epämääräinen pers.


! Imperative
! - - - - - - -
! teevvad
LEXICON VSUF-IA-IMP
 +Sg2:%^VOWL FINAL1	; !WG
 +Sg3:X3as			FINAL1	; !WG
 +Pl1:ap			FINAL1	;
 +Pl2:ad			FINAL1	;
 +Pl3:%^VOWSHaz		FINAL1	;

LEXICON VSUF-IE-IMP
 +Sg2:%^VOWL%^VOWFront%^PAL FINAL1	; !WG
 +Sg3:%^VOWL%>as		FINAL1	; !WG
 +Pl1:ep			FINAL1	;
 +Pl2:ed			FINAL1	;
 +Pl3:%^1VOWaz		FINAL1	;


! Potentialis & Konditionalis
! not including +Pot+Sg3
! - - - - - - - - - - - - - -

LEXICON VSUF-I-POTKOND
 +Pot+Sg1:%>žem FINAL1	;
 +Pot+Sg2:%>žiǩ FINAL1	;
 +Pot+Pl1:%>žep FINAL1	;
 +Pot+Pl2:%>žid FINAL1	;
 +Pot+Pl3:%>že FINAL1	;
 +Pot+4:%>žet FINAL1 ;
 +Cond+Sg1:%>čem FINAL1	;
 +Cond+Sg2:%>čiǩ FINAL1	;
 +Cond+Sg3:%>či FINAL1	;
 +Cond+Pl1:%>čep FINAL1	;
 +Cond+Pl2:%>čid FINAL1	;
 +Cond+Pl3:%>če FINAL1	;
 +Cond+4:%>češ FINAL1 ;


! Suffix series II
! - - - - - - - -

! Other series
! - - - - - -


! Auxiliary verbs
! ---------------


