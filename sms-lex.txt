Multichar_Symbols

  A1 A2 A3 A4 E1 E2 I1 I2 I3 I4 O1 O2 U1 U2 Æ Å1 Å2 Å3 Å4 D1
  +N +Sg +Pl +Nom +Acc +Gen +Ine +Ela +Ill +Ess +Kom

LEXICON Root
  0:#			Noun	;

! Nominal inflection sublexica
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


!Inflection for nouns ending in '-ie'
! Only the cases that require special treatment.
! The regular cases are treated by continuing to N_SG and N_PL.

LEXICON N_IE
  +N+Sg:0		N_SG	;
  +N+Sg+Ill:%>A1n	FINAL1	;
  +N+Pl:0		N_PL	;
  +N+Sg+Ess:%>inI1	FINAL1	;

! Inflection for nouns ending in oe
! Only the cases that require special treatment.
! The regular cases are treated by continuing to N_SG and N_PL.

LEXICON N_OE
  +N+Sg:0		N_SG_OE	;
  +N+Sg+Ill:%>A1sD1e	FINAL1	;	! Causes Umlaut
  +N+Sg+Ill:%>se	FINAL1	;	! does NOT cause Umlaut
  +N+Pl:0		N_PL_OE	;
  +N+Sg+Ess:%>jnI1	FINAL1	;

! Inflection for all other nouns
! The same cases as covered by the two other nounclass lexicons above
! The rest of the cases are treated by continuing to N_SG and N_PL.

LEXICON N_OTHERS
  +N+Sg:0		N_SG	;
  +N+Sg+Ill:%>A1sD1e	FINAL1	;
  +N+Pl:0		N_PL	;
  +N+Sg+Ess:%>inI1	FINAL1	;

LEXICON N_SG
  +Nom:0		FINAL1	;
  +Acc:%>m		FINAL1	;
  +Gen:%>n		FINAL1	;
  +Ine:%>snI1		FINAL1	;
  +Ela:%>stI1		FINAL1	;
  +Kom:%>inI1		FINAL1	;

LEXICON N_PL
  +Nom:%>h		FINAL1	;
  +Acc:%>idI1		FINAL1	;
  +Gen:%>I3		FINAL1	;
  +Ill:%>idI1		FINAL1	;
  +Ine:%>inI1		FINAL1	;
  +Ela:%>ijstI1		FINAL1	;
  +Kom:%>I3+gujmie	FINAL1	;
! +Kom:%>I3+guejmie	FINAL1	;
! I do not understand how the "guejmie" form has crept in. TT-011105.

LEXICON N_SG_OE
  +Nom:0		FINAL1	;
  +Acc:%>m		FINAL1	;
  +Gen:%>n		FINAL1	;
  +Ine:%>snI1		FINAL1	;
  +Ela:%>stI1		FINAL1	;
  +Kom:%>jnI1		FINAL1	;

LEXICON N_PL_OE
  +Nom:%>h		FINAL1	;
  +Acc:%>jdI1		FINAL1	;
  +Gen:%>I3		FINAL1	;
  +Ill:%>jdI1		FINAL1	;
  +Ine:%>jnI1		FINAL1	;
  +Ela:%>jstI1		FINAL1	;
  +Kom:%>I3+gujmie	FINAL1	;
  +Kom:%>I3+guejmie	FINAL1	;

LEXICON FINAL1
  0:#			#	;
